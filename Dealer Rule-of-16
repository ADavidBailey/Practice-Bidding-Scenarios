Script,RuleOf16
setDealerCode(
`
dealer north

# East passes
##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

balEast   = shape(east,any 4333 +any 4432)
balWest   = shape(west,any 4333 +any 4432)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####


# Calculate the Rule of 16 for South (cards>7 + HCP)
r16S = top5(south,spades)   + hascard(south,9S) + hascard(south,8S) 
r16H = top5(south,hearts)   + hascard(south,9H) + hascard(south,8H)
r16D = top5(south,diamonds) + hascard(south,9D) + hascard(south,8D)
r16C = top5(south,clubs)    + hascard(south,9C) + hascard(south,8C)
r16  = r16S + r16H + r16D + r16C + hcp(south)


########## Place the Club 4 based on the Rule of 16 ##########
club4 = r16>15 ? hascard(south,4C) : hascard(north,4C)

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###

lev14 = r16==14 and keep97
lev15 = r16==15 and keep70
lev16 = r16==16 and keep70
lev17 = r16==17 and keep
lev18 = r16==18 and keep
levelTheDeal = lev14 or lev15 or lev16 or lev17 or lev18


# North opens 1N with 15-17 HCP
nOpens = shape(north,any 4333+any 4432+any 5332+any 5422 -5xxx-x5xx) and hcp(north)>14 and hcp(north)<18

# South has 8-9 HCP and more than 13 R16 points
sActs = shape(south, any 4333+any 4432+any 5332-5xxx-x5xx) and hcp(south)>7 and hcp(south)<10 and r16>13

# North opens 1N, East passes, and South acts
nOpens and calmEast and sActs and club4
#and levelTheDeal

generate 20000000
produce 10000
action
#printoneline,
average "R16 points = 14 " 100 * (r16==14),
average "R16 points = 15 " 100 * (r16==15),
average "R16 points = 16 " 100 * (r16==16),
average "R16 points = 17 " 100 * (r16==17),
average "R16 points = 18 " 100 * (r16==18),
average "  ------------  " 0,

average "HCP(south) = 8  " 100 * (hcp(south)==8),
average "HCP(south) = 9  " 100 * (hcp(south)==9),
average "  ------------  " 0,

average "HCP(north) = 15 " 100 * (hcp(north)==15),
average "HCP(north) = 16 " 100 * (hcp(north)==16),
average "HCP(north) = 17 " 100 * (hcp(north)==17),
average "  ------------  " 0,


average "N/S Combined HCP" (hcp(north) + hcp(south)),
average "  ------------  " 0,

`,"N",true)
Script
Button,Rule of 16,\n\
---  North opens 1N with 15-17 HCP and East passes. South has a balanced hand with 8-9 HCP and more than 13 R16 points\n\
(HCP + number of cards higher than an 8).  When South has more than 15 R16 points，South will will have the 4!C;\n\
otherwise，North will have the 4!C.  Sit South and put Robots in the other three seats.  Check Double-Dummy to see\n\
how you did. If you want，you can test with specific R16 values (15，16，17，or 18).  On all of these the opener will\n\
have exactly 15 HCP -- hands that would not accept an invitation.  Let me know how the Rule of 16 works for you.\n\
%RuleOf16%,

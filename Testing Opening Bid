produce 100000
# Testing Opening Bid

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and top4(south,spades)==1 and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and top4(south,hearts)==1 and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)==1 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and top4(south,clubs)==1 and not hascard(south,AC) ? 1 : 0
ntDownGrade = S2H + H2H + D2H + C2H

# Define notrump points for south (ntPoints)
ntPoints = hcp(south) + lengthPoints - ntDownGrade

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

# Define robot notrump shape and exclude any 5card 
ntShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422 -5xxx -x5xx)

# Define ntPoint ranges
oneNT = ntShape and ntPoints>14 and ntPoints<18
twoNT = ntShape and ntPoints>19 and ntPoints<22
weakNT = ntShape and ntPoints>10 and ntPoints<15
overcallNT = ntShape and ntPoints>14 and ntPoints<19  // 15-18

# Define Game Force 2C
gameForce2C = hcp(south)>21

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or twoNT or oneNT

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
range1 = suitPoints>11
oS = s>4 and s>=h and s>=d and s>=c and range1 and not P2
oH = not oS and h>4 and h>=d and h>=c and range1 and not P2
oD = not (oS or oH) and (d>3 and d>=c) and range1 and not P2
oC = not (oS or oH or oD) and range1 and not P2
openingSuit = (oS or oH or oD or oC)
P3 = P2 or openingSuit

# Define South's Weak 2 Bids with any 4-card
tp = hcp(south)
sp = hcp(south,spades) 
hp = hcp(south,hearts)
dp = hcp(south,diamonds)

// at least half of south's points are in the suit
// don't allow 5-card major
w2s = shape(south,6xxx-x6xx-x5xx) and 2 * sp > tp    
w2h = shape(south,x6xx-6xxx-5xxx) and 2 * hp > tp
w2d = shape(south,xx6x-x6xx-x5xx-6xxx-5xxx) and 2* dp > tp

w2Range = tp<11 and losers(south)<7

weak2S = w2s and w2Range and not P3
weak2H = w2h and w2Range and not P3
weak2D = w2d and w2Range and not P3
weakTwo = (weak2S or weak2H or weak2D)
P4 = P3 or weakTwo

# Define South's loose Weak 2 Bids
loose2S = spades(south)==6 and top4(south,spades)>1 and w2Range and not P4
loose2H = hearts(south)==6 and top4(south,hearts)>1 and w2Range and not P4
loose2D = diamonds(south)==6 and top4(south,diamonds)>1 and w2Range and not P4
P5 = P4 or (loose2S or loose2H or loose2D)

# Define Preempts
preempt = shape(south, any 9xxx +any 8xxx +any 7xxx) and hcp(south)<12 and not P4
P6 = P5 or preempt

# Define Pass
pass = not P6
P7 = P6 or pass

# Define opening Major, opening Minor
oneSpade = oS
oneHeart = oH
oneDiamond = oD
oneClub = oC
oneMajor = (oS or oH)
oneMinor = (oC or oD)

C2x = gameForce2C
N2x = twoNT
N1x = oneNT
S1x = oneSpade
H1x = oneHeart
D1x = oneDiamond
C1x = oneClub
S2x = weak2S
H2x = weak2H
D2x = weak2D
Sl2 = loose2S
Hl2 = loose2H
Dl2 = loose2D
P3x = preempt
NBx = pass

P7  // any bid

# Level responder hand types:
#and (not x3 or reduce_97_percent )
#and (not M4 or reduce_97_percent )
#and (not M3 or reduce_50_percent )
#and (not x4 or reduce_60_percent )
#and (not N3 or reduce_75_percent )

action
average " 1. 2C 22+ HCP  " 100 * C2x,
average " 2. 2N 20-21 NP " 100 * N2x,
average " 3. 1N 15-17 NP " 100 * N1x,
average " 4. 1S 10+ LP   " 100 * S1x,
average " 5. 1H          " 100 * H1x,
average " 6. 1D          " 100 * D1x,
average " 7. 1C          " 100 * C1x,
average " 8. 2S <11 LP   " 100 * S2x,
average " 9. 2H          " 100 * H2x,
average "10. 2D          " 100 * D2x,
average "11. 2s          " 100 * Sl2,
average "12. 2h          " 100 * Hl2,
average "13. 2d          " 100 * Dl2,
average "14. 3x 7+ cards " 100 * P3x,
average "Pass            " 100 * NBx,

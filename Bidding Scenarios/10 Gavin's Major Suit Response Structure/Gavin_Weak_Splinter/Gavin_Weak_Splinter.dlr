# alias: AmbiguousSplinter
# button-text: Weak Splinter (9-11 HCP)
# scenario-title: --- Weak Splinter (9-11 HCP)
# dealer-position: N
# gib-works: false
# bba-works: true
# auction-filter: .

/*@chat
--- Weak Splinter (9-11 HCP)
  • 1!H - 3!S, 3N asks, answers are low, middle, high
  • 1!S - 3N, 4!C asks, answers are low, middle, high
https://tinyurl.com/4dvh42e7
@chat*/
dealer north

# Weak Splinter (9-11 HCP)

# Avoid NT opener
NT = shape(north, any 4333 +any 4432 +any 5332 +any 5422 -5xxx -x5xx) and hcp(north)>14 and hcp(north)<18

# Avoid Pesky Opponents
##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# Defining Major Fit
SpadeFit = ((spades(north)>4 and hearts(north)<5) and spades(south)==4)
HeartFit = ((hearts(north)>4 and spades(north)<5) and hearts(south)==4)
MajorFit = (SpadeFit or HeartFit) and diamonds(north)<6 and clubs(north)<6 and not NT 

hcp(north)>10 and hcp(north)<20 
and calmOpps
and MajorFit
and shape(south,any 1xxx +any 0xxx)
and hcp(south)>8 and hcp(south)<12

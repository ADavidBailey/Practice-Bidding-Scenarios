# alias: xyNT
# button-text: 2-Way NMF - xyNT (Lev)
# scenario-title: ---  Two Way New Minor Forcing aka xyNT allows you to describe four different responding hand types.
# dealer-position: N
# gib-works: true
# bba-works: true
# auction-filter: 1[CDHS] +Pass +1[DHS] +Pass\n1N +Pass

/*@chat
---  Two Way New Minor Forcing aka xyNT allows you to describe four different responding hand types.
  • Part Score  -- after 1x-1y-1N, 2!H or 2!S ane natural and non-forcing, 2N relays to 3!C P/C.
  • Game Invite -- after 1x-1y-1N, bid 2!C, an artifical relay to 2!D.  Then, all continuations are invitational.
  • Game Force  -- after 1x-1y-1N, bid 2!D, an artifical game force.
  • Slam Invite -- after 1x-1y-1N, a suit bid at the 3-level is a natural slam try.
The deals are leveled so that the hand types occur at about the same frequency.
https://bridgewinners.com/article/view/two-way-new-minor-forcing-aka-xynt
@chat*/
dealer north

produce 300000

# Two Way New Minor Forcing, aka xyNT
# Predict North's opening suit
sN = spades(north)
hN = hearts(north)
dN = diamonds(north)
cN = clubs(north)
nS = sN>4 and sN>=hN and sN>=dN and sN>=cN
nH = not nS and hN>4 and hN>=dN and hN>=cN
nD = not nS and not nH and ((dN>3 and dN>=cN))
nC = not nS and not nH and not nD
northOpens = (nC or nD or nH) and hcp(north)>11 and hcp(north)<15 and shape(north,any 4333+any 4432+any 5332+any 5422 -5xxx)

# Predict South's responding suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
sS = s>=h and s>=d and s>=c
sH = not sS and h>=d and h>=c
sD = not sS and not sH and d>=c and d>4

cO = nC and (sD or sH or sS)
dO = nD and (sH or sS)
hO = nH and sS

# North opens, South responds, and North rebids 1N
southResponds = (cO or dO or hO)
partscore   = hcp(south)<11
invite      = hcp(south)>10 and hcp(south)<13
game        = hcp(south)>12 and hcp(south)<18
slam        = hcp(south)>17

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####
### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###

levPartscore = partscore and keep19
levInvite    = invite    and keep25
levGame      = game      and keep14
levSlam      = slam      and keep
levelTheDeal = (levPartscore or levInvite or levGame or levSlam)

northOpens and southResponds and calmOpps and (partscore or invite or game or slam)
and levelTheDeal

action
average "partscore " 100 * partscore,
average "invite    " 100 * invite,
average "game      " 100 * game,
average "slam      " 100 * slam,

# alias: ReverseAfterTwoOverOne
# button-text: Reverse After 2/1
# scenario-title: --- Reverse After 2/1 is for those that don't play 2/1 GF
# dealer-position: S
# gib-works: true
# bba-works: true
# auction-filter: (1D +Pass +2C.*\n2D +Pass +2[HS])|(1H +Pass +2[CD].*\n2H +Pass +2S)

/*@chat
--- Reverse After 2/1 is for those that don't play 2/1 GF
Reverses show 5+ cards in the suit opened and 4+ cards in the suit rebid.  They are
natural and Game Forcing.  Possible Reverses After 2/1 are...
• 1!D - 2!C - 2!D - 2!H
• 1!D - 2!C - 2!D - 2!S
• 1!H - 2!C - 2!H - 2!S
• 1!H - 2!D - 2!H - 2!S
Reverses are NOT a convention.  If you don't have Game Forcing strength, don't reverse.
@chat*/
dealer south

# Reverse by Resoponder

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# 1!D-2!C-2!D- 2!H or 2!S
sDD = shape(south,xx6x+xx7x)
nC  = shape(north,xxx5+xxx6)
nHorS = shape(north,x4xx+4xxx)
DCDHorS = sDD and nC and nHorS

# 1!H-2!C/!D - 2!H - 2!S
sHH = shape(south,x6xx+x7xx)
nCorD = shape(north,xxx5+xxx6)
nS    = shape(north,4xxx)
HCorDS = sHH and nCorD and nS

sMin = hcp(south)>11 and hcp(south)<15
nGF = hcp(north)>13

# Now do it
sMin and nGF and (DCDHorS or HCorDS) and calmOpps

action
average "D-C-D-H/S " 100 * DCDHorS,
average "H-C/D-H-S " 100 * HCorDS,

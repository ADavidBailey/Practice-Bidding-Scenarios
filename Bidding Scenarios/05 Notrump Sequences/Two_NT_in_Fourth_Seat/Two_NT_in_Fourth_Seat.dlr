# alias: TwoN4thSeat
# button-text: 2NT in 4th Seat
# scenario-title: --- 2N in 4th Seat shows 20-21 HCP.
# dealer-position: W
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\nPass +Pass +Pass +2N

/*@chat
--- 2N in 4th Seat shows 20-21 HCP.
@chat*/
dealer west

# Open In Fourth Seat

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####
# Defines calmOpps

# North passes
nPasses = hcp(north)<12 and shape(north,xxxx-any 7xxx-any 6xxx-any 55xx)

# South opens 2NT
s2N = (hcp(south)>19 and hcp(south)<22) and shape(south,any 5422+any 5332+any 4432+any 4333)

# Now do it
calmOpps and nPasses and s2N

produce 500
action
average "south 2N" s2N,

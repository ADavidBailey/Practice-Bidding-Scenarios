# alias: Splinter_After_NT
# button-text: Splinter after NT
# scenario-title: ---  Splinter after GIB Notrump
# dealer-position: S
# gib-works: true
# bba-works: true
# auction-filter: Note..1:1N-3[DM] splinter
# convention-card: 21GF-GIB

/*@chat
---  Splinter after GIB Notrump
A 3!D, 3!H, or 3!S response to 1N showing a stiff or void with 4-cards
in each unbid major and at least 4 in unbid minors. and 12+ TP.
Note: GIB does splinter with a stiff A, K, or Q.
@chat*/
dealer south

# Splinter after NT
produce 10000

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####
# defines calmOpps

##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major or with 4 spades
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422-5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# defines gibNT

# Define splinter distributions -- BBA, unlike GIB, does not splinter with a stiff A, K, or Q
dSpl = shape(north,4405+4414)
hSpl = shape(north,4045+4054+4144)
sSpl = shape(north,0445+0454+1444)

# Define North's tp (+3 for void, +2 for stiff, -1 for a stiff honor)
s0 = shape(north,any 0xxx) ? 3 : 0
s1 = shape(north,any 1xxx) ? 2 : 0

dH = shape(north, xx1x) and hcp(north,diamonds)>0
hH = shape(north, x1xx) and hcp(north,hearts)>0
sH = shape(north, 1xxx) and hcp(north,spades)>0

tp = hcp(north) + s0 + s1 - (dH + hH + sH)

//dAKQ = shape(north, xx1x) and hcp(north,diamonds)>1
//hAKQ = shape(north, x1xx) and hcp(north,hearts)>1
//sAKQ = shape(north, 1xxx) and hcp(north,spades)>1

# Define splinter
splinter = (dSpl or hSpl or sSpl) and tp>11 //and (dAKQ or hAKQ or sAKQ)

# Now do it
gibNT and splinter and calmOpps
produce 1000
action

average "hcp(south)"     hcp(south),
average "hcp(north)"     hcp(north),
average "TP north  "     tp,


average "1N-3S " 100 * sSpl,
average "1N-3H " 100 * hSpl,
average "1N-3D " 100 * dSpl,

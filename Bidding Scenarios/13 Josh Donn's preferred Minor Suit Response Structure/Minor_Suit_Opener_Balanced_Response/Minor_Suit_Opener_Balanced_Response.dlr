# alias: MinorOpenerBalancedResponse
# button-text: 1m & Bal Resp (Lev)
# scenario-title: ---  Minor Opening w/Bal Resp (Lev)
# dealer-position: S
# gib-works: false
# bba-works: true
# auction-filter: (1[CD] Pass 1[DHSN])|(1[CD] Pass [23]N)
# convention-card: 21GF-NoInvertedMinor

/*@chat
---  Minor Opening w/Bal Resp (Lev)
6-10  (21.6%) 1!D, 1!H, 1!S, 1N
11-12 (20.0%) 2!H
13-15 (22.8%) 2N, GF
16-17 (15.4%) 3N
18-19 (20.2%) 2N, GF+
@chat*/
dealer south

# Minor Opening w/Bal Resp (Lev)

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

ntShape = shape(south, any 4333 +any 4432 +any 5422 +any 5332)
oneNT = ntShape and hcp(south)>14 and hcp(south)<18
twoNT = ntShape and hcp(south)>19 and hcp(south)<22

notNT = not (oneNT or twoNT)
notMajor = spades(south)<5 and hearts(south)<5
hcpRange = hcp(south)>11 and hcp(south)<22
oneMinor = notNT and notMajor and hcpRange

nbalanced = shape(north, any 5332+any 4333+any 4432-5xxx-x5xx)
n06_10 = hcp(north)>5 and hcp(north)<11
n11_12 = hcp(north)>10 and hcp(north)<13
n13_15 = hcp(north)>12 and hcp(north)<16
n16_17 = hcp(north)>15 and hcp(north)<18
n18_19 = hcp(north)>17 and hcp(north)<20

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###

# Do the Leveling
lev06_10 = n06_10 and keep11
lev11_12 = n11_12 and keep14
lev13_15 = n13_15 and keep14
lev16_17 = n16_17 and keep25
lev18_19 = n18_19 and keep
levelTheDeal = lev06_10 or lev11_12 or lev13_15 or lev16_17 or lev18_19

oneMinor and nbalanced and hcp(north)>5 and calmOpps
and levelTheDeal

produce 1000
action
average "06_10 " 100 * n06_10,
average "11_12 " 100 * n11_12,
average "13_15 " 100 * n13_15,
average "16_17 " 100 * n16_17,
average "18_19 " 100 * n18_19,
average "18_19 " 100 * n18_19,

# alias: WeakTwoBidsLev
# button-text: (Lev)
# scenario-title: --- Weak Two Bids without 4-card major - Leveled
# dealer-position: S
# gib-works: true
# bba-works: true
# auction-filter: 2[DHS]

/*@chat
--- Weak Two Bids without 4-card major - Leveled
Opener has a 6-card suit without a 4-card side major, at least half of the HCP
are in the suit, less than 11 HCP and less than 7 losers.  This script is leveled.
Meaning that responder is equally likely to have a pass, raise, invite, or force.
https://www.bridgebum.com/weak_two.php
@chat*/
dealer south


# Define South's Weak 2 Bids w/o 4-card major side suit -- Strict

     // at least half of south's points are in the suit
     // allow no 4-card side major

tp = hcp(south)
sp = hcp(south,spades)
hp = hcp(south,hearts)
dp = hcp(south,diamonds)
 
w2s = shape(south,6xxx-x6xx-x5xx-x4xx) and 2 * sp > tp    
w2h = shape(south,x6xx-6xxx-5xxx-4xxx) and 2 * hp > tp
w2d = shape(south,xx6x-x6xx-x5xx-x4xx-6xxx-5xxx-4xxx) and 2* dp > tp
 
w2Range = tp>4 and tp<11 and losers(south)<7
 
weakTwo = (w2s or w2h or w2d) and w2Range

# Define responders hand types

# Responder Raises
sFit = w2s and spades(north)>2
hFit = w2s and hearts(north)>2
dFit = w2s and diamonds(north)>2
fit = sFit or hFit or dFit

# Define Feature -- an Ace or King outside the trump suit
sAK = top2(south,spades)==1 and not sFit
hAK = top2(south,hearts)==1 and not hFit
dAK = top2(south,diamonds)==1 and not dFit
cAK = top2(south,clubs)==1
oHonor = (sAK or hAK or dAK or cAK)

rPass = hcp(north)<15 and not fit
rRaise = fit and hcp(north)<14               // Extend the preempt
rInvite = fit and hcp(north)>13              // 2N
rForce = hcp(north)>16 and not rInvite       // Do something

# Responder bids new suit
rGS = (spades(north)==5 && top5(north,spades)>3) or (spades(north)>5 and top5(north,spades) >2)
rGH = (hearts(north)==5 && top5(north,hearts)>3) or (hearts(north)>5 and top5(north,hearts) >2)
rGD = (diamonds(north)==5 && top5(north,diamonds)>3) or (diamonds(north)>5 and top5(north,diamonds) >2)
rGC = (clubs(north)==5 && top5(north,clubs)>3) or (clubs(north)>5 and top5(north,clubs) >2)
rGSuit = rGS or rGH or rGD or rGC
rNewSuit = rForce and rGSuit

# Opener shows feature
oFeature = rInvite and hcp(south)>8 and oHonor

# Opener rebids suit
oRebid = rInvite and not oFeature

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###

# Level the deal
case1 = rPass and keep11
case2 = rRaise and keep19
case3 = rInvite and keep56
case4 = rForce and keep
levelTheDeal = (case1 or case2 or case3 or case4)

# do it
weakTwo
and levelTheDeal

action
average "Responder pass          " 100 * rPass,
average "Responder raise         " 100 * rRaise,
average "Responder invite        " 100 * rInvite,
average "Responder force         " 100 * rForce,

average "2NT/Opener rebids major " 100 * oRebid,
average "2NT/Opener shows feature" 100 * oFeature,
average "New suit response       " 100 * rNewSuit,
average "Trump fit               " 100 * fit,

# alias: LeapingMichaels
# button-text: Leaping Michaels
# scenario-title: --- Leaping Michaels
# gib-works: false
# bba-works: true
# auction-filter: (Note...:Leaping Michaels)|(Note...:Michaels)
# convention-card-ns: 21GF-DEFAULT
# convention-card-ew: 21GF-GIB

/*@chat
--- Leaping Michaels
https://www.bridgebum.com/leaping_michaels.php
@chat*/

produce 1000
dealer east

# Leaping Michaels

# Define good suits south
gS = spades(south)>4   and top5(south,spades)>2
gH = hearts(south)>4   and top5(south,hearts)>2
gD = diamonds(south)>4 and top5(south,diamonds)>2
gC = clubs(south)>4    and top5(south,clubs)>2

# Define 2 good suits south
g2notS = (gH and gD) or (gH and gC) or (gD and gC)
g2notH = (gS and gD) or (gS and gC) or (gC and gD)
g2notD = (gS and gH) or (gS and gC) or (gH and gC)

# Define East Weak 2 suits
sE = top3(east,spades)>1   and spades(east)==6   and hearts(east)<4                    and g2notS
hE = top3(east,hearts)>1   and hearts(east)==6   and spades(east)<4                    and g2notH 
dE = top3(east,diamonds)>1 and diamonds(east)==6 and spades(east)<4 and hearts(east)<4 and g2notD

# East opens a Weak Two and South has 2 good suits and less than 5 losers
hcp(east)>5 and hcp(east)<10 and (sE or hE or dE) and hcp(south)>11 and losers(south)<5

action printoneline,
average "           2S " 100 * sE,
average "           2H " 100 * hE,
average "           2D " 100 * dE,
average "losers(south) " losers(south),
average "hcp(south)    " hcp(south),

# alias: TwoSuitedOvercalls
# button-text: 2-Suit Overcalls
# scenario-title: Please note:  This scenario includes all two-suited hands. Specific major/minor combinations
# dealer-position: E
# gib-works: false
# bba-works: true
# auction-filter: (1C 2C)|(1D 2D)|(1H 2H)|(1S 2S)|(1[CDHS] 2N)

/*@chat
Please note:  This scenario includes all two-suited hands. Specific major/minor combinations
are problematic -- Michaels Cuebids and Top & Bottom Cuebids can't be played together.
·
You may want to practice with the Michaels and Unusual NT scenario instead.
·
Let me know if you want to practice with Top & Bottom and Unusual NT.
--- 2-Suit Overcalls
@chat*/
dealer east

# Two-Suited Overcalls
produce 1000

##### Imported Script -- Predict Opening 1-Bid East #####
# 01/19/2026 -- Changed to use TP and separate ranges for C, D, and majors
# 03/30/2025 -- Changed to use gibNT

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(east) + shape(east,5xxx+x5xx)
nt1 = shape(east, any 5332+any 4432+any 4333) and hcp(east)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(east, any 5422 -5xxx-x5xx-4xxx) and hcp(east)>14 and hcp(east)<17

gibNT = nt1 or nt2

## Calculate TP for East -- NOTE:  This uses different variable names than TP to avoid conflicts when both are used.
# Calculate East's Short-Suit Penalty Points
ssp = spades(east)<3 and hcp(east,spades)>0     ? 1 : 0
shp = hearts(east)<3 and hcp(east,hearts)>0     ? 1 : 0
sdp = diamonds(east)<3 and hcp(east,diamonds)>0 ? 1 : 0
scp = clubs(east)<3 and hcp(east,clubs)>0       ? 1 : 0

# Calculate East's Short-Suit Points
sv01 = shape(east, any 0xxx) ? 3 : 0
ss01 = shape(east, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss02 = shape(east, any 11xx) ? 2 : 0
sd01 = shape(east, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd02 = shape(east, any 22xx) ? 1 : 0
sd03 = shape(east, any 222x) ? 1 : 0

# Calculate East's Total Points
sTP = hcp(east) + sv01+ss01+ss02+sd01+sd02+sd03 -ssp-shp-sdp-scp

# Calculate length points for East (lengthPoints)
lp1 = spades(east)>4 ? spades(east)-4 : 0
lp2 = hearts(east)>4 ? hearts(east)-4 : 0
lp3 = diamonds(east)>4 ? diamonds(east)-4 : 0
lp4 = clubs(east)>4 ? clubs(east)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for east (suitPoints)
suitPoints = hcp(east) + lengthPoints

TwoNtShape = shape(east, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(east)>19 and hcp(east)<22

# Define Gambling 3N
g3Shape = shape(east,any 9xxx +any 8xxx +any 7xxx -any 4xxx -any 5xxx -any 6xxx)
gamble3 = (top3(east,clubs)==3 and clubs(east)>6) or (top3(east,diamonds)==3 and diamonds(east)>6) and hcp(east)<12 and g3Shape

# Define Game Forcing 2C
case1 = hcp(east)>18 and sTP>21
case2 = hcp(east)>18 and sTP>19 and losers(east)<5 and (spades(east)>5 or hearts(east)>5)
case3 = hcp(east)>18 and sTP>19 and losers(east)<4 and (diamonds(east)>5 or clubs(east)>5)
gameForce2C = (case1 or case2 or case3)

### Predict East's opening BID
P1 = gameForce2C
P2 = P1 or gamble3 or twoNT or oneNT

# Define quality and length lower limits for 10 HCP openers

q2of3 = (top3(east,spades)>1 or top3(east,hearts)>1) and controls(east)>3
lgt5  = (spades(east)>4 or hearts(east)>4) and shape(east,any 7xxx +any 64xx +any 55xx +any 1xxx+any 0xxx -any 8xxx -any 54xx) and sTP>(hcp(east) +2)

# Predict East's Opening suit
# What's the longest suit?
s = spades(east)
h = hearts(east)
d = diamonds(east)
c = clubs(east)

## GIB has different requirements for 1H/S, 1D, and 1C openings
#Define range for 1 H/S
lS = s>4 and s>=h and s>=d and s>=c
lH = h>4 and h>s and h>=d and h>=c
lM = lS or lH

hsOK1 = lM and hcp(east)>11
hsOK2 = lM and hcp(east)>10 and sTP>12 and not hsOK1
hsOK3 = lM and hcp(east)>9  and lgt5 and q2of3 and not (hsOK1 or hsOK2)
hsRange = (hsOK1 or hsOK2 or hsOK3) and not P2

# Define 1D Range
dOK1 = hcp(east)>11 and (diamonds(east)>3 or shape(east, 4432))
dOK2 = hcp(east)>9  and sTP>12 and diamonds(east)>5 and not dOK1
dRange = (dOK1 or dOK2) and not P2

# Define 1C Range
cOK1 = hcp(east)>10 and sTP>12 and spades(east)==4
cOK2 = hcp(east)>9  and sTP>12 and clubs(east)>5 and not cOK1
cOK3 = hcp(east)>11 and clubs(east)>2            and not (cOK1 or cOK2)
cRange = (cOK1 or cOK2 or cOK3) and clubs(east)>2 and clubs(east)>diamonds(east) and not P2

oS = s>4 and s>=h and s>=d and s>=c and hsRange
oH = not oS and h>4 and h>=d and h>=c and hsRange
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and dRange
oC = not (oS or oH or oD) and cRange
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid-East #####


# 2-suited overcalls

# Define good suits
GoodS = spades(south)>4 and top5(south,spades)>2
GoodH = hearts(south)>4 and top5(south,hearts)>2
GoodD = diamonds(south)>4 and top5(south,diamonds)>2
GoodC = clubs(south)>4 and top5(south,clubs)>2

# Define 2-suits
SH = (GoodS and GoodH)
SD = (GoodS and GoodD)
SC = (GoodS and GoodC)
HD = (GoodH and GoodD)
HC = (GoodH and GoodC)
DC = (GoodD and GoodC)

# Define 2-Suits -- not opener's suit
oneS = oS and (HD or HC or DC)  // Michaels or Michaels or Unusual NT
oneH = oH and (SD or SC or DC)  // Michaels or Michaels or Unusual NT
oneD = oD and (SH or HC or SC)  // Michaels or Unusual NT or ???? - no way to show SC
oneC = oC and (SH or HD or SD)  // Michaels or Unusual NT or ???? - no way to show SD

oSuit = (oneS or oneH or oneD or oneC) and shape(east,any xxxx-8xxx-7xxx-6xxx)
oRange = hcp(east)>11 and hcp(east)<15

# Intervener is weak or strong
iWeak   = losers(south)<8 and hcp(south)>7  and hcp(south)<12
iStrong = losers(south)<5 and hcp(south)>14 and hcp(south)<20
iRange = iWeak or iStrong

# This is an attempt to get stats for overcaller weak/strong.  I don't think I can get it to work.
wHCP    = iWeak   ? hcp(south)    : 0
wLosers = iWeak   ? losers(south) : 0
sHCP    = iStrong ? hcp(south)    : 0
sLosers = iStrong ? losers(south) : 0

# Responder's minimum HCP -- you might try adjusting this
rRange = hcp(west)>7

### Now do it
oSuit and iRange and rRange

# To get stats for south weak/strong uncomment one of the following
#and iWeak
#and iStrong

# You might also play with choseing south's suits
#and SC or SD  // If you just want the problematic ones

action
#printoneline,

average "Weak       " 100 * iWeak,
average "  HCP      "     wHCP,
average "  Losers   "     wLosers,
average "Strong     " 100 * iStrong,
average "  HCP      "     sHCP,
average "  Losers   "     sLosers,

average "1S         " 100 * (oneS and (HD or HC or DC)),
average "   (2S)  HD" 100 * (oneS and HD),
average "   (2S)  HC" 100 * (oneS and HC),
average "   (2N)  DC" 100 * (oneS and DC),
average "1H         " 100 * (oneH and (SD or SC or DC)),
average "   (2H)  SD" 100 * (oneH and SD),
average "   (2H)  SC" 100 * (oneH and SC),
average "   (2N)  DC" 100 * (oneH and DC),
average "1D         " 100 * (oneD and (SH or HC or SC)),
average "   (2D)  SH" 100 * (oneD and SH),
average "   (2N)  HC" 100 * (oneD and HC),
average "         SC" 100 * (oneD and SC),
average "1C         " 100 * (oneC and (SH or HD or SD)),
average "   (2C)  SH" 100 * (oneC and SH),
average "   (2N)  HD" 100 * (oneC and HD),
average "         SD" 100 * (oneC and SD),

frequency "Opener HCP    " (hcp(east),12,14),
frequency "Intervener HCP" (hcp(south),8,19),
frequency "Responder HCP " (hcp(west),6,17),

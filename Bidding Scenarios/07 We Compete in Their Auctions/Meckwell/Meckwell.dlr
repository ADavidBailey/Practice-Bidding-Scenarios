# alias: Meckwell
# button-text: Meckwell (Lev)
# scenario-title: ---  15-17 Notrump Opening and Meckwell action.
# gib-works: false
# bba-works: true
# auction-filter: Note...:Multi-Landy
# convention-card-ns: 21GF-Multi-Landy
# convention-card-ew: 21GF-GIB

/*@chat
---  15-17 Notrump Opening and Meckwell action.
  • X   = !C, !D, !H & !S, or a good hand
  • 2!C = !C and a major
  • 2!D = !D and a major
  • 2!H = 5+ !H
  • 2!S = 5+ !S
  • 2N  = 5-5 or better in !C & !D
The Meckwell actions are leveled.  The NT opener is not leveled.
https://www.bridgebum.com/meckwell_defense_to_1nt.php
@chat*/

dealer east

### gibNT changed from south to east
# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(east) + shape(east,5xxx+x5xx)
nt1 = shape(east, any 5332+any 4432+any 4333) and hcp(east)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(east, 4252+4225+2452+2425+2254+2245) and hcp(east)>14 and hcp(east)<17

gibNT = nt1 or nt2

# Defines gibNT

# Define South's good suits
sGS = top2(south,spades)>0   and top4(south,spades)>1   and top5(south,spades)>2
sGH = top2(south,hearts)>0   and top4(south,hearts)>1   and top5(south,hearts)>2
sGD = top2(south,diamonds)>0 and top4(south,diamonds)>1 and top5(south,diamonds)>2
sGC = top2(south,clubs)>0    and top4(south,clubs)>1    and top5(south,clubs)>2

# Define South's suit lengths
sS = spades(south)
sH = hearts(south)
sD = diamonds(south)
sC = clubs(south)

# Define South's shapes
majorAndLongerC   = ((sGS and sS>3) or (sGH and sH>3)) and sGC and sC>sS and sC>sH 
majorAndLongerD   = ((sGS and sS>3) or (sGH and sH>3)) and sGD and sD>sS and sD>sH 
bothMajors        = sGS and sGH and shape(south,66xx+65xx+56xx+55xx+54xx+45xx)
oneSuit           = shape(south,any 9xxx+any 8xxx+any 7xxx+any 6xxx+any 5xxx-any 76xx-any 75xx-any 65xx-any 4xxx)

goodHand          = oneSuit and (sGS or sGH or sGD or sGC) and hcp(south)>15
oneMinor          = oneSuit and (sGD or sGC) and shape(south,any 0xxx+any 1xxx)
heartSuit         = oneSuit and sGH and shape(south,any 0xxx+any 1xxx)
spadeSuit         = oneSuit and sGS and shape(south,any 0xxx+any 1xxx)
bothMinors        = sGD and sGC and shape(south,xx76+xx67+xx66+xx65+xx56+xx55+xx54+xx45)

# Define South's Meckwell action
cX  = oneMinor or bothMajors or goodHand
c2C = not cX and majorAndLongerC             
c2D = not cX and majorAndLongerD             
c2H = not cX and heartSuit
c2S = not cX and spadeSuit
c2N = bothMinors

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###

# Define Leveling
lX = cX  and keep03
lC = c2C and keep89
lD = c2D and keep89
lH = c2H and keep11
lS = c2S and keep11
lN = c2N and keep
levelTheDeal = lX or lC or lD or lH or lS or lN

# Now OPEN 1NT
gibNT and (cX or c2C or c2D or c2H or c2S or c2N) and hcp(south)>9
and levelTheDeal

# Generate Statistics
action printoneline,
average "X    " 100 * cX,
average "2C   " 100 * c2C,
average "2D   " 100 * c2D,
average "2H   " 100 * c2H,
average "2S   " 100 * c2S,
average "2N   " 100 * c2N,
frequency  "HCP South "  (hcp(south) , 9, 20),

# alias: Splinters
# button-text: Splinters
# scenario-title: --- Splinter Bids are double jumps.  They show a singleton or void in the suit bid and 4+ cards
# dealer-position: S
# gib-works: true
# bba-works: true
# auction-filter: Note..1.Splinter

/*@chat
--- Splinter Bids are double jumps.  They show a singleton or void in the suit bid and 4+ cards
in support of opener's major.  GIB splinters with a singleton A, K, or Q.  Some players do NOT.
• After 1!H a jump to 3!S, 4!C, or 4!D
• After 1!S a jump to 4!C, 4!D, or 4!H
@chat*/
dealer south

# Splinters

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####
# Defines calmWest
##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major or with 4 spades
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422-5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# Defines gibNT
### Imported script: TP (Total Points)
//   Total Points = HCP + Short-Suit Points - Penalty Points
//   Short-Suit Points: 3 for void, 2 for singleton, 1 for doubleton
//   Penalty Points: 1 for each honor (K/Q/J) in a short suit (devalues wasted honors like Kx, Qx)

## Calculate TP for North
# Short-Suit Points: void=3, singleton=2, doubleton=1
nSpadeSSP = spades(north)<3   ? 3-spades(north)   : 0
nHeartSSP = hearts(north)<3   ? 3-hearts(north)   : 0
nDiamSSP  = diamonds(north)<3 ? 3-diamonds(north) : 0
nClubSSP  = clubs(north)<3    ? 3-clubs(north)    : 0
nSSP = nSpadeSSP + nHeartSSP + nDiamSSP + nClubSSP

# Penalty Points: deduct 1 for honors in short suits
nSpadePP = spades(north)<3   and hcp(north,spades)>0   ? 1 : 0
nHeartPP = hearts(north)<3   and hcp(north,hearts)>0   ? 1 : 0
nDiamPP  = diamonds(north)<3 and hcp(north,diamonds)>0 ? 1 : 0
nClubPP  = clubs(north)<3    and hcp(north,clubs)>0    ? 1 : 0
nPP = nSpadePP + nHeartPP + nDiamPP + nClubPP

tpNorth = hcp(north) + nSSP - nPP

## Calculate TP for East
eSpadeSSP = spades(east)<3   ? 3-spades(east)   : 0
eHeartSSP = hearts(east)<3   ? 3-hearts(east)   : 0
eDiamSSP  = diamonds(east)<3 ? 3-diamonds(east) : 0
eClubSSP  = clubs(east)<3    ? 3-clubs(east)    : 0
eSSP = eSpadeSSP + eHeartSSP + eDiamSSP + eClubSSP

eSpadePP = spades(east)<3   and hcp(east,spades)>0   ? 1 : 0
eHeartPP = hearts(east)<3   and hcp(east,hearts)>0   ? 1 : 0
eDiamPP  = diamonds(east)<3 and hcp(east,diamonds)>0 ? 1 : 0
eClubPP  = clubs(east)<3    and hcp(east,clubs)>0    ? 1 : 0
ePP = eSpadePP + eHeartPP + eDiamPP + eClubPP

tpEast = hcp(east) + eSSP - ePP

## Calculate TP for South
sSpadeSSP = spades(south)<3   ? 3-spades(south)   : 0
sHeartSSP = hearts(south)<3   ? 3-hearts(south)   : 0
sDiamSSP  = diamonds(south)<3 ? 3-diamonds(south) : 0
sClubSSP  = clubs(south)<3    ? 3-clubs(south)    : 0
sSSP = sSpadeSSP + sHeartSSP + sDiamSSP + sClubSSP

sSpadePP = spades(south)<3   and hcp(south,spades)>0   ? 1 : 0
sHeartPP = hearts(south)<3   and hcp(south,hearts)>0   ? 1 : 0
sDiamPP  = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
sClubPP  = clubs(south)<3    and hcp(south,clubs)>0    ? 1 : 0
sPP = sSpadePP + sHeartPP + sDiamPP + sClubPP

tpSouth = hcp(south) + sSSP - sPP

## Calculate TP for West
wSpadeSSP = spades(west)<3   ? 3-spades(west)   : 0
wHeartSSP = hearts(west)<3   ? 3-hearts(west)   : 0
wDiamSSP  = diamonds(west)<3 ? 3-diamonds(west) : 0
wClubSSP  = clubs(west)<3    ? 3-clubs(west)    : 0
wSSP = wSpadeSSP + wHeartSSP + wDiamSSP + wClubSSP

wSpadePP = spades(west)<3   and hcp(west,spades)>0   ? 1 : 0
wHeartPP = hearts(west)<3   and hcp(west,hearts)>0   ? 1 : 0
wDiamPP  = diamonds(west)<3 and hcp(west,diamonds)>0 ? 1 : 0
wClubPP  = clubs(west)<3    and hcp(west,clubs)>0    ? 1 : 0
wPP = wSpadePP + wHeartPP + wDiamPP + wClubPP

tpWest = hcp(west) + wSSP - wPP

## Partnership Totals
tpNS = tpNorth + tpSouth
tpEW = tpEast + tpWest

### End of Imported Script: TP
# Defines tpSouth, tpNorth

sMin = hcp(south)==11 and tpSouth==12
sLen = sMin ? 6 : 5

sFit = spades(south)>=sLen and hearts(south)<5 and spades(north)==4
hFit = hearts(south)>=sLen and spades(south)<5 and hearts(north)==4
fit = sFit or hFit

strong2 = hcp(south)>21 or tpSouth>22 or losers(south)<5
gib2N   = shape(south, any 5332) and (hcp(south)==20 or hcp(south)==21)

sShape = shape(south,any 8xxx +any 7xxx +any 6xxx +any 5xxx -any 85xx -any 76xx -any 75xx -any 65xx)
sOpens = hcp(south)>10 and tpSouth>11 and sShape and not (strong2 or gibNT or gib2N)

# GIB uses a Jacoby 2N response with 16 TP and a singleton
jacoby2N = shape(north,any 1xxx) and tpNorth==16

# North Splinters w/4-card support and any shortness
anyShortness = shape(north,any 1xxx +any 0xxx)

nSplinters = tpNorth>12 and tpNorth<17 and anyShortness and not jacoby2N

game1 = tpSouth==12 or tpSouth==13
game2 = tpSouth==14 or tpSouth==15
slam1 = tpSouth==16 or tpSouth==17
slam2 = tpSouth>17

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###
lev1 = game1 and keep25
lev2 = game2 and keep19
lev3 = slam1 and keep33
lev4 = slam2 and keep94
levelTheDeal = lev1 or lev2 or lev3 or lev4

sOpens and fit and nSplinters and calmWest
and levelTheDeal

action
average "12-13 & 13-16" 100 * game1,
average "14-15 & 13-16" 100 * game2,
average "16-17 & 13-16" 100 * slam1,
average "18-21 & 13-16" 100 * slam2,

# alias: OneMinorTwoMinor
# button-text: 1m-1x-2m
# scenario-title: --- 1m-1x-2m
# dealer-position: S
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\n(1C +Pass +1.* Pass\n2C|1D +Pass 1.* Pass\n2D)

/*@chat
--- 1m-1x-2m
@chat*/
dealer south

# South Open's 1 of a Minor
produce 1000

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

NT = shape(north,any 4432+any 4333+any 5332-5xxx-x5xx)
jumpNT = NT and hcp(north)>10 // and hearts(north)<4 and spades(north)<4
twoOverOne = hcp(north)>12
weakOrInvJump = shape(north,any 7xxx +any 6xxx) and hcp(north)>4

sMinor = shape(south,xx7x+xx6x+xxx7+xxx6-5xxx-4xxx-x5xx-x4xx -any 66xx -any 65xx -any 75xx) and hcp(south)>11 and hcp(south)<15
n1Bid  = hcp(north)>5 and not weakOrInvJump and not jumpNT and not twoOverOne
sLength = clubs(south)>diamonds(south) ? clubs(south) : diamonds(south)

# Do it
sMinor and n1Bid and calmOpps

action
average "hcp(south)      " hcp(south),
average "hcp(north)      " hcp(north),
average "south length    " sLength,

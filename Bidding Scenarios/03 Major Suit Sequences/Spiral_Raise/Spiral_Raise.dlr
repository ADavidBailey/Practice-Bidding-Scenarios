# alias: SpiralRaise
# button-text: Spiral Raise
# scenario-title: --- Spiral Raise
# gib-works: true
# bba-works: true
# auction-filter: ((1[CD] +Pass +1H +Pass\n2H +Pass +[23])|(1[CD] +Pass +1S +Pass\n2S +Pass +[23]))
# convention-card-ns: 21GF-DEFAULT
# convention-card-ew: 21GF-GIB

/*@chat
--- Spiral Raise
After 1m 1M 2M, 2N is Spiral asking bid
In the simplest version Opener shows 3- or 4-card support and min/max HCP
• 3!C 3-card support, minimum
• 3!D 3-card support, maximum
• 3!H 4-card support, minimum
• 3!S 4-card support, maximum
Some use a step-bid instead of 2N as the asking bid
https://bridgewinners.com/article/view/spiral-raises/
https://bridgewinners.com/article/view/1m-1m-now-what/
@chat*/

produce 1000
dealer south

# Spiral Raises (All Flavors

# Define 12-14 minor opening, w/3- or 4-card support of either major, and shortness
minorOpening = hcp(south)>11 and hcp(south)<15 and shape(south,44xx+43xx+34xx+33xx)

# either 8-card fit or opener has shortness
sFit8 = spades(south) + spades(north) > 7
hFit8 = hearts(south) + hearts(north) > 7
shortness  = sFit8 or hFit8 ? shape(south,xxxx-any 0xxx - any 1xxx) : shape(south,any 0xxx+ any 1xxx)

# North has an invitational hand & responds in a major
northResponds = shape(north, 44xx+43xx+34xx-any 6xxx -any 5xxx) and hcp(north)>10 and hcp(north)<14

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# Now do it…
minorOpening and shortness and northResponds and calmOpps
action printoneline,

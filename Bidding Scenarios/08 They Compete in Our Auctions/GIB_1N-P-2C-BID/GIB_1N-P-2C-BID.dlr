# alias: GibAfterStayman
# button-text: GIB after Stayman
# scenario-title: 
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\n(1NT? +Pass +2C.* +[X2-7])
# convention-card-ns: 21GF-GIB
# convention-card-ew: 21GF-GIB

/*
South opens 1N and North responds 2C and East bids.
*/

dealer south

# 1 Notrump Opening
##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major or with 4 spades
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422-5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# Defines gibNT

### Imported script: TP (Total Points)
//   Total Points = HCP + Short-Suit Points - Penalty Points
//   Short-Suit Points: 3 for void, 2 for singleton, 1 for doubleton
//   Penalty Points: 1 for each honor (K/Q/J) in a short suit (devalues wasted honors like Kx, Qx)

## Calculate TP for North
# Short-Suit Points: void=3, singleton=2, doubleton=1
nSpadeSSP = spades(north)<3   ? 3-spades(north)   : 0
nHeartSSP = hearts(north)<3   ? 3-hearts(north)   : 0
nDiamSSP  = diamonds(north)<3 ? 3-diamonds(north) : 0
nClubSSP  = clubs(north)<3    ? 3-clubs(north)    : 0
nSSP = nSpadeSSP + nHeartSSP + nDiamSSP + nClubSSP

# Penalty Points: deduct 1 for honors in short suits
nSpadePP = spades(north)<3   and hcp(north,spades)>0   ? 1 : 0
nHeartPP = hearts(north)<3   and hcp(north,hearts)>0   ? 1 : 0
nDiamPP  = diamonds(north)<3 and hcp(north,diamonds)>0 ? 1 : 0
nClubPP  = clubs(north)<3    and hcp(north,clubs)>0    ? 1 : 0
nPP = nSpadePP + nHeartPP + nDiamPP + nClubPP

tpNorth = hcp(north) + nSSP - nPP

## Calculate TP for East
eSpadeSSP = spades(east)<3   ? 3-spades(east)   : 0
eHeartSSP = hearts(east)<3   ? 3-hearts(east)   : 0
eDiamSSP  = diamonds(east)<3 ? 3-diamonds(east) : 0
eClubSSP  = clubs(east)<3    ? 3-clubs(east)    : 0
eSSP = eSpadeSSP + eHeartSSP + eDiamSSP + eClubSSP

eSpadePP = spades(east)<3   and hcp(east,spades)>0   ? 1 : 0
eHeartPP = hearts(east)<3   and hcp(east,hearts)>0   ? 1 : 0
eDiamPP  = diamonds(east)<3 and hcp(east,diamonds)>0 ? 1 : 0
eClubPP  = clubs(east)<3    and hcp(east,clubs)>0    ? 1 : 0
ePP = eSpadePP + eHeartPP + eDiamPP + eClubPP

tpEast = hcp(east) + eSSP - ePP

## Calculate TP for South
sSpadeSSP = spades(south)<3   ? 3-spades(south)   : 0
sHeartSSP = hearts(south)<3   ? 3-hearts(south)   : 0
sDiamSSP  = diamonds(south)<3 ? 3-diamonds(south) : 0
sClubSSP  = clubs(south)<3    ? 3-clubs(south)    : 0
sSSP = sSpadeSSP + sHeartSSP + sDiamSSP + sClubSSP

sSpadePP = spades(south)<3   and hcp(south,spades)>0   ? 1 : 0
sHeartPP = hearts(south)<3   and hcp(south,hearts)>0   ? 1 : 0
sDiamPP  = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
sClubPP  = clubs(south)<3    and hcp(south,clubs)>0    ? 1 : 0
sPP = sSpadePP + sHeartPP + sDiamPP + sClubPP

tpSouth = hcp(south) + sSSP - sPP

## Calculate TP for West
wSpadeSSP = spades(west)<3   ? 3-spades(west)   : 0
wHeartSSP = hearts(west)<3   ? 3-hearts(west)   : 0
wDiamSSP  = diamonds(west)<3 ? 3-diamonds(west) : 0
wClubSSP  = clubs(west)<3    ? 3-clubs(west)    : 0
wSSP = wSpadeSSP + wHeartSSP + wDiamSSP + wClubSSP

wSpadePP = spades(west)<3   and hcp(west,spades)>0   ? 1 : 0
wHeartPP = hearts(west)<3   and hcp(west,hearts)>0   ? 1 : 0
wDiamPP  = diamonds(west)<3 and hcp(west,diamonds)>0 ? 1 : 0
wClubPP  = clubs(west)<3    and hcp(west,clubs)>0    ? 1 : 0
wPP = wSpadePP + wHeartPP + wDiamPP + wClubPP

tpWest = hcp(west) + wSSP - wPP

## Partnership Totals
tpNS = tpNorth + tpSouth
tpEW = tpEast + tpWest

### End of Imported Script: TP
# Defines tpNorth, tpSouth, tpEast, tpWest

## GIB Suit Quality Definitions for East
## Based on https://doc.bridgebase.com/lobbynews/gib_descriptions.html

# 8421 Points per suit (A=8, K=4, Q=2, J=1)
e8421S = (hascard(east,AS)?8:0) + (hascard(east,KS)?4:0) + (hascard(east,QS)?2:0) + (hascard(east,JS)?1:0)
e8421H = (hascard(east,AH)?8:0) + (hascard(east,KH)?4:0) + (hascard(east,QH)?2:0) + (hascard(east,JH)?1:0)
e8421D = (hascard(east,AD)?8:0) + (hascard(east,KD)?4:0) + (hascard(east,QD)?2:0) + (hascard(east,JD)?1:0)
e8421C = (hascard(east,AC)?8:0) + (hascard(east,KC)?4:0) + (hascard(east,QC)?2:0) + (hascard(east,JC)?1:0)

# 3-card: exactly 3 cards
e3S = spades(east)==3
e3H = hearts(east)==3
e3D = diamonds(east)==3
e3C = clubs(east)==3

# 4-card: exactly 4 cards
e4S = spades(east)==4
e4H = hearts(east)==4
e4D = diamonds(east)==4
e4C = clubs(east)==4

# Biddable: 5+ cards, OR 4 cards with (3+ of top 5 honors OR more than 4 8421 points)
eBidS = spades(east)>4 or (spades(east)==4 and (top5(east,spades)>2 or e8421S>4))
eBidH = hearts(east)>4 or (hearts(east)==4 and (top5(east,hearts)>2 or e8421H>4))
eBidD = diamonds(east)>4 or (diamonds(east)==4 and (top5(east,diamonds)>2 or e8421D>4))
eBidC = clubs(east)>4 or (clubs(east)==4 and (top5(east,clubs)>2 or e8421C>4))

# Rebiddable: biddable + 1 card (6+ or 5 with quality)
eRebidS = spades(east)>5 or (spades(east)==5 and (top5(east,spades)>2 or e8421S>4))
eRebidH = hearts(east)>5 or (hearts(east)==5 and (top5(east,hearts)>2 or e8421H>4))
eRebidD = diamonds(east)>5 or (diamonds(east)==5 and (top5(east,diamonds)>2 or e8421D>4))
eRebidC = clubs(east)>5 or (clubs(east)==5 and (top5(east,clubs)>2 or e8421C>4))

# Twice Rebiddable: rebiddable + 1 card (7+ or 6 with quality)
e2RebidS = spades(east)>6 or (spades(east)==6 and (top5(east,spades)>2 or e8421S>4))
e2RebidH = hearts(east)>6 or (hearts(east)==6 and (top5(east,hearts)>2 or e8421H>4))
e2RebidD = diamonds(east)>6 or (diamonds(east)==6 and (top5(east,diamonds)>2 or e8421D>4))
e2RebidC = clubs(east)>6 or (clubs(east)==6 and (top5(east,clubs)>2 or e8421C>4))

# Strong Rebiddable: twice rebiddable with 4 of 5 honors or AKQ
eStrongS = e2RebidS and (top5(east,spades)>3 or top3(east,spades)==3)
eStrongH = e2RebidH and (top5(east,hearts)>3 or top3(east,hearts)==3)
eStrongD = e2RebidD and (top5(east,diamonds)>3 or top3(east,diamonds)==3)
eStrongC = e2RebidC and (top5(east,clubs)>3 or top3(east,clubs)==3)

# Solid 6-card: AKQTxx or AKQJxx (6 cards with AKQ and T or J)
eSolid6S = spades(east)==6 and top3(east,spades)==3 and (hascard(east,TS) or hascard(east,JS))
eSolid6H = hearts(east)==6 and top3(east,hearts)==3 and (hascard(east,TH) or hascard(east,JH))
eSolid6D = diamonds(east)==6 and top3(east,diamonds)==3 and (hascard(east,TD) or hascard(east,JD))
eSolid6C = clubs(east)==6 and top3(east,clubs)==3 and (hascard(east,TC) or hascard(east,JC))

# Solid 7-card: AKQxxxx (7 cards with AKQ)
eSolid7S = spades(east)==7 and top3(east,spades)==3
eSolid7H = hearts(east)==7 and top3(east,hearts)==3
eSolid7D = diamonds(east)==7 and top3(east,diamonds)==3
eSolid7C = clubs(east)==7 and top3(east,clubs)==3

# Solid 8-card: AKJxxxxx or AKxxxxxxx or better (8 cards with AK and J, or 8+ with AK)
eSolid8S = spades(east)>7 and hascard(east,AS) and hascard(east,KS) and (hascard(east,JS) or spades(east)>8)
eSolid8H = hearts(east)>7 and hascard(east,AH) and hascard(east,KH) and (hascard(east,JH) or hearts(east)>8)
eSolid8D = diamonds(east)>7 and hascard(east,AD) and hascard(east,KD) and (hascard(east,JD) or diamonds(east)>8)
eSolid8C = clubs(east)>7 and hascard(east,AC) and hascard(east,KC) and (hascard(east,JC) or clubs(east)>8)

## End of GIB Suit Quality Definitions
# Defines e8421S/H/D/C, eRebidS/H/D/C, e2Rebid/S/H/D/C, and eStrongS/H/D/C

# Define West's Cappellitti actions
west4x     = shape(west,any 9xxx +any 8xxx +any 7xxx -any 6xxx -any 5xxx -any 4xxx)                     and tpWest>2
west2C     = shape(west,any 9xxx +any 8xxx +any 7xxx +any 6xxx -any 5xxx)                               and tpWest>9
west2D     = spades(west)>3 and hearts(west)>3 and (spades(west) + hearts(west))>8                      and tpWest>10
west2other = shape(west,any 76xx +any 75xx +any 74xx +any 66xx +any 65xx +any 64xx+ any 55xx +any 54xx) and tpWest>10
west3x     = shape(west,any 9xxx +any 8xxx +any 7xxx +any 6xxx -any 5xxx -any 4xxx)                     and tpWest>12 and tpWest<17
minorCards = clubs(west) + diamonds(west)
west2N     = clubs(west)>4 and diamonds(west)>4                                                         and (tpWest + minorCards - 9)>14
westX      = hcp(west)>15
calmWest   = not (west4x or west2C or west2D or west2other or west3x or west2N or westX)

# Boost quality of North's major
nQS = spades(north)==4 and hcp(north,spades)>2
nQH = hearts(north)==4 and hcp(north,hearts)>2
nQ  = nQS or nQH

# Define splinter
splinter = shape(north, 4405 + any 4441 -xxx1) and tpNorth>11

# Define mSS and mST
mss = diamonds(north)>3 and clubs(north)>3 and (diamonds(north) + clubs(north))>8 and tpNorth>9
mst = (clubs(north)>5 or diamonds(north)>5) and not mss

# Define various 2C bids -- GIB does not play Drop Dead or Crawling Stayman

# NOTE:  GIB does NOT bid Stayman with 4-3-3-3 and 8 HCP
smolen      = shape(north,54xx +45xx)         and tpNorth>9
majorInvite = shape(north,54xx) and tpNorth>8 and tpNorth<11 and hcp(north)<10    // 2C & correct D to S 
stayman     = shape(north,44xx +4xxx +x4xx -any 9xxx -any 8xxx -any 7xxx -any 6xxx -5xxx -x5xx) and hcp(north)>7 and tpNorth>8 and nQ and not (majorInvite or smolen or splinter)

# Define Texas, Jacoby, & Splinter responses
texas  = (spades(north)>5 or hearts(north)>5) and tpNorth>9
jacoby = (spades(north)>4 or hearts(north)>4) and not (texas or smolen or majorInvite)

n2Clubs = (stayman or smolen or majorInvite) and not (splinter or mss or mst or texas or jacoby)

# Define East's twice biddable suits: (5 cards or 4 cards with (3 of top 5 or more than 4  8421 points)) + 2 cards
# Only quality suits count for 2-level bids

# East has one 6+card D, H, or S suit, less than 5 clubs (avoid X), no second suit longer than 4-cards, a singleton or void
eShape = shape(east, any 9xxx +any 8xxx+ any 7xxx +any 6xxx -any 85xx -any 76xx -any 66xx -any 65xx) and shape(east, any 1xxx +any 0xxx) and (clubs(south)<5 or clubs(south)>6)

eRebid  = (eRebidS  or eRebidH  or eRebidD  or eRebidC)  and tpEast>14
e2Rebid = (e2RebidS or e2RebidH or e2RebidD or e2RebidC) and tpEast>13
eStrong = (eStrongS or eStrongH or eStrongD or eStrongC) and tpEast>12

eSuits  = eRebid or e2Rebid or eStrong

eCforX = eRebidC
eTPforX = hcp(east)>5 and tpEast>6

eX6 = tpEast>6 and tpEast<11 and e2RebidC and top4(east,clubs)>1 and top5(east,clubs)>2 
eX5 = tpEast>6               and eRebidC  and top4(east,clubs)>1 and top5(east,clubs)>2 and hcp(east,clubs)>5

# Define East's X
eX = (eX6 or eX5) and shape(east, xxxx -any 9xxx -any 8xxx -any 76xx -any 75xx -any 74xx -any 66xx -any 65xx -any 64xx -any 55xx -any 54xx)

# Define East's bids over 2C -- Twice Bdiddable with extra strength
e2Range = tpEast>10 and tpEast<17

e2D = diamonds(east)==6 and e2Range and eSuits
e2H = hearts(east)==6   and e2Range and eSuits
e2S = spades(east)==6   and e2Range and eSuits
e2B = e2D or e2H or e2S

# 3- and 4-level suits require length and may have suit-quality
e3Range = hcp(east)>9 and tpEast>10 and tpEast<17

e3C = clubs(east)>6    and eSuits and e3Range
e3D = diamonds(east)>6 and eSuits and e3Range and not e2D
e3H = hearts(east)>6   and eSuits and e3Range and not e2H
e3S = spades(east)>6   and eSuits and e3Range and not e2S
e3B = e3C or e3D or e3H or e3S

# Define East's 4-level weak bids
e4wRange = hcp(east)<10

e4Cw = clubs(east)>8    and e4wRange
e4Dw = diamonds(east)>7 and e4wRange and not (e2D or e3D)
e4Hw = hearts(east)>7   and e4wRange and not (e2H or e3H)
e4Sw = spades(east)>7   and e4wRange and not (e2S or e3S)
e4Bw = e4Dw or e4Hw or e4Sw

# Define East's 4-level strong bids
e4sRange = hcp(east)>9
e4Cs = eSolid8C and not e3C
e4Ds = eSolid8D and not (e2D or e3D)
e4Hs = eSolid8H and not (e2H or e3H)
e4Ss = eSolid8S and not (e2S or e3S)
e4Bs = e4Ds or e4Hs or e4Ss

e4B = e4Bw or e4Bs

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###

levX  = eX  and keep11
lev2B = e2B and keep33
lev3B = e3B and keep44
lev4B = e4B and keep
levelTheDeal = lev2B or lev3B or lev4B or levX

# Now do it
gibNT and tpSouth<19 and calmWest and n2Clubs and (eShape or eX) and (e2B or e3B or e4B or eX)
and levelTheDeal

action printoneline,
average "smolen      " 100 * smolen,
average "stayman     " 100 * stayman,
average "major invite" 100 * majorInvite,
average "Any 2C-Resp-" 100 * (smolen or stayman or majorInvite),
average "X           " 100 * eX,
average "2D          " 100 * e2D,
average "2H          " 100 * e2H,
average "2S          " 100 * e2S,
average "Any 2-Bid---" 100 * e2B,
average "3C          " 100 * e3C,
average "3D          " 100 * e3D,
average "3H          " 100 * e3H,
average "3S          " 100 * e3S,
average "Any 3-Bid---" 100 * e3B,
average "4 weak      " 100 * e4Bw,
average "4 strong    " 100 * e4Bs,
average "Any 4-Bid---" 100 * (e4Bw or e4Bs),

# alias: EqualLevConv
# button-text: Equal Level Conv
# scenario-title: --- Equal Level Conversion.  East opens 1 of a major.  South has 4 of the other major and 5+ !D.
# gib-works: false
# bba-works: true
# auction-filter: Auction.....\n1[HS] +(2D|X) +Pass
# convention-card-ns: 21GF-DEFAULT
# convention-card-ew: 21GF-GIB

/*@chat
--- Equal Level Conversion.  East opens 1 of a major.  South has 4 of the other major and 5+ !D.
South X's with the intention of correcting North's 2!C bid to !D, showing nothing extra.
https://www.bridgebum.com/equal_level_conversion.php
@chat*/

# Equal Level Conversion
produce 500
dealer east

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####
# Defines calmWest

# Predict East's Opening Suit
s = spades(east)
h = hearts(east)
d = diamonds(east)
c = clubs(east)
oS = s>4 and s>=h and s>=d and s>=c
oH = not oS and h>4 and h>=d and h>=c
oD = not (oS or oH) and ((d>3 and d>=c) or c<3)
oC = not (oS or oH or oD)

# East opens 1H or 1S and South is short in openerâ€™s suit w/4om and 5+D, west does NOT have support for east
He = oH and hearts(west)<3 and hearts(south)<3 and spades(south)==4 and diamonds(south)>4 and clubs(south)<4 
Se = oS and spades(west)<3 and spades(south)<3 and hearts(south)==4 and diamonds(south)>4 and clubs(south)<4
eOpens = hcp(east)>11 and hcp(east)<15 and (He or Se)

oMajorCards  = He ? spades(north) + spades(south) : hearts(north) + hearts(south)
diamondCards = diamonds(north) + diamonds(south)
clubCards    = clubs(north) + clubs(south)

# South has 5+ Diamonds and 4-cards in the other major and less than 4-clubs
sDoubles = hcp(south)>11 and hcp(south)<15 and shape(south,4x5x +4x6x +4x7x +4x8x +4x9x +x45x +x46x +x47x +x48x +x49x)

# Now do it
eOpens and sDoubles and calmWest

action printoneline,
average "4 spades & 5+ diamonds " 100 * shape(south,4xxx),
average "4x5x" 100 * shape(south,4x5x),
average "4x6x" 100 * shape(south,4x6x), 
average "4x7x" 100 * shape(south,4x7x), 
average "4x8x" 100 * shape(south,4x8x), 
average "4x9x" 100 * shape(south,4x9x), 
average " 4 hearts & 5+ diamonds" 100 * shape(south,x4xx),
average "x45x" 100 * shape(south,x45x), 
average "x46x" 100 * shape(south,x46x), 
average "x47x" 100 * shape(south,x47x), 
average "x48x" 100 * shape(south,x48x), 
average "x49x" 100 * shape(south,x49x),
average "  all shapes           " 100 * shape(south,4xxx +x4xx),

average "M Fit      " 100 * (oMajorCards>7),
average "D only     " 100 * (oMajorCards<8 and diamondCards>7),
average "neither    " 100 * (oMajorCards<8 and diamondCards<8),

# alias: ExitTransfers
# button-text: Exit Transfers
# scenario-title: --- Exit Transfers are usually runouts after the opponents Double our 1N opener.
# gib-works: false
# bba-works: true
# auction-filter: Auction.....\n1NT? X.* Pass .
# convention-card-ns: 21GF-Cappelletti
# convention-card-ew: 21GF-GIB

/*@chat
--- Exit Transfers are usually runouts after the opponents Double our 1N opener.
• Pass - Requires partner to make an artificial redouble.  After partner's forced XX, 
continuations are like DONT -- 2!C is !C & Higher, 2!D is !D & Higher, and 2!H is !H & !S.
• XX - 5+!C
• 2!C - 5+!D
• 2!D - 5+!H
• 2!H - 5+!S
https://www.bridgebum.com/exit_transfers.php
@chat*/

# Opponent doubles 1NT
dealer south

##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major or with 4 spades
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422-5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# defines gibNT

# Define good suits with a likely outside entry
GS = spades(west)>4 and top5(west,spades)>3 and (top2(west,hearts)==1 or top2(west,diamonds)==1 or top2(west,clubs)==1)
GH = hearts(west)>4 and top5(west,hearts)>3 and (top2(west,diamonds)==1 or top2(west,clubs)==1 or top2(west,spades)==1)
GD = diamonds(west)>4 and top5(west,diamonds)>3 and (top2(west,spades)==1 or top2(west,hearts)==1 or top2(west,clubs)==1)
GC = clubs(west)>4 and top5(west,clubs)>3 and (top2(west,spades)==1 or top2(west,hearts)==1 or top2(west,diamonds)==1) 

# West Doubles
wX = (shape(west, any 4333 +any 4432 +any 5332) or (GC or GD or GH or GS)) and hcp(west)>14

oneSuit = shape(north, any 6322+any 6331+any 5332) and hcp(north)<7
twoSuit = shape(north, any 54xx+any 44xx) and hcp(north)<7
runout  = (oneSuit or twoSuit)
aHand   = hcp(north)>6
nActs   = runout or aHand

gibNT and wX and nActs

action printoneline,
average "oneSuit   " 100 * oneSuit,
average "twoSuit   " 100 * twoSuit,
average "a hand    " 100 * aHand,
average "hcp north " hcp(north),
average "hcp west  " hcp(west),

# alias: OppsTwoSuitedOvercalls
# button-text: 2-Suit Overcalls
# scenario-title: --- Opponents 2-Suit Overcalls
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\n1[CDHS] +2[CDHSN] +
# convention-card-ns: 21GF-DEFAULT
# convention-card-ew: 21GF-GIB

/*@chat
--- Opponents 2-Suit Overcalls
double is penalty oriented
raise is natural (about 7-10)
a jump raise is preemptive
bidding new (unshown) suit is natural & not forcing
with 1 known opponent suit -- a cue bid is a limit+ raise
with 2 known opponent suits -- cue lower for lower & higher for higher
https://www.bridgebum.com/unusual_vs_unusual.php
https://www.bridgebum.com/defense_to_michaels.php
@chat*/

# Opponent's Two-Suited Overcalls
dealer south
produce 1000


##### Imported Script -- Predict Opening 1-Bid South #####
# 01/19/2026 -- Changed to use TP and separate ranges for C, D, and majors
# 03/30/2025 -- Changed to use gibNT

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422 -5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

## Calculate TP for South -- NOTE:  This uses different variable names than TP to avoid conflicts when both are used.
# Calculate South's Short-Suit Penalty Points
ssp = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
shp = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sdp = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
scp = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv01 = shape(south, any 0xxx) ? 3 : 0
ss01 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss02 = shape(south, any 11xx) ? 2 : 0
sd01 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd02 = shape(south, any 22xx) ? 1 : 0
sd03 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv01+ss01+ss02+sd01+sd02+sd03 -ssp-shp-sdp-scp

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22

# Define Gambling 3N
g3Shape = shape(south,any 9xxx +any 8xxx +any 7xxx -any 4xxx -any 5xxx -any 6xxx)
threeNT = (top3(south,clubs)==3 and clubs(south)>6) or (top3(south,diamonds)==3 and diamonds(south)>6) and hcp(south)<12 and g3Shape

# Define Game Forcing 2C
case1 = hcp(south)>18 and sTP>21
case2 = hcp(south)>18 and sTP>19 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and sTP>19 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3)

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT

# Define quality and length lower limits for 10 HCP openers

q2of3 = (top3(south,spades)>1 or top3(south,hearts)>1) and controls(south)>3
lgt5  = (spades(south)>4 or hearts(south)>4) and shape(south,any 7xxx +any 64xx +any 55xx +any 1xxx+any 0xxx -any 8xxx -any 54xx) and sTP>(hcp(south) +2)

# Predict South's Opening suit
# What's the longest suit?
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)

## GIB has different requirements for 1H/S, 1D, and 1C openings 
#Define range for 1 H/S
lS = s>4 and s>=h and s>=d and s>=c
lH = h>4 and h>s and h>=d and h>=c
lM = lS or lH

hsOK1 = lM and hcp(south)>11
hsOK2 = lM and hcp(south)>10 and sTP>12 and not hsOK1
hsOK3 = lM and hcp(south)>9  and lgt5 and q2of3 and not (hsOK1 or hsOK2)
hsRange = (hsOK1 or hsOK2 or hsOK3) and not P2

# Define 1D Range
dOK1 = hcp(south)>11 and (diamonds(south)>3 or shape(south, 4432))
dOK2 = hcp(south)>9  and sTP>12 and diamonds(south)>5 and not dOK1
dRange = (dOK1 or dOK2) and not P2

# Define 1C Range
cOK1 = hcp(south)>10 and sTP>12 and spades(south)==4
cOK2 = hcp(south)>9  and sTP>12 and clubs(south)>5 and not cOK1
cOK3 = hcp(south)>11 and clubs(south)>2            and not (cOK1 or cOK2)
cRange = (cOK1 or cOK2 or cOK3) and clubs(south)>2 and clubs(south)>diamonds(south) and not P2

oS = s>4 and s>=h and s>=d and s>=c and hsRange
oH = not oS and h>4 and h>=d and h>=c and hsRange
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and dRange
oC = not (oS or oH or oD) and cRange
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####

# Opponents 2-suited overcalls

# Define good suits
GoodS = spades(west)>4 and top5(west,spades)>2
GoodH = hearts(west)>4 and top5(west,hearts)>2
GoodD = diamonds(west)>4 and top5(west,diamonds)>2
GoodC = clubs(west)>4 and top5(west,clubs)>2

# Intervener is 2-suited -- all require at least QJT; so, at least 6 HCP for the intervener 
SH = (GoodS and GoodH)
SD = (GoodS and GoodD)
SC = (GoodS and GoodC)
HD = (GoodH and GoodD)
HC = (GoodH and GoodC)
DC = (GoodD and GoodC)

# Intervener's 2-Suits -- NOT opener's suit
oneS = oS and (HD or HC or DC)  // Michaels, Michaels/Top&Bottom, Unusual NT
oneH = oH and (SD or SC or DC)  // Michaels, Michaels/Top&Bottom, Unusual NT
oneD = oD and (SH or SC or HC)  // Michaels, Top&Bottom, Unusual NT
oneC = oC and (SH or SD or HD)  // Michaels, Top&Bottom, Unusual NT

oSuit = (oneS or oneH or oneD or oneC) and shape(south,any xxxx-8xxx-7xxx-6xxx)
oRange = hcp(south)>11 and hcp(south)<22

# West is weak or strong, 6-11 or 15-19
iWeak   = losers(west)<8 and hcp(west)>5  and hcp(west)<12
iStrong = losers(west)<5 and hcp(west)>14 and hcp(west)<20
iRange = iWeak or iStrong

# Responder's minimum HCP
rRange = hcp(north)>6

### Now do it
oSuit and oRange and iRange and rRange

# You might play with chosing west's suits
#and SC or SD  // If you just want the problematic ones

#  Michaels Cue Bid (MCB), Unusual 2NT (U2N), Top & Bottom Cue Bid (T&B)

action printoneline,
average "1S         " 100 * (oneS and (HD or HC or DC)),
average "   (2S MCB     = HD) " 100 * (oneS and HD),
average "   (2S MCB/T&B = HC) " 100 * (oneS and HC),
average "   (2N U2N     = DC) " 100 * (oneS and DC),
average "1H         " 100 * (oneH and (SD or SC or DC)),
average "   (2H MCB     = SD) " 100 * (oneH and SD),
average "   (2H MCB/T&B = SC) " 100 * (oneH and SC),
average "   (2N U2N     = DC) " 100 * (oneH and DC),
average "1D         " 100 * (oneD and (SH or HC or SC)),
average "   (2D MCB     = SH) " 100 * (oneD and SH),
average "   (2N 2NT     = HC) " 100 * (oneD and HC),
average "   (2D T&B     = SC) " 100 * (oneD and SC),
average "1C         " 100 * (oneC and (SH or HD or SD)),
average "   (2C MCB     = SH) " 100 * (oneC and SH),
average "   (2N U2N     = DC) " 100 * (oneC and HD),
average "   (2C T&B     = SD) " 100 * (oneC and SD),

frequency "Opener HCP"     (hcp(south),12,21),
frequency "Intervener HCP" (hcp(west),6,19),
frequency "Responder HCP"  (hcp(north),6,19),

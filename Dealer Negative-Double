Script,NegativeDouble
setDealerCode(
`

# Negative Double

dealer south
produce 40

## Imported Script -- Predict Opening Bid ##

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and top4(south,spades)==1 and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and top4(south,hearts)==1 and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)==1 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and top4(south,clubs)==1 and not hascard(south,AC) ? 1 : 0
ntDownGrade = S2H + H2H + D2H + C2H

# Define notrump points for south (ntPoints)
ntPoints = hcp(south) + lengthPoints - ntDownGrade

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

# Define robot notrump shape and exclude any 5card 
ntShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422 -5xxx -x5xx)

# Define ntPoint ranges
oneNT   = ntShape and ntPoints>14 and ntPoints<18
twoNT   = ntShape and ntPoints>19 and ntPoints<22
threeNT = ntShape and ntPoints>25 and ntPoints<28
weakNT  = ntShape and ntPoints>10 and ntPoints<15

# Define Game Forcing 2C
case1 = hcp(south)>22
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT // or weakNT  weakNT will be duplicated diamond and club openings

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade = oS
oneHeart = oH
oneDiamond = oD                                                              // duplicates weakNT
oneClub = oC                                                                 // duplicates weakNT

### End of Predict-Opening-Bid ###

# Predict West's overcall
sW = spades(west)
hW = hearts(west)
dW = diamonds(west)
cW = clubs(west)
wS = sW>=hW and sW>=dW and sW>=cW
wH = not wS and hW>=dW and hW>=cW
wD = not (wS or wH) and (dW>=cW or cW==3)
wC = not (wS or wH or wD)

# South Opens
sOpens = (oS or oH or oD or oC) and hcp(south)>11

# West overcalls
wOvercalls = (wD or wH or wS or wC) and shape(west,any 6xxx+any 5xxx-any 65xx -any 55xx)
wP1 = hcp(west)>5
wP2 = hcp(west)>10

# North doubles
nM = shape(north,44xx)
nH = shape(north,x4xx)
nS = shape(north,4xxx)
x1 = oC and wD         ? nM and wP1 : 0
x2 = oC and wH         ? nS and wP1 : 0
x3 = oC and wS         ? nH and wP1 : 0
x4 = oD and wH         ? nS and wP1 : 0
x5 = oD and wS         ? nH and wP1 : 0
x6 = oD and wC         ? nM and wP2 : 0
x7 = oH and (wC or wD) ? nS and wP2 : 0
x8 = oS and (wC or wD) ? nH and wP2 : 0

p1 = hcp(north)>5
p2 = hcp(north)>8
p3 = hcp(north)>11

nDoubles = (((x1 or x2) and p1) or ((x3 or x4 or x5) and p2) or ((x6 or x7 or x8) and p3))

sOpens and wOvercalls and nDoubles

action
printoneline,
average "x1 " 100 * x1,
average "x2 " 100 * x2,
average "x3 " 100 * x3,
average "x4 " 100 * x4,
average "x5 " 100 * x5,
average "x6 " 100 * x6,
average "x7 " 100 * x7,
average "x8 " 100 * x8,

`)
Script
Button,Negative Double,\n\
--- Negative Double\n\
After x (y)ï¼ŒX is a Negative Double.  It's for takeout. At a lower level\n\
it promises 4-Cards in each unbid major suit and should have some backup\n\
plan if partner doesn't have a major -- maybe a tolerance for opener's suit.\n\
https://www.larryco.com/bridge-articles/negative-doubles\n\
%NegativeDouble%

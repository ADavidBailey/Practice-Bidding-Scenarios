# alias: DropDeadCrawling
# button-text: Drop Dead/Crawling
# scenario-title: --- Drop Dead/Crawling Stayman
# gib-works: false
# bba-works: true
# auction-filter: Auction.....\n1NT? +Pass +2C +
# convention-card-ns: 21GF-DEFAULT
# convention-card-ew: 21GF-GIB

/*@chat
--- Drop Dead/Crawling Stayman
2!C asks the opener for a 4-card major.  Responses are as follow...
• 2!D - No 4-card major
• 2!H - 4+!H, may also have 4!S
• 2!S - 4+!S and denies 4!H
Normally, Stayman requires 8+HCP; but, Drop Dead Stayman and Crawling Stayman are exceptions.
With Drop Dead Stayman, responder will pass anything that opener bids.
With Crawling Stayman, responder will correct 2!D to 2!H showing a weak 3-suited hand w/short !D.
https://www.bridgebum.com/stayman.php
https://www.bridgebum.com/crawling_stayman.php
@chat*/

# Stayman
dealer south

# 1 Notrump Opening
##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major or with 4 spades
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422-5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# Defines gibNT
##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####
# Defines calmWest

dropDeadStayman = shape(north,4441+4450) and hcp(north)<8
crawlingStayman = shape(north,4414+4405) and hcp(north)<8

gibNT and (dropDeadStayman or crawlingStayman) and calmWest

action printoneline,
average "dropDead    " 100 * dropDeadStayman,
average "crawling    " 100 * crawlingStayman,

# button-text: NT Ladder (Lev)
# scenario-title: --- NT Ladder (Lev)
# auction-filter: Auction.....\n[1-7]
# NT_Ladder
dealer south

# Balanced shape for South
balanced = shape(south, any 4333 + any 4432 + any 5332)

# Define HCP ranges
hcp12_14 = hcp(south) >= 12 and hcp(south) <= 14
hcp15_17 = hcp(south) >= 15 and hcp(south) <= 17
hcp18_19 = hcp(south) >= 18 and hcp(south) <= 19
hcp20_21 = hcp(south) >= 20 and hcp(south) <= 21
hcp22_24 = hcp(south) >= 22 and hcp(south) <= 24

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###

# Level the ranges (12-14 is most common, 22-24 is rarest)
# Tuned to balance around the rarest range (22-24)
level12_14 = hcp12_14 and keep03
level15_17 = hcp15_17 and keep045
level18_19 = hcp18_19 and keep19
level20_21 = hcp20_21 and keep56
level22_24 = hcp22_24 and keep
levelTheDeal = level12_14 or level15_17 or level18_19 or level20_21 or level22_24

##### Now do it #####
condition
balanced and hcp(south) >= 12 and hcp(south) <= 24
and levelTheDeal

action
average "1. 12-14 HCP" 100 * hcp12_14,
average "2. 15-17 HCP" 100 * hcp15_17,
average "3. 18-19 HCP" 100 * hcp18_19,
average "4. 20-21 HCP" 100 * hcp20_21,
average "5. 22-24 HCP" 100 * hcp22_24,

frequency "HCP South " (hcp(south), 12, 24),

printpbn

# button-text: Anything Goes
# scenario-title: South Opens...
# Anything_Goes
dealer south


/*
convention-card:
auction-filter: Auction.....\n(?!P)
*/

# Anything Goes

##### Imported Script -- Predict Opening 1-Bid #####

# Changed to use gibNT 3/30/2025

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422 -5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22
threeNT = TwoNtShape and hcp(south)>24 and hcp(south)<28

# Define Game Forcing 2C
case1 = hcp(south)>21
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####

twoD = shape(south,xx6x - any 7xxx -any 66xx -any 65xx) and hcp(south)<11
twoH = shape(south,x6xx - any 7xxx -any 66xx -any 65xx) and hcp(south)<11
twoS = shape(south,6xxx - any 7xxx -any 66xx -any 65xx) and hcp(south)<11

threeC = shape(south,xxx7 + xxx8 + xxx9 -any 94xx -any 85xx -any 76xx -any 75xx) and hcp(south)<11
threeD = shape(south,xx7x + xx8x + xx9x -any 94xx -any 85xx -any 76xx -any 75xx) and hcp(south)<11
threeH = shape(south,x7xx + x8xx + x9xx -any 94xx -any 85xx -any 76xx -any 75xx) and hcp(south)<11
threeS = shape(south,7xxx + 8xxx + 9xxx -any 94xx -any 85xx -any 76xx -any 75xx) and hcp(south)<11

1


action
average "1C    " 100 * oC,
average "1D    " 100 * oD,
average "1H    " 100 * oH,
average "1S    " 100 * oS,
average "1NT   " 100 * gibNT,
average "2C    " 100 * gameForce2C,
average "2D    " 100 * twoD,
average "2H    " 100 * twoH,
average "2S    " 100 * twoS,
average "2NT   " 100 * twoNT,
average "3C    " 100 * threeC,
average "3D    " 100 * threeD,
average "3H    " 100 * threeH,
average "3S    " 100 * threeS,
average "3NT   " 100 * threeNT,



printpbn

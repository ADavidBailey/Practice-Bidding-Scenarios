# button-text: GIB OC w/4-cards
# scenario-title: GIB overcalls in a 4-card suit
# GIB_Overcall_with_4
dealer south


/*
South opens 1C, 1D, or 1H and West overcalls at the 1-level in a 4-card suit.

convention-card: 21GF-GIB
auction-filter: Auction.....\n1[CDH]

*/


##### Imported Script -- Predict Opening 1-Bid #####

# Changed to use gibNT 3/30/2025

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422 -5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22
threeNT = TwoNtShape and hcp(south)>24 and hcp(south)<28

# Define Game Forcing 2C
case1 = hcp(south)>21
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
# Defines oneClub, oneDiamond, and oneHeart
##### Imported Script - Calculate Total Points for West #####

## Calculate TP for West
# Calculate West's Short-Suit Penalty Points
wSP = spades(west)<3   and hcp(west,spades)>0   ? 1 : 0
wHP = hearts(west)<3   and hcp(west,hearts)>0   ? 1 : 0
wDP = diamonds(west)<3 and hcp(west,diamonds)>0 ? 1 : 0
wCP = clubs(west)<3    and hcp(west,clubs)>0    ? 1 : 0

# Calculate West's Short-Suit Points
wv1 = shape(west, any 0xxx) ? 3 : 0 // allow for 1 void
ws1 = shape(west, any 1xxx) ? 2 : 0 // allow for 2 singletons
ws2 = shape(west, any 11xx) ? 2 : 0
wd1 = shape(west, any 2xxx) ? 1 : 0 // allow for 3 doubletons
wd2 = shape(west, any 22xx) ? 1 : 0
wd3 = shape(west, any 222x) ? 1 : 0

# Calculate West's Total Points
wTP = hcp(west) + wv1+ws1+ws2+wd1+wd2+wd3 -wSP-wHP-wDP-wCP

### End of Calculate TP for West ###
# Defines wTP

# Define suits suitable for 4-card overcall
wAS = hascard(west,AS)
wKS = hascard(west,KS)
wQS = hascard(west,QS)
wJS = hascard(west,JS)
wTS = hascard(west,TS)
w9S = hascard(west,9S)

wAH = hascard(west,AH)
wKH = hascard(west,KH)
wQH = hascard(west,QH)
wJH = hascard(west,JH)
wTH = hascard(west,TH)
w9H = hascard(west,9H)

wAD = hascard(west,AD)
wKD = hascard(west,KD)
wQD = hascard(west,QD)
wJD = hascard(west,JD)
wTD = hascard(west,TD)
w9D = hascard(west,9D)

# Define West's possible overcall suits: AKxx，AQJx，AQTx，AQ9x，AJT9，KQJx，or KQTx
wS4 = ((wAS and wKS) or (wAS and wQS and (wJS or wTS or w9S)) or (wAS and wJS and wTS and w9S) or (wKS and wQS and (wJS or wTS))) and shape(west,4xxx)
wH4 = ((wAH and wKH) or (wAH and wQH and (wJH or wTH or w9H)) or (wAH and wJH and wTH and w9H) or (wKH and wQH and (wJH or wTH))) and shape(west,x4xx)
wD4 = ((wAD and wKD) or (wAD and wQD and (wJD or wTD or w9D)) or (wAD and wJD and wTD and w9D) or (wKD and wQD and (wJD or wTD))) and shape(west,xx4x)
wss = (wS4 and wH4) or (wS4 and wD4) or (wH4 or wD4) 

# Avoid Takeout Double -- 13/14 HCP and equal length in the majors
X = (hcp(west)==13 or hcp(west)==14) and shape(west,44xx+33xx)

wRange = wTP>=12 and wTP<=14 and not X

# Define West's overcalls
cOC = oneClub    and (wD4 or wH4 or wS4) and clubs(west)==3    //and hcp(west,clubs)==0
dOC = oneDiamond and (wH4 or wS4)        and diamonds(west)==3 //and hcp(west,diamonds)==0
hOC = oneHeart   and wS4                 and hearts(west)==3   //and hcp(west,hearts)==0
wOvercalls = (cOC or dOC or hOC) and wRange and shape(west,any 4432 +any 4333)

sOpens = hcp(south)>11 and hcp(south)<15

# Now do it.
sOpens and wOvercalls


action
average "1C-1D   " 100 * (cOC and wD4),
average "1C-1H   " 100 * (cOC and wH4),
average "1C-1S   " 100 * (cOC and wS4),
average "1D-1H   " 100 * (dOC and wH4),
average "1D-1S   " 100 * (dOC and wS4),
average "1H-1S   " 100 * (hOC and wS4),


printpbn

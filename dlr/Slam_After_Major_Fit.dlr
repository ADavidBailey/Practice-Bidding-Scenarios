# button-text: Major Suit Slam
# scenario-title: Slam w/Major Suit Fit -- N/S have 29+ HCP
# Slam_After_Major_Fit
dealer south


# convention-card:
# auction-filter: Auction.....\\n1[HS]

# Major_Suit_Fit

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

balEast   = shape(east,any 4333 +any 4432)
balWest   = shape(west,any 4333 +any 4432)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# Returns calmWest

##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, 4252+4225+2452+2425+2254+2245) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# Returns gibNT

heartFit = shape(south,x7xx + x6xx + x5xx -any 65xx -any 55xx) and hearts(north)>2
spadeFit = shape(south,7xxx + 6xxx + 5xxx -any 65xx -any 55xx) and spades(north)>2

# North opens 1 Major and South has a fit
oneM = (heartFit or spadeFit) and not gibNT

oneM and hcp(south)>11 and hcp(south)<20 and hcp(north)>5 and (hcp(north)+hcp(south))>28 and calmWest

action
average "hcp south  " 100 * hcp(south),
average "hcp north  " 100 * hcp(north),
average "hcp total  " 100 * (hcp(north)+hcp(south)),


printpbn

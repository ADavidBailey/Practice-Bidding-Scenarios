# button-text: Jacoby Super-Accept
# scenario-title: Jacoby Super-Accept
# Jacoby_Super-Accept
dealer south


# Import Define-1N and Calm Opponents
##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

balEast   = shape(east,any 4333 +any 4432)
balWest   = shape(west,any 4333 +any 4432)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####


# Define GIB NT
NT = hcp(south)>14 and hcp(south)<18 and shape(south,any 4333+any 4432+any 5332)

# Spade/Heart Support Points
ssP = hcp(north) + spades(north)-4
hsP = hcp(north) + hearts(north)-4

# Define North's Jacoby Transfer and South's Super Accept (not Smolen, allow 6-card suits, avoid Texas)
nSwq = spades(north)>4  and hearts(north)<4 and spades(south)>3 and (ssP<9 or (ssP>15 and ssP<17))   // 6 w/Weak or Quantitave
nHwq = hearts(north)>4  and spades(north)<4 and hearts(south)>3 and (hsP<9 or (hsP>15 and hsP<17))
nS5  = spades(north)==5 and hearts(north)<4 and spades(south)>3 and ssP>9                            // 5 w/any strength
nH5  = hearts(north)==5 and spades(north)<4 and hearts(south)>3 and hsP>9

# Now OPEN 1NT
NT and (nSwq or nHwq or nS5 or nH5) and calmWest

# Generate Statistics
action
average "Heart Fit" 100 *(nHwq or nH5),
average "Spade Fit" 100 * (nSwq or nS5), 


printpbn

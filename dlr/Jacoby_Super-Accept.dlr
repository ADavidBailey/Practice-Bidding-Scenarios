# button-text: Jacoby Super-Accept
# scenario-title: Jacoby Super-Accept
# Jacoby_Super-Accept
dealer south


# convention-card:
# auction-filter: Note.* acceptance after NT

# Import Define-1N and Calm Opponents
##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

#unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
#unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

#balEast   = shape(east,any 4333 +any 4432)
#balWest   = shape(west,any 4333 +any 4432)

balEast    = shape(east,any 4432+any 4333)
unbalEast  = not balEast

balWest    = shape(west,any 4432+any 4333)
unbalWest  = not balWest

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# Defines calmWest

### Imported: Define 1 Notrump Opening ###

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 or lp2 or lp3 or lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and   top4(south,spades)>0   and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and   top4(south,hearts)>0   and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)>0 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and    top4(south,clubs)>0    and not hascard(south,AC) ? 1 : 0
dgPoints = S2H + H2H + D2H + C2H

# Define notrump points for south (ntPoints)
ntPoints = lengthPoints>dgPoints ? hcp(south) + lengthPoints - dgPoints : hcp(south)

ntMin = ntPoints<16
ntAvg = ntPoints==16
ntMax = ntPoints>16

# Define 1NT -- these include hands that might have rebid problems if opened in a suit
NT = shape(south, 2452+2425+2245+2254+4522 +any 4333+any 4432+any 5332) and ntPoints>14 and ntPoints<18

### End of Imported 1 Notrump ###

# Defines NT

# Define North's Jacoby Transfer and South's Super Accept (not Smolen, allow 6-card suits, avoid Texas)
nSwq = shape(north,6xxx) and hearts(north)<4 and spades(south)>3 and hcp(north)<8  // avoid Texas
nHwq = shape(north,x6xx) and spades(north)<4 and hearts(south)>3 and hcp(north)<8
nS5  = shape(north,5xxx) and hearts(north)<4 and spades(south)>3
nH5  = shape(north,x5xx) and spades(north)<4 and hearts(south)>3

# Now OPEN 1NT
NT and (nSwq or nHwq or nS5 or nH5) and calmWest and shape(south,xxxx-any 4333) and hcp(south)==17

# Generate Statistics
action
average "Heart Fit" 100 *(nHwq or nH5),
average "Spade Fit" 100 * (nSwq or nS5),
average "any 5422" 100 * shape(north,any 5422),
average "not 5422" 100 * (not shape(north,any 5422)),

# button-text: Gerber by Responder (Lev)
# scenario-title: Gerber by Responder (Leveled)
# Gerber_By_Responder
dealer south


# auction-filter: Note....Gerber
#
# Gerber by Responder
# 

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
p = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and p<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, 4252+4225+2452+2425+2254+2245) and hcp(south)>14 and hcp(south)<17

# Define NT shape for other ranges
ntShape = shape(south, any 5422+any 5332+any 4432+any 4333)

# Define the ranges for NT shape hands -- the Notrump Ladder w/1 hcp short of slam
nt1214 = hcp(south)>=12 and hcp(south)<=14 and hcp(north)==21
gib1N  = (nt1 or nt2)                      and hcp(north)==17
nt1819 = hcp(south)>=18 and hcp(south)<=19 and hcp(north)==14
nt2021 = hcp(south)>=20 and hcp(south)<=21 and hcp(north)==12
nt2224 = hcp(south)>=22 and hcp(south)<=24 and hcp(north)==10
nt2527 = hcp(south)>=25 and hcp(south)<=27 and hcp(north)==7
nt28   = hcp(south)>=28                    and hcp(north)==4

# all other balanced hands with 12+ hcp
otherNT = (nt1214 or nt1819 or nt2021 or nt2224 or nt2527 or nt28) and ntShape
sOpens = gib1N or otherNT

# Responder has a hand without a 4+ card major and a good source of tricks and 1 HCP short of slam
nGC = clubs(north)>5    and top3(north,clubs)==3
nGD = diamonds(north)>5 and top3(north,diamonds)==3
nResponds = shape(north,xx6x+xxx6+any 5422+any 5332+any 4432+any 4333-5xxx-x5xx-4xxx-x4xx) and (nGC or nGD)

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###


# Level the deal
lev1214 = nt1214 and keep0
lev1517 = gib1N  and keep
lev1819 = nt1819 and keep
lev2021 = nt2021 and keep
lev2224 = nt2224 and keep0
lev2527 = nt2527 and keep0
lev28   = nt28   and keep0
levelTheDeal = lev1214 or lev1517 or lev1819 or lev2021 or lev2224 or lev2527 or lev28

# Now do it
sOpens and nResponds
and levelTheDeal

action
average "12-14  " 100 * nt1214,
average "15-17  " 100 * gib1N,
average "18-19  " 100 * nt1819,
average "20-21  " 100 * nt2021,
average "22-24  " 100 * nt2224,
average "25-27  " 100 * nt2527,
average "28     " 100 * nt28,


printpbn

# ./Dealer Ned-3-Level-Resp-to-1N

generate 10000000

produce 500

dealer south




# Import Define-1N and Leveling
### Imported: Define 1 Notrump Opening ###

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 or lp2 or lp3 or lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and   top4(south,spades)>0   and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and   top4(south,hearts)>0   and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)>0 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and    top4(south,clubs)>0    and not hascard(south,AC) ? 1 : 0
dgPoints = S2H + H2H + D2H + C2H

# Define notrump points for south (ntPoints)
ntPoints = lengthPoints>dgPoints ? hcp(south) + lengthPoints - dgPoints : hcp(south)

ntMin = ntPoints<16
ntAvg = ntPoints==16
ntMax = ntPoints>16

# Define 1NT -- these include hands that might have rebid problems if opened in a suit
NT = shape(south, 2452+2425+2245+2254+4522 +any 4333+any 4432+any 5332) and ntPoints>14 and ntPoints<18

### End of Imported 1 Notrump ###

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###
### See -Stats-Leveling for statistics on these values ###
### Example of how to generate Statistics for Leveling ###


# North has a good suit and slam interest

# Define Good suits -- 5+ cards with 2 of the top 3
gS = spades(north)>4 and top3(north,spades)>1
gH = hearts(north)>4 and top3(north,hearts)>1
gD = diamonds(north)>4 and top3(north,diamonds)>1
gC = clubs(north)>4 and top3(north,clubs)>1

nResponds = hcp(north)>15 and (gS or gH or gD or gC) and shape(north,any 65xx +any 64xx +any 55xx)

# Level it
levMin = ntMin and keep30
levAvg = ntAvg and keep67
levMax = ntMax and keep
levelTheDeal = levMin or levAvg or levMax

### Ready for Leveling

# Now OPEN 1NT, Respond, and levelThe Deal

NT and nResponds
and levelTheDeal

# Generate Statistics

action

average "Minimum     " 100 * ntMin,
average "Avg         " 100 * ntAvg,
average "Max         " 100 * ntMax,

frequency  "HCP South "  (hcp(south) , 14, 18),
frequency  "ntPoints  "  (ntPoints   , 14, 18),


printpbn

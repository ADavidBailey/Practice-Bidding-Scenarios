# button-text: Lebensohl
# scenario-title: --- Lebensohl
# auction-filter: Auction.....\n1N +2[DHS].*(2[HS]|2NT|3[CDHSN])
# convention-card: 21GF-Multi-Landy
# Lebensohl
dealer south
# convention-card: 21GF-Multi-Landy


# Lebensohl


# South opens 1N, Robot style
sOpens = hcp(south)>14 and hcp(south)<18 and shape(south,any 4333 +any 4432 +any 5332 -5xxx-x5xx)

# Good Suits West -- QJT or better
GSW = top4(west,spades)>1   and top5(west,spades)>2   and spades(west)>3
GHW = top4(west,hearts)>1   and top5(west,hearts)>2   and hearts(west)>3
GDW = top4(west,diamonds)>1 and top5(west,diamonds)>2 and diamonds(west)>3
GCW = top4(west,clubs)>1    and top5(west,clubs)>2    and clubs(west)>3

wStrong = hcp(west)>15
w2Suit  = (GSW + GHW + GDW + GCW)==2 and shape(west,any 64xx+any 55xx+any 54xx) and hcp(west)>11 and hcp(west)<16
w1Suit  = (GSW or GHW or GDW or GCW) and shape(west,any 7xxx+any 6xxx+any 5xxx) and hcp(west)>10 and hcp(west)<16 and not w2Suit

wCompetes = (wStrong or w2Suit or w1Suit)

# Good Suits North -- 5+ to the QJT or better
GSN = top5(north,spades)>2   and shape(north,6xxx+5xxx+any 1xxx+any 0xxx)
GHN = top5(north,hearts)>2   and shape(north,x6xx+x5xx+any 1xxx+any 0xxx)
GDN = top5(north,diamonds)>2 and shape(north,xx6x+xx5x+any 1xxx+any 0xxx)
GCN = top5(north,clubs)>2    and shape(north,xxx6+xxx5+any 1xxx+any 0xxx)

n0Suit  = shape(north,any 4432+any 4333+any 4441) and hcp(north)>9
n1Suit  = (GSN or GHN or GDN or GCN)              and hcp(north)>8
nForces = (n0Suit or n1Suit)

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###

# Reduce West's 1-suited hands
levStrong = wStrong and keep
lev1Suit  = w1Suit  and keep03
lev2Suit  = w2Suit  and keep
levelTheDeal = (levStrong or lev2Suit or lev1Suit)

# Now do it
sOpens and wCompetes and nForces
and levelTheDeal

action
average "South HCP      " hcp(south),
average "North HCP      " hcp(north),
average "North Balanced " 100 * n0Suit,
average "North 1 Suit   " 100 * n1Suit,
average "West Strong    " 100 * wStrong,
average "West 2-Suited  " 100 * w2Suit,
average "West 1-Suited  " 100 * w1Suit,

printpbn

# button-text: then Gerber
# scenario-title: We Overcall 1NT then partner bids 4!C
# We_Overcall_1N_then_Gerber
dealer east


# No MSS, MST, Texas, or "Gerber only for 1NT and 2NT opening"
# convention-card: 21GF-Gerber
# auction-filter: 1[CDHS] 1NT Pass 4C

# Predeal West low cards to increase the produce/generate ratio.  Without this
# it produces only a handfull of deals.  I chose 3-cards in each major because
# I think it's more likely to get BBA to bid Gerber with a Minor than a Major.
# The 3 randomly dealt cards are rarely honors.
predeal west ST62, HJ73, D84, C95

# We Overcall 1NT_then_Stayman

# Predict East's opening suit
s = spades(east)
h = hearts(east)
d = diamonds(east)
c = clubs(east)
eS = s>4 and s>=h and s>=d and s>=c
eH = not eS and h>4 and h>=d and h>=c
eD = not eS and not eH and ((d>3 and d>=c) or c<3)
eC = not eS and not eH and not eD

# Avoid opening 1N
ntShape = shape(east, any 4333 +any 4432 +any 5332 +any 5422 -5xxx-x5xx)
notEastNT = not (hcp(east)>14 and hcp(east)<18 and ntShape)

# South has a stop in East's opening suit -- at least Axx, Kxx or QJx
eSws = eS and hcp(south,spades)>2 and spades(south)>2
eHws = eH and hcp(south,hearts)>2 and hearts(south)>2
eDws = eD and hcp(south,diamonds)>2 and diamonds(south)>2
eCws = eC and hcp(south,clubs)>2 and clubs(south)>2

# East opens 1 of a suit & South has stop
eOpens = notEastNT and (eSws or eHws or eDws or eCws) and hcp(east)>11 and hcp(east)<20

# South bids 1N
sNT = shape(south, any 4333 +any 4432 +any 5332 - 5xxx - x5xx) and hcp(south)>14 && hcp(south)<19

# North has a good long suit w/shortness
# There are NOT enough HCPs in the deck for Balanced or Semi-Balanced slams
nGS = spades(north)>5   and top3(north,spades)>1
nGH = hearts(north)>5   and top3(north,hearts)>1
nGD = diamonds(north)>5 and top3(north,diamonds)>1
nGC = clubs(north)>5    and top3(north,clubs)>1

longMinor = (nGD or nGC) and spades(north)<4 and hearts(north)<4    // prevent major suit exploration
longMajor = (nGS or nGH)
longSuit = (longMinor or longMajor) and shape(north, any 0xxx+any 1xxx)

# North also has and controls -- and bids Gerber OR NOT
nBidsGerber = longSuit and controls(north)>3 and hcp(north)>10

# Now do it
eOpens and sNT and nBidsGerber

# Calculate Net Losers
n0 = (shape(north,0xxx) ? 3 : 0) + (shape(north,00xx) ? 3 : 0)
n1 = (shape(north,1xxx) ? 2 : 0) + (shape(north,11xx) ? 2 : 0)
n2 = (shape(north,2xxx) ? 1 : 0) + (shape(north,22xx) ? 1 : 0)

cCovers = hascard(north,AC)+(hascard(north,KC) and clubs(north)>1)
dCovers = hascard(north,AD)+(hascard(north,KD) and diamonds(north)>1)
hCovers = hascard(north,AH)+(hascard(north,KH) and hearts(north)>1)
sCovers = hascard(north,AS)+(hascard(north,KS) and spades(north)>1)
covers = n0+n1+n2 +cCovers+dCovers+hCovers+sCovers
netLosers = losers(south) - covers


action
frequency "losers south " (losers(south),5,9),
frequency "covers north " (covers,5,9),
frequency "net losers   " (netLosers,0,3),


printpbn

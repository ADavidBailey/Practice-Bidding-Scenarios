# button-text: We Overcall 1NT
# scenario-title: We Overcall 1NT then partner transfers to !C (2!S) or !D (2N)
# We_Overcall_NT_then_MST
dealer east


# convention-card: 21GF-MST
# auction-filter: Note...:Minor Suit Transfers

# We Overcall 1NT then Minor Suit Transfers (MST)

# Predict East's opening suit
s = spades(east)
h = hearts(east)
d = diamonds(east)
c = clubs(east)
eS = s>4 and s>=h and s>=d and s>=c
eH = not eS and h>4 and h>=d and h>=c
eD = not eS and not eH and ((d>3 and d>=c) or c<3)
eC = not eS and not eH and not eD

# Avoid opening 1N
ntShape = shape(east, any 4333 +any 4432 +any 5332 +any 5422 -5xxx-x5xx)
notEastNT = not (hcp(east)>14 and hcp(east)<18 and ntShape)

# South has a stop in East's opening suit -- at least Axx, Kxx or QJx
eSws = eS and hcp(south,spades)>2 and spades(south)>2
eHws = eH and hcp(south,hearts)>2 and hearts(south)>2
eDws = eD and hcp(south,diamonds)>2 and diamonds(south)>2
eCws = eC and hcp(south,clubs)>2 and clubs(south)>2

# East opens 1 of a suit & South has stop
eOpens = notEastNT and (eSws or eHws or eDws or eCws) and hcp(east)>11 and hcp(east)<20

# South bids 1N
sNT = shape(south, any 4333 +any 4432 +any 5332 - 5xxx - x5xx) and hcp(south)>14 && hcp(south)<19

# North transfers to Clubs(2S) or Diamonds (2N)
n2S = (shape(north,xxx7+xxx6+xx9x+xx8x+xx7x+xx6x) and hcp(north)>7) or (shape(north,xxx9+xxx8+xx9x+xx8x) and hcp(north)>5)

# Now do it
eOpens and sNT and n2S



action printpbn

# button-text: Two-Way Game Try
# scenario-title: Two-Way Game Try
# Two-Way_Game_Try
dealer south


# Select Two-Way Game Tries
# convention-card: Two-Way-Game-Try
# auction-filter: Note...:Two way game tries

# Two-Way Game Tries

# Calculate Dummy Points for responder North (dp)
v1 = shape(north, any 0xxx) ? 5 : 0
s1 = shape(north, any 1xxx) ? 3 : 0
d1 = shape(north, any 2xxx) ? 1 : 0 // allow for 3 doubletons
d2 = shape(north, any 22xx) ? 1 : 0
d3 = shape(north, any 222x) ? 1 : 0
dp = v1+s1+d1+d2+d3 + hcp(north)

# Calculate Length Points for opener South (lp)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lp = lp1+lp2+lp3+lp4 + hcp(south)

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

#unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
#unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

#balEast   = shape(east,any 4333 +any 4432)
#balWest   = shape(west,any 4333 +any 4432)

balEast    = shape(east,any 4432+any 4333)
unbalEast  = not balEast

balWest    = shape(west,any 4432+any 4333)
unbalWest  = not balWest

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# Defines calmWest, calmEast, and calmOpps

# Define openers
sOpener = spades(south)>4 and spades(south)>=hearts(south) and spades(south)>=diamonds(south) and spades(south)>=clubs(south)
hOpener = hearts(south)>4 and hearts(south)>=diamonds(south) and hearts(south)>=clubs(south) and not sOpener

# Define suit fit
sFit = sOpener and spades(north)>2 and spades(north)<5
hFit = hOpener and hearts(north)>2 and hearts(north)<5 and spades(north)<4 
fit  = (sFit or hFit) and shape(north,any 0xxx+any 1xxx+any 22xx)

(sFit or hFit) and lp>15 and lp<18 and dp>5 and dp<11 and calmWest and shape(south,any 61xx+any 51xx)

action
average "spade fit" 100 * sFit,
average "heart fit" 100 * hFit,
average "hcp(south)" hcp(south),
average "hcp(north)" hcp(north),
frequency "hcp(south)" (hcp(south),14,18),


printpbn

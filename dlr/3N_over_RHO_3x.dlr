# button-text: 3NT over RHO's 3x (Lev)
# button-title: 3N over RHO's 3x Preempt.
# 3N_over_RHO_3x
dealer east


### 3N over RHO's 3x ###

# define south's controls for each suit
sCs = controls(south,spades)
sCh = controls(south,hearts)
sCd = controls(south,diamonds)
sCc = controls(south,clubs)

# define honor cards
aS = sCs==2
kS = sCs==1
aH = sCh==2
kH = sCh==1
aD = sCd==2
kD = sCd==1
aC = sCc==2
kC = sCc==1

qS = hascard(south,QS)
qH = hascard(south,QH)
qD = hascard(south,QD)
qC = hascard(south,QC)
jS = hascard(south,JS)
jH = hascard(south,JH)
jD = hascard(south,JD)
jC = hascard(south,JC)

# define number of cards in each suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)

# define south's sHCP points
sHCP = hcp(south)

# Define South's Long Rebiddable suits -- 6+ cards 3 of the top 4
lrS = spades(south)>5   and top4(south,spades)==3
lrH = hearts(south)>5   and top4(south,hearts)==3
lrD = diamonds(south)>5 and top4(south,diamonds)==3
lrC = clubs(south)>5    and top4(south,clubs)==3
sRebid = (lrS or lrH or lrD or lrC)

# east preempts and the 3-level
sE = shape(east, 7xxx-x4xx-any 5xxx) and top2(east,spades)==1 and top5(east,spades)>2
hE = shape(east, x7xx-4xxx-any 5xxx) and top2(east,hearts)==1 and top5(east,hearts)>2
dE = shape(east, xx7x-4xxx-x4xx-any 5xxx) and top2(east,diamonds)==1 and top5(east,diamonds)>2
cE = shape(east, xxx7-4xxx-x4xx-any 5xxx) and top2(east,clubs)==1 and top5(east,clubs)>2
ePreempts = (sE or hE or dE or cE) and hcp(east)>6 and hcp(east)<11

# define south has only 1 stop -- A, Kx, QJx
s1s = sE and (aS or (kS and s>1) or (qS and jS and s>2))
h1s = hE and (aH or (kH and h>1) or (qH and jH and h>2))
d1s = dE and (aD or (kD and d>1) or (qD and jD and d>2))
c1s = cE and (aC or (kC and c>1) or (qC and jC and c>2))
oneStop = (s1s or h1s or d1s or c1s)

# define south has 2 stops -- AK, AQ, KQx
s2s = sE and ((aS and kS) or (aS and qS) or (kS and qS and s>2))
h2s = hE and ((aH and kH) or (aH and qH) or (kH and qH and h>2))
d2s = dE and ((aD and kD) or (aD and qD) or (kD and qD and d>2))
c2s = cE and ((aC and kC) or (aC and qC) or (kC and qC and c>2))
twoStop = (s2s or h2s or d2s or c2s)

# define the three cases
case1 = oneStop and sHCP>17
case2 = oneStop and sRebid and sHCP>15 and not case1
case3 = twoStop and sHCP>15 and not (case1 or case2)

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###
### See -Stats-Leveling for statistics on these values ###
### Example of how to generate Statistics for Leveling ###


# Level it
level1 = case1 and keep06
level2 = case2 and keep81
level3 = case3 and keep67
levelTheDeal = level1 or level2 or level3

# south bids 3N
ePreempts and (case1 or case2 or case3)
and levelTheDeal

action
average "1 Stop and >17             " 100 * case1,
average "1 Stop, good suit, and >15 " 100 * case2,
average "2 Stop and >15             " 100 * case3,


printpbn

# button-text: Suction
# scenario-title: 15-17 Notrump Opening and Suction.
# Suction
dealer east


# convention-card:
# auction-filter: Auction.....\\n1NT.


# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(east) + shape(east,5xxx+x5xx)
nt1 = shape(east, any 5332+any 4432+any 4333) and hcp(east)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(east, 4252+4225+2452+2425+2254+2245) and hcp(east)>14 and hcp(east)<17

gibNT = nt1 or nt2

# Define South's suits for 1-suited hands
sS1 = hcp(south,spades)>6   and spades(south)>5
sH1 = hcp(south,hearts)>6   and hearts(south)>5
sD1 = hcp(south,diamonds)>6 and diamonds(south)>5
sC1 = hcp(south,clubs)>6    and clubs(south)>5

# Define South's suits for 2-suited hands
sS2 = hcp(south,spades)>3   and spades(south)>4 
sH2 = hcp(south,hearts)>3   and hearts(south)>4
sD2 = hcp(south,diamonds)>3 and diamonds(south)>4
sC2 = hcp(south,clubs)>3    and clubs(south)>4

# Define South's 2-suited combinations
sCD = sC2 and sD2
sCH = sC2 and sH2
sCS = sC2 and sS2
sDH = sD2 and sH2
sDS = sD2 and sS2
sHS = sH2 and sS2

# Define South's 1-Suited hands
sS = sS1 and not sCD
sH = sH1 and not sCS
sD = sD1 and not sHS
sC = sC1 and not sDH

# Define South's Suction actions -- 1-suited or 2-suited
cX  =        sCH
c2C = (sD or sHS) and not  cX
c2D = (sH or sCS) and not (cX  or c2C)
c2H = (sS or sCD) and not (cX  or c2C or c2D)
c2S = (sC or sDH) and not (cX  or c2C or c2D or c2H)
c2N =        sDS  and not (cX  or c2C or c2D or c2H or c2S)

# Now OPEN 1NT
gibNT and (cX or c2C or c2D or c2H or c2S or c2N) and hcp(south)>9 and shape(south,any 0xxx+any 1xxx)

# Generate Statistics
action
average "X    " 100 * cX,
average "2C   " 100 * c2C,
average "2D   " 100 * c2D,
average "2H   " 100 * c2H,
average "2S   " 100 * c2S,
average "2N   " 100 * c2N,
average "-----" 100 * (cX or c2C or c2D or c2H or c2S or c2N),

average "C&D  " 100 * sCD,
average "C&H  " 100 * sCH,
average "C&S  " 100 * sCS,
average "D&H  " 100 * sDH,
average "D&S  " 100 * sDS,
average "H&S  " 100 * sHS,
average "-----" 100 * (sCD or sCH or sCS or sDH or sDS or sHS),

average "S    " 100 * sS,
average "H    " 100 * sH,
average "D    " 100 * sD,
average "C    " 100 * sC,
average "-----" 100 * (sS or sH or sD or sC),

# button-text: Losing Trick Count
# scenario-title: Losing Trick Count
# Losing_Trick_Count
dealer south


# auction-filter: Auction.....\\n(?!Pass)


##### Imported Script -- Predict Opening 1-Bid #####

# Changed to use gibNT 3/30/2025

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, 4252+4225+2452+2425+2254+2245) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22
threeNT = TwoNtShape and hcp(south)>24 and hcp(south)<28

# Define Game Forcing 2C
case1 = hcp(south)>21
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
# Defines oneClub, oneDiamond, oneHeart, oneSpade, oneNT, twoNT, and gameForce2C

# Define normal opener
hcOpener = oneClub or oneDiamond or oneHeart or oneSpade or oneNT or twoNT or threeNT or gameForce2C
w2Opener = hcp(south)>5 and hcp(south)<12 and shape(south,any 6xxx-xxx6)
w3Opener = hcp(south)>5 and hcp(south)<12 and shape(south,any 9xxx+any 8xxx+any 7xxx)
normalOpener = (hcOpener or w2Opener or w3Opener)

# Define Losing Trick Count opener
ltcOpener = losers(south)<8 and hcp(south)>7 and hcp(south)<14

# Define cover cards -- for statistics only
qC = oneClub    and hascard(north,QC)
qD = oneDiamond and hascard(north,QD)
qH = oneHeart   and hascard(north,QH)
qS = oneSpade   and hascard(north,QS)
coverCards = top2(north,hearts) + top2(north,spades) + top2(north,diamonds) + top2(north,clubs) + qC + qD + qH + qS

game = coverCards > 3
part = coverCards < 4

ltcOpener and not normalOpener

action
average "opener's HCP    " hcp(south),
average "losers          " losers(south),
average "responder's HCP " hcp(north),
average "cover cards     " coverCards,
average "combined HCP    " hcp(south) + hcp(north),
average "losers-covers   " losers(south) - coverCards,
average "partscore       " 100 * part,
average "game            " 100 * game,

frequency "losers        " (losers(south),0,7),   
frequency "cover cards   " (coverCards,0,7),

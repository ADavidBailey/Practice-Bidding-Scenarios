# button-text: After 2 Passes...
# scenario-title: After 2 Passs...
# After_2_Passes
dealer north


# auction-filter: Auction.....\\nPass.Pass.

# After 2 Passes...


ccn = top5(north,clubs   )<3 and hcp(north,clubs   )<5
cdn = top5(north,diamonds)<3 and hcp(north,diamonds)<5
chn = top5(north,hearts  )<3 and hcp(north,hearts  )<5
csn = top5(north,spades  )<3 and hcp(north,spades  )<5
noConNorth = ccn and cdn and chn and csn

cce = top5(east ,clubs   )<3 and hcp(east ,clubs   )<5
cde = top5(east ,diamonds)<3 and hcp(east ,diamonds)<5
che = top5(east ,hearts  )<3 and hcp(east ,hearts  )<5
cse = top5(east ,spades  )<3 and hcp(east ,spades  )<5
noConEast = cce and cde and che and cse

# Hands with 5+ cards and no concentrateion of values are seldom opened with 8 or less HCP
length5or6North = shape(north,xxxx -any 8xxx -any 7xxx -any 65xx -any 55xx)
length5or6East  = shape(east ,xxxx -any 8xxx -any 7xxx -any 65xx -any 55xx)

# Hands with 5+ cards are often opened with 11 HCP
length4North  = shape(north,any 4333 +any 44xx)
length4East   = shape(east ,any 4333 +any 44xx)

calmNorth = ((length5or6North and noConNorth and hcp(north)<9) or (length4North and hcp(north)<12)) and controls(north)<5
calmEast  = ((length5or6East  and noConEast  and hcp(east) <9) or (length4East  and hcp(east) <12)) and controls(east) <5

shaded = hcp(south)>3 and hcp(south)<12 and shape(south,any 64xx+any 63xx+any 54xx+any 5332-any 55xx-xxx6-xxx5)
full   = hcp(south)>11 and hcp(south)<15

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###


levShaded = shaded and keep
levFull   = full   and keep53
levelTheDeal = levShaded or levFull

# Now do it...
calmNorth and calmEast and (shaded or full)
and levelTheDeal

action

average "shaded  " 100 * shaded,
average "full    " 100 * full,

average "hcp S   " hcp(south),
average "hcp W   " hcp(west),
average "hcp N   " hcp(north),
average "hcp E   " hcp(east),


printpbn

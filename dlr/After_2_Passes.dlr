# button-text: After 2 Passes...
# scenario-title: After 2 Passs...
# After_2_Passes
dealer north


# auction-filter: Auction.....\\nPass.Pass.

# After 2 Passes...


ccn = top5(north,clubs   )<3 and hcp(north,clubs   )<5
cdn = top5(north,diamonds)<3 and hcp(north,diamonds)<5
chn = top5(north,hearts  )<3 and hcp(north,hearts  )<5
csn = top5(north,spades  )<3 and hcp(north,spades  )<5
noConNorth = ccn and cdn and chn and csn

cce = top5(east ,clubs   )<3 and hcp(east ,clubs   )<5
cde = top5(east ,diamonds)<3 and hcp(east ,diamonds)<5
che = top5(east ,hearts  )<3 and hcp(east ,hearts  )<5
cse = top5(east ,spades  )<3 and hcp(east ,spades  )<5
noConEast = cce and cde and che and cse

# Hands with 5+ cards and no concentrateion of values are seldom opened with 8 or less HCP
length5or6North = shape(north,xxxx -any 8xxx -any 7xxx -any 65xx -any 55xx)
length5or6East  = shape(east ,xxxx -any 8xxx -any 7xxx -any 65xx -any 55xx)

# Hands with 5+ cards are often opened with 11 HCP
length4North  = shape(north,any 4333 +any 44xx)
length4East   = shape(east ,any 4333 +any 44xx)

calmNorth = ((length5or6North and noConNorth and hcp(north)<9) or (length4North and hcp(north)<12)) and controls(north)<5
calmEast  = ((length5or6East  and noConEast  and hcp(east) <9) or (length4East  and hcp(east) <12)) and controls(east) <5

# Now do it...
calmNorth and calmEast and hcp(south)>5 and hcp(south)<12

action
average "losers S" losers(south),
average "losers W" losers(west),
average "losers N" losers(north),
average "losers E" losers(east),

average "hcp S   " hcp(south),
average "hcp W   " hcp(west),
average "hcp N   " hcp(north),
average "hcp E   " hcp(east),

frequency "losers S " (losers(south), 4, 9),
frequency "losers W " (losers(west) , 4, 9),
frequency "losers N " (losers(north), 4, 9),
frequency "losers E " (losers(east) , 4, 5),

frequency "hcp S    " (hcp(south), 5, 12),
frequency "hcp W    " (hcp(west) , 5, 12),
frequency "hcp N    " (hcp(north), 5, 12),
frequency "hcp E    " (hcp(east) , 9, 20),


printpbn

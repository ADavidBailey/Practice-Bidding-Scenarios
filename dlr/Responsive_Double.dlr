# alias: ResponsiveDouble
# button-text: Responsive Double
# scenario-title: West Opens, North Bids or Doubles, East raises, and South does anything
# dealer-position: W
# gib-works: false
# bba-works: true
# auction-filter: Auction.....\n(1C +[X1].? +2C|1D +[X1].? +2D|1H +[X1].? +2H|1S +[X1].? +2S)

/*@chat
West Opens, North Bids or Doubles, East raises, and South does anything
@chat*/
dealer west

# Predict West's Opening suit
s = spades(west)
h = hearts(west)
d = diamonds(west)
c = clubs(west)
oS = s>4 and s>=h and s>=d and s>=c
oH = not oS and h>4 and h>=d and h>=c
oD = not (oS or oH) and ((d>3 and d>=c) or c<3)
oC = not (oS or oH or oD)
westSuit = (oS or oH or oD or oC)

# Distribution in West's opening suit -- north<3, east has support, west has 4 in unbid major(s) & 3+ in unbid minor(s).
dS = oS ? spades(east)  >3 and spades(north)<2   and hearts(north)==4 and shape(south,1444+2443+2434)       : 0
dH = oH ? hearts(east)  >3 and hearts(north)<2   and spades(north)==4 and shape(south,4144+4243+4234)       : 0
dD = oD ? diamonds(east)>4 and diamonds(north)<2 and shape(north,44xx+43xx+34xx) and shape(south,4414+4423) : 0
dC = oC ? clubs(east)   >5 and clubs(north)<2    and shape(north,44xx+43xx+34xx) and shape(south,4441+4432) : 0
dAll = (dS or dH or dD or dC) and shape(north,any 5440+any 5431+any 5332+any 4441+any 4432-5xxx-x5xx)

westOpens    = hcp(west) >11 and hcp(west) <15
northDoubles = hcp(north)>9  and hcp(north)<15
eastRaises   = hcp(east) >7  and hcp(east) <10
southDoubles = hcp(south)>7

##### Now do it #####
westOpens and northDoubles and eastRaises and southDoubles and dAll

generate 1000000
produce 30
action
printoneline,

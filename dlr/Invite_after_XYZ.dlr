# button-text: Invite after xyz
# scenario-title: Invite after xyz
# Invite_after_xyz
dealer south


# Invite after xyz

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

balEast   = shape(east,any 4333 +any 4432)
balWest   = shape(west,any 4333 +any 4432)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# returns calmOpps
##### Imported Script -- Predict Opening 1-Bid #####

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and   top4(south,spades)>0   and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and   top4(south,hearts)>0   and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)>0 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and    top4(south,clubs)>0    and not hascard(south,AC) ? 1 : 0
dgPoints = S2H + H2H + D2H + C2H

# Define notrump points for south (ntPoints)
ntPoints = lengthPoints>dgPoints ? hcp(south) + lengthPoints - dgPoints : hcp(south)

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

# Define robot notrump shape and exclude any 5card 
ntShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422 -5xxx -x5xx)

# Define ntPoint ranges
oneNT   = ntShape and ntPoints>14 and ntPoints<18
twoNT   = ntShape and ntPoints>19 and ntPoints<22
threeNT = ntShape and ntPoints>25 and ntPoints<28
weakNT  = ntShape and ntPoints>10 and ntPoints<15

# Define Game Forcing 2C
case1 = hcp(south)>22
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT // or weakNT  weakNT will be duplicated diamond and club openings

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade = oS
oneHeart = oH
oneDiamond = oD
oneClub = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
# returns oS
##### Predict Response to 1-Bid ##### 

# These variables are defined in the imported scripts above
# The number of cards South has in each suit: s, h, d, c
# South's predicted opening suit: oC, oD, oH, oS 

sP = hcp(south)
nP = hcp(north)

# Predict North's 1-level response
sN = spades(north)
hN = hearts(north)
dN = diamonds(north)
cN = clubs(north)

# North responds in D, H, or S (Walsh style), or raises C.
######
# If less than 13, Walsh Style; otherwise, longest suit.
nS  =  nP<13 ? sN>3 and sN>hN : sN>3 and sN>hN and sN>=dN and sN>=cN
nH  = (nP<13 ? hN>3           : hN>3           and hN>=dN and hN>=cN) and not (nS)
nD = dN>3           and dN>=cN and not (nS or nH)
nC = cN>4           and cN>4   and not (nS or nH or nD)
nN = not (nS or nH or nD or nC)

# Define South's possible new-suit/NT rebids
sRC = clubs(south)>3
sRD = diamonds(south)>3
sRH = hearts(south)>3
sRS = spades(south)>3
sRN = shape(south,any 4333+any 4432+any 5332)


# Defining South's opening (not NT) & North's non-GF response
CD = oC and nD
CH = oC and nH
CS = oC and nS
CN = oC and nN
CC = oC and nC

DH = oD and nH
DS = oD and nS
DN = oD and nN
DD = oD and not (DH or DS or DN)

HS = oH and nS
HH = oH and hN>2 and not HS
HN = oH and not (HS or HH)
##### End of Predict Response to 1-Bid ##### 

# returns nN

SN = oS and nN


##### Predict Opener's Rebid #####
CDH = CD and sRH
CDS = CD and sRS and not (CDH)
CDN = CD and sRN and not (CDH or CDS)
CDC = CD and c>5 and not (CDH or CDS or CDN)
CDD = CD and d>3 and not (CDH or CDS or CDN or CDC)

CHS = CH and sRS
CHN = CH and sRN and not (CHS)
CHC = CH and c>5 and not (CHS or CHN)
CHD = CH and sRD and not (CHS or CHN or CHC)
CHH = CH and sRH and not (CHS or CHN or CHC or CHD)

CSN = CS and sRN
CSC = CS and c>5 and not (CSN)
CSD = CS and sRD and not (CSN or CSC)
CSH = CS and sRH and not (CSN or CSC or CSD)
CSS = CS and sRS and not (CSN or CSC or CSD or CSH)

CNC = CN and c>5
CND = CN and sRD and not (CNC)
CNH = CN and sRH and not (CNC or CND)
CNS = CN and sRS and not (CNC or CND or CNH)
CNN = CN and sRN and not (CNC or CND or CNH or CNS)

CCD = CC and sRD
CCH = CC and sRH and not (CCD)
CCS = CC and sRS and not (CCD or CCH)
CCN = CC and sRN and not (CCD or CCH or CCS)
CCC = CC and c>5 and not (CCD or CCH or CCS or CCN)

DHS = DH and sRS
DHN = DH and sRN and not (DHS)
DHC = DH and sRC and not (DHS or DHN)
DHD = DH and d>5 and not (DHS or DHC or DHN)
DHH = DH and sRH and not (DHS or DHC or DHN or DHD)

DSN = DS and sRN
DSC = DS and sRC and not (DSN)
DSD = DS and d>5 and not (DSN or DSC)
DSH = DS and sRH and not (DSN or DSC or DSD)
DSS = DS and sRS and not (DSN or DSC or DSD or DSH)

DNC = DN and sRC
DND = DN and d>5 and not (DNC)
DNH = DN and sRH and not (DNC or DND)
DNS = DN and sRS and not (DNC or DND or DNH)
DNN = DN and sRN and not (DNC or DND or DNH or DNS)


HSN = HS and sRN
HSC = HS and sRC and not (HSN)
HSD = HS and sRD and not (HSN or HSC)
HSH = HS and h>5 and not (HSN or HSC or HSD)
HSS = HS and sRS and not (HSN or HSC or HSD or HSH)

HNC = HN and sRC
HND = HN and sRD and not (HNC)
HNH = HN and h>5 and not (HNC or HND)
HNS = HN and sRS and not (HNC or HND or HNH)
HNN = HN and sRN and not (HNC or HND or HNH or HNS)
##### End of Predict Opener's Rebid #####

# returns a bunch of variables.  S,H,D,C,and N stand for Spades, Hearts, Diamonds, Clubs, and NT respectively
# for example SNC means 1S (P) 1N (P) 2C
SNC = SN and sRC
SND = SN and sRD and not (SNC)
SNH = SN and sRH and not (SNC or SND)
SNS = SN and sRS and not (SNC or SND or SNH)
SNN = SN and sRN and not (SNC or SND or SNH or SNS)

strength = hcp(south)>11 and hcp(south)<15 and hcp(north)>9 and hcp(north)<13
sShape = shape(south,any 5422+any 5431+any 4432+any 4333-5xxx)
nShape = shape(north,any 60xx+61xx+any 5440+any 5431+any 4441-5xxx-x5xx+any 5332+any 4432+any 4333)

# Define north's short suits
s01 = shape(north,0xxx+1xxx)
h01 = shape(north,x1xx+x0xx)
d01 = shape(north,xx1x+xx0x)
c01 = shape(north,xxx0+xxx1)

# north is short in the 3rd or 4th suit and does not have 4-card support for opener's major
CDH and s01
CDS and h01
CDN and (s01 or h01)
CHS and d01          and hearts(south)<4
CHN and (d01 or s01) and hearts(south)<4
CSN and (d01 or h01) and spades(south)<4

DHS and c01          and hearts(south)<4
DHN and (s01 or c01) and hearts(south)<4
DHC and s01          and hearts(south)<4
DSN and (c01 or h01) and spades(south)<4
DSC and h01          and spades(south)<4
   
HSN and (c01 or d01) and spades(south)<4
HSC and d01          and spades(south)<4
HSD and c01          and spades(south)<4

SNC and (d01 or h01)
SND and c01
SNH and (d01 or c01)
 
xyz = (CDH or CDS or CDN or CHS or CHN or CSN or DHS or DHN or DHC or DSN or DSC or HSN or HSC or HSD or SNC or SND or SNH)

xyz and strength and calmOpps and sShape and nShape

action

average "5332" 100 * shape(north,any 5332),
average "4432" 100 * shape(north,any 4432),
average "4333" 100 * shape(north,any 4333),
average "nD  " 100 * nD,
average "nH  " 100 * nH,
average "nS  " 100 * nS,
average "nN  " 100 * nN,
average "CD  " 100 * CD,
average "CH  " 100 * CH,
average "CS  " 100 * CS,
average "CN  " 100 * CN,
average "DH  " 100 * DH,
average "DS  " 100 * DS,
average "DN  " 100 * DN,
average "HS  " 100 * HS,
average "HN  " 100 * HN,
average "SN  " 100 * SN,
average "CDH " 100 * CDH,
average "CDS " 100 * CDS,
average "CDN " 100 * CDN,
average "CHS " 100 * CHS,
average "CHN " 100 * CHN,
average "CSN " 100 * CSN,
average "DHS " 100 * DHS,
average "DHN " 100 * DHN,
average "DHC " 100 * DHC,
average "DSN " 100 * DSN,
average "DSC " 100 * DSC,
average "HSN " 100 * HSN,
average "HSC " 100 * HSC,
average "HSD " 100 * HSD,
average "SNC " 100 * SNC,
average "SND " 100 * SND,
average "SNH " 100 * SNH,

# Dealer Lebensohl.txt
generate 10000000
produce 500
dealer south


# Lebensohl

# South opens 1N, Robot style
sOpens = hcp(south)>14 and hcp(south)<18 and shape(south,any 4333 +any 4432 +any 5332 -5xxx-x5xx)

# Good Suits West -- QJT or better
GSW = top4(west,spades)>1   and top5(west,spades)>2
GHW = top4(west,hearts)>1   and top5(west,hearts)>2
GDW = top4(west,diamonds)>1 and top5(west,diamonds)>2
GCW = top4(west,clubs)>1    and top5(west,clubs)>2

wStrong = hcp(west)>14
w2Suit  = (GSW or GHW or GDW or GCW) and shape(west,any 54xx+any 44xx) and hcp(west)>11 and hcp(west)<15
w1Suit  = (GSW or GHW or GDW or GCW) and shape(west,any 6xxx+any 5xxx) and hcp(west)>10 and hcp(west)<15 and not w2Suit

wCompetes = (wStrong or w2Suit or w1Suit)

# Good Suits North -- 5+ to the QJT or better
GSN = top5(north,spades)>2   and shape(north,6xxx+5xxx+any 1xxx+any 0xxx)
GHN = top5(north,hearts)>2   and shape(north,x6xx+x5xx+any 1xxx+any 0xxx)
GDN = top5(north,diamonds)>2 and shape(north,xx6x+xx5x+any 1xxx+any 0xxx)
GCN = top5(north,clubs)>2    and shape(north,xxx6+xxx5+any 1xxx+any 0xxx)

n0Suit  = shape(north,any 4432+any 4333+any 4441) and hcp(north)>9
n1Suit  = (GSN or GHN or GDN or GCN)              and hcp(north)>8
nForces = (n0Suit or n1Suit)

#Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/-Script-Leveling

# Now do it
sOpens and wCompetes and nForces

action
average "South HCP      " 100 * hcp(south),
average "North HCP      " 100 * hcp(north),
average "North Balanced " 100 * n0Suit,
average "North 1 Suit   " 100 * n1Suit,
average "West Strong    " 100 * wStrong,
average "West 2-Suited  " 100 * w2Suit,
average "West 1-Suited  " 100 * w1Suit,


printpbn

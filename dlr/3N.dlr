# button-text: 3NT (25-27)
# scenario-title: 25-27 Notrump and any Response.
# 3N
dealer south


# auction-filter: Auction.....\\n3NT.Pass.
# convention-card: 21GF-Not-Gambling

# Three Notrump Opening

### SOUTH ###
# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and top4(south,spades)==1 and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and top4(south,hearts)==1 and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)==1 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and top4(south,clubs)==1 and not hascard(south,AC) ? 1 : 0
ntDownGrade = S2H or H2H or D2H or C2H

# Define notrump points for south (ntPoints)
ntPoints = ntDownGrade ? hcp(south) : hcp(south) + lengthPoints

# Define robot notrump shape and exclude any 5-card major
ntShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422 -5xxx -x5xx)

# Define Two NT Point range
threeNT = ntShape and ntPoints>24 and ntPoints<28

### Do it ###
threeNT and hcp(north)>4     // Increase the probability of slam

### Generate Statistics ###

combined = ntPoints + hcp(north)

partscore = combined<25
game = combined>24 and combined<33
slam = combined>32

action
average "Game         " 100 * game,
average "Slam         " 100 * slam,


printpbn

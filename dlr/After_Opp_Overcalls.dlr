# alias: AfterOppOvercalls
# button-text: After Opp Overcalls
# scenario-title: --- After Partner Overcalls
# gib-works: true
# bba-works: true
# auction-filter: 1[CDHS] +[12][CDHSN]
# convention-card-ns: 21GF-DEFAULT
# convention-card-ew: 21GF-GIB

/*@chat
--- After Partner Overcalls
• Raising a minor denies a 4-card Major and shows a fit and 7-9 TP
• Double denies a Major fit and shows at least 4-cards in the unbid major suits and 6+ HCP
• Raising a Major shows a fit and 6-10 TP
• Cue Biddding RHO's suit shows a fit and 11+ TP
• New Suit denies a fit and shows 10+ HCP
@chat*/

# After Opponent Overcalls partner's opening bid

dealer south
produce 10000

##### Imported Script -- Predict Opening 1-Bid South #####
# 01/19/2026 -- Changed to use TP and separate ranges for C, D, and majors
# 03/30/2025 -- Changed to use gibNT

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422 -5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

## Calculate TP for South -- NOTE:  This uses different variable names than TP to avoid conflicts when both are used.
# Calculate South's Short-Suit Penalty Points
ssp = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
shp = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sdp = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
scp = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv01 = shape(south, any 0xxx) ? 3 : 0
ss01 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss02 = shape(south, any 11xx) ? 2 : 0
sd01 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd02 = shape(south, any 22xx) ? 1 : 0
sd03 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv01+ss01+ss02+sd01+sd02+sd03 -ssp-shp-sdp-scp

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22

# Define Gambling 3N
g3Shape = shape(south,any 9xxx +any 8xxx +any 7xxx -any 4xxx -any 5xxx -any 6xxx)
threeNT = (top3(south,clubs)==3 and clubs(south)>6) or (top3(south,diamonds)==3 and diamonds(south)>6) and hcp(south)<12 and g3Shape

# Define Game Forcing 2C
case1 = hcp(south)>18 and sTP>21
case2 = hcp(south)>18 and sTP>19 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and sTP>19 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3)

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT

# Define quality and length lower limits for 10 HCP openers

q2of3 = (top3(south,spades)>1 or top3(south,hearts)>1) and controls(south)>3
lgt5  = (spades(south)>4 or hearts(south)>4) and shape(south,any 7xxx +any 64xx +any 55xx +any 1xxx+any 0xxx -any 8xxx -any 54xx) and sTP>(hcp(south) +2)

# Predict South's Opening suit
# What's the longest suit?
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)

## GIB has different requirements for 1H/S, 1D, and 1C openings 
#Define range for 1 H/S
lS = s>4 and s>=h and s>=d and s>=c
lH = h>4 and h>s and h>=d and h>=c
lM = lS or lH

hsOK1 = lM and hcp(south)>11
hsOK2 = lM and hcp(south)>10 and sTP>12 and not hsOK1
hsOK3 = lM and hcp(south)>9  and lgt5 and q2of3 and not (hsOK1 or hsOK2)
hsRange = (hsOK1 or hsOK2 or hsOK3) and not P2

# Define 1D Range
dOK1 = hcp(south)>11 and (diamonds(south)>3 or shape(south, 4432))
dOK2 = hcp(south)>9  and sTP>12 and diamonds(south)>5 and not dOK1
dRange = (dOK1 or dOK2) and not P2

# Define 1C Range
cOK1 = hcp(south)>10 and sTP>12 and spades(south)==4
cOK2 = hcp(south)>9  and sTP>12 and clubs(south)>5 and not cOK1
cOK3 = hcp(south)>11 and clubs(south)>2            and not (cOK1 or cOK2)
cRange = (cOK1 or cOK2 or cOK3) and clubs(south)>2 and clubs(south)>diamonds(south) and not P2

oS = s>4 and s>=h and s>=d and s>=c and hsRange
oH = not oS and h>4 and h>=d and h>=c and hsRange
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and dRange
oC = not (oS or oH or oD) and cRange
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####

# North has a fit for opener's suit
nFit = (oC and clubs(north)>4) or (oD and diamonds(north)>3) or (oH and hearts(north)>2) or (oS and spades(north)>2)

# North Support Points
v1 = shape(north, any 0xxx) ? 5 : 0  // allow 1 void
s1 = shape(north, any 1xxx) ? 3 : 0  // allow 2 singletons
s2 = shape(north, any 11xx) ? 3 : 0
d1 = shape(north, any 2xxx) ? 1 : 0  // allow 3 doubletons
d2 = shape(north, any 22xx) ? 1 : 0
d3 = shape(north, any 222x) ? 1 : 0
nssp = v1 + s1 + s2 + d1 + d2 + d3
nsp = nssp>2 ? hcp(north) + 3 : hcp(north) + nssp   // Limit Dummy Points to 3

# Define Good Suits for West (not in South's opening suit)
gS = spades(west)>4   and top3(west,spades)>1   and not oS
gH = hearts(west)>4   and top3(west,hearts)>1   and not oH
gD = diamonds(west)>4 and top3(west,diamonds)>1 and not oD
gC = clubs(west)>4    and top3(west,clubs)>1    and not oC

# Predict West's overcall
sW = spades(west)
hW = hearts(west)
dW = diamonds(west)
cW = clubs(west)
wS = sW>=hW and sW>=dW and sW>=cW
wH = not wS and hW>=dW and hW>=cW
wD = not (wS or wH) and (dW>=cW or cW==3)
wC = not (wS or wH or wD)

# South Opens
sOpens = (oS or oH or oD or oC) and hcp(south)>11 and hcp(south)<15

# West overcalls with a single-suited hand
wOvercalls = (wD or wH or wS or wC) and (gS or gH or gD or gC) and shape(west,any 6xxx+any 5xxx-any 65xx -any 55xx) and hcp(west)>10

p1 = hcp(north)>5
p2 = hcp(north)>9
p3 = hcp(north)>11

# North doubles
nM = shape(north,44xx+45xx+54xx)
nH = shape(north,x4xx) and not nM
nS = shape(north,4xxx) and not nM
x1 = oC and wD         ? nM and p1 : 0
f1 = oC and wD         ? (nH or nS) and p1 : 0 // What if Partner bids the wrong M?
x2 = oC and wH         ? nS and p1 : 0
x3 = oC and wS         ? nH and p1 : 0
x4 = oD and wH         ? nS and p1 : 0
x5 = oD and wS         ? nH and p1 : 0
x6 = oD and wC         ? nM and p2 : 0
f2 = oD and wC         ? (nH or nS) and p2 : 0 // What if Partner bids the wrong M?
x7 = oH and (wC or wD) ? nS and p2 : 0
x8 = oS and (wC or wD) ? nH and p2 : 0

nDoubles = (((x1 or x2 or f1) and p1) or ((x3 or x4 or x5) and p2) or ((x6 or f2 or x7 or x8) and p3))
nRaisesMajor = (oH or oS) and nFit and nsp>7 and nsp<11
nCueBids     = (oH or oS) and nFit and nsp>10
nNewSuit     = not(nFit or nDoubles) and (spades(north)>4 or hearts(north)>4 or diamonds(north)>4 or clubs(north)>4)
nRaisesMinor = (oC or oD) and nFit and (wD or wH) nsp>5

nActs = nCueBids or nRaisesMajor or nDoubles or nRaisesMinor // or nNewSuit 
sOpens and wOvercalls and nActs

action printoneline,
average "cue bids  " 100 * nCueBids,
average "raise M   " 100 * nRaisesMajor,
average "neg double" 100 * nDoubles,
average "new suit  " 100 * nNewSuit,
average "raises m  " 100 * nRaisesMinor,

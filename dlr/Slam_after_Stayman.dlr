# button-text: Slam after Stayman
# scenario-title: --- Slam after Stayman or Smolen.
# auction-filter: Auction.....\n1N +Pass +2C
# Slam_after_Stayman
dealer south

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####
# Defines calmWest

# Slam after Stayman or Jacoby
##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major or with 4 spades
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422-5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# Defines gibNT

staymanOrSmolen = shape(north, 54xx+45xx+44xx+43xx+34xx)  // Stayman and/or Smolen
nRange = hcp(north)>15

gibNT and staymanOrSmolen and nRange and calmWest

action printpbn

# button-text: Gerber by Opener (Lev)
# scenario-title: Gerber by Opener (Leveled)
# Gerber_By_Opener
dealer south


# These are sequences where 4C-Gerber might be bid by Opener
# auction-filter: Auction.....\\n(1[CD].* Pass 2NT |2C.* Pass 2NT )

#
# Gerber by Opener
#
# 1m - 2N - ?
# 2C - 2N - ?


##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

balEast   = shape(east,any 4333 +any 4432)
balWest   = shape(west,any 4333 +any 4432)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# Defines calmOpps

# South has a control in all suits
sControlsOK = controls(south,spades)>0 and controls(south,hearts)>0 and controls(south,diamonds)>0 and controls(south,clubs)>0

### Define south's 1m - 2N
# South has 1 good minor -- a source of tricks and north doesn't have a raise.  Avoid 1C - 1D
gC = clubs(south)>4    and top3(south,clubs)>1    and top3(north,clubs)==1    and diamonds(north)<5 and hcp(north,diamonds)==2
gD = diamonds(south)>4 and top3(south,diamonds)>1 and top3(north,diamonds)==1
sGoodMinorOK = (gC or gD) and (gC + gD) == 1

# Define North's partial stops
nPSC = (clubs(north)>2    and hcp(north,clubs)>1    and hcp(north,diamonds)<4) or (sGoodMinorOK and gC)
nPSD = (diamonds(north)>2 and hcp(north,diamonds)>1 and hcp(north,clubs)<4)    or (sGoodMinorOK and gD)
nPSH =  hearts(north)>2   and hcp(north,hearts)>1   and hcp(north,spades)<4
nPSS =  spades(north)>2   and hcp(north,spades)>1   and hcp(north,hearts)<4
nPSOK = nPSC and nPSD and nPSH and nPSS

sShape1m = shape(south,any 7222+any 6322+any 5422+any 5332)
nShape1m = shape(north,any 6322+any 5332+any 4432+any 4333) and spades(north)<4 and hearts(north)<4
shape1mOK  = sShape1m and nShape1m

# South gets extra points for length
sLP = gD ? diamonds(south)-4 : clubs(south)-4
sTP = hcp(south) + sLP

ntShape = shape(south, any 5422+any 5332+any 4432+any 4333)
s1NT = ntShape and hcp(south)>15 and hcp(south)<18
s2NT = ntShape and hcp(south)>18 and hcp(south)<21
sNTOK = not (s1NT or s2NT)

sRange1m = sTP>17 and hcp(south)<22 and sNTOK
nRange1m = hcp(north)>10 and hcp(north)<13
range1mOK = sRange1m and nRange1m

oneMinor2N = sControlsOK and sGoodMinorOK and nPSOK and shape1mOK and range1mOK

### Define south's 2C - 2NT

# North has bad minors
bC = clubs(north)>4    ? top3(south,clubs)==0    : 1
bD = diamonds(north)>4 ? top3(south,diamonds)==0 : 1
nBadMinorsOK = bC and bD

sShape2C = shape(south,any 7222+any 6322+any 5422+any 5332+any 4432+any 4333)
nShape2C = shape(north,any 6322+any 5332+any 4432+any 4333) and spades(north)<4 and hearts(north)<4
shape2cOK  = sShape2C and nShape2C

sRange2C = hcp(south)>21
nRange2C = hcp(north)>7
range2cOK = sRange2C and nRange2C

# North has at least 1 control
nControlsOK = controls(north)>0
controlsOK = sControlsOK and nControlsOK

twoClubs2N = controlsOK and nBadMinorsOK and shape2cOK and range2cOK

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###


# Level the deal
level1m2N = oneMinor2N and keep
level2C2N = twoClubs2N and keep
levelTheDeal = level1m2N or level2C2N

# Now do it
(oneMinor2N or twoClubs2N) and calmOpps
and levelTheDeal

action
average "1m - 2N  " 100 * oneMinor2N,
average "2C - 2N  " 100 * twoClubs2N,


printpbn

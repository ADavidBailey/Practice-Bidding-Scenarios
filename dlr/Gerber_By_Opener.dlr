# button-text: Gerber by Opener (Lev)
# scenario-title: Gerber by Opener (Leveled)
# Gerber_By_Opener
dealer south


# These are sequences where 4C-Gerber might be bid by Opener
# auction-filter: Auction.....\\n(1[CD].* Pass 2NT |2C.* Pass 2NT )


#Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Calm-Opponents
# Defines calmWest
##### Imported Script -- Predict Opening 1-Bid #####

# Changed to use gibNT 3/30/2025

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, 4252+4225+2452+2425+2254+2245) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22
threeNT = TwoNtShape and hcp(south)>24 and hcp(south)<28

# Define Game Forcing 2C
case1 = hcp(south)>21
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 5422+any 5332+any 4432+any 4333)
unbalEast  = not balEast and shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

balWest    = shape(west,any 5422+any 5332+any 4432+any 4333)
unbalWest  = not balWest and shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<6) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<6) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# Defines oneClub, oneDiamond

# Calculate South's TP
sC = clubs(south) >4   ? clubs(south) - 4    : 0
sD = diamonds(south)>4 ? diamonds(south) - 4 : 0
sH = hearts(south)>4   ? hearts(south) - 4   : 0
sS = spades(south)>4   ? spades(south) - 4   : 0
sTP = hcp(south) + sC + sD + sH + sS

# Define suit quality for NT Response needed for other minor and both majors
cQ = hcp(north,clubs)>2
dQ = hcp(north,diamonds)>2
mQ = hcp(north,hearts)>2 and hcp(north,spades)>2

# South opens a minor and North does not respond 1D, 1H, or 1S or raise -- allow as much length as possible  
# There are too many constraints for 1C - 2N, so I removed the code
s1D = oneDiamond and cQ and hcp(north,diamonds)==0 and hearts(north)<4 and spades(north)<4

# North responds in NT -- balanced w/major suit quality
ntResponse = shape(north, any 5422 +any 5332 +any 4432 +any 4333) and mQ

# South opens 2C or 1m
s2C = hcp(south)>21 and hearts(north)<5 and spades(north)<5
s1m = hcp(south)<22 and sTP>20 and s1D

# Define south's 1m-2N and 2C-2N responses
oneMinor2N = s1m and ntResponse and hcp(north)>10 and hcp(north)<13
twoClubs2N = s2C and ntResponse and hcp(north)>7

(oneMinor2N or twoClubs2N) and calmWest

action
average "1m - 2N  " 100 * oneMinor2N,
average "2C - 2N  " 100 * twoClubs2N,


printpbn

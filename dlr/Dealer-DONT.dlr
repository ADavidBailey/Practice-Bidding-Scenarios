# ./Dealer DONT

generate 10000000

produce 10000
dealer east




# Define East's 1N Opener
NT = hcp(east)>14 and hcp(east)<18 and shape(east,any 4333+any 4432+any 5422+any 5332-5xxx-x5xx)

# Define South's good suits
sGS = top2(south,spades)>0   and top4(south,spades)>1   and top5(south,spades)>2
sGH = top2(south,hearts)>0   and top4(south,hearts)>1   and top5(south,hearts)>2
sGD = top2(south,diamonds)>0 and top4(south,diamonds)>1 and top5(south,diamonds)>2
sGC = top2(south,clubs)>0    and top4(south,clubs)>1    and top5(south,clubs)>2

# Define South's shapes
oneSuit = shape(south,any 9xxx+any 8xxx+any 7xxx+any 6xxx+any 5xxx-any 85xx-any 76xx-any 75xx-any 65xx-any 55xx-any 5332-any 4xxx)

# Define South's lengthPoints
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# South needs extra shape or hcp
strength = (hcp(south) + lengthPoints) > 10

maxWithSpades = sGS and strength>10
minWithSpades = sGS and strength<11

# Define South's Meckwell action
cX  = oneSuit and (sGC or sGD or sGH or maxWithSpades)
c2C = (sGC    and (sGD or sGH or sGS)) and not cX
c2D = (sGD    and (sGH or sGS)) and not (cX or c2C) 
c2H = (sGH    and sGS)          and not (cX or c2C or c2D) 
c2S = (minWithSpades)           and not (cX or c2C or c2D or c2H) 

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###
### See -Stats-Leveling for statistics on these values ###
### Example of how to generate Statistics for Leveling ###


# Define Leveling
lX = cX  and keep06
lC = c2C and keep25
lD = c2D and keep33
lH = c2H and keep75
lS = c2S and keep03
levelTheDeal = lX or lC or lD or lH or lS

# Now OPEN 1NT
NT and (cX or c2C or c2D or c2H or c2S) and strength
and levelTheDeal

# Generate Statistics
action
average "X    " 100 * cX,
average "2C   " 100 * c2C,
average "2D   " 100 * c2D,
average "2H   " 100 * c2H,
average "2S   " 100 * c2S,


printpbn

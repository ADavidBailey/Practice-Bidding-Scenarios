# alias: TooStrongForOvercall4th
# button-text: Overcalls in 4th??
# scenario-title: --- Overcalls in 4th??  Some hands are too strong for an overcall in 4th seat.
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\n1[CDHS] +Pass +Pass +X
# convention-card-ns: 21GF-DEFAULT
# convention-card-ew: 21GF-GIB

/*@chat
--- Overcalls in 4th??  Some hands are too strong for an overcall in 4th seat.
Simple overcalls -- those at the same level as the opening bid -- show
• 7-17 at the 1-level
• 11-17 at the 2-level
With stronger hands, start with a double.  Double then new suit shows 18+.
Overcall then Double is take-out in the 15-17 range.
@chat*/

# X then Bid a new suit
dealer east

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(north) + shape(north,5xxx+x5xx)
nt1 = shape(north, any 5332+any 4432+any 4333) and hcp(north)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(north, 4252+4225+2452+2425+2254+2245) and hcp(north)>14 and hcp(north)<16

gibNT = nt1 or nt2
# defines gibNT

# Define good suits
GoodS = spades(north)>4 and top5(north,spades)>2
GoodH = hearts(north)>4 and top5(north,hearts)>2
GoodD = diamonds(north)>4 and top5(north,diamonds)>2
GoodC = clubs(north)>4 and top5(north,clubs)>2

# Predict East's opening suit
s = spades(east)
h = hearts(east)
d = diamonds(east)
c = clubs(east)
oS = s>4 and s>=h and s>=d and s>=c
oH = not oS and h>4 and h>=d and h>=c
oD = not (oS or oH) and ((d>3 and d>=c) or c<3)
oC = not (oS or oH or oD)
openingSuit = (oS or oH or oD or oC)

# Avoid a natural Take-out Double
oneSpade = oS and spades(north)==3
oneHeart = oH and hearts(north)==3
oneDiamond = oD and diamonds(north)==3
oneClub = oC and clubs(north)==3

# East opens
eOpens = (oneSpade or oneHeart or oneDiamond or oneClub) and hcp(east)>11 and hcp(east)<15

# North has a decent suit, too strong to overcall and NOT a NT hand
nDoubles = (GoodS or GoodH or GoodD or GoodC) and hcp(north)>17 and not gibNT

eOpens and nDoubles

action printoneline,
average "(hcp east)  " hcp(east),
average "(hcp north) " hcp(north),

# alias: TooStrongForOvercall
# button-text: Overcalls??
# scenario-title: --- Overcalls??  Some hands are too strong for an overcall.
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\n1[CDHS] +X
# convention-card-ns: 21GF-DEFAULT
# convention-card-ew: 21GF-GIB

/*@chat
--- Overcalls??  Some hands are too strong for an overcall.
Simple overcalls -- those at the same level as the opening bid -- show
• 7-17 at the 1-level
• 11-17 at the 2-level
With stronger hands, start with a double.  Double then new suit shows 18+.
Overcall then Double is take-out in the 15-17 range.
@chat*/

# X then Bid a new suit
dealer east

##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major or with 4 spades
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422-5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# defines gibNT

# Define good suits
GoodS = spades(south)>4 and top5(south,spades)>2
GoodH = hearts(south)>4 and top5(south,hearts)>2
GoodD = diamonds(south)>4 and top5(south,diamonds)>2
GoodC = clubs(south)>4 and top5(south,clubs)>2

# Predict East's opening suit
s = spades(east)
h = hearts(east)
d = diamonds(east)
c = clubs(east)
oS = s>4 and s>=h and s>=d and s>=c
oH = not oS and h>4 and h>=d and h>=c
oD = not (oS or oH) and ((d>3 and d>=c) or c<3)
oC = not (oS or oH or oD)
openingSuit = (oS or oH or oD or oC)

# Avoid a natural Take-out Double
oneSpade = oS and spades(south)==3
oneHeart = oH and hearts(south)==3
oneDiamond = oD and diamonds(south)==3
oneClub = oC and clubs(south)==3

# East opens
eOpens = (oneSpade or oneHeart or oneDiamond or oneClub) and hcp(east)>11 and hcp(east)<15

# South has a decent suit, too strong to overcall and NOT a NT hand
sDoubles = (GoodS or GoodH or GoodD or GoodC) and hcp(south)>17 and not gibNT

eOpens and sDoubles

action printoneline,
average "(hcp east)  " hcp(east),
average "(hcp south) " hcp(south),

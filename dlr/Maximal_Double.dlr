# button-text: Maximal Double
# scenario-title: Maximal Overcall Double:  After we've bid and raised a major suit
# Maximal_Double
dealer north


# Add 1M - 3M inviting, add Maximal Doubles, kill Reverse Bergen, Kill Support 1N, kill Two-way game tries
# convention-card: 21GF-MaximalDouble
# auction-filter: 1H .* 2H 3D|1S .* 2S 3H

# Maximal Overcall Double

##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422-5xxx-x5xx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# Defines gibNT
##### Imported Script - Calculate Total Points for South #####

## Calculate TP for South
# Calculate South's Short-Suit Penalty Points
sSp = spades(south)<3 and hcp(south,spades)>1     ? 1 : 0
sHp = hearts(south)<3 and hcp(south,hearts)>1     ? 1 : 0
sDp = diamonds(south)<3 and hcp(south,diamonds)>1 ? 1 : 0
sCp = clubs(south)<3 and hcp(south,clubs)>1       ? 1 : 0

# Calculate South's Short-Suit Points
v1 = shape(south, any 0xxx) ? 3 : 0
s1 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
s2 = shape(south, any 11xx) ? 2 : 0
d1 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
d2 = shape(south, any 22xx) ? 1 : 0
d3 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + v1+s1+s2+d1+d2+d3 -sSp-sHp-sDp-sCp

### End of Calculate TP for South ###
# Defines sTP

# North opens 1M
S_ = spades(north)>4 and hearts(north)<5 and diamonds(north)<=5 and clubs(north)<=5
H_ = hearts(north)>4 and spades(north)<5 and diamonds(north)<=5 and clubs(north)<=5
n1M = (S_ or H_) and hcp(north)>11 and hcp(north)<18 and losers(north)<8 and not gibNT

nLP = S_ ? spades(north) - 4 : hearts(north) - 4
nTP = hcp(north) + nLP

# Define East overcalls in Maximal suit
e2D = H_ and diamonds(east)>5 and top3(east,diamonds)>1 and spades(east)<5
e2H = S_ and hearts(east)>5   and top3(east,hearts)>1
eShape = shape(east,any 7xxx+ any 6xxx - any 76xx - any 75xx - any 65xx)
eRange = hcp(east)>10 and hcp(east)<14 and controls(east)>2
eMaxOC = (e2D or e2H) and eRange and eShape

# Define East Takeout Double
eHX = H_ and hearts(east)<2 and shape(east,any 5440 +any 5431 +any 4441 -5xxx)
eSX = S_ and spades(east)<2 and shape(east,any 5440 +any 5431 +any 4441 -x5xx)
eX = (eHX or eSX) and hcp(east)>10 and hcp(east)<15 and not eMaxOC

# avoid other overcalls
eWJS    = shape(east,any 9xxx +any 8xxx +any 7xxx +any 6xxx) and hcp(east)<11
e2suits = shape(east,any 85xx +any 76xx + any 75xx +any 65xx +any 55xx)
ePass = not (eWJS or e2suits)

# Define East bids
eBids = ePass or eX or eMaxOC

# Define South's major suit raises with 3/4-cards
spadeFit = S_ and shape(south,3xxx+4xxx) and hcp(south,spades)>1 and (controls(south)-controls(south,spades)==1)
heartFit = H_ and shape(south,x3xx+x4xx) and hcp(south,hearts)>1 and (controls(south)-controls(south,hearts)==1) and spades(south)<4
sRaise = (spadeFit or heartFit) and sTP>5 and sTP<10 and hcp(south)<10 and shape(south,xxxx -any 4333)

# Define West's raise of Maximal overcall
wHR = e2H and hearts(west)==3   and hcp(west,hearts)>1
wDR = e2D and diamonds(west)==3 and hcp(west,diamonds)>1
wRaise = (wHR or wDR) and hcp(west)>6 and hcp(west)<11

# Define West's response to X
wD = heartFit and diamonds(west)>4
wH = spadeFit and hearts(west)>4
wRespToX = eX and (wD or wH) and shape(west,xxxx-any 76xx -any 75xx -any 65xx -any 55xx) and hcp(west)>6 and hcp(west)<11

# Define West's free bid of Maximal suit.  //Include 2 of opener's suit to avoid X
wGD = heartFit and top5(west,diamonds)>2  and shape(west,xx6x)
wGH = spadeFit and top5(west,hearts)>2    and shape(west,x6xx)
wFreeBid = ePass and (wGD or wGH) and shape(west,any 0xxx +any 1xxx -any 76xx -any 75xx -any 66xx -any 65xx) and hcp(west)>10 and hcp(west)<14

# Define West's 3 of the Maximal suit
w3Max = (wRaise or wRespToX or wFreeBid)

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###


nMin = nTP<14
nMid = nTP>13 and nTP<16
nMax = nTP>15

levMax = nMax and keep //70
levMid = nMid and keep //67
levMin = nMin and keep

levelTheDeal = (levMax or levMid or levMin)

# Now do it
n1M and eBids and sRaise and w3Max
and levelTheDeal

action
average "south TP       " sTP,
average "north TP       " nTP,
average "north 11-13 TP " nMin,
average "north 14-15 TP " nMid,
average "north 16+   TP " nMax,


printpbn

# button-text: Maximal Double
# scenario-title: Maximal Overcall Double after we've bid and raised a major suit...
# Maximal_Double
dealer north


# Add 1M - 3M inviting, add Maximal Doubles, kill Reverse Bergen, Kill Support 1N, kill Two-way game tries
# convention-card: 21GF-MaximalDouble
# auction-filter: Auction.....\\n((1H .* 2H 3D|1S .* 2S 3H))

# Maximal Overcall Double

# convention-card: 21GF-MaximalDouble

# North opens 1 Major // and short in the suit below (the maximal suit) 
S_ = spades(north)>4 and spades(north)>=diamonds(north) and spades(north)>=clubs(north) // and hearts(north)<2
H_ = hearts(north)>4 and hearts(north)>spades(north)    and hearts(north)>=clubs(north) // and diamonds(north)<2
n1M = (S_ or H_) and hcp(north)>13 and hcp(north)<18 and shape(north,any 6xxx+any 5xxx-any 65xx-any 55xx) // 13 HCP minimum is to increase the game tries

# East overcalls any suit or passes
eGC = shape(east,xxx6+xxx5-any 66xx-any 65xx-any 55xx) and top5(east,clubs)>2
eGD = shape(east,xx6x+xx5x-any 66xx-any 65xx-any 55xx) and top5(east,diamonds)>2
eGH = shape(east,x6xx+x5xx-any 66xx-any 65xx-any 55xx) and top5(east,hearts)>2
eGS = shape(east,5xxx     -any 66xx-any 65xx-any 55xx) and top5(east,spades)>2 // restrict 5-cards to avoid WJO

# Define East overcalls
e1S = H_ and eGS and hcp(east)>8
e2C =        eGC and hcp(east)>10
e2D =        eGD and hcp(east)>10
e2H = S_ and eGH and hcp(east)>10

# Define East Takeout Double
H_X = H_ and shape(east,any 6430+any 6331+any 5440+any 5431+any 4441+x0xx+x1xx)
S_X = S_ and shape(east,any 6430+any 6331+any 5440+any 5431+any 4441+0xxx+1xxx)
eX = (H_X or S_X) and hcp(east)>10

# Define East Pass
eP = not (e1S or e2C or e2D or e2H or eX) 

# Reduce south's max hcp 1 for each support card over 3 to avoid jumps to 3
spadeFit = S_ and spades(south)>2 and hcp(south,spades)>1 and hcp(south)<(11-spades(south)) and hcp(south)>4 and shape(south,any 4432+any 5431+any 5422)
heartFit = H_ and hearts(south)>2 and hcp(south,hearts)>1 and hcp(south)<(11-hearts(south)) and hcp(south)>4 and shape(south,any 4432+any 5431+any 5422)
sRaise = (spadeFit or heartFit)

# Define West's fit for Maximal suit and tolerance for non-maximal suit
wS = e1S and spades(west)==2
wH = S_ ? e2H and hearts(west)>2   and hcp(west,hearts)>1   : e2H and hearts(west)==2
wD = H_ ? e2D and diamonds(west)>2 and hcp(west,diamonds)>1 : e2D and diamonds(west)==2
wC = e2C and clubs(west)==2

# West bids 3 of the maximal suit
maximalRaise = ((spadeFit and wH) or (heartFit and wD)) and hcp(west)>6
maximalNewSuit = ((spadeFit and (wC or wD) and (hearts(west)>4)) or ((heartFit and (wS or wC)) and diamonds(west)>4)) and hcp(west)>9
maximalFreeBid = eP and ((spadeFit and hearts(west)>6) or (heartFit and diamonds(west)>6)) and hcp(west)>8 and shape(west,any 0xxx+any 1xxx-any 85xx-any 75xx)

w3Max = (maximalRaise or maximalNewSuit or maximalFreeBid) and not eX

# Now do it
n1M and sRaise and w3Max

action
average "hcp(north)   " hcp(north),
average "hcp(south)   " hcp(south),
average "hcp(east)    " hcp(east),
average "hcp(west)    " hcp(west),
average "N 3M         " 100 * (hcp(north)<15),
average "N X          " 100 * (hcp(north)>14),
average "N losers<6   " 100 * (losers(north)<6),
average "N losers>5   " 100 * (losers(north)>5),
average "Max Raise    " 100 * maximalRaise,
average "Max New Suit " 100 * maximalNewSuit,
average "Max Free Bid " 100 * maximalFreeBid,


printpbn

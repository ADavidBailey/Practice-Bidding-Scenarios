# button-text: Maximal Double
# scenario-title: Maximal Overcall Double:  After we've bid and raised a major suit
# Maximal_Double
dealer north


# Add 1M - 3M inviting, add Maximal Doubles, kill Reverse Bergen, Kill Support 1N, kill Two-way game tries
# convention-card: 21GF-MaximalDouble
# auction-filter: 1H .* 2H 3D|1S .* 2S 3H

# Continuations P3X4: HCP 1[2-9](?:.*\n){12}(1H (\d.|Pass|X) 2H 3D|1S (\d.|Pass|X) 2S 3H)\n[P3X45] // 28+66+55+54+00 = 326
# West Max Raise    :  (1H 2D 2H 3H|1S 2H 2S 3H)\n[P3X45]                                          // 20+33+04+13+00 =  74
# West Max resp to X:  (1H X 2H 3D|1S X 2S 3H)\n[P3X45]                                            // 06+06+00+02+00 =   9
# West Free Bid     :  (1H Pass 2H 3D|1S Pass 2S 3H)\n[P3X45]                                      // 34+67+52+69+01 = 227
# None of the above :Auction.....\n(?!(1H 2D 2H 3D|1S 2H 2S 3H)\n[P3X45]|(1H X 2H 3D|1S X 2S 3H)|(1H Pass 2H 3D|1S Pass 2S 3H)\n[P3X45])

# Maximal Overcall Double

##### Imported Script - Calculate Total Points for South #####

## Calculate TP for South
# Calculate South's Short-Suit Penalty Points
sSp = spades(south)<3 and hcp(south,spades)>1     ? 1 : 0
sHp = hearts(south)<3 and hcp(south,hearts)>1     ? 1 : 0
sDp = diamonds(south)<3 and hcp(south,diamonds)>1 ? 1 : 0
sCp = clubs(south)<3 and hcp(south,clubs)>1       ? 1 : 0

# Calculate South's Short-Suit Points
v1 = shape(south, any 0xxx) ? 3 : 0
s1 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
s2 = shape(south, any 11xx) ? 2 : 0
d1 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
d2 = shape(south, any 22xx) ? 1 : 0
d3 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + v1+s1+s2+d1+d2+d3 -sSp-sHp-sDp-sCp

### End of Calculate TP for South ###
# Defines sTP

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(north) + shape(north,5xxx+x5xx)
nt1 = shape(north, any 5332+any 4432+any 4333) and hcp(north)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(north, any 5422-5xxx-x5xx) and hcp(north)>14 and hcp(north)<17

gibNT = nt1 or nt2

# North opens 1M
S_ = spades(north)>4 and hearts(north)<5 and diamonds(north)<=5 and clubs(north)<=5
H_ = hearts(north)>4 and spades(north)<5 and diamonds(north)<=5 and clubs(north)<=5
n1M = (S_ or H_) and hcp(north)>11 and hcp(north)<22 and losers(north)>3 and losers(north)<8 and not gibNT

nLP = S_ ? spades(north) - 4 : hearts(north) - 4
nTP = hcp(north) + nLP

# Define East's Total Points -- discount short-suit points
eSp = spades(east)<3   and hcp(east,spades)>1   ? 1 : 0
eHp = hearts(east)<3   and hcp(east,hearts)>1   ? 1 : 0
eDp = diamonds(east)<3 and hcp(east,diamonds)>1 ? 1 : 0
eCp = clubs(east)<3    and hcp(east,clubs)>1    ? 1 : 0
eTP = hcp(east) - eSp - eHp - eDp - eCp

# Define East overcalls in Maximal suit
e2D = H_ and diamonds(east)>5 and top3(east,diamonds)>1 and spades(east)<5
e2H = S_ and hearts(east)>5   and top3(east,hearts)>1
eShape = shape(east,any 7xxx +any 6xxx -any 76xx -any 75xx -any 66xx -any 65xx)
eRange = eTP>10 and eTP<14 and controls(east)>2
eMaxOC = (e2D or e2H) and eRange and eShape

# avoid other overcalls
eWJS    = shape(east,any 9xxx +any 8xxx +any 7xxx +any 6xxx) and hcp(east)<11
e2suits = shape(east,any 85xx +any 76xx + any 75xx +any 65xx +any 55xx)
HS = spades(east)>4 and hcp(east)>5
HC = clubs(east)>4 and hcp(east)>9
SC = spades(east)>4 and hcp(east)>9
SD = diamonds(east)>4 and hcp(east)>9
ePass = not (eWJS or e2suits or HS or HC or SC or SD)

# Define East Takeout Double for West's max response
eHCP = hcp(east,spades)<7 and hcp(east,hearts)<7 and hcp(east,diamonds)<7 and hcp(east,clubs)<7  // no concentration
eHX = H_ and hearts(east)<3 and shape(east,any 5440 +any 5431 +any 5332 +any 4441 -5xxx) and hcp(east,hearts)==0 and spades(west)<4
eSX = S_ and spades(east)<3 and shape(east,any 5440 +any 5431 +any 5332 +any 4441 -x5xx) and hcp(east,spades)==0 and hearts(west)<4
eX = (eHX or eSX) and eHCP and hcp(east)>11 and not eMaxOC

# Define East bids
eBids = ePass or eX or eMaxOC

# Define South's fits:  3+, 4+, and 5+
sFit3  = S_ and spades(south)>2
hFit3  = H_ and hearts(south)>2
fit3   = sFit3 or hFit3 and spades(south)<5
sFit4  = S_ and spades(south)>3
hFit4  = H_ and hearts(south)>3
fit4   = sFit4 or hFit4 and spades(south)<5
sFit5  = S_ and spades(south)>4
hFit5  = H_ and hearts(south)>4
fit5   = sFit5 or hFit5 and spades(south)<5

# Define South's major suit raises
# Preemptive raise 1M-?-4M -- 5+ cards; 9- HCP; 6+ total points
PR      = fit5 and hcp(south)<10 and sTP>5

# Free jump raise  1M-X-3M -- 4+ cards; 8- HCP; 7+ total points
FJR    = fit4 and hcp(south)<9 and sTP>6 and eMaxOC

# Free Major raise 1M-X-2M -- 3+ cards;       ; 6-10 total points
FMR    = fit3 and sTP>5 and sTP<11 and eMaxOC and not FJR

# Limit raise     1M-P-3M -- 4+ cards; 10-12 total points
LR     = fit4 and sTP>9 and sTP<13 and ePass

# Simple raise     1M-P-2M -- 3+ cards; 7-10 total points
SR     = fit3 and sTP>6 and sTP<11 and not shape(south,any 4333) and not LR

# Define South's 2-level raise
sRaise = (SR or FMR) and controls(south)>1 and not PR and shape(south,xxxx -any 7xxx -any 6xxx)

# Define West's Total Points -- discount short-suit points
wSp = spades(west)<3   and hcp(west,spades)>1   ? 1 : 0
wHp = hearts(west)<3   and hcp(west,hearts)>1   ? 1 : 0
wDp = diamonds(west)<3 and hcp(west,diamonds)>1 ? 1 : 0
wCp = clubs(west)<3    and hcp(west,clubs)>1    ? 1 : 0
wTP = hcp(west) - wSp - wHp - wDp - wCp

# Define West's raise of Maximal overcall
wHR = e2H and shape(west,x3xx+x4xx) and wTP>1
wDR = e2D and shape(west,xx3x+xx4x) and hcp(west,diamonds)>1
wRaise = (wHR or wDR) and wTP>6 and wTP<11 and shape(west,xxxx -5xxx -x5xx -any 7xxx -any 6xxx) // avoid new suit

# Define West's response to X     // avoid preference to 4-card major
wD = H_ and diamonds(west)>4
wH = S_ and hearts(west)>4
wRespToX = eX and (wD or wH) and shape(west,xxxx -4xxx -x4xx -any 76xx -any 75xx -any 65xx -any 55xx) and wTP>6 and wTP<11

# Define West's free bid of Maximal suit.
wGD = H_ and top4(west,diamonds)>2  and shape(west,xx6x)
wGH = S_ and top4(west,hearts)>2    and shape(west,x6xx)
wFreeBid = ePass and (wGD or wGH) and shape(west,any 0xxx +any 1xxx -any 76xx -any 75xx -any 66xx -any 65xx) and wTP>10 and wTP<14

# Define West's 3 of the Maximal suit
w3Max = (wRaise or wRespToX or wFreeBid) 

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###


nMin = nTP<15
nMax = nTP>14

levMax = nMax and keep
levMin = nMin and keep

levelTheDeal = (levMax or levMin)

# Now do it
n1M and eBids and sRaise and w3Max
and levelTheDeal

action
average "free bid       " 100 * wFreeBid,
average "raise          " 100 * wRaise,
average "response to X  " 100 * wRespToX,
average "north 12-14 TP " 100 * nMin,
average "north 15+   TP " 100 * nMax,
frequency "HCP North    "  (nTP, 11, 23),


printpbn

# button-text: GIB NT-P-2C-bid
# scenario-title: After 1NT-P-2C-bid...  What now???
# GIB_NT-P-2C-BID
dealer south


/*
auction-filter: Auction.....\n1NT?.* Pass.* 2C.* [2-4]
*/

# Stayman

# 1 Notrump Opening
##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422-5xxx-x5xx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# Defines gibNT
##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# Defines calmWest
##### Imported Script - Calculate Total Points for South #####

## Calculate TP for South
# Calculate South's Short-Suit Penalty Points
sSP = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
sHP = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sDP = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
sCP = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv1 = shape(south, any 0xxx) ? 3 : 0
ss1 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss2 = shape(south, any 11xx) ? 2 : 0
sd1 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd2 = shape(south, any 22xx) ? 1 : 0
sd3 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv1+ss1+ss2+sd1+sd2+sd3 -sSP-sHP-sDP-sCP

### End of Calculate TP for South ###
# Defines sTP
##### Imported Script - Calculate Total Points for East #####

## Calculate TP for East
# Calculate East's Short-Suit Penalty Points
eSP = spades(east)<3   and hcp(east,spades)>0   ? 1 : 0
eHP = hearts(east)<3   and hcp(east,hearts)>0   ? 1 : 0
eDP = diamonds(east)<3 and hcp(east,diamonds)>0 ? 1 : 0
eCP = clubs(east)<3    and hcp(east,clubs)>0    ? 1 : 0

# Calculate East's Short-Suit Points
ev1 = shape(east, any 0xxx) ? 3 : 0 // allow for 1 void
es1 = shape(east, any 1xxx) ? 2 : 0 // allow for 2 singletons
es2 = shape(east, any 11xx) ? 2 : 0
ed1 = shape(east, any 2xxx) ? 1 : 0 // allow for 3 doubletons
ed2 = shape(east, any 22xx) ? 1 : 0
ed3 = shape(east, any 222x) ? 1 : 0

# Calculate East's Total Points
eTP = hcp(east) + ev1+es1+es2+ed1+ed2+ed3 -eSP-eHP-eDP-eCP

### End of Calculate TP for East ###
# Defines eTP

# Calculate North's Short-Suit Penalty Points
nSP = spades(north)<3 and hcp(north,spades)>0     ? 1 : 0
nHP = hearts(north)<3 and hcp(north,hearts)>0     ? 1 : 0
nDP = diamonds(north)<3 and hcp(north,diamonds)>0 ? 1 : 0
nCP = clubs(north)<3 and hcp(north,clubs)>0       ? 1 : 0
nTP = hcp(north) -nSP -nHP -nDP -nCP

# Boost quality of North's major
nQS = spades(north)==4 and hcp(north,spades)>1
nQH = hearts(north)==4 and hcp(north,hearts)>1
nQ  = nQS or nQH

# Define various 2C bids -- GIB does not play Drop Dead or Crawlling Stayman
smolen  = shape(north,54xx +45xx) and hcp(north)>9
stayman = shape(north,4xxx +x4xx -any 4441 - any 4450 - any 9xxx -any 8xxx -any 7xxx -any 6xxx) and nTP>7 and nQ and not smolen  // avoid splinter
n2Clubs = stayman or smolen

# Define East's 8421 Points for each suit
eC8 = hascard(east,AC) ? 8 : 0
eC4 = hascard(east,KC) ? 4 : 0
eC2 = hascard(east,QC) ? 2 : 0
eC1 = hascard(east,JC) ? 1 : 0
eC8421 = eC8+eC4+eC2+eC1

eD8 = hascard(east,AD) ? 8 : 0
eD4 = hascard(east,KD) ? 4 : 0
eD2 = hascard(east,QD) ? 2 : 0
eD1 = hascard(east,JD) ? 1 : 0
eD8421 = eD8+eD4+eD2+eD1

eH8 = hascard(east,AH) ? 8 : 0
eH4 = hascard(east,KH) ? 4 : 0
eH2 = hascard(east,QH) ? 2 : 0
eH1 = hascard(east,JH) ? 1 : 0
eH8421 = eH8+eH4+eH2+eH1

eS8 = hascard(east,AS) ? 8 : 0
eS4 = hascard(east,KS) ? 4 : 0
eS2 = hascard(east,QS) ? 2 : 0
eS1 = hascard(east,JS) ? 1 : 0
eS8421 = eS8+eS4+eS2+eS1

# Define East's twice biddable suits: (5 cards or 4 cards with (3 of top 5 or more than 4  8421 points)) + 2 cards
# Only quality suits count for 2-level bids

# East has one 6+card D, H, or S suit, less than 5 clubs (avoid X), no second suit longer than 4-cards, a singleton or void
eShape = shape(east, any 9xxx +any 8xxx+ any 7xxx +any 6xxx -any 85xx -any 76xx -any 66xx -any 65xx) and shape(east, any 1xxx +any 0xxx) and clubs(south)<5

# Twice Biddable suits w/3 of top 5
eTBD3 = eShape and diamonds(east)>5 and (top5(east,diamonds)==3 or eD8421>4)
eTBH3 = eShape and hearts(east)>5   and (top5(east,hearts)==3   or eH8421>4)
eTBS3 = eShape and spades(east)>5   and (top5(east,spades)==3   or eS8421>4)
eTB3  = eTBD3 or eTBH3 or eTBS3

# Twice Biddable suits w/4 of top 5 -- this is also Strong Rebiddable & 8421 must be >4
eTBD4 = diamonds(east)>3 and (top5(east,diamonds)==4)
eTBH4 = hearts(east)>3   and (top5(east,hearts)==4)
eTBS4 = spades(east)>3   and (top5(east,spades)==4)
eTB4  = eTBD4 or eTBH4 or eTBS4

# Twice Biddable suits w/5 of top 5 -- this is also Strong Rebiddable
eTBD5 = diamonds(east)>3 and (top5(east,diamonds)==5)
eTBH5 = hearts(east)>3   and (top5(east,hearts)==5)
eTBS5 = spades(east)>3   and (top5(east,spades)==5)
eTB5  = eTBD5 or eTBH5 or eTBS5


# Define East's bids over 2C -- Twice Bdiddable with extra strength


eSuits  = ((eTB3 and eTP>14) or (eTB4 and eTP>13) or (eTB5 and eTP>12))
e2Range = hcp(east)>9 and eTP<17

e2D = diamonds(east)==6 and e2Range and eSuits
e2H = hearts(east)==6   and e2Range and eSuits
e2S = spades(east)==6   and e2Range and eSuits
e2B = e2D or e2H or e2S

# 3- and 4-level suits require length and may have suit-quality
e3Range = hcp(east)>9 and eTP<17
e3D = diamonds(east)>6 and eSuits and e3Range and not e2D
e3H = hearts(east)>6   and eSuits and e3Range and not e2H
e3S = spades(east)>6   and eSuits and e3Range and not e2S
e3B = e3D or e3H or e3S

# Define East's 4-level weak bids
e4wRange = hcp(east)<10 and eTP>9
e4Dw = diamonds(east)>7 and top4(east,diamonds)>2 and e4wRange and not (e2D or e3D)
e4Hw = hearts(east)>7   and top4(east,hearts)>2   and e4wRange and not (e2H or e3H)
e4Sw = spades(east)>7   and top4(east,spades)>2   and e4wRange and not (e2S or e3S)
e4Bw = e4Dw or e4Hw or e4Sw

# Define East's 4-level strong bids
e4sRange = hcp(east)>9 and eTP>16
e4Ds = diamonds(east)>7 and top4(east,diamonds)>2 and e4sRange and not (e2D or e3D)
e4Hs = hearts(east)>7   and top4(east,hearts)>2   and e4sRange and not (e2H or e3H)
e4Ss = spades(east)>7   and top4(east,spades)>2   and e4sRange and not (e2S or e3S)
e4Bs = e4Ds or e4Hs or e4Ss

e4B = e4Bw or e4Bs

# Define East's other hands w/long-suits
eBOther = shape(east,any 9xxx +any 8xxx +any 7xxx +any 6xxx) and clubs(east)<6 and not (e2B or e3B or e4B)

# Now do it
gibNT and sTP<19 and calmWest and n2Clubs and eShape and (e2B or e3B or e4B)

action
average "smolen      " 100 * smolen,
average "stayman     " 100 * stayman,
average "2D          " 100 * e2D,
average "2H          " 100 * e2H,
average "2S          " 100 * e2S,
average "3D          " 100 * e3D,
average "3H          " 100 * e3H,
average "3S          " 100 * e3S,
average "4 weak      " 100 * e4Bw,
average "4 strong    " 100 * e4Bs,

printpbn

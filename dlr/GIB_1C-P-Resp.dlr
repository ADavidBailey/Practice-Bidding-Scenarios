# button-text: GIB after 1C
# scenario-title: GIB responses to 1!C
# GIB_1C-P-Resp
dealer south


/*
South opens 1C and North responds 1D, 1H, 1S, 1N, 2C, 2N, 3C, 3D, 3H, 3S, or 3N.

convention-card: 21GF-GIB
auction-filter: Auction.....\n1C +Pass +(1[DHS]|[1-3]NT?|[2-3]C|3[CDHS]) +

*/


##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# Defines calmOpps
##### Imported Script -- Predict Opening 1-Bid #####

# Changed to use gibNT 3/30/2025

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422 -5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22
threeNT = TwoNtShape and hcp(south)>24 and hcp(south)<28

# Define Game Forcing 2C
case1 = hcp(south)>21
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
# Defines oneClub
### Imported script: TP (Total Points)

//   Note:  My initial implementation used shape staments for counting voids, singletons, and doubletons.
//   Dealer has a limit 32, so I eliminated the shape statements in this script.

## Calculate TP for North
# Calculate North's Short-Suit Penalty Points
nsPP = spades(north)  <3 and hcp(north,spades)  >0 ? 1 : 0
nhPP = hearts(north)  <3 and hcp(north,hearts)  >0 ? 1 : 0
ndPP = diamonds(north)<3 and hcp(north,diamonds)>0 ? 1 : 0
ncPP = clubs(north)   <3 and hcp(north,clubs)   >0 ? 1 : 0
nPP = nsPP+nhPP+ndPP+ncPP

# Calculate North's Short-Suit Points
ns0 = spades(north)  ==0 ? 3 : 0   // 3 for each void
nh0 = hearts(north)  ==0 ? 3 : 0
nd0 = diamonds(north)==0 ? 3 : 0
nc0 = clubs(north)   ==0 ? 3 : 0

ns1 = spades(north)  ==1 ? 2 : 0   // 2 for each singleton
nh1 = hearts(north)  ==1 ? 2 : 0
nd1 = diamonds(north)==1 ? 2 : 0
nc1 = clubs(north)   ==1 ? 2 : 0

ns2 = spades(north)  ==2 ? 1 : 0   // 1 for each doubleton
nh2 = hearts(north)  ==2 ? 1 : 0
nd2 = diamonds(north)==2 ? 1 : 0
nc2 = clubs(north)   ==2 ? 1 : 0

nSSP = ns0+nh0+nd0+nc0+ns1+nh1+nd1+nc1+ns2+nh2+nd2+nc2

tpNorth = hcp(north) + nSSP - nPP

## Calculate TP for East
# Calculate East's Short-Suit Penalty Points
esPP = spades(east)  <3 and hcp(east,spades)  >0 ? 1 : 0
ehPP = hearts(east)  <3 and hcp(east,hearts)  >0 ? 1 : 0
edPP = diamonds(east)<3 and hcp(east,diamonds)>0 ? 1 : 0
ecPP = clubs(east)   <3 and hcp(east,clubs)   >0 ? 1 : 0
ePP = esPP+ehPP+edPP+ecPP

# Calculate East's Short-Suit Points
es0 = spades(east)==0     ? 3 : 0
eh0 = hearts(east)==0     ? 3 : 0
ed0 = diamonds(east)==0   ? 3 : 0
ec0 = clubs(east)==0      ? 3 : 0

es1 = spades(east)==1     ? 2 : 0
eh1 = hearts(east)==1     ? 2 : 0    
ed1 = diamonds(east)==1   ? 2 : 0
ec1 = clubs(east)==1      ? 2 : 0

es2 = spades(east)==2     ? 1 : 0
eh2 = hearts(east)==2     ? 1 : 0
ed2 = diamonds(east)==2   ? 1 : 0
ec2 = clubs(east)==2      ? 1 : 0

eSSP = es0+eh0+ed0+ec0+es1+eh1+ed1+ec1+es2+eh2+ed2+ec2

tpEast = hcp(east) + eSSP - ePP


## Calculate TP for South
# Calculate South's Short-Suit Penalty Points
ssPP = spades(south)  <3 and hcp(south,spades)  >0 ? 1 : 0   
shPP = hearts(south)  <3 and hcp(south,hearts)  >0 ? 1 : 0
sdPP = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
scPP = clubs(south)   <3 and hcp(south,clubs)   >0 ? 1 : 0
sPP = ssPP+shPP+sdPP+scPP

# Calculate South's Short-Suit Points
ss0 = spades(south)==0     ? 3 : 0
sh0 = hearts(south)==0     ? 3 : 0
sd0 = diamonds(south)==0   ? 3 : 0
sc0 = clubs(south)==0      ? 3 : 0   

ss1 = spades(south)==1     ? 2 : 0
sh1 = hearts(south)==1     ? 2 : 0
sd1 = diamonds(south)==1   ? 2 : 0
sc1 = clubs(south)==1      ? 2 : 0

ss2 = spades(south)==2     ? 1 : 0
sh2 = hearts(south)==2     ? 1 : 0
sd2 = diamonds(south)==2   ? 1 : 0
sc2 = clubs(south)==2      ? 1 : 0

sSSP = ss0+sh0+sd0+sc0+ss1+sh1+sd1+sc1+ss2+sh2+sd2+sc2

tpSouth = hcp(south) + sSSP - sPP


## Calculate TP for West
# Calculate West's Short-Suit Penalty Points
wsPP = spades(west)  <3 and hcp(west,spades)  >0 ? 1 : 0
whPP = hearts(west)  <3 and hcp(west,hearts)  >0 ? 1 : 0
wdPP = diamonds(west)<3 and hcp(west,diamonds)>0 ? 1 : 0
wcPP = clubs(west)   <3 and hcp(west,clubs)   >0 ? 1 : 0
wPP = wsPP+whPP+wdPP+wcPP

# Calculate West's Short-Suit Points
ws0 = spades(west)==0     ? 3 : 0
wh0 = hearts(west)==0     ? 3 : 0
wd0 = diamonds(west)==0   ? 3 : 0
wc0 = clubs(west)==0      ? 3 : 0

ws1 = spades(west)==1     ? 2 : 0
wh1 = hearts(west)==1     ? 2 : 0
wd1 = diamonds(west)==1   ? 2 : 0
wc1 = clubs(west)==1      ? 2 : 0

ws2 = spades(west)==2     ? 1 : 0
wh2 = hearts(west)==2     ? 1 : 0
wd2 = diamonds(west)==2   ? 1 : 0
wc2 = clubs(west)==2      ? 1 : 0

wSSP = ws0+wh0+wd0+wc0+ws1+wh1+wd1+wc1+ws2+wh2+wd2+wc2

tpWest = hcp(west) + wSSP - wPP

### End of Imported Script: TP
# Defines tpSouth, tpWest, tpNorth

## Define South's 1C opening -- GIB doesn't open all 11 HCP hands
cOK1 = hcp(south)==11 and tpSouth==13 and spades(south)==4
cOK2 = hcp(south)==11 and tpSouth==13 and clubs(south)>5
cOK3 = hcp(south)>11
sRange = (cOK1 or cOK2 or cOK3) and hcp(south)<22 and tpSouth>11 and tpSouth<21  // avoid strong 2C
sClubs = oneClub and sRange

# Define suits suitable for 4-card overcall
wAS = hascard(west,AS)
wKS = hascard(west,KS)
wQS = hascard(west,QS)
wJS = hascard(west,JS)
wTS = hascard(west,TS)
w9S = hascard(west,9S)

wAH = hascard(west,AH)
wKH = hascard(west,KH)
wQH = hascard(west,QH)
wJH = hascard(west,JH)
wTH = hascard(west,TH)
w9H = hascard(west,9H)

wAD = hascard(west,AD)
wKD = hascard(west,KD)
wQD = hascard(west,QD)
wJD = hascard(west,JD)
wTD = hascard(west,TD)
w9D = hascard(west,9D)

# Define West's possible overcall suits: AKxx，AQJx，AQTx，AQ9x，AJT9，KQJx，or KQTx
wS4 = ((wAS and wKS) or (wAS and wQS and (wJS or wTS or w9S)) or (wAS and wJS and wTS and w9S) or (wKS and wQS and (wJS or wTS))) and spades(west)==4
wH4 = ((wAH and wKH) or (wAH and wQH and (wJH or wTH or w9H)) or (wAH and wJH and wTH and w9H) or (wKH and wQH and (wJH or wTH))) and hearts(west)==4
wD4 = ((wAD and wKD) or (wAD and wQD and (wJD or wTD or w9D)) or (wAD and wJD and wTD and w9D) or (wKD and wQD and (wJD or wTD))) and diamonds(west)==4
wss = (wS4 and wH4) or (wS4 and wD4) or (wH4 or wD4) 

# Avoid Takeout Double -- 13/14 HCP and equal length in the majors
X = (hcp(west)==13 or hcp(west)==14) and spades(west)==hearts(west)==(3 or 4)  // I'm out of 'shape' statements
reverse = shape(south,45xx+x45x+xx45) and hcp(south)>16

wRange = tpWest>=12 and tpWest<=14 and not X

# Define West's overcalls
cOC = sClubs     and (wD4 or wH4 or wS4) and clubs(west)==3
dOC = oneDiamond and (wH4 or wS4)        and diamonds(west)==3
hOC = oneHeart   and wS4                 and hearts(west)==3
wOvercalls = (cOC or dOC or hOC) and wRange and shape(west,any 4432 +any 4333)


TakeOutDouble = (balWest and hcp(west)>10 and clubs(west)==2) or (balEast and hcp(east)>10 and clubs(east)==2)  // Must know opening suit
oppsPass = calmOpps and not TakeOutDouble

## Define things...

# Define North's suit lengths
nC = clubs(north)
nD = diamonds(north)
nH = hearts(north)
nS = spades(north)

# Define North's rebiddable suit
nRS = nS>5 or (nS==5 and top5(north,spades)>2)
nRH = nH>5 or (nH==5 and top5(north,hearts)>2)
nRD = nD>5 or (nD==5 and top5(north,diamonds)>2)
nRC = nC>5 or (nC==5 and top5(north,clubs)>2)
nRebiddableSuit = nRS or nRH or nRD or nRC

# GIB defines partial stop: length+HCP = 4 (Qx or Jxx).  But, it bids differently with 3-cards or honor doubleton
dStop = (hcp(north,diamonds) + nD)>3 ? 2 : 0      // Dealer doesn't allow fractions; so 2 pts for stop
hStop = (hcp(north,hearts)   + nH)>3 ? 2 : 0
sStop = (hcp(north,spades)   + nS)>3 ? 2 : 0

dHalf = not dStop and ((hcp(north,diamonds)>0) + nD)==3   // and 1 pt for half-stop: any 3-cards or honor doubleton
hHalf = not hStop and ((hcp(north,hearts)>0)   + nH)==3
sHalf = not sStop and ((hcp(north,spades)>0)   + nS)==3

stops = dStop + hStop + sStop + dHalf + hHalf +sHalf

# I've exceeded the number or 'shape' statements that Dealer allows, I'll use the less efficient version.
s3334 = nS==3 and nH==3  and nD==3
s3343 = nS==3 and nH==3  and nD==4
sxx44 = (nS==3 or nS==2) and nD==4 and nC==4  // 3244 or 2344
nBal  = (s3334 or s3343 or sxx44)  // no 4-card major


nRange = nRebiddableSuit ? tpNorth>5 and tpNorth<17 : tpNorth>5   // no upper range without rebiddable suit

## 1. Major responses

rS1 = nS>4 and nS>=nH                   // higher ranking of 5+card suits
rS2 = tpNorth<13 and nS==4 and nS>nH        // skip longer D w/<13
rS3 = nS==4 and nS>nH and nD<5          // prefer 4-card H/S to 4-card D
r1S = rS1 or rS2 or rS3

rH1 = nH>4 and nH>nS
rH2 = tpNorth<13 and nH==4 and nH>=nS
rH3 = nH==4 and nD<5  
r1H = (rH1 or rH2 or rH3) and not r1S

rM = r1H or r1S

## 2. Club responses

r3x   = nC>4 and shape(north,any 0xxx +any 1xxx) and tpNorth>13         and not rM          // splinter
r2C34 = s3334       and hcp(north)>9  and hcp(north)<13 and stops<5 and not rM
r2C44 = sxx44       and hcp(north)>9  and hcp(north)<13 and stops<4 and not rM
r2C3N = s3334+sxx44 and hcp(north)>12 and hcp(north)<16             and not rM          // GIB goes through 2C w/a bal 13-15
r2Cx5 = nC>4        and nD<5 and hcp(north)>9                       and not (rM or r3x)
r2C = r2C34 or r2C44 or r2C3N or r2Cx5
r3C = nC>4 and nD<5 and hcp(north)<10 and tpNorth>4                     and not (rM or r3x)

rC  = r2C34 or r2C44 or r2C3N or r2Cx5 or r3C or r3x

## 3. Notrump responses

r1N  = hcp(north)>5  and hcp(north)<11 and nBal and not rC

r2Na = hcp(north)>10 and hcp(north)<13 and sxx44 and stops>3 and hcp(north,diamonds)>0 and not rC // prefer 1D or 2C
r2Nb = hcp(north)>10 and hcp(north)<13 and s3334 and stops>4                           and not rC // no alternative
r2Nc = hcp(north)>10 and hcp(north)<13 and s3343 and stops>5                           and not rC // prefer 1D
r2N = r2Na or r2Nb or r2Nc

r3N = s3343 and hcp(north)>12 and hcp(north)<16  and nBal  and stops>5                 and not rC

rN  = r1N or r2N or r3N

## 4. Diamonds

r1D = nD>3 and not (rM or rN or rC)

// Change the following line to get the responses you want --------
//
nResponds = r1D or r1H or r1S or r1N or r2C or r2N or r3C or r3x or r3N
//
// ----------------------------------------------------------------

sClubs and sRange and not wOvercalls and nRange and nResponds and oppsPass

action
average "1D   " 100 * r1D,
average "1H   " 100 * r1H,
average "1S   " 100 * r1S,
average "1N   " 100 * r1N,
average "2C   " 100 * r2C,
average "2N   " 100 * r2N,
average "3C   " 100 * r3C,
average "3x   " 100 * r3x,
average "3N   " 100 * r3N,


printpbn

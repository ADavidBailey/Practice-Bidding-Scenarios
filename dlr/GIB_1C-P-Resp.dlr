# alias: GibRespTo1C
# button-text: GIB after 1C
# scenario-title: --- GIB responses to 1!C
# dealer-position: S
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\n1C +Pass +(1[DHS]|[1-3]NT?|[2-3]C|3[CDHS]) +
# convention-card: 21GF-GIB

/*@chat
--- GIB responses to 1!C
South opens 1!C and North responds w/anything other than Soloway JS.
You may go to 'Deal source' to get specific responses. Near the bottom you'll
find the line that starts with nResponds = ... pick the response(s) you want.
@chat*/
dealer south

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####
# Defines calmOpps
##### Imported Script -- Predict Opening 1-Bid South #####
# 01/19/2026 -- Changed to use TP and separate ranges for C, D, and majors
# 03/30/2025 -- Changed to use gibNT

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422 -5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

## Calculate TP for South -- NOTE:  This uses different variable names than TP to avoid conflicts when both are used.
# Calculate South's Short-Suit Penalty Points
ssp = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
shp = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sdp = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
scp = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv01 = shape(south, any 0xxx) ? 3 : 0
ss01 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss02 = shape(south, any 11xx) ? 2 : 0
sd01 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd02 = shape(south, any 22xx) ? 1 : 0
sd03 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv01+ss01+ss02+sd01+sd02+sd03 -ssp-shp-sdp-scp

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22

# Define Gambling 3N
g3Shape = shape(south,any 9xxx +any 8xxx +any 7xxx -any 4xxx -any 5xxx -any 6xxx)
threeNT = (top3(south,clubs)==3 and clubs(south)>6) or (top3(south,diamonds)==3 and diamonds(south)>6) and hcp(south)<12 and g3Shape

# Define Game Forcing 2C
case1 = hcp(south)>18 and sTP>21
case2 = hcp(south)>18 and sTP>19 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and sTP>19 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3)

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT

# Define quality and length lower limits for 10 HCP openers

q2of3 = (top3(south,spades)>1 or top3(south,hearts)>1) and controls(south)>3
lgt5  = (spades(south)>4 or hearts(south)>4) and shape(south,any 7xxx +any 64xx +any 55xx +any 1xxx+any 0xxx -any 8xxx -any 54xx) and sTP>(hcp(south) +2)

# Predict South's Opening suit
# What's the longest suit?
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)

## GIB has different requirements for 1H/S, 1D, and 1C openings 
#Define range for 1 H/S
lS = s>4 and s>=h and s>=d and s>=c
lH = h>4 and h>s and h>=d and h>=c
lM = lS or lH

hsOK1 = lM and hcp(south)>11
hsOK2 = lM and hcp(south)>10 and sTP>12 and not hsOK1
hsOK3 = lM and hcp(south)>9  and lgt5 and q2of3 and not (hsOK1 or hsOK2)
hsRange = (hsOK1 or hsOK2 or hsOK3) and not P2

# Define 1D Range
dOK1 = hcp(south)>11 and (diamonds(south)>3 or shape(south, 4432))
dOK2 = hcp(south)>9  and sTP>12 and diamonds(south)>5 and not dOK1
dRange = (dOK1 or dOK2) and not P2

# Define 1C Range
cOK1 = hcp(south)>10 and sTP>12 and spades(south)==4
cOK2 = hcp(south)>9  and sTP>12 and clubs(south)>5 and not cOK1
cOK3 = hcp(south)>11 and clubs(south)>2            and not (cOK1 or cOK2)
cRange = (cOK1 or cOK2 or cOK3) and clubs(south)>2 and clubs(south)>diamonds(south) and not P2

oS = s>4 and s>=h and s>=d and s>=c and hsRange
oH = not oS and h>4 and h>=d and h>=c and hsRange
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and dRange
oC = not (oS or oH or oD) and cRange
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
# Defines oneClub
### Imported script: TP (Total Points)
//   Total Points = HCP + Short-Suit Points - Penalty Points
//   Short-Suit Points: 3 for void, 2 for singleton, 1 for doubleton
//   Penalty Points: 1 for each honor (K/Q/J) in a short suit (devalues wasted honors like Kx, Qx)

## Calculate TP for North
# Short-Suit Points: void=3, singleton=2, doubleton=1
nSpadeSSP = spades(north)<3   ? 3-spades(north)   : 0
nHeartSSP = hearts(north)<3   ? 3-hearts(north)   : 0
nDiamSSP  = diamonds(north)<3 ? 3-diamonds(north) : 0
nClubSSP  = clubs(north)<3    ? 3-clubs(north)    : 0
nSSP = nSpadeSSP + nHeartSSP + nDiamSSP + nClubSSP

# Penalty Points: deduct 1 for honors in short suits
nSpadePP = spades(north)<3   and hcp(north,spades)>0   ? 1 : 0
nHeartPP = hearts(north)<3   and hcp(north,hearts)>0   ? 1 : 0
nDiamPP  = diamonds(north)<3 and hcp(north,diamonds)>0 ? 1 : 0
nClubPP  = clubs(north)<3    and hcp(north,clubs)>0    ? 1 : 0
nPP = nSpadePP + nHeartPP + nDiamPP + nClubPP

tpNorth = hcp(north) + nSSP - nPP

## Calculate TP for East
eSpadeSSP = spades(east)<3   ? 3-spades(east)   : 0
eHeartSSP = hearts(east)<3   ? 3-hearts(east)   : 0
eDiamSSP  = diamonds(east)<3 ? 3-diamonds(east) : 0
eClubSSP  = clubs(east)<3    ? 3-clubs(east)    : 0
eSSP = eSpadeSSP + eHeartSSP + eDiamSSP + eClubSSP

eSpadePP = spades(east)<3   and hcp(east,spades)>0   ? 1 : 0
eHeartPP = hearts(east)<3   and hcp(east,hearts)>0   ? 1 : 0
eDiamPP  = diamonds(east)<3 and hcp(east,diamonds)>0 ? 1 : 0
eClubPP  = clubs(east)<3    and hcp(east,clubs)>0    ? 1 : 0
ePP = eSpadePP + eHeartPP + eDiamPP + eClubPP

tpEast = hcp(east) + eSSP - ePP

## Calculate TP for South
sSpadeSSP = spades(south)<3   ? 3-spades(south)   : 0
sHeartSSP = hearts(south)<3   ? 3-hearts(south)   : 0
sDiamSSP  = diamonds(south)<3 ? 3-diamonds(south) : 0
sClubSSP  = clubs(south)<3    ? 3-clubs(south)    : 0
sSSP = sSpadeSSP + sHeartSSP + sDiamSSP + sClubSSP

sSpadePP = spades(south)<3   and hcp(south,spades)>0   ? 1 : 0
sHeartPP = hearts(south)<3   and hcp(south,hearts)>0   ? 1 : 0
sDiamPP  = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
sClubPP  = clubs(south)<3    and hcp(south,clubs)>0    ? 1 : 0
sPP = sSpadePP + sHeartPP + sDiamPP + sClubPP

tpSouth = hcp(south) + sSSP - sPP

## Calculate TP for West
wSpadeSSP = spades(west)<3   ? 3-spades(west)   : 0
wHeartSSP = hearts(west)<3   ? 3-hearts(west)   : 0
wDiamSSP  = diamonds(west)<3 ? 3-diamonds(west) : 0
wClubSSP  = clubs(west)<3    ? 3-clubs(west)    : 0
wSSP = wSpadeSSP + wHeartSSP + wDiamSSP + wClubSSP

wSpadePP = spades(west)<3   and hcp(west,spades)>0   ? 1 : 0
wHeartPP = hearts(west)<3   and hcp(west,hearts)>0   ? 1 : 0
wDiamPP  = diamonds(west)<3 and hcp(west,diamonds)>0 ? 1 : 0
wClubPP  = clubs(west)<3    and hcp(west,clubs)>0    ? 1 : 0
wPP = wSpadePP + wHeartPP + wDiamPP + wClubPP

tpWest = hcp(west) + wSSP - wPP

## Partnership Totals
tpNS = tpNorth + tpSouth
tpEW = tpEast + tpWest

### End of Imported Script: TP
# Defines tpWest, tpNorth

# Define suits suitable for 4-card overcall
wAS = hascard(west,AS)
wKS = hascard(west,KS)
wQS = hascard(west,QS)
wJS = hascard(west,JS)
wTS = hascard(west,TS)
w9S = hascard(west,9S)

wAH = hascard(west,AH)
wKH = hascard(west,KH)
wQH = hascard(west,QH)
wJH = hascard(west,JH)
wTH = hascard(west,TH)
w9H = hascard(west,9H)

wAD = hascard(west,AD)
wKD = hascard(west,KD)
wQD = hascard(west,QD)
wJD = hascard(west,JD)
wTD = hascard(west,TD)
w9D = hascard(west,9D)

# Define West's possible overcall suits: AKxx，AQJx，AQTx，AQ9x，AJT9，KQJx，or KQTx
wS4 = ((wAS and wKS) or (wAS and wQS and (wJS or wTS or w9S)) or (wAS and wJS and wTS and w9S) or (wKS and wQS and (wJS or wTS))) and spades(west)==4
wH4 = ((wAH and wKH) or (wAH and wQH and (wJH or wTH or w9H)) or (wAH and wJH and wTH and w9H) or (wKH and wQH and (wJH or wTH))) and hearts(west)==4
wD4 = ((wAD and wKD) or (wAD and wQD and (wJD or wTD or w9D)) or (wAD and wJD and wTD and w9D) or (wKD and wQD and (wJD or wTD))) and diamonds(west)==4
wss = (wS4 and wH4) or (wS4 and wD4) or (wH4 or wD4) 

# Avoid Takeout Double -- 13/14 HCP and equal length in the majors
X = (hcp(west)==13 or hcp(west)==14) and spades(west)==hearts(west)==(3 or 4)  // I'm out of 'shape' statements
reverse = shape(south,45xx+x45x+xx45) and hcp(south)>16

wRange = tpWest>=12 and tpWest<=14 and not X

# Define West's actions
cOC = oneClub    and (wD4 or wH4 or wS4) and clubs(west)==3
dOC = oneDiamond and (wH4 or wS4)        and diamonds(west)==3
hOC = oneHeart   and wS4                 and hearts(west)==3
wOvercalls = (cOC or dOC or hOC) and wRange and shape(west,any 4432 +any 4333)

wTakeOutDouble = (balWest and hcp(west)>10 and clubs(west)==2) or (balEast and hcp(east)>10 and clubs(east)==2)  // Must know opening suit
oppsPass = calmOpps and not (wOvercalls or wTakeOutDouble)

## Define things...

# Define North's suit lengths
nC = clubs(north)
nD = diamonds(north)
nH = hearts(north)
nS = spades(north)

# Define North's rebiddable suit
nRS = nS>5 or (nS==5 and top5(north,spades)>2)
nRH = nH>5 or (nH==5 and top5(north,hearts)>2)
nRD = nD>5 or (nD==5 and top5(north,diamonds)>2)
nRC = nC>5 or (nC==5 and top5(north,clubs)>2)
nRebiddableSuit = nRS or nRH or nRD or nRC

# GIB defines partial stop: length+HCP = 4 (Qx or Jxx).  But, it bids differently with 3-cards or honor doubleton
dStop = (hcp(north,diamonds) + nD)>3 ? 2 : 0      // Dealer doesn't allow fractions; so 2 pts for stop
hStop = (hcp(north,hearts)   + nH)>3 ? 2 : 0
sStop = (hcp(north,spades)   + nS)>3 ? 2 : 0

dHalf = not dStop and ((hcp(north,diamonds)>0) + nD)==3   // and 1 pt for half-stop: any 3-cards or honor doubleton
hHalf = not hStop and ((hcp(north,hearts)>0)   + nH)==3
sHalf = not sStop and ((hcp(north,spades)>0)   + nS)==3

stops = dStop + hStop + sStop + dHalf + hHalf +sHalf

# I've exceeded the number or 'shape' statements that Dealer allows, I'll use the less efficient version.
s3334 = nS==3 and nH==3  and nD==3
s3343 = nS==3 and nH==3  and nD==4
sxx44 = (nS==3 or nS==2) and nD==4 and nC==4  // 3244 or 2344
nBal  = (s3334 or s3343 or sxx44)  // no 4-card major


nRange = nRebiddableSuit ? tpNorth>5 and tpNorth<17 : tpNorth>5   // no upper range without rebiddable suit

## 1. Major responses

rS1 = nS>4 and nS>=nH                   // higher ranking of 5+card suits
rS2 = tpNorth<13 and nS==4 and nS>nH        // skip longer D w/<13
rS3 = nS==4 and nS>nH and nD<5          // prefer 4-card H/S to 4-card D
r1S = rS1 or rS2 or rS3

rH1 = nH>4 and nH>nS
rH2 = tpNorth<13 and nH==4 and nH>=nS
rH3 = nH==4 and nD<5  
r1H = (rH1 or rH2 or rH3) and not r1S

rM = r1H or r1S

## 2. Club responses

r3x   = nC>4 and shape(north,3316+3307+any 0xxx +any 1xxx) and tpNorth>13 and not rM    // splinter
r2C34 = s3334       and hcp(north)>9  and hcp(north)<13 and stops<5 and not rM
r2C44 = sxx44       and hcp(north)>9  and hcp(north)<13 and stops<4 and not rM
r2C3N = s3334+sxx44 and hcp(north)>12 and hcp(north)<16             and not rM          // GIB goes through 2C w/a bal 13-15
r2Cx5 = nC>4        and nD<5 and hcp(north)>9                       and not (rM or r3x)
r2C = r2C34 or r2C44 or r2C3N or r2Cx5
r3C = nC>4 and nD<5 and hcp(north)<10 and tpNorth>4                     and not (rM or r3x)

rC  = r2C34 or r2C44 or r2C3N or r2Cx5 or r3C or r3x

## 3. Notrump responses

r1N  = hcp(north)>5  and hcp(north)<11 and nBal and not rC

r2Na = hcp(north)>10 and hcp(north)<13 and sxx44 and stops>3 and hcp(north,diamonds)>0 and not rC // prefer 1D or 2C
r2Nb = hcp(north)>10 and hcp(north)<13 and s3334 and stops>4                           and not rC // no alternative
r2Nc = hcp(north)>10 and hcp(north)<13 and s3343 and stops>5                           and not rC // prefer 1D
r2N = r2Na or r2Nb or r2Nc

r3N = s3343 and hcp(north)>12 and hcp(north)<16  and nBal  and stops>5                 and not rC

rN  = r1N or r2N or r3N

## 4. Diamonds

r1D = nD>3 and not (rM or rN or rC)

// Change the following line to get the responses you want --------
//
nResponds = r1D or r1H or r1S or r1N or r2C or r2N or r3C or r3x or r3N
//
// ----------------------------------------------------------------

oneClub and not wOvercalls and nRange and nResponds and oppsPass

action
average "1C Pass 1D" 100 * r1D,
average "1C Pass 1H" 100 * r1H,
average "1C Pass 1S" 100 * r1S,
average "1C Pass 2C" 100 * r2C,
average "1C Pass 2N" 100 * r2N,
average "1C Pass 3C" 100 * r3C,
average "1C Pass 3x" 100 * r3x,
average "1C Pass 3N" 100 * r3N,

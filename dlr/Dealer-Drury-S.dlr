# Dealer Drury
generate 100000000
produce 500
dealer south


# Reverse Drury


##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

balEast   = shape(east,any 4333 +any 4432)
balWest   = shape(west,any 4333 +any 4432)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####


# South passes
sPasses = shape(south,xxxx-any 9xxx-any 8xxx-any 7xxx-any 6xxx-any 65xx-any 55xx) and hcp(south)>9 and hcp(south)<12

# Define North's opening major
nt =  hcp(north)>14 and hcp(north)<18 and shape(north,any 4333+any 4432+any 5332)
nSpades = spades(north)>4 and hearts(north)<5 and spades(south)>2
nHearts = hearts(north)>4 and spades(north)<5 and hearts(south)>2
nMajor  = (nSpades or nHearts) and not nt

nOpens = nMajor and hcp(north)>11 and hcp(north)<20 and shape(north,any 6xxx+any 5xxx-any 65xx-any 55xx)

# Now do it
sPasses and nOpens and calmOpps

action
average "HCP North=" hcp(north),
average "HCP South=" hcp(south),



printpbn

# button-text: Major Suit Fit (Lev)
# scenario-title: Major Suit Fit
# Major_Suit_Fit
dealer south


# convention-card:
# auction-filter: Auction.....\\n(1H Pass (1NT|[2-7]H))|1S Pass (1NT|[2-7]S)

# Major_Suit_Fit

##### Imported Script -- Predict Opening 1-Bid #####

# Changed to use gibNT 3/30/2025

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, 4252+4225+2452+2425+2254+2245) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22
threeNT = TwoNtShape and hcp(south)>24 and hcp(south)<28

# Define Game Forcing 2C
case1 = hcp(south)>21
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
# Returns oS & oH

# South opens 1 Major and North has a fit
S_ = oS and spades(north)>2
H_ = oH and hearts(north)>2

# Avoid preempts, weak 2, Michaels, and Unusual NT
wPreempt  = shape(west,any 9xxx +any 8xxx +any 7xxx +any 6xxx) and hcp(west)>3
w2Suits   = shape(west,any 76xx +any 75xx +any 66xx +any 65xx +any 55xx) and hcp(west)>3
wOvercall = shape(west,any 5xxx) and hcp(west)>7
wPasses   = hcp(west)<11 and not(wPreempt or w2Suits or wOvercall)

# Define North's 3- 4+ and 5+ card fits
spadeFit3 = S_ and spades(north)==3
spadeFit4 = S_ and spades(north)>3
spadeFit5 = S_ and spades(north)>4
heartFit3 = H_ and hearts(north)==3
heartFit4 = H_ and hearts(north)>3
heartFit5 = H_ and hearts(north)>4

# Avoid a splinter
v1 = shape(north, any 0xxx) ? 3 : 0
s1 = shape(north, any 1xxx) ? 2 : 0
d1 = shape(north, any 2xxx) ? 1 : 0 // allow for 2 doubletons
d2 = shape(north, any 22xx) ? 1 : 0
nTP = v1+s1+d1+d2 + hcp(north)
Splinter = ((oH and hearts(north)>3) or (oS and spades(north)>3)) and shape(north,any 0xxx +any 1xxx) and nTP>10 and nTP<16

preempt = nTP<8             and (spadeFit5 or heartFit5)
faulty  = nTP>3  and nTP<8  and (S_ or H_) and not preempt
simple  = nTP>7  and nTP<11 and (S_ or H_)
invite3 = nTP>10 and nTP<13 and (spadeFit3 or heartFit3) 
invite4 = nTP>10 and nTP<13 and (spadeFit4 or heartFit4)

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###


lev1 = faulty  and keep14
lev2 = simple  and keep06
lev3 = invite3 and keep19
lev4 = invite4 and keep44
lev5 = preempt and keep
levelTheDeal = lev1 or lev2 or lev3 or lev4 or lev5

sOpens = (oH or oS) and hcp(south)>11 and hcp(south)<19
nResponds = (preempt or faulty or simple or invite3 or invite4) and not Splinter

sOpens and wPasses and nResponds
and levelTheDeal

action
average "faulty raise  " 100 * faulty,
average "simple raise  " 100 * simple,
average "3-card invite " 100 * invite3,
average "4-card invite " 100 * invite4,
average "preempt       " 100 * preempt,


printpbn

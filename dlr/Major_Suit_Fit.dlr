# button-text: Major Suit Fit (Lev)
# scenario-title: Major Suit Fit
# Major_Suit_Fit
dealer north


# convention-card:
# auction-filter: Auction.....\\n(1H Pass |1S Pass )
# Major_Suit_Fit

# auction-filter: (Auction.....\\n1[HS] Pass [12]NT)|(Auction.....\\n1H Pass 2H)|(Auction.....\\n1S Pass 2S)

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

#unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
#unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

#balEast   = shape(east,any 4333 +any 4432)
#balWest   = shape(west,any 4333 +any 4432)

balEast    = shape(east,any 4432+any 4333)
unbalEast  = not balEast

balWest    = shape(west,any 4432+any 4333)
unbalWest  = not balWest

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# Returns calmEast

# North opens 1 Major and South has a fit
S_ = spades(north)>4 and spades(north)>= hearts(north) and spades(north)>=diamonds(north) and spades(north)>=clubs(north)
H_ = hearts(north)>4 and hearts(north)>spades(north)   and hearts(north)>=diamonds(north) and hearts(north)>=clubs(north)

spadeFit3 = S_ and spades(south)==3
spadeFit4 = S_ and spades(south)==4
spadeFit5 = S_ and spades(south)>4
heartFit3 = H_ and hearts(south)==3
heartFit4 = H_ and hearts(south)==4
heartFit5 = H_ and hearts(south)>4

fit5  = hcp(south)<8                    and (spadeFit5 or heartFit5)
fit1  = hcp(south)>3 and hcp(south)<8   and (spadeFit3 or heartFit3 or spadeFit4 or heartFit4) and not fit5
fit2  = hcp(south)>7 and hcp(south)<11  and (spadeFit3 or heartFit3 or spadeFit4 or heartFit4)
fit3  = hcp(south)>10 and hcp(south)<13 and (spadeFit3 or heartFit3) 
fit4  = hcp(south)>10 and hcp(south)<13 and (spadeFit4 or heartFit4)

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###


lev1 = fit1 and keep11
lev2 = fit2 and keep06
lev3 = fit3 and keep25
lev4 = fit4 and keep44
lev5 = fit5 and keep
levelTheDeal = lev1 or lev2 or lev3 or lev4 or lev5

hcp(north)>11 and hcp(south)>5 and calmEast
and levelTheDeal

action
average "faulty raise  " 100 * fit1,
average "simple raise  " 100 * fit2,
average "3-card invite " 100 * fit3,
average "4-card invite " 100 * fit4,
average "preempt       " 100 * fit5,


printpbn

# button-text: Xfer after 1M (X)
# scenario-title: Transfers after Opponents double our 1!H or 1!S with or without a fit.
# Xfer_after_1M_X
dealer south


# auction-filter: Auction.....\\n1[HS] X

# South opens 1 Major

##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, 4252+4225+2452+2425+2254+2245) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# Defines gibNT

# South opens 1M
oneH = shape(south,x7xx + x6xx + x5xx -any 75xx -any 65xx -any 55xx)
oneS = shape(south,7xxx + 6xxx + 5xxx -any 75xx -any 65xx -any 55xx)
southM = (oneH or oneS) and not gibNT and hcp(south)>11 and hcp(south)<20

# West Doubles
wShort = (oneH and hearts(west)<3) or (oneS and spades(west)<3)
wShape = shape(west,any 5440 +any 5431 +any 4441 +any 4432)
wHCP   = hcp(west)>11 and hcp(west)<15
westX  = wShort and wShape and wHCP

# North fits and xFit
n3 = (oneH and hearts(north)==3) or (oneS and spades(north)==3)
n4 = (oneH and hearts(north)==4) or (oneS and spades(north)==4)
n5 = (oneH and hearts(north)>4)  or (oneS and spades(north)>4)
xFit = not (n3 or n4 or n5)   // no fit

# North strengths
rWeak  = hcp(north)<7                    // weak
rConst = hcp(north)>6 and hcp(north)<10  // constructive
rInv   = hcp(north)>9 and hcp(north)<13  // invitational
rCI    = hcp(north)>6 and hcp(north)<13  // constructive plus

# Define North's actions w/o fit
XX = xFit and hcp(north)>10
S1 = xFit and rCI and spades(north)>3   and hcp(north,spades)>2 and oneH and not (XX)             // Redouble -- it's our hand
N1 = xFit and rCI and clubs(north)>4    and top5(north,clubs)>2          and not (XX or S1)       // Bid 1NT -- Transfer to 2C
c2 = xFit and rCI and diamonds(north)>4 and top5(north,diamonds)>2       and not (XX or S1 or N1) // Bid 2C -- Transfer to 2D
     //Note: In Dealer code,C2 refers to the 2 of Clubs; so, I can't use it in my code.

# Define North's actions w/fit
M4 = n5                                                // Bid 4M
X2 = n3 and rCI   and not (M4)                         // Transfer to 2M
M2 = n3 and rWeak and not (M4 or X2)                   // Bid 2M
N2 = n4 and rInv  and not (M4 or X2 or M2)             // Bid 2NT
M3 = n4 and rWeak and not (M4 or X2 or M2 or N2)       // Bid 3M
X3 = n4 and rCI   and not (M4 or X2 or M2 or N2 or M3) // Transfer to 3M

nActsWithFit    =  M4 or X2 or M2 or N2 or M3 or X3
nActsWithoutFit = XX or S1 or N1 or c2

# Now do it
southM and westX and (nActsWithFit or nActsWithoutFit)

action

average "no fit           " 100 * nActsWithoutFit,
average "XX (10+ no fit)  " 100 * XX,
average "1S (spades)      " 100 * S1,
average "1N (clubs)       " 100 * N1,
average "2C (diamonds)    " 100 * c2,
average "fit              " 100 * nActsWithFit, 
average "xM (cons+ w/3)   " 100 * X2,
average "M2 (weak w/3)    " 100 * M2,
average "2N (inv+ w/4)    " 100 * N2,
average "X3 (cons w/4)    " 100 * X3,
average "M3 (weak w/4)    " 100 * M3,
average "M4 (any 5)       " 100 * M4,


printpbn

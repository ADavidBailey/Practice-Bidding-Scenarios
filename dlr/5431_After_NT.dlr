# button-text: 5431 After NT
# scenario-title: 5431 After Notrump
# 5431_After_NT
dealer south


# 5431 after 1NT
# convention-card: 21GF-5431-After-NT
# auction-filter: Auction.....\\n1NT Pass 3[HS]

# 5431 After NT

##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422-5xxx-x5xx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# Returns gibNT
##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

#unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
#unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

#balEast   = shape(east,any 4333 +any 4432)
#balWest   = shape(west,any 4333 +any 4432)

balEast    = shape(east,any 4432+any 4333)
unbalEast  = not balEast

balWest    = shape(west,any 4432+any 4333)
unbalWest  = not balWest

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# Returns calmOpps


# TP -1 for stiff A, K, Q, J
s1 = shape(north, 1xxx) and hcp(north,spades)>0 ? 1 : 0
h1 = shape(north, x1xx) and hcp(north,hearts)>0 ? 1 : 0
nTP = hcp(north) - s1 - h1

# Responder holds (3-1 or 3-0 in the majors) and 4+ in each of the minors and 8+ HCP
M31   = shape(north,31xx+13xx) and shape(north,xx54+xx45)
M30   = shape(north,30xx+03xx) and shape(north,xx64+xx46+xx55)
n5431 = (M31 or M30) and nTP>8

# Now do it
gibNT and n5431 and calmOpps

action

average "HCP S " hcp(south),
average "HCP N " hcp(north),
average "1S 3H " 100 * shape(north,1xxx),
average "3S 1H " 100 * shape(north,x1xx),
average "0S 3H " 100 * shape(north,0xxx),
average "3S 0H " 100 * shape(north,x0xx),

average "5D 4C " 100 * shape(north,xx54),
average "4D 5C " 100 * shape(north,xx45),

average "5D 5C " 100 * shape(north,xx55),
average "6D 4C " 100 * shape(north,xx64),
average "4D 6C " 100 * shape(north,xx46),


printpbn

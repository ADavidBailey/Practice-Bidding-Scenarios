# button-text: Splinters After Notrump
# button-title: Splinters After Notrump
# Splinters_After_Notrump-DELETE ME
dealer south


# Splinters After Notrump

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
p = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and p<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, 4252+4225+2452+2425+2254+2245) and hcp(south)>14 and hcp(south)<17

# Responder holds (3-1 or 3-0 in the majors) and 4+ in each of the minors and 8+ HCP
dSplinter = shape(north,31xx+30xx+13xx+03xx) and shape(north,xx64+xx46+xx55+xx54+xx45) and hcp(north)>7

# Calculate Total Points
s0 = shape(north,0xxx) ? 3 : 0
s1 = shape(north,1xxx) ? 2 : 0
tp = hcp(south) + s0 + s1

# Define splinter distributions
dSpl= shape(north,4405+4414)
hSpl = shape(north,4045+4054+4144)
sSpl = shape(north,0445+0454+1444)

# Define splinter
splinter = (dSpl or hSpl or sSpl) and tp>11

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

balEast   = shape(east,any 4333 +any 4432)
balWest   = shape(west,any 4333 +any 4432)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####


# Now do it
(nt1 or nt2) and splinter and calmOpps

action

average "HCP S " hcp(south),
average "HCP N " hcp(north),

average "1N-3S " 100 * shape(north,0xxx+1xxx),
average "1N-3H " 100 * shape(north,x0xx+x1xx),
average "1N-3D " 100 * shape(north,xx0x+xx1x),


printpbn

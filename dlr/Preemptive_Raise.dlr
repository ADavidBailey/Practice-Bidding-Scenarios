# button-text: Preemptive Raise
# scenario-title: South opens and North，with 3-6 HCP and an 11+ card minor fit or a 10+ major fit，
# Preemptive_Raise
dealer south


# Preemptive Raise

# auction-filter: Auction.....\\n(1C.[PX12].* 5C|1D.[PX12].* 5D|1H.[PX12].* 5H|1S.[PX12].* 5S)


##### Imported Script -- Predict Opening 1-Bid #####

# Changed to use gibNT 3/30/2025

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, 4252+4225+2452+2425+2254+2245) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22
threeNT = TwoNtShape and hcp(south)>24 and hcp(south)<28

# Define Game Forcing 2C
case1 = hcp(south)>21
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
# Defines oneClub, oneDiamond, oneHeart, oneSpade

# Define 10+ card fits
nClubs    = oneClub    and clubs(north)>7
nDiamonds = oneDiamond and diamonds(north)>6
nHearts   = oneHeart   and hearts(north)>4
nSpades   = oneSpade   and spades(north)>4

nPreempts = hcp(north)>2 and hcp(north)<7

# South open's 1C/D/H/S and North makes a preemptive raise
(nClubs or nDiamonds or nHearts or nSpades) and nPreempts

#  produce 40

action
#  printoneline,
average "1C  " 100 * nClubs,
average "1D  " 100 * nDiamonds,
average "1H  " 100 * nHearts,
average "1S  " 100 * nSpades,


# Spaces don't work for indentation. Using the small bullet is as good as I've found 


printpbn

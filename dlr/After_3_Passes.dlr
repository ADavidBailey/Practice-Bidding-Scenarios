# button-text: After 3 Passes...
# scenario-title: After 3 Passes...
# After_3_Passes
dealer west


# auction-filter: Auction.....\\nPass.Pass.Pass.

# All Pass...

# Avoid concentration of values in long suits -- no AK, AQ, or KQ
ccw = top5(west ,clubs   )<3 and hcp(west ,clubs   )<5
cdw = top5(west ,diamonds)<3 and hcp(west ,diamonds)<5
chw = top5(west ,hearts  )<3 and hcp(west ,hearts  )<5
csw = top5(west ,spades  )<3 and hcp(west ,spades  )<5
noConWest = ccw and cdw and chw and csw

ccn = top5(north,clubs   )<3 and hcp(north,clubs   )<5
cdn = top5(north,diamonds)<3 and hcp(north,diamonds)<5
chn = top5(north,hearts  )<3 and hcp(north,hearts  )<5
csn = top5(north,spades  )<3 and hcp(north,spades  )<5
noConNorth = ccn and cdn and chn and csn

cce = top5(east ,clubs   )<3 and hcp(east ,clubs   )<5
cde = top5(east ,diamonds)<3 and hcp(east ,diamonds)<5
che = top5(east ,hearts  )<3 and hcp(east ,hearts  )<5
cse = top5(east ,spades  )<3 and hcp(east ,spades  )<5
noConEast = cce and cde and che and cse

# Hands with 5+ cards and no concentrateion of values are seldom opened with 8 or less HCP
length5or6West  = shape(west ,xxxx -any 8xxx -any 7xxx -any 65xx -any 55xx)
length5or6North = shape(north,xxxx -any 8xxx -any 7xxx -any 65xx -any 55xx)
length5or6East  = shape(east ,xxxx -any 8xxx -any 7xxx -any 65xx -any 55xx)

# Hands with 5+ cards are often opened with 11 HCP
length4West   = shape(west ,any 4333 +any 44xx)
length4North  = shape(north,any 4333 +any 44xx)
length4East   = shape(east ,any 4333 +any 44xx)

calmWest  = ((length5or6West  and noConWest  and hcp(west) <9) or (length4West  and hcp(west) <12)) and controls(west) <5
calmNorth = ((length5or6North and noConNorth and hcp(north)<9) or (length4North and hcp(north)<12)) and controls(north)<5
calmEast  = ((length5or6East  and noConEast  and hcp(east) <9) or (length4East  and hcp(east) <12)) and controls(east) <5

# For 4th seat, the rule points of 15 applies -- it should be less than 15 for a pass-out.
r15 = hcp(south) + spades(south)

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###


lev14 = r15==14 and keep
lev15 = r15==15 and keep70
lev16 = r15==16 and keep25
levelTheDeal = lev14 or lev15 or lev16

# Now do it
calmWest and calmNorth and calmEast and hcp(south)>10 and hcp(south)<15 and r15>13 and r15<17
and levelTheDeal

action

average "hcp W     " hcp(west),
average "hcp N     " hcp(north),
average "hcp E     " hcp(east),
average "hcp S     " hcp(south),

average "controls W" controls(west),
average "controls N" controls(north),
average "controls E" controls(east),
average "controls S" controls(south),

average "losers W  " losers(west),
average "losers N  " losers(north),
average "losers E  " losers(east),
average "losers S  " losers(south),

frequency "r15" (r15,10,20),


printpbn

# button-text: Mixed Raise in Comp
# scenario-title: Opponents open 1C/1D/1H.  We overcall one 1H/1S. Partner has
# Mixed_Raise_In_Comp
dealer west


# convention-card: 21GF-MixedRaise
# auction-filter: Note....Mixed raise


##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

#unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
#unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

#balEast   = shape(east,any 4333 +any 4432)
#balWest   = shape(west,any 4333 +any 4432)

balEast    = shape(east,any 4432+any 4333)
unbalEast  = not balEast

balWest    = shape(west,any 4432+any 4333)
unbalWest  = not balWest

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# Defines calmEast

# Predict West's opening 1C, 1D, 1H
s = spades(west)
h = hearts(west)
d = diamonds(west)
c = clubs(west)
oS = s>4 and s>=h and s>=d and s>=c
oH = not oS and h>4 and h>=d and h>=c
oD = not (oS or oH) and ((d>3 and d>=c) or c<3)
oC = not (oS or oH or oD)
oneHeart   = oH and hearts(east)<3
oneDiamond = oD and diamonds(east)<4
oneClub    = oC and clubs(east)<4

wSuit = oneHeart or oneDiamond or oneClub
wRange = hcp(west)>11 and hcp(west)<15
wOpens = wRange and wSuit

# North overcalls 1H or 1S and South has 6-10 HCP and 4-Card Support 

nSpades = spades(north)>4 and spades(south)>3 and hcp(north,spades)>4
nHearts = hearts(north)>4 and hearts(south)>3 and hcp(north,hearts)>4
nShape  = shape(north,xxxx-any 76xx-any 75xx- any 65xx- any 55xx)  // Kill the 2-suited hands
nWeak   = shape(north,any 8xxx +any 7xxx +any 6xxx) and hcp(north)>6 and hcp(north)<12
sShape  = shape(south,any 0xxx +any 1xxx -any 4333)
nRange = hcp(north)>7 and hcp(north)<17
sRange = hcp(south)>6 and hcp(south)<11 and controls(south)>1
nOvercalls = (nSpades or nHearts) and nShape and sShape and nRange and sRange and not nWeak

wOpens and nOvercalls // and calmEast

action
average "N/S Spades" 100 * nSpades,
average "N/S Hearts" 100 * nHearts,


printpbn

# button-text: Splinters
# scenario-title: Splinter Bids are double jumps.  They show a singleton or void in the suit bid and 4+ cards
# Splinters
dealer south


# auction-filter: Note..1.Splinter

# Splinters

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

#unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
#unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

#balEast   = shape(east,any 4333 +any 4432)
#balWest   = shape(west,any 4333 +any 4432)

balEast    = shape(east,any 4432+any 4333)
unbalEast  = not balEast

balWest    = shape(west,any 4432+any 4333)
unbalWest  = not balWest

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, 4252+4225+2452+2425+2254+2245) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# Defines gibNT

sOpens = hcp(south)>11 and hcp(south)<22 and not gibNT

sFit = spades(south)>4 and hearts(south)<5 and spades(north)==4
hFit = hearts(south)>4 and spades(south)<5 and hearts(north)==4
fit = sFit or hFit

# North Splinters w/4-card support and shortness that's not an A, K, or Q
cSpl = shape(north, xxx1+xxx0) and hcp(north,clubs)<2
dSpl = shape(north, xx1x+xx0x) and hcp(north,diamonds)<2
hSpl = shape(north, x1xx+x0xx) and hcp(north,hearts)<2
sSpl = shape(north, 1xxx+0xxx) and hcp(north,spades)<2
nonAKQshortness = cSpl or dSpl or hSpl or sSpl

# North Splinters w/4-card support and any shortness
anyShortness    = shape(north,any 1xxx +any 0xxx)

# Define North's tp (+3 for void, +2 for stiff, -1 for a stiff A, K, Q, or J)
s0 = shape(north,any 0xxx) ? 3 : 0
s1 = shape(north,any 1xxx) ? 2 : 0

dH = shape(north, xx1x) and hcp(north,diamonds)>0
hH = shape(north, x1xx) and hcp(north,hearts)>0
sH = shape(north, 1xxx) and hcp(north,spades)>0

tp = hcp(north) + s0 + s1 - (dH + hH + sH)

nSplinters = losers(north)<8 and tp>11 and hcp(north)<13 and anyShortness

sOpens and fit and nSplinters and calmWest


action printpbn

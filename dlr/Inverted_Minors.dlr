# button-text: Inverted Minors
# scenario-title: Inverted Minors
# Inverted_Minors
dealer south


# convention-card:
# auction-filter: Auction.....\\n((1C Pass [23]C)|(1D Pass [23]D))


# Inverted Minors

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

#unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
#unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

#balEast   = shape(east,any 4333 +any 4432)
#balWest   = shape(west,any 4333 +any 4432)

balEast    = shape(east,any 4432+any 4333)
unbalEast  = not balEast

balWest    = shape(west,any 4432+any 4333)
unbalWest  = not balWest

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# Defines calmOpps

# Exclude NT hands for South and North
southNT = shape(south, any 4333 +any 4432 +any 5332 +any 5422 -5xxx -x5xx) and hcp(south)>14 and hcp(south)<18
northNT = shape(north, any 4333 +any 4432 +any 5332 +any 5422) and hcp(north)>5 and hcp(north)<11
noNT = not(southNT or northNT)

# Predict South's Opening Suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
oS = s>4 and s>=h and s>=d and s>=c
oH = not oS and h>4 and h>=d and h>=c
oD = not (oS or oH) and ((d>3 and d>=c) or c<3)
oC = not (oS or oH or oD)

# North has no major and has support for opener's minor
noMajor = spades(north)<4 and hearts(north)<4
cFit = oC and clubs(north)>4
dFit = oD and diamonds(north)>3
minorRaise = (cFit or dFit) and noMajor and noNT 

# South opens 1m and North raises

minorRaise and hcp(south)>11 and hcp(south)<20 and hcp(north)>5 and hcp(north)<13 and calmOpps


action printpbn

# alias: ExclusionStaJac
# button-text: Exclusion Sta/Jac
# scenario-title: --- Exclusion Blackwood after Stayman or Jacoby
# gib-works: false
# bba-works: true
# auction-filter: Note....Exclusion
# convention-card-ns: 21GF-Exclusion
# convention-card-ew: 21GF-GIB

/*@chat
--- Exclusion Blackwood after Stayman or Jacoby
Leveled for Stayman vs Jacoby.
@chat*/

dealer south
produce 1000

##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major or with 4 spades
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422-5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# Defines gibNT

hFit4 = hearts(north)==4 and hearts(south)>3   // 4-4 or 4-5
sFit4 = spades(north)==4 and spades(south)>3
hFit5 = hearts(north)==5 and hearts(south)>2   // 5-3, 5-4, or 5-5
sFit5 = spades(north)==5 and spades(south)>2
jac = shape(north, 5xxx + x5xx -54xx -45xx)    // exclude smolen
jacFit = jac and (hFit5 or sFit5)
sta = shape(north, 4xxx + x4xx)                // includes smolen
staFit = sta and (hFit4 or sFit4)

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###

# Define Leveling
case1  = jacFit    and keep70
case2  = staFit    and keep
levelTheDeal = case1 or case2

# Do it
gibNT and hcp(north)>13 and shape(north,any 0xxx) and (case1 or case2)
and levelTheDeal

action printoneline,
average "Jac + Fit " 100 * jacFit,
average "Sta + Fit " 100 * staFit,
average "hcp south " hcp(south),

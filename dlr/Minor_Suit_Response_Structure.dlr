# button-text: 1m & Raise/Bal (Lev)
# scenario-title: 1 Minor w/Raise or Balanced Response -- Leveled
# Minor_Suit_Response_Structure
dealer south


# convention-card:
# auction-filter: .

# Minor Opener w/Raise or Balanced Response -- Leveled

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

balEast   = shape(east,any 4333 +any 4432)
balWest   = shape(west,any 4333 +any 4432)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

##### Imported Script -- Predict Opening 1-Bid #####

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and   top4(south,spades)>0   and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and   top4(south,hearts)>0   and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)>0 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and    top4(south,clubs)>0    and not hascard(south,AC) ? 1 : 0
dgPoints = S2H + H2H + D2H + C2H

# Define notrump points for south (ntPoints)
ntPoints = lengthPoints>dgPoints ? hcp(south) + lengthPoints - dgPoints : hcp(south)

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

# Define robot notrump shape and exclude any 5card 
ntShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422 -5xxx -x5xx)

# Define ntPoint ranges
oneNT   = ntShape and ntPoints>14 and ntPoints<18
twoNT   = ntShape and ntPoints>19 and ntPoints<22
threeNT = ntShape and ntPoints>25 and ntPoints<28
weakNT  = ntShape and ntPoints>10 and ntPoints<15

# Define Game Forcing 2C
case1 = hcp(south)>22
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT // or weakNT  weakNT will be duplicated diamond and club openings

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade = oS
oneHeart = oH
oneDiamond = oD
oneClub = oC

##### End of Imported Script -- Predict Opening 1-Bid #####

# Define 1 Minor and Mixed/Invitational Raise -- no 1-level suit or NT response 
cRaise = oC and clubs(north)>4    and spades(north)<4 and hearts(north)<4 and diamonds(north)<4                    //  5+ card support
dRaise = oD and diamonds(north)>3 and spades(north)<4 and hearts(north)<4 and diamonds(north)>clubs(north)         //  4+ card support
mRange = hcp(north)>5  and hcp(north)<11
iRange = hcp(north)>10 and hcp(north)<13
fRange = hcp(north)>12

shortness = shape(north, any 0xxx+any 1xxx+any 2xxx)

mixedRaise  = (cRaise or dRaise) and mRange and shortness
inviteRaise = (cRaise or dRaise) and iRange and shortness
forceRaise  = (cRaise or dRaise) and fRange and shortness
anyRaise    = (cRaise or dRaise) and hcp(north)>5

# Define one Minor and balanced responses w/o 5-card major
nBalanced = shape(north, any 4333 +any 4432 +any 5422 +any 5332-5xxx-x5xx)

oneMinor = oC or oD

balWkResp  = oneMinor and nBalanced and hcp(north)>5 and hcp(north)<10
balGIResp  = oneMinor and nBalanced and hcp(north)>9 and hcp(north)<13
balGFResp  = oneMinor and nBalanced and hcp(north)>12
anyBalResp = oneMinor and nBalanced and hcp(north)>5

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###
### See -Stats-Leveling for statistics on these values ###
### Example of how to generate Statistics for Leveling ###


# Do the Leveling
levMix   = mixedRaise  and keep67
levInv   = inviteRaise and keep
levBalWk = balWkResp   and keep015
levBalGI = balGIResp   and keep015
levBalGF = balGFResp   and keep015
levelTheDeal = levMix or levInv or levBalWk or levBalGI or levBalGF

# Now do it
(mixedRaise or inviteRaise or anyBalResp) and calmOpps and hcp(south)<22
and levelTheDeal

action
average "1 Club        " 100 * oC,
average "1 Diamond     " 100 * oD,
average "hcp south     "       hcp(south),
average "hcp north     "       hcp(north),
average "any raise     " 100 * (mixedRaise or inviteRaise),
average "raise w/1 Dbl " 100 * ((mixedRaise or inviteRaise) and shape(north,any 2xxx-any 22xx)),
average "Mixed raise   " 100 * mixedRaise,
average "Invite raise  " 100 * inviteRaise,

average "any void      " 100 * shape(north,any 0xxx),
average "any singleton " 100 * shape(north,any 1xxx),
average "one doubleton " 100 * shape(north,any 2xxx-any 22xx),
average "two doubletons" 100 * shape(north,any 22xx),

average "any balanced  " 100 * nBalanced,
average "Weak balanced " 100 * balWkResp,
average "GI balanced   " 100 * balGIResp,
average "GF balanced   " 100 * balGFResp,

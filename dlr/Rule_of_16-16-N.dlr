# Rule_of_16-16
generate 100000000
produce 500
dealer north


# Rule of 16 = 16

# East passes
##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

balEast   = shape(east,any 4333 +any 4432)
balWest   = shape(west,any 4333 +any 4432)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####


# Calculate the Rule of 16 for South (cards>7 + HCP)
r16S = top5(south,spades)   + hascard(south,9S) + hascard(south,8S) 
r16H = top5(south,hearts)   + hascard(south,9H) + hascard(south,8H)
r16D = top5(south,diamonds) + hascard(south,9D) + hascard(south,8D)
r16C = top5(south,clubs)    + hascard(south,9C) + hascard(south,8C)
r16  = r16S + r16H + r16D + r16C + hcp(south)

########## Place the Club 4 based on the Rule of 16 ##########
club4 = r16>15 ? hascard(south,4C) : hascard(north,4C)

# North opens 1N with 15 HCP
nOpens = shape(north,any 4333+any 4432+any 5332 -5xxx-x5xx) and hcp(north)==15

# South has 8-9 HCP and more than 13 R16 points
sActs = shape(south, any 4333+any 4432+any 5332+any 5422-5xxx-x5xx) and hcp(south)>7 and hcp(south)<10 and r16==16

# North opens 1N, East passes, and south has 8-9 HCP and an exact number of R16 points
nOpens and calmEast and sActs and club4

action
#printoneline,
frequency "R16 points" (r16,13,19),



printpbn

# button-text: GIB NT-P-2C-bid
# scenario-title: After 1NT-P-2C-bid...  What now???
# GIB_1N-P-2C-BID
dealer south


/*
South opens 1N and North responds 2C and East bids.

convention-card: 21GF-GIB
auction-filter: Auction.....\n(1NT? +Pass +2C.* +[X2-7])
*/


# 1 Notrump Opening
##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major or with 4 spades
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422-5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# Defines gibNT

### Imported script: TP (Total Points)

//   Note:  My initial implementation used shape staments for counting voids, singletons, and doubletons.
//   Dealer has a limit 32, so I eliminated the shape statements in this script.

## Calculate TP for North
# Calculate North's Short-Suit Penalty Points
nsPP = spades(north)  <3 and hcp(north,spades)  >0 ? 1 : 0
nhPP = hearts(north)  <3 and hcp(north,hearts)  >0 ? 1 : 0
ndPP = diamonds(north)<3 and hcp(north,diamonds)>0 ? 1 : 0
ncPP = clubs(north)   <3 and hcp(north,clubs)   >0 ? 1 : 0
nPP = nsPP+nhPP+ndPP+ncPP

# Calculate North's Short-Suit Points
ns0 = spades(north)  ==0 ? 3 : 0   // 3 for each void
nh0 = hearts(north)  ==0 ? 3 : 0
nd0 = diamonds(north)==0 ? 3 : 0
nc0 = clubs(north)   ==0 ? 3 : 0

ns1 = spades(north)  ==1 ? 2 : 0   // 2 for each singleton
nh1 = hearts(north)  ==1 ? 2 : 0
nd1 = diamonds(north)==1 ? 2 : 0
nc1 = clubs(north)   ==1 ? 2 : 0

ns2 = spades(north)  ==2 ? 1 : 0   // 1 for each doubleton
nh2 = hearts(north)  ==2 ? 1 : 0
nd2 = diamonds(north)==2 ? 1 : 0
nc2 = clubs(north)   ==2 ? 1 : 0

nSSP = ns0+nh0+nd0+nc0+ns1+nh1+nd1+nc1+ns2+nh2+nd2+nc2

tpNorth = hcp(north) + nSSP - nPP

## Calculate TP for East
# Calculate East's Short-Suit Penalty Points
esPP = spades(east)  <3 and hcp(east,spades)  >0 ? 1 : 0
ehPP = hearts(east)  <3 and hcp(east,hearts)  >0 ? 1 : 0
edPP = diamonds(east)<3 and hcp(east,diamonds)>0 ? 1 : 0
ecPP = clubs(east)   <3 and hcp(east,clubs)   >0 ? 1 : 0
ePP = esPP+ehPP+edPP+ecPP

# Calculate East's Short-Suit Points
es0 = spades(east)==0     ? 3 : 0
eh0 = hearts(east)==0     ? 3 : 0
ed0 = diamonds(east)==0   ? 3 : 0
ec0 = clubs(east)==0      ? 3 : 0

es1 = spades(east)==1     ? 2 : 0
eh1 = hearts(east)==1     ? 2 : 0    
ed1 = diamonds(east)==1   ? 2 : 0
ec1 = clubs(east)==1      ? 2 : 0

es2 = spades(east)==2     ? 1 : 0
eh2 = hearts(east)==2     ? 1 : 0
ed2 = diamonds(east)==2   ? 1 : 0
ec2 = clubs(east)==2      ? 1 : 0

eSSP = es0+eh0+ed0+ec0+es1+eh1+ed1+ec1+es2+eh2+ed2+ec2

tpEast = hcp(east) + eSSP - ePP


## Calculate TP for South
# Calculate South's Short-Suit Penalty Points
ssPP = spades(south)  <3 and hcp(south,spades)  >0 ? 1 : 0   
shPP = hearts(south)  <3 and hcp(south,hearts)  >0 ? 1 : 0
sdPP = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
scPP = clubs(south)   <3 and hcp(south,clubs)   >0 ? 1 : 0
sPP = ssPP+shPP+sdPP+scPP

# Calculate South's Short-Suit Points
ss0 = spades(south)==0     ? 3 : 0
sh0 = hearts(south)==0     ? 3 : 0
sd0 = diamonds(south)==0   ? 3 : 0
sc0 = clubs(south)==0      ? 3 : 0   

ss1 = spades(south)==1     ? 2 : 0
sh1 = hearts(south)==1     ? 2 : 0
sd1 = diamonds(south)==1   ? 2 : 0
sc1 = clubs(south)==1      ? 2 : 0

ss2 = spades(south)==2     ? 1 : 0
sh2 = hearts(south)==2     ? 1 : 0
sd2 = diamonds(south)==2   ? 1 : 0
sc2 = clubs(south)==2      ? 1 : 0

sSSP = ss0+sh0+sd0+sc0+ss1+sh1+sd1+sc1+ss2+sh2+sd2+sc2

tpSouth = hcp(south) + sSSP - sPP


## Calculate TP for West
# Calculate West's Short-Suit Penalty Points
wsPP = spades(west)  <3 and hcp(west,spades)  >0 ? 1 : 0
whPP = hearts(west)  <3 and hcp(west,hearts)  >0 ? 1 : 0
wdPP = diamonds(west)<3 and hcp(west,diamonds)>0 ? 1 : 0
wcPP = clubs(west)   <3 and hcp(west,clubs)   >0 ? 1 : 0
wPP = wsPP+whPP+wdPP+wcPP

# Calculate West's Short-Suit Points
ws0 = spades(west)==0     ? 3 : 0
wh0 = hearts(west)==0     ? 3 : 0
wd0 = diamonds(west)==0   ? 3 : 0
wc0 = clubs(west)==0      ? 3 : 0

ws1 = spades(west)==1     ? 2 : 0
wh1 = hearts(west)==1     ? 2 : 0
wd1 = diamonds(west)==1   ? 2 : 0
wc1 = clubs(west)==1      ? 2 : 0

ws2 = spades(west)==2     ? 1 : 0
wh2 = hearts(west)==2     ? 1 : 0
wd2 = diamonds(west)==2   ? 1 : 0
wc2 = clubs(west)==2      ? 1 : 0

wSSP = ws0+wh0+wd0+wc0+ws1+wh1+wd1+wc1+ws2+wh2+wd2+wc2

tpWest = hcp(west) + wSSP - wPP

### End of Imported Script: TP
# Defines tpNorth, tpSouth, tpEast, tpWest

# Define West's Cappellitti actions
west4x     = shape(west,any 9xxx +any 8xxx +any 7xxx -any 6xxx -any 5xxx -any 4xxx)                     and tpWest>2
west2C     = shape(west,any 9xxx +any 8xxx +any 7xxx +any 6xxx -any 5xxx)                               and tpWest>9
west2D     = spades(west)>3 and hearts(west)>3 and (spades(west) + hearts(west))>8                      and tpWest>10
west2other = shape(west,any 76xx +any 75xx +any 74xx +any 66xx +any 65xx +any 64xx+ any 55xx +any 54xx) and tpWest>10
west3x     = shape(west,any 9xxx +any 8xxx +any 7xxx +any 6xxx -any 5xxx -any 4xxx)                     and tpWest>12 and tpWest<17
minorCards = clubs(west) + diamonds(west)
west2N     = clubs(west)>4 and diamonds(west)>4                                                         and (tpWest + minorCards - 9)>14
westX      = hcp(west)>15
calmWest   = not (west4x or west2C or west2D or west2other or west3x or west2N or westX)

# Boost quality of North's major
nQS = spades(north)==4 and hcp(north,spades)>2
nQH = hearts(north)==4 and hcp(north,hearts)>2
nQ  = nQS or nQH

# Define splinter
splinter = shape(north, 4405 + any 4441 -xxx1) and tpNorth>11

# Define mSS and mST
mss = diamonds(north)>3 and clubs(north)>3 and (diamonds(north) + clubs(north))>8 and tpNorth>9
mst = (clubs(north)>5 or diamonds(north)>5) and not mss

# Define various 2C bids -- GIB does not play Drop Dead or Crawling Stayman

# NOTE:  GIB does NOT bid Stayman with 4-3-3-3 and 8 HCP
smolen      = shape(north,54xx +45xx)         and tpNorth>9
majorInvite = shape(north,54xx) and tpNorth>8 and tpNorth<11 and hcp(north)<10    // 2C & correct D to S 
stayman     = shape(north,44xx +4xxx +x4xx -any 9xxx -any 8xxx -any 7xxx -any 6xxx -5xxx -x5xx) and hcp(north)>7 and tpNorth>8 and nQ and not (majorInvite or smolen or splinter)

# Define Texas, Jacoby, & Splinter responses
texas  = (spades(north)>5 or hearts(north)>5) and tpNorth>9
jacoby = (spades(north)>4 or hearts(north)>4) and not (texas or smolen or majorInvite)

n2Clubs = (stayman or smolen or majorInvite) and not (splinter or mss or mst or texas or jacoby)

# Define East's 8421 Points for each suit
eC8 = hascard(east,AC) ? 8 : 0
eC4 = hascard(east,KC) ? 4 : 0
eC2 = hascard(east,QC) ? 2 : 0
eC1 = hascard(east,JC) ? 1 : 0
eC8421 = eC8+eC4+eC2+eC1

eD8 = hascard(east,AD) ? 8 : 0
eD4 = hascard(east,KD) ? 4 : 0
eD2 = hascard(east,QD) ? 2 : 0
eD1 = hascard(east,JD) ? 1 : 0
eD8421 = eD8+eD4+eD2+eD1

eH8 = hascard(east,AH) ? 8 : 0
eH4 = hascard(east,KH) ? 4 : 0
eH2 = hascard(east,QH) ? 2 : 0
eH1 = hascard(east,JH) ? 1 : 0
eH8421 = eH8+eH4+eH2+eH1

eS8 = hascard(east,AS) ? 8 : 0
eS4 = hascard(east,KS) ? 4 : 0
eS2 = hascard(east,QS) ? 2 : 0
eS1 = hascard(east,JS) ? 1 : 0
eS8421 = eS8+eS4+eS2+eS1

# Define East's twice biddable suits: (5 cards or 4 cards with (3 of top 5 or more than 4  8421 points)) + 2 cards
# Only quality suits count for 2-level bids

# East has one 6+card D, H, or S suit, less than 5 clubs (avoid X), no second suit longer than 4-cards, a singleton or void
eShape = shape(east, any 9xxx +any 8xxx+ any 7xxx +any 6xxx -any 85xx -any 76xx -any 66xx -any 65xx) and shape(east, any 1xxx +any 0xxx) and (clubs(south)<5 or clubs(south)>6)

# Twice Biddable suits w/3 of top 5
eTBC3 = eShape and clubs(east)>5    and (top5(east,clubs)==3    or eC8421>4)
eTBD3 = eShape and diamonds(east)>5 and (top5(east,diamonds)==3 or eD8421>4)
eTBH3 = eShape and hearts(east)>5   and (top5(east,hearts)==3   or eH8421>4)
eTBS3 = eShape and spades(east)>5   and (top5(east,spades)==3   or eS8421>4)
eTB3  = eTBC3 or eTBD3 or eTBH3 or eTBS3

# Twice Biddable suits w/4 of top 5 -- this is also Strong Rebiddable & 8421 must be >4
eTBC4 = clubs(east)>3    and (top5(east,clubs)==4)
eTBD4 = diamonds(east)>3 and (top5(east,diamonds)==4)
eTBH4 = hearts(east)>3   and (top5(east,hearts)==4)
eTBS4 = spades(east)>3   and (top5(east,spades)==4)
eTB4  = eTBC4 or eTBD4 or eTBH4 or eTBS4

# Twice Biddable suits w/5 of top 5 -- this is also Strong Rebiddable
eTBC5 = clubs(east)>3    and (top5(east,clubs)==5)
eTBD5 = diamonds(east)>3 and (top5(east,diamonds)==5)
eTBH5 = hearts(east)>3   and (top5(east,hearts)==5)
eTBS5 = spades(east)>3   and (top5(east,spades)==5)
eTB5  = eTBC5 or eTBD5 or eTBH5 or eTBS5

eSuits  = ((eTB3 and tpEast>14) or (eTB4 and tpEast>13) or (eTB5 and tpEast>12))

# Define Biddable Clubs and Rebiddable Clubs
eBC = clubs(east)>4 or (clubs(east)>3 and (top4(east,clubs)>2 or eC8421>3))  // East Biddable
eRC = clubs(east)>5 or (clubs(east)>4 and (top4(east,clubs)>2 or eC8421>3))  // East Rebiddable

eCforX = eRC and top3(east,clubs)>1 and top5(east,clubs)>2
eTPforX = hcp(east)>5 and tpEast>6 and (clubs(east)<7 or tpEast<11)

eX6 = clubs(east)>5  and tpEast>6 and tpEast<11 and eRC and top4(east,clubs)>1 and top5(east,clubs)>2 
eX5 = clubs(east)==5 and tpEast>6               and eRC and top4(east,clubs)>1 and top5(east,clubs)>2 and hcp(east,clubs)>5

# Define East's X
eX = (eX6 or eX5) and shape(east, xxxx -any 9xxx -any 8xxx -any 76xx -any 75xx -any 74xx -any 66xx -any 65xx -any 64xx -any 55xx -any 54xx)

e2Range = hcp(east)>9 and tpEast>10 and tpEast<17

# Define East's bids over 2C -- Twice Bdiddable with extra strength
e2D = diamonds(east)==6 and e2Range and eSuits
e2H = hearts(east)==6   and e2Range and eSuits
e2S = spades(east)==6   and e2Range and eSuits
e2B = e2D or e2H or e2S

# 3- and 4-level suits require length and may have suit-quality
e3Range = hcp(east)>9 and tpEast>10 and tpEast<17
e3C = clubs(east)>6    and eSuits and e3Range
e3D = diamonds(east)>6 and eSuits and e3Range and not e2D
e3H = hearts(east)>6   and eSuits and e3Range and not e2H
e3S = spades(east)>6   and eSuits and e3Range and not e2S
e3B = e3C or e3D or e3H or e3S

# Define East's 4-level weak bids
e4wRange = hcp(east)<10
e4Cw = clubs(east)>8    and e4wRange
e4Dw = diamonds(east)>7 and e4wRange and not (e2D or e3D)
e4Hw = hearts(east)>7   and e4wRange and not (e2H or e3H)
e4Sw = spades(east)>7   and e4wRange and not (e2S or e3S)
e4Bw = e4Dw or e4Hw or e4Sw

# Define East's 4-level strong bids
e4sRange = hcp(east)>9
e4Cs = clubs(east)>7    and top4(east,clubs)>2    and e4sRange and not e3C
e4Ds = diamonds(east)>7 and top4(east,diamonds)>2 and e4sRange and not (e2D or e3D)
e4Hs = hearts(east)>7   and top4(east,hearts)>2   and e4sRange and not (e2H or e3H)
e4Ss = spades(east)>7   and top4(east,spades)>2   and e4sRange and not (e2S or e3S)
e4Bs = e4Ds or e4Hs or e4Ss

e4B = e4Bw or e4Bs

# Define East's other hands w/long-suits
eBOther = shape(east,any 9xxx +any 8xxx +any 7xxx +any 6xxx) and clubs(east)<6 and not (e2B or e3B or e4B)

# Now do it
gibNT and tpSouth<19 and calmWest and n2Clubs and (eShape or eX) and (e2B or e3B or e4B or eX)

action
average "smolen      " 100 * smolen,
average "stayman     " 100 * stayman,
average "major invite" 100 * majorInvite,
average "X           " 100 * eX,
average "2D          " 100 * e2D,
average "2H          " 100 * e2H,
average "2S          " 100 * e2S,
average "3C          " 100 * e3C,
average "3D          " 100 * e3D,
average "3H          " 100 * e3H,
average "3S          " 100 * e3S,
average "4 weak      " 100 * e4Bw,
average "4 strong    " 100 * e4Bs,
average "other       " 100 * eBOther,


printpbn

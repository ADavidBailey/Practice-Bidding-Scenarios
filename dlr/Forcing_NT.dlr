# button-text: Forcing NT (Lev)
# scenario-title: Forcing NT (Leveled)
# Forcing_NT
dealer south


# convention-card: 21GF-ADB
# auction-filter: Note....forcing.1NT

# Forcing NT Leveled

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

balEast   = shape(east,any 4333 +any 4432)
balWest   = shape(west,any 4333 +any 4432)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

##### Imported Script -- Predict Opening 1-Bid #####

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and   top4(south,spades)>0   and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and   top4(south,hearts)>0   and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)>0 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and    top4(south,clubs)>0    and not hascard(south,AC) ? 1 : 0
dgPoints = S2H + H2H + D2H + C2H

# Define notrump points for south (ntPoints)
ntPoints = lengthPoints>dgPoints ? hcp(south) + lengthPoints - dgPoints : hcp(south)

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

# Define robot notrump shape and exclude any 5card 
ntShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422 -5xxx -x5xx)

# Define ntPoint ranges
oneNT   = ntShape and ntPoints>14 and ntPoints<18
twoNT   = ntShape and ntPoints>19 and ntPoints<22
threeNT = ntShape and ntPoints>25 and ntPoints<28
weakNT  = ntShape and ntPoints>10 and ntPoints<15

# Define Game Forcing 2C
case1 = hcp(south)>22
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT // or weakNT  weakNT will be duplicated diamond and club openings

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade = oS
oneHeart = oH
oneDiamond = oD
oneClub = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
# defines oneSpade, oneHeart
##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, 4252+4225+2452+2425+2254+2245) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# defines gibNT

# Calculate Total Points for North (tp)
v1 = shape(north, any 0xxx) ? 5 : 0  // allow for 2 voids
v2 = shape(north, any 00xx) ? 5 : 0
s1 = shape(north, any 1xxx) ? 3 : 0 // allow for 2 singletons
s2 = shape(north, any 11xx) ? 3 : 0
d1 = shape(north, any 2xxx) ? 1 : 0 // allow for 3 doubletons
d2 = shape(north, any 22xx) ? 1 : 0
d3 = shape(north, any 222x) ? 1 : 0
tp = v1+v2+s1+s2+d1+d2+d3 + hcp(north)

# South opens 1H or 1S
oneMajor = (oneSpade or oneHeart) and hcp(south)>11 and hcp(south)<20 and not gibNT

# Defining fit
spadeFit = oneSpade and spades(north)>2
heartFit = oneHeart and hearts(north)>2
fit = spadeFit or heartFit
noFit = not fit

# Avoid 1H - 1S
noSpadeBid = not(oneHeart and spades(north)>3)

# Long minor
longC = clubs(north)>5
longD = diamonds(north)>5
longMinor = (longC or longD)

# Define ranges - TP for raises; otherwise HCP
wRange = tp>4 and tp<8
iRange = tp>10 and tp<13
ngfRange = hcp(north)>5 and hcp(north)<13

# Defining hand types for Forcing NT
wFit = fit and wRange
iFit = fit and iRange
lMin = noFit and ngfRange and longMinor  // overlaps w/3-under inv jump
nFit = noFit and ngfRange and not lMin
forcingNT = (wFit or iFit or lMin or nFit) and noSpadeBid

# Defining Opener's rebids
rebidM  = shape(south, any 7xxx+any 6xxx)
rebidN  = shape(south, any 5332+any 5422) and hcp(south)>17 and hcp(south)<20
reverse = shape(south, 45xx+46xx+56xx) and hcp(south)>16 and hcp(south)<22
newSuit = not(rebidM or rebidN or reverse)
openerRebid = rebidM or rebidN or reverse or newSuit

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###
### See -Stats-Leveling for statistics on these values ###
### Example of how to generate Statistics for Leveling ###


# Level Responses
wFitLev = wFit and keep
iFitLev = iFit and keep81
lMinLev = lMin and keep89
nFitLev = nFit and keep25
levelResponse = wFitLev or nFitLev or iFitLev or lMinLev

# Level Rebids
rMajLev = rebidM  and keep015
rNTLev  = rebidN  and keep25
rRevLev = reverse and keep
rNSLev  = newSuit and keep015
levelRebid = rMajLev or rNTLev or rRevLev or rNSLev

levelTheDeal = levelResponse and levelRebid

## Now Do it
# South opens 1 H/S and North bids 1N Forcing and South Rebids
oneMajor and forcingNT and openerRebid and calmWest
#and levelTheDeal

action
average "fit 5-7 TP         " 100 * wFit,
average "fit 11-12 TP       " 100 * iFit,
average "no fit, long minor " 100 * lMin,
average "no fit 6-12 HCP    " 100 * nFit,
average "-------------------" 100 * (wFit + iFit + lMin + nFit),
average "rebid Major        " 100 * rebidM,
average "rebid NT           " 100 * rebidN,
average "rebid Reverse      " 100 * reverse,
average "rebid New Suit     " 100 * newSuit,
average "-------------------" 100 * (rebidM + rebidN + newSuit + reverse),


printpbn

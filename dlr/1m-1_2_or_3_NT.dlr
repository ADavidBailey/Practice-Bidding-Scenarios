# button-text: 1m - 1|2|3 NT
# scenario-title: 1m - 1NT，2NT，or 3NT
# 1m-1_2_or_3_NT
dealer south


/*
South opens 1C or 1D with no other constraints
North has any 4333 hand with the following ranges

 6-9  with at least Qxx in each unbid suit -- no requirement for opener's suit
10-12 with at least Kxx or QJx in each unbid suit AND at least Qxx in the opener's suit
13-15 same as 10-12

auction-filter: Auction.....\n1[CD].Pass.[123]NT
*/


##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# Defines calmWest
##### Imported Script -- Predict Opening 1-Bid #####

# Changed to use gibNT 3/30/2025

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, 4252+4225+2452+2425+2254+2245) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22
threeNT = TwoNtShape and hcp(south)>24 and hcp(south)<28

# Define Game Forcing 2C
case1 = hcp(south)>21
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
# Defines oneClub, oneDiamond

# Define suit quality for NT -- Qxx opener's suit and Kxx or QJx in unbid suits
nC2 = hcp(north,clubs)>1         // at least Qxx
nD2 = hcp(north,diamonds)>1
nM2 = hcp(north,hearts)>1 and hcp(north,spades)>1
nC3 = hcp(north,clubs)>2         // at least Kxx or QJx
nD3 = hcp(north,diamonds)>2
nM3 = hcp(north,hearts)>2 and hcp(north,spades)>2

# Define North's response ranges
n1 = hcp(north)>5  and hcp(north)<10
n2 = hcp(north)>9  and hcp(north)<13
n3 = hcp(north)>12 and hcp(north)<16

# South bids 1C or 1D and North responds 1N or 2/3N
s1Cn1N = oneClub    and nD2 and nM2 and n1           // 1C - 1N with 2 hcp in unbid suits
s1Dn1N = oneDiamond and nC2 and nM2 and n1           // 1D - 1N  
s1m1N  = s1Cn1N or s1Dn1N

s1Cn2N = oneClub    and nC2 and nD3 and nM3 and n2   // 1C - 2N with 3 hcp in unbid suits and 2 in opener's suit
s1Dn2N = oneDiamond and nD2 and nC3 and nM3 and n2   // 1D - 2N 
s1m2N  = s1Cn2N or s1Dn2N

s1Cn3N = oneClub    and nC2 and nD3 and nM3 and n3   // 1C - 3N with 3 hcp in unbid suits and 2 in opener's suit
s1Dn3N = oneDiamond and nD2 and nC3 and nM3 and n3   // 1D - 3N
s1m3N  = s1Cn3N or s1Dn3N

nAny4   = shape(north,any 4333)   // any 4333
nMajor4 = shape(north,4333+3433)  // either major

# Now do it
(s1m1N or s1m2N or s1m3N) and calmWest and (nAny4 or nMajor4)

action

average "1m... 6- 9 " 100 * s1m1N,
average "     10-12 " 100 * s1m2N,
average "     13-15 " 100 * s1m3N,

average "hcp South  " hcp(south),
average "    West   " hcp(west),
average "    North  " hcp(north),
average "    East   " hcp(east),

average "South 4333 " 100 * shape(north,4333),
average "      3433 " 100 * shape(north,3433),
average "      3343 " 100 * shape(north,3343),
average "      3334 " 100 * shape(north,3334),


printpbn

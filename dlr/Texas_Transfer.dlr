# button-text: GIB NT & Texas
# scenario-title: Texas Transfer
# Texas_Transfer
dealer north


# Texas Transfer

# auction-filter: 1NT Pass 4[DH]

##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major or with 4 spades
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422-5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# Defines gibNT
##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# Defines calmEast

# South has a 6-card major w/o 4 in other major and (no slam interest or intending to RKC)
sS = spades(south)>5 and top5(south,spades)>2 and hearts(south)<4   // avoid Smolen
sH = hearts(south)>5 and top5(south,hearts)>2 and spades(south)<4
sM = (sS or sH)
sMwShort  = sM and shape(south, any 0xxx + any 1xxx)
sMwoShort = sM and shape(south, xxxx -any 0xxx -any 1xxx)

sT1 = sMwoShort and hcp(south)>9
sT2 = sMwShort  and hcp(south)>6

gibNT and (sT1 or sT2) and calmEast


action printpbn

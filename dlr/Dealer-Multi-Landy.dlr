# Dealer Multi-Landy
generate 10000000
produce 500
dealer east


# Define East's 1N Opener
NT = hcp(east)>14 and hcp(east)<18 and shape(east,any 4333+any 4432+any 5422+any 5332-5xxx-x5xx)

# Define South's Multi-Landy
# Double...
#... 4-card major & 5+ minor, a 6+ minor, a 5-card Major, a balanced 19+
M4m5  = (spades(south)==4 or hearts(south)==4) and (diamonds(south)>4 or clubs(south)>4)
m6    = (diamonds(south)>5 or clubs(south)>5)
M5    = shape(south, 53xx+5233+35xx+2533-xxx4-xx4x-any 55xx-any 65xx)
bal19 = shape(south,any 4333+any 4432+any 5332) and hcp(south)>18
cX = (M4m5 or m6 or M5 or bal19)

# 2C = at lead 5-4 or 4-5 in hearts and spades
c2C = shape(south,66xx+65xx+56xx55xx+54xx+45xx) 

# 2D = 6 hearts/spades w/o a side suit
c2D = shape(south,6xxx+x6xx-any 65xx-any 64xx)

# 2H = 5 hearts and 4+ minor
c2H = hearts(south)==5 and spades(south)<4 and shape(south,any 54xx)

# 2S = 5 spades and 4+ minor
c2S = spades(south)==5 and hearts(south)<4 and shape(south,any 54xx)

# 2N = 5-5 or better in the minors
c2N = diamonds(south)>4 and clubs(south)>4

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###
### See -Stats-Leveling for statistics on these values ###
### Example of how to generate Statistics for Leveling ###


# Define Leveling
lX = cX  and keep03
lC = c2C and keep11
lD = c2D and keep14
lH = c2H and keep19
lS = c2S and keep14
lN = c2N and keep
levelTheDeal = lX or lC or lD or lH or lS or lN

# Now OPEN 1NT
NT and (cX or c2C or c2D or c2H or c2S or c2N) and hcp(south)>7
and levelTheDeal

# Generate Statistics
action

average "X    " 100 * cX,
average "2C   " 100 * c2C,
average "2D   " 100 * c2D,
average "2H   " 100 * c2H,
average "2S   " 100 * c2S,
average "2N   " 100 * c2N,

frequency  "HCP South "  (hcp(south) , 14, 18),


printpbn

# alias: WB5Roudi
# button-text: Roudi
# scenario-title: --- After 1x (P) 1y (P) 1NT, 2!C is an artificial asking bid.
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\n(1[CDH].* Pass.* 1[DHS].*\n1NT?)
# convention-card-ns: 21GF-DEFAULT
# convention-card-ew: 21GF-GIB

/*@chat
--- After 1x (P) 1y (P) 1NT, 2!C is an artificial asking bid.
2!D shows a min opening and no major fit.
2!H shows a min opening and 3-card in responder's major.
2!S shows a max opening and 3-card in responder's major.
2N shows a max opening and no major fit.
3m shows a max opening and 3-card in responder's major & 5-cards in the suit bid.
@chat*/

dealer south

/*
Roudi
*/

# Roudi:  After 1m (1S) 2m shows 7-10 HCP w/5H

##### Imported Script -- Predict Opening 1-Bid South #####
# 01/19/2026 -- Changed to use TP and separate ranges for C, D, and majors
# 03/30/2025 -- Changed to use gibNT

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422 -5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

## Calculate TP for South -- NOTE:  This uses different variable names than TP to avoid conflicts when both are used.
# Calculate South's Short-Suit Penalty Points
ssp = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
shp = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sdp = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
scp = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv01 = shape(south, any 0xxx) ? 3 : 0
ss01 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss02 = shape(south, any 11xx) ? 2 : 0
sd01 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd02 = shape(south, any 22xx) ? 1 : 0
sd03 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv01+ss01+ss02+sd01+sd02+sd03 -ssp-shp-sdp-scp

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22

# Define Gambling 3N
g3Shape = shape(south,any 9xxx +any 8xxx +any 7xxx -any 4xxx -any 5xxx -any 6xxx)
threeNT = (top3(south,clubs)==3 and clubs(south)>6) or (top3(south,diamonds)==3 and diamonds(south)>6) and hcp(south)<12 and g3Shape

# Define Game Forcing 2C
case1 = hcp(south)>18 and sTP>21
case2 = hcp(south)>18 and sTP>19 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and sTP>19 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3)

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT

# Define quality and length lower limits for 10 HCP openers

q2of3 = (top3(south,spades)>1 or top3(south,hearts)>1) and controls(south)>3
lgt5  = (spades(south)>4 or hearts(south)>4) and shape(south,any 7xxx +any 64xx +any 55xx +any 1xxx+any 0xxx -any 8xxx -any 54xx) and sTP>(hcp(south) +2)

# Predict South's Opening suit
# What's the longest suit?
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)

## GIB has different requirements for 1H/S, 1D, and 1C openings 
#Define range for 1 H/S
lS = s>4 and s>=h and s>=d and s>=c
lH = h>4 and h>s and h>=d and h>=c
lM = lS or lH

hsOK1 = lM and hcp(south)>11
hsOK2 = lM and hcp(south)>10 and sTP>12 and not hsOK1
hsOK3 = lM and hcp(south)>9  and lgt5 and q2of3 and not (hsOK1 or hsOK2)
hsRange = (hsOK1 or hsOK2 or hsOK3) and not P2

# Define 1D Range
dOK1 = hcp(south)>11 and (diamonds(south)>3 or shape(south, 4432))
dOK2 = hcp(south)>9  and sTP>12 and diamonds(south)>5 and not dOK1
dRange = (dOK1 or dOK2) and not P2

# Define 1C Range
cOK1 = hcp(south)>10 and sTP>12 and spades(south)==4
cOK2 = hcp(south)>9  and sTP>12 and clubs(south)>5 and not cOK1
cOK3 = hcp(south)>11 and clubs(south)>2            and not (cOK1 or cOK2)
cRange = (cOK1 or cOK2 or cOK3) and clubs(south)>2 and clubs(south)>diamonds(south) and not P2

oS = s>4 and s>=h and s>=d and s>=c and hsRange
oH = not oS and h>4 and h>=d and h>=c and hsRange
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and dRange
oC = not (oS or oH or oD) and cRange
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
##### Predict Response to 1-Bid ##### 

# These variables are defined in the imported scripts above
# The number of cards South has in each suit: s, h, d, c
# South's predicted opening suit: oC, oD, oH, oS 

sP = hcp(south)
nP = hcp(north)

# Predict North's 1-level response
sN = spades(north)
hN = hearts(north)
dN = diamonds(north)
cN = clubs(north)

# North responds in D, H, or S (Walsh style), or raises C.
######
# If less than 13, Walsh Style; otherwise, longest suit.
nS  =  nP<13 ? sN>3 and sN>hN : sN>3 and sN>hN and sN>=dN and sN>=cN
nH  = (nP<13 ? hN>3           : hN>3           and hN>=dN and hN>=cN) and not (nS)
nD = dN>3           and dN>=cN and not (nS or nH)
nC = cN>4           and cN>4   and not (nS or nH or nD)
nN = not (nS or nH or nD or nC)

# Define South's possible new-suit/NT rebids
sRC = clubs(south)>3
sRD = diamonds(south)>3
sRH = hearts(south)>3
sRS = spades(south)>3
sRN = shape(south,any 4333+any 4432+any 5332)


# Defining South's opening (not NT) & North's non-GF response
CD = oC and nD
CH = oC and nH
CS = oC and nS
CN = oC and nN
CC = oC and nC

DH = oD and nH
DS = oD and nS
DN = oD and nN
DD = oD and not (DH or DS or DN)

HS = oH and nS
HH = oH and hN>2 and not HS
HN = oH and not (HS or HH)

##### End of Predict Response to 1-Bid #####
##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####
# Defines calmOpps
##### Imported Script - Calculate Total Points for North #####

## Calculate TP for North
# Calculate Nouth's Short-Suit Penalty Points
nSP = spades(north)<3   and hcp(north,spades)>0   ? 1 : 0
nHP = hearts(north)<3   and hcp(north,hearts)>0   ? 1 : 0
nDP = diamonds(north)<3 and hcp(north,diamonds)>0 ? 1 : 0
nCP = clubs(north)<3    and hcp(north,clubs)>0    ? 1 : 0

# Calculate North's Short-Suit Points
nv1 = shape(north, any 0xxx) ? 3 : 0 // allow for 1 void
ns1 = shape(north, any 1xxx) ? 2 : 0 // allow for 2 singletons
ns2 = shape(north, any 11xx) ? 2 : 0
nd1 = shape(north, any 2xxx) ? 1 : 0 // allow for 3 doubletons
nd2 = shape(north, any 22xx) ? 1 : 0
nd3 = shape(north, any 222x) ? 1 : 0

# Calculate South's Total Points
nTP = hcp(north) + nv1+ns1+ns2+nd1+nd2+nd3 -nSP-nHP-nDP-nCP

### End of Calculate TP for North ###
# Defines nTP

CDN = CD and h<4 and s<4 and c<6 and d<4
CHN = CH and s<4 and c<6 and h<4
CSN = CS and c<6 and s<4
DHN = DH and s<4 and c<4 and d<6 and h<4
DSN = DS and c<4 and d<6 and s<4
HSN = HS and c<4 and d<4 and h<6 and s<4

sRange = hcp(south)>11 and hcp(south)<16
nRange = nTP>10 and nTP<13
xyn = ((CDN or CHN or CSN) and clubs(north)<4) or ((DHN or DSN) and diamonds(north)<4) or (HSN and hearts(north)<3)

xyn and sRange and calmOpps and nRange
action printoneline,

# button-text: Serious/Non-Serious
# scenario-title: Serious/Non-Serious: When we're in a 2/1 auction and we have a major suit fit
# Serious
dealer north

# auction-filter: 1[DHS].*\\n*3[HS] Pass 

# Serious/Non-Serious

# North's opening suit
NT = shape(north,any 4333+any 4432+any 5332-5xxx-x5xx) and hcp(north)>14 and hcp(north)<18
sN = spades(north)
hN = hearts(north)
dN = diamonds(north)
cN = clubs(north)
nS = sN>4 and sN>=hN and sN>=dN and sN>=cN     and spades(south)<4 and not NT
nH = not nS and hN>4 and hN>=dN and hN>=cN     and hearts(south)<4 and not NT
nD = not nS and not nH and ((dN>3 and dN>=cN)) and spades(north)<4 and hearts(north)<4 and not NT

# South's longest suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
sS = s>=h and s>=d and s>=c
sH = not sS and h>=d and h>=c
sD = not sS and not sH and d>=c
sC = not sS and not sH and not sD

nOpens = ((nS and (sC or sD or sH)) or (nH and (sC or sD)) or (nD and sC)) and hcp(north)>11 and hcp(south)>12

sFit = spades(north) + spades(south) > 7
hFit = hearts(north) + hearts(south) > 7
majorFit = sFit or hFit

totalHCP = hcp(north) + hcp(south)
game = totalHCP>23 and totalHCP<32
slam = totalHCP>31 and totalHCP<36
grand = totalHCP>35

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###


levGame  = game and keep03
levSlam  = slam and keep44
levGrand = grand and keep
levelTheDeal = levGame or levSlam or levGrand

# Now do it
nOpens and majorFit and (game or slam or grand) and hcp(north)<16
and levelTheDeal

action
average "game " 100 * game,
average "slam " 100 * slam,
average "grand" 100 * grand,

# Double_Showing_2_Suits
generate 100000000
produce 500
dealer west

##### Responsive Double #####
# West Opens, North Overcalls/Passes, East Raises/Bids, ...

# Predict West's Opening suit
s = spades(west)
h = hearts(west)
d = diamonds(west)
c = clubs(west)
oS = s>4 and s>=h and s>=d and s>=c
oH = not oS and h>4 and h>=d and h>=c
oD = not (oS or oH) and ((d>3 and d>=c) or c<3)
oC = not (oS or oH or oD)
westSuit = (oS or oH or oD or oC)

nS = oS ? spades(north)<3   : 0
nH = oH ? hearts(north)<3   : 0
nD = oD ? diamonds(north)<3 : 0
nC = oC ? clubs(north)<3    : 0
nOverCalls = (nS or nH or nD or nC) and shape(north,any 6xxx +any 5xxx) and hcp(north)>7 and hcp(north)<11
nPasses    = shape(north, any 4333 +any 4432) and hcp(north)<11

eS = oS ? spades(east)>2   : 0
eH = oH ? hearts(east)>2   : 0
eD = oD ? diamonds(east)>3 : 0
eC = oC ? clubs(east)>3    : 0
eFit = (eS or eH or eD or eC)

westOpens = hcp(west)>11 and hcp(west)<15 and westSuit
northActs = nOverCalls or nPasses
eastActs = (nOverCalls and eFit and hcp(east)>5 and hcp(east)<10) or (nPasses and not eFit and (hcp(east)>10 and hcp(east)<13))
southActs = hcp(south)>12

##### Now do it #####
westOpens and northActs and eastActs and southActs


action printpbn

# button-text: Michaels after 1m
# scenario-title: Michaels Cuebid after 1m
# Michaels_after_1m
dealer south


/*
auction-filter: Auction.....\n(1C +2C|1D +2D|1H +2H|1S +2S)
*/

### Imported script: TP (Total Points)

//   Note:  My initial implementation used shape staments for counting voids, singletons, and doubletons.
//   Dealer has a limit 32, so I eliminated the shape statements in this script.

## Calculate TP for North
# Calculate North's Short-Suit Penalty Points
nsPP = spades(north)  <3 and hcp(north,spades)  >0 ? 1 : 0
nhPP = hearts(north)  <3 and hcp(north,hearts)  >0 ? 1 : 0
ndPP = diamonds(north)<3 and hcp(north,diamonds)>0 ? 1 : 0
ncPP = clubs(north)   <3 and hcp(north,clubs)   >0 ? 1 : 0
nPP = nsPP+nhPP+ndPP+ncPP

# Calculate North's Short-Suit Points
ns0 = spades(north)  ==0 ? 3 : 0   // 3 for each void
nh0 = hearts(north)  ==0 ? 3 : 0
nd0 = diamonds(north)==0 ? 3 : 0
nc0 = clubs(north)   ==0 ? 3 : 0

ns1 = spades(north)  ==1 ? 2 : 0   // 2 for each singleton
nh1 = hearts(north)  ==1 ? 2 : 0
nd1 = diamonds(north)==1 ? 2 : 0
nc1 = clubs(north)   ==1 ? 2 : 0

ns2 = spades(north)  ==2 ? 1 : 0   // 1 for each doubleton
nh2 = hearts(north)  ==2 ? 1 : 0
nd2 = diamonds(north)==2 ? 1 : 0
nc2 = clubs(north)   ==2 ? 1 : 0

nSSP = ns0+nh0+nd0+nc0+ns1+nh1+nd1+nc1+ns2+nh2+nd2+nc2

tpNorth = hcp(north) + nSSP - nPP

## Calculate TP for East
# Calculate East's Short-Suit Penalty Points
esPP = spades(east)  <3 and hcp(east,spades)  >0 ? 1 : 0
ehPP = hearts(east)  <3 and hcp(east,hearts)  >0 ? 1 : 0
edPP = diamonds(east)<3 and hcp(east,diamonds)>0 ? 1 : 0
ecPP = clubs(east)   <3 and hcp(east,clubs)   >0 ? 1 : 0
ePP = esPP+ehPP+edPP+ecPP

# Calculate East's Short-Suit Points
es0 = spades(east)==0     ? 3 : 0
eh0 = hearts(east)==0     ? 3 : 0
ed0 = diamonds(east)==0   ? 3 : 0
ec0 = clubs(east)==0      ? 3 : 0

es1 = spades(east)==1     ? 2 : 0
eh1 = hearts(east)==1     ? 2 : 0    
ed1 = diamonds(east)==1   ? 2 : 0
ec1 = clubs(east)==1      ? 2 : 0

es2 = spades(east)==2     ? 1 : 0
eh2 = hearts(east)==2     ? 1 : 0
ed2 = diamonds(east)==2   ? 1 : 0
ec2 = clubs(east)==2      ? 1 : 0

eSSP = es0+eh0+ed0+ec0+es1+eh1+ed1+ec1+es2+eh2+ed2+ec2

tpEast = hcp(east) + eSSP - ePP


## Calculate TP for South
# Calculate South's Short-Suit Penalty Points
ssPP = spades(south)  <3 and hcp(south,spades)  >0 ? 1 : 0   
shPP = hearts(south)  <3 and hcp(south,hearts)  >0 ? 1 : 0
sdPP = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
scPP = clubs(south)   <3 and hcp(south,clubs)   >0 ? 1 : 0
sPP = ssPP+shPP+sdPP+scPP

# Calculate South's Short-Suit Points
ss0 = spades(south)==0     ? 3 : 0
sh0 = hearts(south)==0     ? 3 : 0
sd0 = diamonds(south)==0   ? 3 : 0
sc0 = clubs(south)==0      ? 3 : 0   

ss1 = spades(south)==1     ? 2 : 0
sh1 = hearts(south)==1     ? 2 : 0
sd1 = diamonds(south)==1   ? 2 : 0
sc1 = clubs(south)==1      ? 2 : 0

ss2 = spades(south)==2     ? 1 : 0
sh2 = hearts(south)==2     ? 1 : 0
sd2 = diamonds(south)==2   ? 1 : 0
sc2 = clubs(south)==2      ? 1 : 0

sSSP = ss0+sh0+sd0+sc0+ss1+sh1+sd1+sc1+ss2+sh2+sd2+sc2

tpSouth = hcp(south) + sSSP - sPP


## Calculate TP for West
# Calculate West's Short-Suit Penalty Points
wsPP = spades(west)  <3 and hcp(west,spades)  >0 ? 1 : 0
whPP = hearts(west)  <3 and hcp(west,hearts)  >0 ? 1 : 0
wdPP = diamonds(west)<3 and hcp(west,diamonds)>0 ? 1 : 0
wcPP = clubs(west)   <3 and hcp(west,clubs)   >0 ? 1 : 0
wPP = wsPP+whPP+wdPP+wcPP

# Calculate West's Short-Suit Points
ws0 = spades(west)==0     ? 3 : 0
wh0 = hearts(west)==0     ? 3 : 0
wd0 = diamonds(west)==0   ? 3 : 0
wc0 = clubs(west)==0      ? 3 : 0

ws1 = spades(west)==1     ? 2 : 0
wh1 = hearts(west)==1     ? 2 : 0
wd1 = diamonds(west)==1   ? 2 : 0
wc1 = clubs(west)==1      ? 2 : 0

ws2 = spades(west)==2     ? 1 : 0
wh2 = hearts(west)==2     ? 1 : 0
wd2 = diamonds(west)==2   ? 1 : 0
wc2 = clubs(west)==2      ? 1 : 0

wSSP = ws0+wh0+wd0+wc0+ws1+wh1+wd1+wc1+ws2+wh2+wd2+wc2

tpWest = hcp(west) + wSSP - wPP

### End of Imported Script: TP
# Defines tpSouth, tpNorth
##### Imported Script -- Predict Opening 1-Bid #####
# 01/19/2026 -- Changed to use TP and separate ranges for C, D, and majors
# 03/30/2025 -- Changed to use gibNT

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422 -5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

## Calculate TP for South -- NOTE:  This uses different variable names than TP to avoid conflicts when both are used.
# Calculate South's Short-Suit Penalty Points
ssp = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
shp = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sdp = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
scp = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv01 = shape(south, any 0xxx) ? 3 : 0
ss01 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss02 = shape(south, any 11xx) ? 2 : 0
sd01 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd02 = shape(south, any 22xx) ? 1 : 0
sd03 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv01+ss01+ss02+sd01+sd02+sd03 -ssp-shp-sdp-scp

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22
threeNT = TwoNtShape and hcp(south)>24 and hcp(south)<28

# Define Game Forcing 2C
case1 = hcp(south)>18 and sTP>21
case2 = hcp(south)>18 and sTP>19 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and sTP>19 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT

# Define quality and length lower limits for 10 HCP openers

q2of3 = (top3(south,spades)>1 or top3(south,hearts)>1) and controls(south)>3
lgt5  = (spades(south)>4 or hearts(south)>4) and shape(south,any 7xxx +any 64xx +any 55xx +any 1xxx+any 0xxx -any 8xxx -any 54xx) and sTP>(hcp(south) +2)

# Predict South's Opening suit
# What's the longest suit?
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)

## GIB has different requirements for 1H/S, 1D, and 1C openings 
#Define range for 1 H/S
lS = s>4 and s>=h and s>=d and s>=c
lH = h>4 and h>s and h>=d and h>=c
lM = lS or lH

hsRange1 = lM and hcp(south)>11
hsRange2 = lM and hcp(south)>10 and sTP>12 and not hsRange1
hsRange3 = lM and hcp(south)>9  and lgt5 and q2of3 and not (hsRange1 or hsRange2)
hsRange = (hsRange1 or hsRange2 or hsRange3) and not P2

# Define 1D Range
dOK1 = hcp(south)>11 and (diamonds(south)>3 or shape(south, 4432))
dOK2 = hcp(south)>9  and sTP>12 and diamonds(south)>5
dRange = (dOK1 or dOK2) and not P2

# Define 1C Range
cOK1 = hcp(south)>10 and sTP>12 and spades(south)==4
cOK2 = hcp(south)>9  and sTP>12 and clubs(south)>5
cOK3 = hcp(south)>11 and clubs(south)>2 and not dRange
cRange = (cOK1 or cOK2 or cOK3) and clubs(south)>2 and clubs(south)>diamonds(south) and not P2

oS = s>4 and s>=h and s>=d and s>=c and hsRange
oH = not oS and h>4 and h>=d and h>=c and hsRange
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and dRange
oC = not (oS or oH or oD) and cRange
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
# Defines oneClub, oneDiamond, oneHeart, oneSpade

predeal west HT9,ST9

sClubs = oneClub and cRange and clubs(north)>3 and clubs(north)>diamonds(north)
sDiamonds = oneDiamond and dRange and diamonds(north)>3 and diamonds(north)>clubs(north)
southOpens = (sDiamonds or sClubs)

# Define West's Michaels Cuebid
wSuits = top5(west,spades)>2 and top5(west,hearts)>2
westMCB= shape(west,76xx +67xx +66xx +65xx +56xx +55xx) and wSuits and tpWest>8

#Define Forcing Raise 13+ -- 3S  // extra strength or extra length
cForceR = sClubs    and ((clubs(north)   ==4 and tpNorth>=16) or (clubs(north)   >=5 and tpNorth>=13))
dForceR = sDiamonds and ((diamonds(north)==4 and tpNorth>=16) or (diamonds(north)>=5 and tpNorth>=13))
force = (cForceR or dForceR)

# Define Limit Raise or better 10+ -- 2H
cLimitR = sClubs    and ((clubs(north)   ==4 and tpNorth>=12) or (clubs(north)   >=5 and tpNorth>=10)) and not cForceR
dLimitR = sDiamonds and ((diamonds(north)==4 and tpNorth>=12) or (diamonds(north)>=5 and tpNorth>=10)) and not dForceR
limit = (cLimitR or dLimitR)

# Define Competitive Raise 7-9 -- 3C/D
cCompR = sClubs    and ((clubs(north)   ==4 and tpNorth>=8) or (clubs(north)   >=5 and tpNorth>=7)) and not (cForceR or cLimitR)
dCompR = sDiamonds and ((diamonds(north)==4 and tpNorth>=8) or (diamonds(north)>=5 and tpNorth>=7)) and not (dForceR or dLimitR)
comp = (cCompR or dCompR)   

# Define Preemptive Raise 4-6 -- 4 or 5 C/D
cPremR = sClubs    and (clubs(north)   >=7 and tpNorth>=4) and not (cForceR or cLimitR or cCompR)
dPremR = sDiamonds and (diamonds(north)>=6 and tpNorth>=4) and not (dForceR or dLimitR or dCompR)
prem = (cPremR or dPremR)

northResponds = (force or limit or comp or prem)

southOpens and westMCB and northResponds

action
average "force" 100 * force,
average "limit" 100 * limit,
average "comp " 100 * comp,
average "prem " 100 * prem,

# button-text: Direct Jump Cuebid
# scenario-title: Direct Jump Cuebid works differently for a major suit and minor suit.
# Direct_Jump_Cuebid
dealer east


# auction-filter: Auction.....\\n(1C 3C)|(1D 3D)|(1H 3H)|(1S 3S)

# Jump Cue Bid 

# Define good minor suits -- preempt in their minor
goodD  = diamonds(south)>6 and hcp(south,diamonds)>6
goodC  = clubs(south)>6    and hcp(south,clubs)>6

# Define solid suits interested in 3N if partner has a stopper
solidD = diamonds(south)>5 and hcp(south,diamonds)>8
solidC = clubs(south)>5    and hcp(south,clubs)>8
solidH = hearts(south)>5   and hcp(south,hearts)>8
solidS = spades(south)>5   and hcp(south,spades)>8

# Predict East's opening suit
s = spades(east)
h = hearts(east)
d = diamonds(east)
c = clubs(east)
oS = s>4 and s>=h and s>=d and s>=c
oH = not oS and h>4 and h>=d and h>=c
oD = not (oS or oH) and ((d>3 and d>=c) or c<3)
oC = not (oS or oH or oD)

# A or Kx in opponent's suit is a stopper
HS = controls(south,hearts)>1   or (controls(south,hearts)==1   and hearts(south)>1)
SS = controls(south,spades)>1   or (controls(south,spades)==1   and spades(south)>1)

openC = oC and goodC
openD = oD and goodD 
openH = oH and (solidC or solidD or solidS) and not HS
openS = oS and (solidC or solidD or solidH) and not SS
openMinor = (openC or openD) and hcp(south)<11                       // aggressive weak jump overcall
openMajor = (openH or openS) and hcp(south)>17 and losers(south)<5   // solid suit asking for 3N

# East opens
eOpens = (openMinor or openMajor) and hcp(east)>11 and hcp(east)<15

eOpens

action
average "(hcp east)  " hcp(east),

average "Over minor ----- " 100 * openMinor,
average "(hcp south) " hcp(south) and openMinor,
average "clubs       " goodC and openMinor,
average "diamonds    " goodD and openMinor,

average "Over major  ----- " 100 * openMajor,
average "(hcp south) " hcp(south) and openMajor,
average "clubs       " solidC and openMajor,
average "diamonds    " solidD and openMajor,
average "hearts      " solidH and openMajor,
average "spades      " solidS and openMajor,


printpbn

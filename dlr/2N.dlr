# button-text: 2NT 20-21 (Lev)
# scenario-title: 20-21 Notrump and any response.  Leveled so that the various
# 2N
dealer south


# auction-filter: Auction.....\\n2NT

### 2N and any response

##### Imported Script -- Predict Opening 1-Bid #####

# Changed to use gibNT 3/30/2025

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, 4252+4225+2452+2425+2254+2245) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22
threeNT = TwoNtShape and hcp(south)>24 and hcp(south)<28

# Define Game Forcing 2C
case1 = hcp(south)>21
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####

### NORTH ###

# Define suit lengths
cN = clubs(north)
dN = diamonds(north)
hN = hearts(north)
sN = spades(north)

# Calculate strength for North -- hcp plus 1 for every card over 4 in any suit 
nlp1 = sN>4 ? sN-4 : 0
nlp2 = hN>4 ? hN-4 : 0
nlp3 = dN>4 ? dN-4 : 0
nlp4 = cN>4 ? cN-4 : 0
strength = nlp1 + nlp2 + nlp3 + nlp4 + hcp(north)

# Define objective
weak = strength<5
game = strength>4
slam = strength>10

# Define One Minor and Both Minors
cSuit  = cN>5 and hcp(north,clubs)>2    and dN<4 and shape(north,xxxx-any 66xx-any 65xx)
dSuit  = dN>5 and hcp(north,diamonds)>2 and cN<4 and shape(north,xxxx-any 66xx-any 65xx)
minor2 = ((cN>4 and dN>3) or (dN>4 and cN>3)) and (hcp(north,clubs) + hcp(north,diamonds))>2

# Define Responses
smolen   = shape(north,54xx+45xx-any 7xxx-any 6xxx-any 55xx)       and game and shape(south,33xx+32xx+23xx)  // rebid 2D
stayman  = shape(north,44xx+4xxx+x4xx-54xx-45xx-any 7xxx-any 6xxx) and game and not smolen
puppet   = shape(north,4xxx+3xxx+x4xx+x3xx-any 6xxx -any 5xxx)     and game and not (smolen or stayman)
MSS      = minor2                                                  and game and not (smolen or stayman)
oneMinor = (cSuit or dSuit)                                        and game and not (smolen or stayman or MSS)
balanced = shape(north,any 4333 +any 4432 +any 5332 -5xxx-x5xx)    and game and not (smolen or stayman or puppet or MSS)
texas    = (sN>5 or hN>5)                                          and game
jacoby   = ((sN>4 and hN<4) or (hN>4 and sN<4))                             and not texas  // no strength requirement
pass     = weak and not jacoby

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###


levPass     = weak     and keep14
levBalanced = balanced and keep33
levPuppet   = puppet   and keep19
levSmolen   = smolen   and keep
levStayman  = stayman  and keep06
levTexas    = texas    and keep19
levJacoby   = jacoby   and keep06
levOneMinor = oneMinor and keep44
levMSS      = MSS      and keep47
levelTheDeal = (levPass or levBalanced or levSmolen or levPuppet or levStayman or levTexas or levJacoby or levOneMinor or levMSS)

### Do it ###
twoNT
and levelTheDeal

### Generate Statistics ###

action
#printoneline,
average "Pass                  " 100 * pass,
average "Balanced, not Stayman " 100 * balanced, //and not (puppet or stayman or MSS)),
average "Puppet, not Stayman   " 100 * puppet,   // and not (stayman or MSS)),
average "Smolen                " 100 * smolen,
average "Stayman, not Smolen   " 100 * stayman,
average "Texas                 " 100 * texas,
average "Jacoby, not Texas     " 100 * jacoby,   // and not texas),
average "One Minor             " 100 * oneMinor, // and not (puppet or stayman or jacoby)),
average "MSS, not Stayman      " 100 * MSS,      // and not (puppet or stayman)),

average "Pass                  " 100 * weak,
average "Game                  " 100 * game,
average "Slam                  " 100 * slam,


printpbn

# button-text: 3-under Inv. Jump
# scenario-title: 3 Under Invitational Jump
# 3_Under_Invitational_Jump
dealer south


# No WJS
# convention-card: 21GF-3-Under-Inv-Jump
# auction-filter: 1D.Pass.3C|1H.Pass.3[CD]|1S.Pass.3[CDH]

# Robot 3 Under Invitational Jump (9-11 HCP)

##### Imported Script -- Predict Opening 1-Bid #####

# Changed to use gibNT 3/30/2025

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, 4252+4225+2452+2425+2254+2245) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22
threeNT = TwoNtShape and hcp(south)>24 and hcp(south)<28

# Define Game Forcing 2C
case1 = hcp(south)>21
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
# Defines oneClub, oneSpade, oneHeart, oneDiamond
##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

#unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
#unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

#balEast   = shape(east,any 4333 +any 4432)
#balWest   = shape(west,any 4333 +any 4432)

balEast    = shape(east,any 4432+any 4333)
unbalEast  = not balEast

balWest    = shape(west,any 4432+any 4333)
unbalWest  = not balWest

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<12)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<12)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####

# Defines calmWest

# Define NoFit
sFit = oneSpade and spades(north)>2
hFit = oneHeart and hearts(north)>2
dFit = oneDiamond and diamonds(north)>3
noFit = not (sFit or hFit or dFit)

# Define North's good suits -- 6+ cards with at least AK, AQ, or KQJ
#predeal north H9, D9, C9
goodHearts   = hearts(north)>5   and hcp(north,hearts)>2   and hcp(north,hearts)<7   and top5(north,hearts)==3
goodDiamonds = diamonds(north)>5 and hcp(north,diamonds)>2 and hcp(north,diamonds)<7 and top5(north,diamonds)==3
goodClubs    = clubs(north)>5    and hcp(north,clubs)>2    and hcp(north,clubs)<7    and top5(north,clubs)==3

# north does NOT have a major
noMajorNorth = spades(north)<4 and hearts(north)<4

n3C = (oneDiamond or oneHeart or oneSpade) and goodClubs and noMajorNorth
n3D = (oneHeart or oneSpade) and goodDiamonds and noMajorNorth
n3H = (oneSpade) and goodHearts

# (Predict South's opening bid sometimes opens w/10hcp
# Now do it 
(n3C or n3D or n3H) and calmWest and noFit and hcp(north)>8 and hcp(north)<12 and hcp(south)>10 //and shape(north,any 0xxx +any 1xxx)

action
average "hcp(south)" hcp(south),
average "hcp(north)" hcp(north),
average "1D-3C        " 100 * (oneDiamond and n3C),
average "1H-3C        " 100 * (oneHeart and n3C),
average "1H-3D        " 100 * (oneHeart and n3D),
average "1S-3C        " 100 * (oneSpade and n3C),
average "1S-3D        " 100 * (oneSpade and n3D),
average "1S-3H        " 100 * (oneSpade and n3H),


printpbn

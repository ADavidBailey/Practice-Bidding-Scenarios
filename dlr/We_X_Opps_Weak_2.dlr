# button-text: We X Opps W2
# scenario-title: We Double Opponents Weak Two opening.
# We_X_Opps_Weak_2
dealer east


# Opponent Opens Weak 2

# We Double

tp = hcp(east)
sp = hcp(east,spades)
hp = hcp(east,hearts)
dp = hcp(east,diamonds)

# Define East weak 2 bids w/o 3-card support by west
w2S = shape(east,6xxx-x4xx     -any 7xxx-any 66xx-any 65xx) and 2 * sp > tp and top3(east,spades)>1   and spades(west)<3
w2H = shape(east,x6xx-4xxx     -any 7xxx-any 66xx-any 65xx) and 2 * hp > tp and top3(east,hearts)>1   and hearts(west)<3
w2D = shape(east,xx6x-x4xx-4xxx-any 7xxx-any 66xx-any 65xx) and 2 * dp > tp and top3(east,diamonds)>1 and diamonds(west)<3

# Define South shortness w/3 suits & shortness in opener's suit
sShortS = w2S and spades(south)<3   and shape(south,x4xx)
sShortH = w2H and hearts(south)<3   and shape(south,4xxx)
sShortD = w2D and diamonds(south)<3 and shape(south,44xx)
sShort = (sShortS or sShortH or sShortD)

# Define South stopper
sStopS = w2S and (top2(south,spades)>0 and spades(south)>1)     or (top3(south,spades)>0 and spades(south)>2)
sStopH = w2H and (top2(south,hearts)>0 and hearts(south)>1)     or (top3(south,hearts)>0 and hearts(south)>2)
sStopD = w2D and (top2(south,diamonds)>0 and diamonds(south)>1) or (top3(south,diamonds)>0 and diamonds(south)>2)
sStop = (sStopS or sStopH or sStopD)

sNT = hcp(south)>15 and hcp(south)<19 and shape(south, any 4333+any 4432+any 5332) and sStop

sX1 = hcp(south)>11 and hcp(south)<16 and sShort
sX2 = hcp(south)>15 and hcp(south)<19 and not sNT
sX3 = hcp(south)>18
sX  = (sX1 or sX2 or sX3) and shape(south,any 5440+any 4441+any 4432)

eOpensWeak2 = (w2S or w2H or w2D) and hcp(east)>6 and hcp(east)<10

# West Passes
wPasses = hcp(west)<10

# North is weak, invitational, or forcing
weak   = hcp(north)<8
invite = hcp(north)>7 and hcp(north)<12
force  = hcp(north)>11

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###
### See -Stats-Leveling for statistics on these values ###
### Example of how to generate Statistics for Leveling ###


# Define leveling
levWeak   = weak   and keep33
levInvite = invite and keep11
levForce  = force  and keep75

levSX1    = sX1 and keep03
levSX2    = sX2 and keep
levSX3    = sX3 and keep25
levelTheDeal = (levWeak or levInvite or levForce or levSX1 or levSX2 or levSX3)

eOpensWeak2 and sX and wPasses and (weak or invite or force)
and levelTheDeal

action
average "South X 12-15 " 100 * sX1,
average "South X 16-18 " 100 * sX2,
average "South X 19+   " 100 * sX3,
average "North Weak    " 100 * weak,
average "North Invite  " 100 * invite,
average "North Force   " 100 * force,


printpbn

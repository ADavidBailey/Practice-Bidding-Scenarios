# button-text: Leb vs W2-Bal Seat
# scenario-title: Lebensohl After 2x-P-P
# Lebensohl_vs_Opps_W2_Bal
dealer west


# auction-filter: Auction.....\\n2[DHS] Pass Pass X

# Lebensohl after (2x) P (P) X

tp = hcp(west)
sp = hcp(west,spades)
hp = hcp(west,hearts)
dp = hcp(west,diamonds)

# Define West's weak 2 bids w/o 3-card support by East nor shortness in North
w2S = shape(west,6xxx-x4xx     -any 7xxx-any 66xx-any 65xx) and 2 * sp > tp and top5(west,spades)>2   and spades(east)<3
w2H = shape(west,x6xx-4xxx     -any 7xxx-any 66xx-any 65xx) and 2 * hp > tp and top5(west,hearts)>2   and hearts(east)<3
w2D = shape(west,xx6x-x4xx-4xxx-any 7xxx-any 66xx-any 65xx) and 2 * dp > tp and top5(west,diamonds)>2 and diamonds(east)<3

# Define South shortness w/3 suits & shortness in opener's suit
sShortS = w2S and spades(south)<3   and shape(south,x4xx) and hcp(south,spades)==0
sShortH = w2H and hearts(south)<3   and shape(south,4xxx) and hcp(south,hearts)==0
sShortD = w2D and diamonds(south)<3 and shape(south,44xx) and hcp(south,diamonds)==0
sShort = (sShortS or sShortH or sShortD)

# Define South stopper
sStopS = w2S and (top2(south,spades)>0 and spades(south)>1)
sStopH = w2H and (top2(south,hearts)>0 and hearts(south)>1)
sStopD = w2D and (top2(south,diamonds)>0 and diamonds(south)>1)
sStop = (sStopS or sStopH or sStopD)

sNT = hcp(south)>15 and hcp(south)<19 and shape(south, any 4333+any 4432+any 5332) and sStop

sDouble1 = hcp(south)>11 and hcp(south)<16 and sShort
sDouble2 = hcp(south)>15 and not sNT
sDoubles = (sDouble1 or sDouble2) and shape(south,any 5440+any 4441+any 4432)

# West opens weak 2
wOpensWeak2 = (w2S or w2H or w2D) and hcp(west)>6 and hcp(west)<10

# North Passes
nPasses = hcp(north)<14 and shape(north, any 5332 + any 4432) and controls(north)<5

# East Passes
ePasses = hcp(east)<10

# North responds
weak   = hcp(north)<8
invite = hcp(north)>7 and hcp(north)<12 and not hascard(west,2C)
force  = hcp(north)>11
nResponds = (weak or invite or force)

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###


# Define leveling
levWeak   = weak   and keep
levInvite = invite and keep
levForce  = force  and keep
levelTheDeal = (levWeak or levInvite or levForce)

wOpensWeak2 and nPasses and ePasses and sDoubles and nResponds
and levelTheDeal

action
average "Weak  " 100 * weak,
average "Invite" 100 * invite,
average "Force " 100 * force,

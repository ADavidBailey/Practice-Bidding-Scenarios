# button-text: MST or MSS
# scenario-title: After we open 1NT and partner bids Minor Suit Stayman or a Minor Suit Transferï¼Œ
# MST_or_MSS
dealer south


# Enable MST and MSS and Exclusion Blackwood
# convention-card: 21GF-MSTandMSS
# auction-filter: Note....1N-(2[SN]|3C)

# MinorSuitTransfer or MinorSuitStayman

##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, 4252+4225+2452+2425+2254+2245) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# Defines gibNT

# West passes

# Define good minor suits
gD = top4(north,diamonds)>2
gC = top4(north,clubs)>2

# Define fair minor suits
fD = top4(north,diamonds)>1
fC = top4(north,clubs)>1

# Define 4-card majors
noMajor = spades(north)<4 and hearts(north)<4

# North has 5-4 or better in the minors for MSS
mss = shape(north, xx76+xx67+xx66+xx65+xx56+xx55+xx54+xx45) and fD and fC  // both minors

# North has 6+ D/C for Transfer
mstC = shape(north,xxx9+xxx8+xxx7+xxx6) and gC  // Clubs
mstD = shape(north,xx9x+xx8x+xx7x+xx6x) and gD  // Diamonds
mst  = mstC or mstD

# Now do it
gibNT and (mss or mst) and noMajor and hcp(north)>14 and losers(north)<5

action
average "mss   " 100 * mss,
average "mst C " 100 * mstC,
average "mst D " 100 * mstD,
average "hcp S " hcp(south),
average "hcp N " hcp(north),
average "hcp   " hcp(south) + hcp(north),

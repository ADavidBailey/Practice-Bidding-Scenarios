# button-text: Cappelletti
# scenario-title: After 1NT...
# Cappelletti
dealer south


/*

auction-filter: Note....Cappelletti

*/

# Cappelletti

##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major or with 4 spades
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422-5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# Defines gibNT
##### Imported Script - Calculate Total Points for West #####

## Calculate TP for West
# Calculate West's Short-Suit Penalty Points
wSP = spades(west)<3   and hcp(west,spades)>0   ? 1 : 0
wHP = hearts(west)<3   and hcp(west,hearts)>0   ? 1 : 0
wDP = diamonds(west)<3 and hcp(west,diamonds)>0 ? 1 : 0
wCP = clubs(west)<3    and hcp(west,clubs)>0    ? 1 : 0

# Calculate West's Short-Suit Points
wv1 = shape(west, any 0xxx) ? 3 : 0 // allow for 1 void
ws1 = shape(west, any 1xxx) ? 2 : 0 // allow for 2 singletons
ws2 = shape(west, any 11xx) ? 2 : 0
wd1 = shape(west, any 2xxx) ? 1 : 0 // allow for 3 doubletons
wd2 = shape(west, any 22xx) ? 1 : 0
wd3 = shape(west, any 222x) ? 1 : 0

# Calculate West's Total Points
wTP = hcp(west) + wv1+ws1+ws2+wd1+wd2+wd3 -wSP-wHP-wDP-wCP

### End of Calculate TP for West ###
# Defines wTP, wPP

# Define minimum suit quality
wC = controls(west,clubs)>0    and hcp(west,clubs)>4
wD = controls(west,diamonds)>0 and hcp(west,diamonds)>4
wH = controls(west,hearts)>0   and hcp(west,hearts)>4
wS = controls(west,spades)>0   and hcp(west,spades)>4

A1 = (wC or wD or wH or wS) and shape(west,any 9xxx +any 8xxx +any 7xxx -any 85xx -any 76xx -any 75xx -any 66xx -any 65xx)
HS = wH and spades(west)>3  and hearts(west)>3 and (spades(west) - hearts(west))==(1 or -1)
Hm = wH and hearts(west)>4  and ((wD and diamonds(west)>4) or (wC and clubs(west)>4))
Sm = wS and spades(west)>4  and ((wD and diamonds(west)>4) or (wC and clubs(west)>4))
mm = wC and clubs(west)>4   and wD and diamonds(west)>4

# Define actions
w2C = A1 and ((wC and wTP>14) or wTP>11)
w2D = HS and wTP>10 and not (w2C)
w2H = Hm and wTP>10 and not (w2C or w2D)
w2S = Sm and wTP>10 and not (w2C or w2D or w2H)
w2N = mm and wTP>14 and not (w2C or w2D or w2H or w2S)
wX  = hcp(west)>15  and not (w2C or w2D or w2H or w2S or w2N) 

gibNT and (wX or w2C or w2D or w2H or w2S or w2N)

action

average "X   " 100 * wX,
average "2C  " 100 * w2C,
average "2D  " 100 * w2D,
average "2H  " 100 * w2H,
average "2S  " 100 * w2S,
average "2N  " 100 * w2N,


printpbn

# button-text: Cappelletti
# scenario-title: After 1NT... Cappelletti is the prefered defense for a Weak NT
# Cappelletti
dealer east


/*

convention-card: 21GF-GIB
auction-filter: Auction.....\n1NT?....[X2-7]

*/

# Cappelletti

##### Imported Script -- GIB 1 Notrump Rotated for East#####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(east) + shape(east,5xxx+x5xx)
nt1 = shape(east, any 5332+any 4432+any 4333) and hcp(east)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major or with 4 spades
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(east, any 5422-5xxx-x5xx-4xxx) and hcp(east)>14 and hcp(east)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
##### Imported Script - Calculate Total Points for South #####

## Calculate TP for South
# Calculate South's Short-Suit Penalty Points
sSP = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
sHP = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sDP = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
sCP = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv1 = shape(south, any 0xxx) ? 3 : 0
ss1 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss2 = shape(south, any 11xx) ? 2 : 0
sd1 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd2 = shape(south, any 22xx) ? 1 : 0
sd3 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv1+ss1+ss2+sd1+sd2+sd3 -sSP-sHP-sDP-sCP

### End of Calculate TP for South ###
# Defines nTP

# Define minimum suit quality
sC = controls(south,clubs)>0    and hcp(south,clubs)>3    and hcp(south,clubs)<9    and top4(south,clubs)>1
sD = controls(south,diamonds)>0 and hcp(south,diamonds)>3 and hcp(south,diamonds)<9 and top4(south,diamonds)>1
sH = controls(south,hearts)>0   and hcp(south,hearts)>3   and hcp(south,hearts)<9   and top4(south,hearts)>1
sS = controls(south,spades)>0   and hcp(south,spades)>3   and hcp(south,spades)<9   and top4(south,spades)>1

# Define possible single-suits -- at least 6-cards
c1 = sC and clubs(south)>5
d1 = sD and diamonds(south)>5
h1 = sH and hearts(south)>5
s1 = sS and spades(south)>5

a1 = (c1 or d1 or h1 or s1)

# Define possible double-suits -- at least 4-cards
c2 = sC and clubs(south)>3
d2 = sD and diamonds(south)>3
h2 = sH and hearts(south)>3
s2 = sS and spades(south)>3

# 2-suited hands are 5/4 or better
HS = h2 and s2 and (hearts(south) + spades(south))>8
Hm = (h2 and d2 and (hearts(south) + (diamonds(south))>8)) or (h2 and c2 and (hearts(south) + clubs(south))>8) and not HS
Sm = (s2 and d2 and (spades(south) + (diamonds(south))>8)) or (s2 and c2 and (spades(south) + clubs(south))>8) and not (HS or Hm)
mm = c2 and d2 and clubs(south)>4 and diamonds(south)>4  // minors must be 5/5 or better


# Define 2-suited actions
s2D = HS and sTP>10
s2H = Hm and sTP>10 and not s2D
s2S = Sm and sTP>10 and not (s2D or s2H)
s2N = mm and sTP>14 and not (s2D or s2H or s2S)

# If there are not 2-suits, then 1-suit
s2C = a1 and hcp(south)<15 and ((sC and sTP>14) or sTP>11) and not (s2D or s2H or s2S or s2N)

# or Double
sX  = hcp(south)>15 and not (s2C or s2D or s2H or s2S or s2N) 

gibNT and (sX or s2C or s2D or s2H or s2S or s2N)

action

average "X   " 100 * sX,
average "2C  " 100 * s2C,
average "2D  " 100 * s2D,
average "2H  " 100 * s2H,
average "2S  " 100 * s2S,
average "2N  " 100 * s2N,


printpbn

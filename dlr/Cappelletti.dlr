# button-text: Cappelletti
# scenario-title: After 1NT... Cappelletti is the prefered defense for a Weak NT
# Cappelletti
dealer east


/*

auction-filter: Auction.....\n1NT?....[X2-7]

*/

# Cappelletti

##### Imported Script -- GIB 1 Notrump Rotated for East#####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(east) + shape(east,5xxx+x5xx)
nt1 = shape(east, any 5332+any 4432+any 4333) and hcp(east)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major or with 4 spades
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(east, any 5422-5xxx-x5xx-4xxx) and hcp(east)>14 and hcp(east)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
##### Imported Script - Calculate Total Points for South #####

## Calculate TP for South
# Calculate South's Short-Suit Penalty Points
sSP = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
sHP = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sDP = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
sCP = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv1 = shape(south, any 0xxx) ? 3 : 0
ss1 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss2 = shape(south, any 11xx) ? 2 : 0
sd1 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd2 = shape(south, any 22xx) ? 1 : 0
sd3 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv1+ss1+ss2+sd1+sd2+sd3 -sSP-sHP-sDP-sCP

### End of Calculate TP for South ###
# Defines nTP

# Define minimum suit quality
sC = controls(south,clubs)>0    and hcp(south,clubs)>4
sD = controls(south,diamonds)>0 and hcp(south,diamonds)>4
sH = controls(south,hearts)>0   and hcp(south,hearts)>4
sS = controls(south,spades)>0   and hcp(south,spades)>4

A1 = (sC or sD or sH or sS) and shape(south,any 9xxx +any 8xxx +any 7xxx +any 6xxx -any 85xx -any 76xx -any 75xx -any 66xx -any 65xx)
HS = sH and spades(south)>3  and hearts(south)>3 and (spades(south) - hearts(south))==(1 or -1)
Hm = sH and hearts(south)>4  and ((sD and diamonds(south)>4) or (sC and clubs(south)>4))
Sm = sS and spades(south)>4  and ((sD and diamonds(south)>4) or (sC and clubs(south)>4))
mm = sC and clubs(south)>4   and sD and diamonds(south)>4

# Define actions
s2C = A1 and ((sC and sTP>14) or sTP>11)
s2D = HS and sTP>10 and not (s2C)
s2H = Hm and sTP>10 and not (s2C or s2D)
s2S = Sm and sTP>10 and not (s2C or s2D or s2H)
s2N = mm and sTP>14 and not (s2C or s2D or s2H or s2S)
sX  = hcp(south)>15  and not (s2C or s2D or s2H or s2S or s2N) 

gibNT and (sX or s2C or s2D or s2H or s2S or s2N)

action

average "X   " 100 * sX,
average "2C  " 100 * s2C,
average "2D  " 100 * s2D,
average "2H  " 100 * s2H,
average "2S  " 100 * s2S,
average "2N  " 100 * s2N,


printpbn

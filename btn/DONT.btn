# alias: DONT
# button-text: DONT (Lev)
# dealer-position: S
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\n1N +[X23]

/*@chat
--- 15-17 Notrump Opening and DONT action
  • X   = A one-suited hand. Asks partner to bid 2!C
  • 2!C = !C and a higher ranking suit
  • 2!D = !D and a higher ranking suit
  • 2!H = !H and !S
  • 2!S = 5+ !S, natural and less than 11 TP
The DONT actions are leveled.  The NT opener is not leveled.
https://www.bridgebum.com/dont.php
@chat*/

dealer east

#include "script/GIB-1N-East"
# Defines gibNT

#include "script/TP"
# Defines tpSouth

# Define South's good suits -- 4+ cards with KQT or better
sGS = spades(south)>4   and top3(south,spades)>1   and top5(south,spades)>2  //and controls(south,spades)>0
sGH = hearts(south)>4   and top3(south,hearts)>1   and top5(south,spades)>2  //and controls(south,hearts)>0
sGD = diamonds(south)>4 and top3(south,diamonds)>1 and top5(south,spades)>2  //and controls(south,diamonds)>0
sGC = clubs(south)>4    and top3(south,clubs)>1    and top5(south,spades)>2  //and controls(south,clubs)>0

# Define South's shapes
oneSuit = shape(south,7xxx +any 6xxx -any 9xxx -any 8xxx -any 76xx -any 66xx -any 5xxx)
sGoodSuit = oneSuit and sGS and spades(south)>5
oGoodSuit = oneSuit and (sGH or sGD or sGC)

maxWithSpades = sGoodSuit and tpSouth>10
minWithSpades = sGoodSuit and tpSouth<11

# Define South's DONT actions
cX  = oGoodSuit or maxWithSpades
c2C = (sGC    and (sGD or sGH or sGS)) and not cX
c2D = (sGD    and (sGH or sGS))        and not (cX or c2C) 
c2H = (sGH    and sGS)                 and not (cX or c2C or c2D) 
c2S = minWithSpades                    and not (cX or c2C or c2D or c2H) 

#include "script/Leveling"

# Define Leveling
lX = cX  and keep03
lC = c2C and keep19
lD = c2D and keep33
lH = c2H and keep75
lS = c2S and keep03
levelTheDeal = lX or lC or lD or lH or lS

# Now OPEN 1NT
gibNT and (cX or c2C or c2D or c2H or c2S) and tpSouth>8
and levelTheDeal

# Generate Statistics
action
average "X    " 100 * cX,
average "2C   " 100 * c2C,
average "2D   " 100 * c2D,
average "2H   " 100 * c2H,
average "2S   " 100 * c2S,


action printpbn

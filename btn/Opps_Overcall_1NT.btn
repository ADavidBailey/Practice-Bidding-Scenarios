# alias: OppsOvercall1NT
# button-text: 1NT Overcall
# gib-works: true
# bba-works: true
# auction-filter: 1[CDHS] +1N
# convention-card-ns: 21GF-DEFAULT
# convention-card-ew: 21GF-GIB

/*@chat
--- We open and opponent overcalls 1NT.\n
@chat*/

# Opponents Overcall 1NT
dealer south

# Predict South's opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)

sS = s>4 and s>=h and s>=d and s>=c
sH = not sS and h>4 and h>=d and h>=c
sD = not sS and not sH and ((d>3 and d>=c) or c<3)
sC = not sS and not sH and not sD

# Avoid opening 1N
ntShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422 -5xxx-x5xx)
notSouthNT = not (hcp(south)>14 and hcp(south)<18 and ntShape)

# West has a stop in South's opening suit -- at least Axx, Kxx or QJx
sSws = sS and hcp(west,spades)>2 and spades(west)>2
sHws = sH and hcp(west,hearts)>2 and hearts(west)>2
sDws = sD and hcp(west,diamonds)>2 and diamonds(west)>2
sCws = sC and hcp(west,clubs)>2 and clubs(west)>2

# South opens 1 of a suit & West has stop
notSouthNT
and (sSws or sHws or sDws or sCws)
and hcp(south)>11 and hcp(south)<20

# West bids 1N
and shape(west, any 4333 +any 4432 +any 5332 - 5xxx - x5xx)
and hcp(west)>14 && hcp(west)<18

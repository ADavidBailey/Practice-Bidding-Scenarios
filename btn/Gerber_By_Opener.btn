# alias: GerberByOpener
# button-text: Gerber by Opener (Lev)
# dealer-position: S
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\\n(1[CD].* Pass 2NT |2C.* Pass 2NT )

/*@chat
--- Gerber by Opener (Leveled)
There are two sequences where Gerber might be used -- after 1m - 2N
or 2!C - 2N and opener has slam interest.  The 1m - 2N sequences are rare.
https://www.bridgebum.com/gerber.php
@chat*/

# These are sequences where 4C-Gerber might be bid by Opener

# Gerber by Opener
#
# 1m - 2N - ?
# 2C - 2N - ?

dealer south

#include "script/Calm-Opponents"
# Defines calmWest
#include "script/Predict-Opening-1-Bid"
# Defines oneClub, oneDiamond

# Calculate South's TP
sC = clubs(south) >4   ? clubs(south) - 4    : 0
sD = diamonds(south)>4 ? diamonds(south) - 4 : 0
sH = hearts(south)>4   ? hearts(south) - 4   : 0
sS = spades(south)>4   ? spades(south) - 4   : 0
sTP = hcp(south) + sC + sD + sH + sS

# Define suit quality for NT -- Qxx opener's suit and Kxx or QJx in unbid suits
cQ = hcp(north,clubs)>0 and hcp(north,diamonds)>2  // 10-12 doesn't all room for 3-points in each suit
dQ = hcp(north,diamonds)>0 and hcp(north,clubs)>2
mQ = hcp(north,hearts)>2 and hcp(north,spades)>2

# South opens a minor and North has any 4333 with 10-12 and honor(s)in every suit
s1C = oneClub and dQ and clubs(south)>5
s1D = oneDiamond and cQ and diamonds(south)>5   // north doesn't have the strength for 2/1
s1mShape = shape(south,any 7xxx+any 6xxx)       // south shape upgrades required for TP
s1mRange = sTP>21 and hcp(south)<22
n1mRange = hcp(north)>10 and hcp(north)<13      // north responds 2N (I hope!)
n1mShape = shape(north,any 4xxx)

# South opens 1m and North responds 2N
s1m = (s1C or s1D) and s1mShape and s1mRange and mQ  and n1mRange

# South opens 2C and north responds 2N
n2cRange = hcp(north)>7
n2cShape = shape(north,3343+3334)  // avoid Stayman after 2C - 2D - 2N
n2cQ     = hcp(north,clubs)>1 and hcp(north,diamonds)>1 and hcp(north,hearts)>1 and hcp(north,spades)>1

s2C = hcp(south)>21 and n2cRange and n2cShape and n2cQ 

# Now do it
(s1m and calmWest) or s2C
action
average "1m - 2N  " 100 * s1m,
average "2C - 2N  " 100 * s2C,


action printpbn

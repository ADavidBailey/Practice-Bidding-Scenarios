# alias: Drury
# button-text: Drury
# gib-works: true
# bba-works: true
# auction-filter: Note...:Reverse.drury
# convention-card-ns: 21GF-DEFAULT
# convention-card-ew: 21GF-GIB

/*@chat
--- Drury
After P (P) 1M (P), 2!C is Drury or Reverse Drury.
2!C asks about the opener's hand.
Drury:  2!D denies game interest and 2M shows game interest.
Reverse Drury: 2!D shows some game interest and 2M denies game interest.
@chat*/

# Reverse Drury

dealer south
produce 1000

#include "script/Calm-Opponents"
# Defines calmOpps
#include "script/TP"
# Defines tpNorth and tpSouth

# South passes
sPasses   = tpSouth<12 and hcp(south)<11 and shape(south,any xxxx -any 9xxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

# South Support Jump-Shift
rebiddableD = diamonds(south)==5 and hcp(south,diamonds)>3
rebbidableC = clubs(south)==5    and hcp(south,clubs)>3
sSJS = (rebiddableD or rebbidableC) and hcp(south)>10 and tpSouth>10

# North opens 1H or 1S
nNT = shape(north,any 4333+any 4432+any 5332) and hcp(north)>14 and hcp(north)<18

//nMin = tpNorth==12 and hcp(north)==11
nSpade = spades(north)>4 and hearts(north)<5 and diamonds(north)<5 and clubs(north)<5 and spades(south)>2 and hearts(south)<5
nHeart = hearts(north)>4 and spades(north)<5 and diamonds(north)<5 and clubs(north)<5 and hearts(south)>2 and spades(south)<5
len = hearts(north)>spades(north) ? hearts(north) : spades(north)
nMin = len == 5 ? 12 : 11

nRange = hcp(north)>nMin and hcp(north)<18
nOpens = (nSpade or nHeart) and nRange and not nNT and diamonds(north)<len and clubs(north)<len

sResponds = tpSouth>9 and not sSJS

# Now do it
sPasses and nOpens and calmOpps and sResponds

action
average "HCP North " hcp(north),
average "TP North  " tpNorth,
average "HCP South " hcp(south),
average "TP South  " tpSouth,

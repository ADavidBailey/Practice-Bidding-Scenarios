# alias: MultiLandy
# button-text: Multi-Landy (Lev)
# gib-works: false
# bba-works: true
# auction-filter: Note....Multi-Landy
# convention-card-ns: 21GF-Multi-Landy
# convention-card-ew: 21GF-GIB

/*@chat
---  15-17 Notrump Opening and Multi-Landy action.
  • X  = 4-card !H/!S w/5+!C/!D, a 6+ !C/!D, a 5-card !H/!S, a balanced 19+
  • 2C = at least 5-4 or 4-5 in !H&!S
  • 2D = 6 !H/!S w/o a side suit
  • 2H = 5 !H & 4+ !C/!D
  • 2S = 5 spades & 4+ !C/!D
  • 2N = 5-5 or better in !C/!D
The Multi-Landy actions are leveled.  The NT opener is not leveled.
https://www.bridgebum.com/multi_landy.php
@chat*/

produce 1000
dealer east

# Define East's 1N Opener
### gibNT changed from south to east
# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(east) + shape(east,5xxx+x5xx)
nt1 = shape(east, any 5332+any 4432+any 4333) and hcp(east)>14 and ntP<18
# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(east, 4252+4225+2452+2425+2254+2245) and hcp(east)>14 and hcp(east)<17
gibNT = nt1 or nt2


# Define South's Multi-Landy
# Double...
#... 4-card major & 5+ minor, a 6+ minor, a 5-card Major, a balanced 19+
M4m5  = (spades(south)==4 or hearts(south)==4) and (diamonds(south)>4 or clubs(south)>4)
m6    = (diamonds(south)>5 or clubs(south)>5) and not M4m5
M5    = shape(south, 53xx+5233+35xx+2533-xxx4-xx4x-any 55xx-any 65xx)
bal19 = shape(south,any 4333+any 4432+any 5332) and hcp(south)>18
cX = (M4m5 or m6 or M5 or bal19)

# 2C = at least 5-4 or 4-5 in hearts and spades
c2C = shape(south,76xx+67xx+66xx+65xx+56xx+55xx+54xx+45xx) 

# 2D = 6 hearts/spades w/o a side suit
c2D = shape(south,6xxx+x6xx-any 65xx-any 64xx)

# 2H = 5 hearts and 4+ minor
c2H = hearts(south)>4 and spades(south)<4 and shape(south,any 54xx)

# 2S = 5 spades and 4+ minor
c2S = spades(south)>4 and hearts(south)<4 and shape(south,any 54xx)

# 2N = 5-5 or better in the minors
c2N = diamonds(south)>4 and clubs(south)>4

#include "script/Leveling"

# Define Leveling
lC     = c2C   and keep56
lD     = c2D   and keep70
lH     = c2H   and keep75
lS     = c2S   and keep75
lN     = c2N   and keep

# The following are included in the X and are leveled separately
lM4m5  = M4m5  and keep25
lm6    = m6    and keep47
lM5    = M5    and keep30
lBal19 = bal19 and keep

levelTheDeal = lC or lD or lH or lS or lN or lM4m5 or lm6 or lM5 or lBal19

# Now OPEN 1NT
gibNT and (cX or c2C or c2D or c2H or c2S or c2N) and hcp(south)>10
and levelTheDeal

# Generate Statistics
action

average "2C   " 100 * c2C,
average "2D   " 100 * c2D,
average "2H   " 100 * c2H,
average "2S   " 100 * c2S,
average "2N   " 100 * c2N,

average "X----" 100 * (lM4m5 or lm6 or lM5 or lBal19),

average "M4m5 " 100 * lM4m5,
average "m6   " 100 * lm6,
average "M5   " 100 * lM5,
average "bal19" 100 * lBal19,

frequency  "HCP South "  (hcp(south) , 11, 19),

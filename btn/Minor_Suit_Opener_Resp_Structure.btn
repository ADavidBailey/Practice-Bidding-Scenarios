# alias: MinorOpenerRespStr
# button-text: 1m & Raise/Bal (Lev)
# dealer-position: S
# gib-works: false
# bba-works: true
# auction-filter: (1[CD] Pass 1[DHSN])|(1C Pass 2C)|(1D Pass 2D)|(1[CD] Pass [23]N)
# convention-card: 21GF-NoInvertedMinor

/*@chat
--- 1 Minor w/Raise OR Balanced Response -- Leveled
1!C/!D - 1N shows 6-10 HCP balanced
1!C/!D - 2!C/!D shows 5/4+ card support and 6-10 HCP, a Mixed Raise
1!C/!D - 2!H shows 11-12 HCP balanced, forcing
1!C/!D - 2!S shows 5/4+ card support and invitational+ values 11+ HCP, forcing
1!C/!D - 2N shows 13-15 or 18-19 HCP balanced, GF+
1!C/!D - 3N shows 16-17 HCP balanced
This scenario is leveled such that the mixed raise, invitational raise, and
balanced weak, invitational, and forcing responses occur at about the same frequency.
@chat*/

# Minor w/Raise OR Balanced Response -- Leveled
dealer south

#include "script/Calm-Opponents"
#include "script/Predict-Opening-1-Bid"

# Define 1 Minor and Mixed/Invitational Raise -- no 1-level suit or NT response 
cRaise = oC and clubs(north)>4    and spades(north)<4 and hearts(north)<4 and diamonds(north)<4                    //  5+ card support
dRaise = oD and diamonds(north)>3 and spades(north)<4 and hearts(north)<4 and diamonds(north)>clubs(north)         //  4+ card support
wkRange = hcp(north)>5  and hcp(north)<11
giRange = hcp(north)>10 and hcp(north)<13
gfRange = hcp(north)>12

shortness = shape(north, any 0xxx+any 1xxx+any 22xx)  // 2+ shortness points

raise  = (cRaise or dRaise) and shortness and hcp(north)>5
wkRaise = raise and wkRange
giRaise = raise and giRange
gfRaise = raise and gfRange

# Define one Minor and balanced responses w/o 5-card major and w/o 2 doubletons
balResp = (oC or oD) and shape(north,any 4333+any 4432+any 5332-5xxx-x5xx) and hcp(north)>5
wkBal   = balResp and wkRange
giBal   = balResp and giRange
gfBal   = balResp and gfRange

#include "script/Leveling"

# Do the Leveling
levwkb = wkBal and keep03
levgib = giBal and keep045
levgfb = gfBal and keep015

levwkr = wkRaise and keep
levgir = giRaise and keep
levgfr = gfRaise and keep
levelTheDeal = levwkr or levgir or levgfr or levwkb or levgib or levgfb

# Now do it
(wkRaise or giRaise or gfRaise or wkBal or giBal or gfBal) and calmOpps and hcp(south)<22
and levelTheDeal

action
average "1 Club        " 100 * oC,
average "1 Diamond     " 100 * oD,
average "hcp south     "       hcp(south),
average "hcp north     "       hcp(north),

average "--any balanced-" 100 * balResp,
average "Weak balanced  " 100 * wkBal,
average "GI balanced    " 100 * giBal,
average "GF balanced    " 100 * gfBal,

average "---any raise---" 100 * raise,
average "Mixed raise    " 100 * wkRaise,
average "GI raise       " 100 * giRaise,
average "GF raise       " 100 * gfRaise,

average "---bal/raise---" 100 * (raise and shape(north,any 4333+any 2xxx-any 22xx)),

average "any void      " 100 * shape(north,any 0xxx),
average "any singleton " 100 * shape(north,any 1xxx),
average "2/3 doubletons" 100 * shape(north,any 22xx+any 222x),
average "1 doubleton   " 100 * shape(north,any 2xxx-any 22xx-any 222x-any 0xxx-any 1xxx),


action printpbn

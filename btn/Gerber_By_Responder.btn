# alias: GerberByResponder
# button-text: Gerber by Responder (Lev)
# gib-works: true
# bba-works: true
# auction-filter: Note....A=
# convention-card-ns: 21GF-Gerber
# convention-card-ew: 21GF-GIB

/*@chat
--- Gerber by Responder (Leveled)
The Notrump Ladder describes the opening sequences for all balanced hands.  These deals, allow
you to explore for slam. The opener has a balanced hand.  The responder will have values to
investigate slam.  The pair has no 8-card major or 9-card minor fit.  The hands are leveled so
that the first several ranges of NT ladder occur with approximately the same frequency.
https://www.bridgebum.com/gerber.php
@chat*/

dealer south

# These are sequences where 4C-Gerber might be bid by Responder
# Gerber by Responder
#
# 1x - 1y - 1N - ?
# 1x - 1y - 2N - ?
# 1m - 2N - ?
# 1N - ?
# 2N - ?
# 3N - ?
# 2C 2D 2N - ?
# 2C 2D 3N - ?

#include "script/Predict-Opening-1-Bid"
# Defines oneNT, twoNT, gameForce2C, threeNT
#include "script/Predict-Response-to-1-Bid"
#include "script/Predict-Openers-Rebid"
# Defines CDN, CHN, CSN, DHN, DSN, HSN

# Create hands where the only question is "How may keycards does partner have?"
#predeal south SA,HA,DAK,CA
#predeal north S,H,DQJT9876,CK5

# avoid New Minor Forcing
cdn = CDN
chn = CHN and hearts(north)<5
csn = CSN and spades(north)<5
dhn = DHN and hearts(north)<5
dsn = DSN and spades(north)<5
hsn = HSN and spades(north)<5
xyNT = cdn or chn or csn or dhn or dsn or hsn

gfNT = gameForce2C and shape(south, any 5422+any 5332+any 4432+any 4333)

# Avoid major suit fit
sFit = (spades(south)+spades(north))>7
hFit = (hearts(south)+hearts(north))>7
noMfit = not (sFit or hFit)

# North is balanced with no 4-card major.  May have a long minor.
northBal = shape(north,43xx+34xx+33xx+32xx+23xx+22xx-any 0xxx-any 1xxx)
northNo4M = shape(north,33xx+32xx+23xx+22xx-any 0xxx-any 1xxx)

# North is anywhere within their range and the pair has at least 32
nt12 = xyNT    and hcp(south)>=12 and hcp(south)<=14 and hcp(north)>=20 and northBal
nt15 = oneNT   and hcp(south)>=15 and hcp(south)<=17 and hcp(north)>=17 and northNo4M
nt18 = xyNT    and hcp(south)>=18 and hcp(south)<=19 and hcp(north)>=14 and northBal
nt20 = twoNT   and hcp(south)>=20 and hcp(south)<=21 and hcp(north)>=12 and northNo4M
nt22 = gfNT    and hcp(south)>=22 and hcp(south)<=24 and hcp(north)>=10 and northNo4M
nt25 = threeNT and hcp(south)>=25 and hcp(south)<=27 and hcp(north)>=7  and northNo4M
nt28 = gfNT    and hcp(south)>=28                    and hcp(north)>=4  and northNo4M
anyNT = nt12 or nt15 or nt18 or nt20 or nt22 or nt25 or nt28

#include "script/Leveling"

# Level the deal
lev12 = nt12   and keep
lev15 = nt15   and keep25
lev18 = nt18   and keep81
lev20 = nt20   and keep44
lev22 = nt22   and keep75
lev25 = nt25   and keep
lev28 = nt28   and keep

# Define tp = combined hcp
tp = hcp(south)+hcp(north)

# Keep at least one of these
grandSlam = tp>36
smallSlam = tp>32 and not grandSlam
slamTry   = not (grandSlam or smallSlam)

levst = slamTry   and keep
levss = smallSlam and keep
levgs = grandSlam and keep
levelTheDeal = (lev12 or lev15 or lev18 or lev20 or lev22 or lev25 or lev28) and (levst or levss or levgs)

# Now do it
anyNT and noMfit
and levelTheDeal


produce 10
action
average "12 + 21: 1x-1y-1N-? " 100 * nt12,
average "15 + 18: 1N-?       " 100 * nt15,
average "18 + 15: 1x-1y-2N-? " 100 * nt18,
average "20 + 13: 2N-?       " 100 * nt20,
average "22 + 11: 2C-2x-2N-? " 100 * nt22,
average "25 +  8: 3N-?       " 100 * nt25,
average "28 +  5: 2C-2x-3N-? " 100 * nt28,
average "hcp S               " hcp(south),
average "hcp N               " hcp(north),
average "total < 32  " 100 * slamTry,
average "total 33-36 " 100 * smallSlam,
average "total > 36  " 100 * grandSlam,

# alias: SupportDouble
# button-text: Support Double
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\n.*\nX
# convention-card-ns: 21GF-DEFAULT
# convention-card-ew: 21GF-GIB

/*@chat
--- Support Double
Partner opens 1!C, 1!D, or 1!H
You respond 1!H or 1!S
Your LHO bids or doubles
Your partner doubles or redoubles.
@chat*/

dealer south

#include "script/Predict-Opening-1-Bid"
#include "script/Calm-Opponents"

# Define responder's suits and opener's 3-card support
nH = hearts(north)>3 and hearts(north)>=spades(north) and hearts(south)==3
nS = spades(north)>3 and spades(north)>hearts(north)  and spades(south)==3
nResponds = (nH or nS) and hcp(north)>5

# South opens 1C, 1D, or 1H
sOpens = (oC or oD or oH) and hcp(south)>11

CH = oC and nH and (spades(east)>3   and diamonds(east)>3 or (spades(east)>4   or diamonds(east)>4))
CS = oC and nS and (hearts(east)>3   and diamonds(east)>3 or (hearts(east)>4   or diamonds(east)>4))
DH = oD and nH and (spades(east)>3   and clubs(east)>3    or (spades(east)>4   or clubs(east)>4))
DS = oD and nS and (hearts(east)>3   and clubs(east)>3    or (hearts(east)>4   or clubs(east)>4))
HS = oH and nS and (diamonds(east)>3 and clubs(east)>3    or (diamonds(east)>4 or clubs(east)>4))

eBids = (hcp(east)>9 and (CH or DH)) or hcp(east)>11 and (CS or DS or HS)

sOpens and calmWest and nResponds and eBids

produce 30
action
printoneline,
average "CH     " 100 * CH,
average "CS     " 100 * CS,
average "DH     " 100 * DH,
average "DS     " 100 * DS,
average "HS     " 100 * HS,

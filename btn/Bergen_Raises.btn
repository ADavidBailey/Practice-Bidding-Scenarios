# alias: BergenRaises
# button-text: Bergen Raises (Lev)
# dealer-position: N
# gib-works: false
# bba-works: true
# auction-filter: Auction.....\n1[HS] +Pass +3[CDHSN]
# convention-card: 21GF-Bergen

/*@chat
--- Bergen Raises all show exactly 4-card support.
• 3!C = Constructive (7-10) TP
• 3!D = Limit Raise (11-12) TP
• 3M  = Preemptive Raise (3-6) TP
• 3OM = Ambiguous Splinter (12+) TP
• 3N  = 4333 (13-15) HCP
https://www.bridgebum.com/bergen_raises.php
@chat*/

/*
Select Bergen, 1M 3M blocking, and turn off 1N opening shape 5422
convention-card: 21GF-Bergen
auction-filter: Auction.....\n1[HS] +Pass +3[CDHSN]
*/

# Bergen Raises
dealer north

#include "script/Calm-Opponents"
# Defines calmEast

### changed GIB-1N from South to North
# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(north) + shape(north,5xxx+x5xx)
nt1 = shape(north, any 5332+any 4432+any 4333) and hcp(north)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(north, any 5422-5xxx-x5xx) and hcp(north)>14 and hcp(north)<17

gibNT = nt1 or nt2
### changed GIB-1N from South to North

# Calculate Total Points for south (tp) for south
v1 = shape(south, any 0xxx) ? 5 : 0
s1 = shape(south, any 1xxx) ? 3 : 0
d1 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
d2 = shape(south, any 22xx) ? 1 : 0
d3 = shape(south, any 222x) ? 1 : 0
ssp = v1+s1+d1+d2+d3          // possible short suit points

# Short Suit Points is limited to the number of trump support cards
tp = ssp<5 ? ssp + hcp(south) : 4 + hcp(south)

# Define openers
sOpener = spades(north) >= hearts(north) and spades(north) >=5 and spades(north) >= diamonds(north) and spades(north) >= clubs(north)
hOpener = hearts(north) >=5 and not sOpener                    and hearts(north) >= diamonds(north) and hearts(north) >= clubs(north)

# Define suit fit w/4-card support
sFit = sOpener and spades(south)==4
hFit = hOpener and hearts(south)==4
fit = sFit or hFit
nOpens = fit and hcp(north)>11 and hcp(north)<20 and not gibNT

# 3M = Preemptive Raise 3-6 TP
case3M = tp>2 and tp<7

# 3C = Constructive 7-10 TP
case3C = tp>6 and tp<11

# 3oM = Ambiguous Splinter 12+ TP
case3oM = tp>11 and shape(south, any 14xx+any 04xx)

# 3N = 4333 13-15 TP/HCP
case3N = tp>12 and tp<16 and shape(south, any 4333)

# 3D = Limit Raise 11-12 TP
case3D = tp>10 and tp<13 and not (case3oM or case3N)

#include "script/Leveling"

lC  = case3C  and keep045
lD  = case3D  and keep11
loM = case3oM and keep06
lM  = case3M  and keep19
lN  = case3N  and keep
levelTheDeal = lC or lD or loM or lM or lN

# Now do it
nOpens and (case3M or case3C or case3oM or case3N or case3D) and calmEast
and levelTheDeal

produce 5000
action
average "3C   7-10 TP      " 100 * case3C,
average "3D  11-12 TP      " 100 * case3D,
average "3oM 11-13 41/40xx " 100 * case3oM,
average "3M   0-6  TP      " 100 * case3M,
average "3N  13-15 4333    " 100 * case3N,


action printpbn

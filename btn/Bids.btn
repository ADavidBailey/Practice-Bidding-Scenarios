# alias: Bids
# button-text: After 1C (Pass)
# dealer-position: S
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\n1C +Pass +(1[DHS]|1NT?|2C|2NT?|3[CDHS]|3NT?) +
# convention-card: 21GF-GIB

/*@chat
--- Responses to 1C
South opens 1!C.  North has 5+!D and one or both 4-card majors
This does not use 'Randomly rotate' -- go to 'Deal source' to change it. 
@chat*/

dealer south

#include "script/Calm-Opponents"
# Defines calmOpps
#include "script/Predict-Opening-1-Bid"
# Defines oneClub
#include "script/TP"
# Defines tpSouth, tpNorth

# South bids 1C and North responds
sRange = hcp(south)>10 and hcp(south)<22 and tpSouth>11 and tpSouth<23
sClubs = oneClub and clubs(south)<7

nRange = tpNorth>5 and tpNorth<16    // avoid strong jump shifts

# Define responses to 1C
nD = diamonds(north)
nH = hearts(north)
nS = spades(north)

rS1 = nS>4 and nS>=nH             // higher ranking of 5+card suits
rS2 = tpNorth<13 and nS==4 and nS>nH  // skip longer D w/<13 and bid up the line with 4
rS3 = shape(north,4xxx) and nS>nH and nD<5
r1S = rS1 or rS2 or rS3

rH1 = nH>4 and nH>nS
rH2 = tpNorth<13 and nH==4 and nH>=nS
rH3 = shape(north,x4xx) and nD<5
r1H = (rH1 or rH2 or rH3) and not r1S

rHS = r1H or r1S

# GIB defines partial stop: length+HCP = 4 (Qx or Jxx)
dStop = (hcp(north,diamonds) + nD)>3
hStop = (hcp(north,hearts)   + nH)>3
sStop = (hcp(north,spades)   + nS)>3
stops = dStop and hStop and sStop

nClubs = clubs(north)>4 and shape(north,any 1xxx) and nD<5   // prefer 2C
nBal   = shape(north, any 4432 +any 4333)                    // prefer NT

r1N = nBal and hcp(north)>5  and hcp(north)<11           and not rHS
r2N = nBal and hcp(north)>10 and hcp(north)<13 and stops and not rHS
r3N = nBal and hcp(north)>12 and hcp(north)<16 and stops and not rHS
rN = (r1N or r2N or r3N)

r3x = clubs(north)>4 and shape(north,any 0xxx +any 1xxx) and tpNorth>13  and not rHS //splinter
r2C = nClubs and hcp(north)>9                                        and not(rHS or r3x)
r3C = nClubs and hcp(north)<10 and tpNorth>4 and clubs(north)>nD and     not(rHS or r3x)
rC  = (r2C or r3C or r3x)
r1D = nD>3 and not (rHS or rN or rC)

nResponds = r1D or r1H or r1S or r1N or r2C or r2N or r3C or r3x or r3N

oneClub and sClubs and sRange and nRange and nResponds and calmOpps

action
average "CD " 100 * r1D,
average "CH " 100 * r1H,
average "CS " 100 * r1S,
average "CN " 100 * r1N,
average "2C " 100 * r2C,
average "2N " 100 * r2N,
average "3C " 100 * r3C,
average "3x " 100 * r3x,
average "3N " 100 * r3N,


action printpbn

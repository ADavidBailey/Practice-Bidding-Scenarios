# alias: SolowayJumpShiftType3
# button-text: 3  
# dealer-position: S
# gib-works: true
# bba-works: true
# auction-filter: \n(1C +Pass +2[DHS]|1D +Pass +2[HS]|1H +Pass +2S)
# convention-card: 21GF-SolowayJumpShift

/*@chat
--- Soloway Jump Shift - Type 3
3. Rebiddable suit w/18+ HCP and 4+ controls and 5332 or 6322 shape
https://www.bridgebase.com/doc/gib_system_notes.php#Soloway_Jump_Shift
@chat*/

# Soloway Jump Shift
dealer south

# Calculate Total Points for North (tp) for North
v1 = shape(north, any 0xxx) ? 5 : 0
s1 = shape(north, any 1xxx) ? 3 : 0
d1 = shape(north, any 2xxx) ? 1 : 0 // allow for 3 doubletons
d2 = shape(north, any 22xx) ? 1 : 0
d3 = shape(north, any 222x) ? 1 : 0
tp = v1+s1+d1+d2+d3 + hcp(north)

# Define openers
sOpener = spades(south) >= hearts(south) and spades(south) >=5 and spades(south) >= diamonds(south) and spades(south) >= clubs(south)
hOpener = not sOpener and hearts(south) >=5 and hearts(south) >= diamonds(south) and hearts(south) >= clubs(south)
MOpener = sOpener or hOpener
mOpener = not (sOpener or hOpener)
dOpener = mOpener and (diamonds(south) >= clubs(south) or shape(south, xx32))
cOpener = mOpener and not dOpener


# Define strong rebiddable suit
Gs = spades(north)>5 and top5(north,spades)>3 and not sOpener
Gh = hearts(north)>5 and top5(north,hearts)>3 and (dOpener or cOpener)
Gd = diamonds(north)>5 and top5(north,diamonds)>3 and cOpener

goodSuit = (Gs or Gh or Gd) and hcp(north)>16


# Define solid suits
Ss = spades(north)>5 and top4(north,spades)==4 and not sOpener
Sh = hearts(north)>5 and top4(north,hearts)==4 and (dOpener or cOpener)
Sd = diamonds(north)>5 and top4(north,diamonds)==4 and cOpener
solidSuit = (Ss or Sh or Sd) and hcp(north)>16

# Define suit fit
Fs = sOpener and spades(north)>3
Fh = hOpener and hearts(north)>3
Fd = dOpener and diamonds(north)>3
Fc = cOpener and clubs(north)>3
supportedSuit = (Fs or Fh or Fd or Fc)

# Define rebidbidable suits
Rs = spades(north)>4 and top4(north,spades)>2
Rh = hearts(north)>4 and top4(north,hearts)>2
Rd = diamonds(north)>4 and top4(north,diamonds)>2

rebidAndShape = (Rs or Rh or Rd) and shape(north, any 5332 +any 6322) and hcp(north)>18
rebidAndSupport = (Rs or Rh or Rd) and supportedSuit and tp>17

# These are possible Soloway Jump Shifts
# 1C - 2D
# 1C - 2H
# 1C - 2S
# 1D - 2H
# 1D - 2S
# 1H - 2S

# Now do it

# South Opens 1C, 1D, or 1H
hcp(south)>11 and hcp(south)<15 and spades(south)<5

# West passes
and hcp(west)<8
and shape(west, xxxx - any 7xxx +any 6xxx +any 5xxx) 

and controls(north)>3
and
(
# Type 3
   (rebidAndShape and hcp(north)>17)
 )

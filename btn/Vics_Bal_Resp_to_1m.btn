# alias: vicsBalRespTo1m
# button-text: Vic's Bal Resp to 1m
# dealer-position: S
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\\n1[CD].Pass.[123]NT

/*@chat
--- Vic's balanced-hand responses to 1!C or 1!D
South opens 1!C or 1!D and North has any balanced hand without a major suit.
• 1N = 6-10
• 2!H = 11-12 (a relay to 2!S)
• 2N = 13-15
• 3N = 16-17
https://www.nxtbook.com/acbl/bridgebulletin/2025_05/index.php#/p/70
@chat*/

# South opens 1C or 1D with no other constraints
# North has a balanced hand without a major suit and the following ranges

#  6-10 1N
# 11-12 2H
# 13-15 2N
# 16-17 3N


dealer south

#include "script/Calm-Opponents"
# Defines calmWest
#include "script/Predict-Opening-1-Bid"
# Defines oneClub, oneDiamond

nNT = shape(north,any 5332 +any 4432 +any 4333 -5xxx-4xxx-x5xx-x4xx)

# Define Vic's HCP North's response ranges
n1 = hcp(north)>= 6 and hcp(north)<=10
n2 = hcp(north)>=11 and hcp(north)<=12
n3 = hcp(north)>=13 and hcp(north)<=15
n4 = hcp(north)>=16 and hcp(north)<=17

#include "script/Leveling"
lev1N = n1 and keep14
lev2H = n2 and keep30
lev2N = n3 and keep33
lev3N = n4 and keep
levelTheDeal = lev1N or lev2H or lev2N or lev3N

# Now do it
(oneClub or oneDiamond) and (n1 or n2 or n3 or n4) and nNT and calmWest
and levelTheDeal

action

average "1N  6-10 " 100 * n1,
average "2H 11-12 " 100 * n2,
average "2N 13-15 " 100 * n3,
average "3N 16-17 " 100 * n4,


action printpbn

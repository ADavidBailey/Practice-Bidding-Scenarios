# alias: Jacoby2N
# button-text: Jacoby 2N (4x-Suit)
# dealer-position: S
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\n1[HS] +Pass +2N

/*@chat
--- Jacoby 2N
Five different Opening hands can be described after a major suit open and a Jacoby 2N response. The percentages
are the approximate frequency of occurrence for the various hand types. Use the (Leveled) script to even that out.
• 4x (08%) - any strength with a good (5-cards with 2+ of top 3) second suit
• 3x (43%) - any strength with a singleton/void
• 3M (03%) - 18+ Semi-Balanced
• 3N (12%) - 15-17 Semi-Balanced
• 4M (34%) - 12-14 Semi-Balanced
@chat*/

produce 2000
dealer south

# Jacoby 2N Leveled

#include "script/Predict-Opening-1-Bid"
#include "script/Calm-Opponents"

##### Jacoby 2N #####
oneMajor = (oS or oH)
oneMinor = (oC or oD)

# Define 4+ card fits for south
sFit = oneSpade and spades(north)>3
hFit = oneHeart and hearts(north)>3
mFit = sFit or hFit

# Define Good suits -- 5+ cards with 2 of the top 3
gS = spades(south)>4   and top3(south,spades)>1
gH = hearts(south)>4   and top3(south,hearts)>1
gD = diamonds(south)>4 and top3(south,diamonds)>1
gC = clubs(south)>4    and top3(south,clubs)>1
  
# Define Strong suits -- 5+ cards with 3 of the top 4
# Define Solid suits -- 5 cards with 4 of the top 4 or 6+ cards with 3 of the top 3  

# Define Majors with 2nd suits
sFit2nd = oneSpade and sFit and (gH or gD or gC)
hFit2nd = oneHeart and hFit and (gS or gD or gC)

# Define the 5 types of opening hands in priority order
x4 = sFit2nd or hFit2nd
x3 = mFit and shape(south, any 0xxx+any 1xxx) and not x4
M3 = mFit and suitPoints>17                   and not (x4 or x3)
N3 = mFit and suitPoints>14 and suitPoints<18 and not (x4 or x3 or M3)
M4 = mFit and suitPoints>11 and suitPoints<15 and not (x4 or x3 or M3 or N3)

##### End of Jacoby 2N #####

#include "script/Leveling"

# Do the leveling
levelx4 = x4 and keep25
levelx3 = x3 and keep03
levelM3 = M3 and keep86
levelN3 = N3 and keep14
levelM4 = M4 and keep03
levelTheDeal = levelx3 or levelM4 or levelM3 or levelx4 or levelN3

##### Now do it #####
condition
(x4 or x3 or M3 or N3 or M4) and suitPoints>12 and calmWest and hcp(north)>12
#and levelTheDeal

action
#printoneline,
average "1. x4" 100 * x4,
average "2. x3" 100 * x3,
average "3. M3" 100 * M3,
average "4. N3" 100 * N3,
average "5. M4" 100 * M4,


action printpbn

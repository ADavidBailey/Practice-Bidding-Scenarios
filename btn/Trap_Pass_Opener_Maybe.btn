# alias: TrapPassOpenerMaybe
# button-text: Trap Pass Opener?
# dealer-position: E
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\n[1-7][CDHS] +Pass +Pass +X

/*@chat
--- Trap Pass after opener's major or weak 2 (Maybe)
About half are trap passes about half are normal passes.
@chat*/

# Stack in Opponents Suit -- for training Ben

dealer east

# Predict Opening 1-Bid --- rotated to East
##### Imported Script -- Predict Opening 1-Bid #####

# Changed to use gibNT 3/30/2025

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(east) + shape(east,5xxx+x5xx)
nt1 = shape(east, any 5332+any 4432+any 4333) and hcp(east)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(east, 4252+4225+2452+2425+2254+2245) and hcp(east)>14 and hcp(east)<17

gibNT = nt1 or nt2

# Calculate length points for South (lengthPoints)
lp1 = spades(east)>4 ? spades(east)-4 : 0
lp2 = hearts(east)>4 ? hearts(east)-4 : 0
lp3 = diamonds(east)>4 ? diamonds(east)-4 : 0
lp4 = clubs(east)>4 ? clubs(east)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for east (suitPoints)
suitPoints = hcp(east) + lengthPoints

TwoNtShape = shape(east, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(east)>19 and hcp(east)<22
threeNT = TwoNtShape and hcp(east)>24 and hcp(east)<28

# Define Game Forcing 2C
case1 = hcp(east)>21
case2 = hcp(east)>18 and losers(east)<5 and (spades(east)>5 or hearts(east)>5)
case3 = hcp(east)>18 and losers(east)<4 and (diamonds(east)>5 or clubs(east)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT

# Predict South's Opening suit
s = spades(east)
h = hearts(east)
d = diamonds(east)
c = clubs(east)
s1Range = suitPoints>11 and hcp(east)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

P3 = P1 or P2 or oneSpade or oneHeart or oneDiamond or oneClub

##### End of Imported Script -- Predict Opening 1-Bid #####
# Defines gibNT, oneHeart, oneSpade, 

sBalMin = shape(south, any 4333 +any 4432) and hcp(south)<13


#            Trap Pass ------------------------------------    Not Trap Pass -------------------------------
spadeMix   = (spades(south)>4 and top5(south,spades)>2)     or (spades(south)<5 and top5(south,spades)<2)     and sBalMin
heartMix   = (hearts(south)>4 and top5(south,hearts)>2)     or (hearts(south)<5 and top5(south,hearts)<2)     and sBalMin
diamondMix = (diamonds(south)>4 and top5(south,diamonds)>2) or (diamonds(south)<5 and top5(south,diamonds)<2) and sBalMin
clubMix    = (clubs(south)>4 and top5(south,clubs)>2)       or (clubs(south)<5 and top5(south,clubs)<2)       and sBalMin

# define east's 1H, 1S or any preemptive bid -- I want it to look the same as a Trap Pass from North's perspective
#     East opens -------------------------------     Pass/Trap      no raise  ----   North is short in East's suit
o1S = oneSpade         and top4(east,spades)  >1 and spadeMix   and spades(west)  <3 and spades(north)  <2
o1H = oneHeart         and top4(east,hearts)  >1 and heartMix   and hearts(west)  <3 and hearts(north)  <2
wS  = spades(east)  >5 and top4(east,spades)  >1 and spadeMix   and spades(west)  <3 and spades(north)  <2 and not P3
wH  = hearts(east)  >5 and top4(east,hearts)  >1 and heartMix   and hearts(west)  <3 and hearts(north)  <2 and not P3
wD  = diamonds(east)>5 and top4(east,diamonds)>1 and diamondMix and diamonds(west)<3 and diamonds(north)<2 and not P3
wC  = clubs(east)   >6 and top4(east,clubs)   >1 and clubMix    and clubs(west)   <3 and clubs(north)   <2 and not P3

eOpens1 = (o1S or o1H)
eOpens2 = (wS or wH or wD or wC) and hcp(east)>5 and hcp(east)<12 and shape(east,any 7xxx +any 6xxx +any 5xxx)
eOpens = eOpens1 or eOpens2

trap1S = oneSpade and top5(south,spades)>2
trap1H = oneHeart and top5(south,hearts)>2
trapwS = wS       and top5(south,spades)>2
trapwH = wH       and top5(south,hearts)>2
trapwD = wD       and top5(south,diamonds)>2
trapwC = wC       and top5(south,clubs)>2
trap   = trap1S or trap1H or trapwS or trapwH or trapwD or trapwC

#include "script/Leveling"

l1t = eOpens1 and trap     and keep19
l1n = eOpens1 and not trap and keep06
l2t = eOpens2 and trap     and keep
l2n = eOpens2 and not trap and keep25

levelTheDeal = l1t or l1n or l2t or l2n

wPasses   = hcp(west)<6
nBalances = hcp(north)>10 and shape(north,any 5440+any 5431+any 4441)

# Now do it
eOpens and wPasses and nBalances
and levelTheDeal

action
average "---- 1-level ----" 100 * eOpens1,
average "     1-level trap    " 100 * l1t,
average "     1-level no trap " 100 * l1n,
average "---- preempt ----" 100 * eOpens2,
average "     preempt trap    " 100 * l2t,
average "     preempt no trap " 100 * l2n,

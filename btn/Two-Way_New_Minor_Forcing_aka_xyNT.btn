# alias: xyNT
# button-text: 2-Way NMF - xyNT (Lev)
# gib-works: true
# bba-works: true
# auction-filter: 1[CDHS] +Pass +1[DHS] +Pass\n1N +Pass
# convention-card-ns: 21GF-DEFAULT
# convention-card-ew: 21GF-GIB

/*@chat
---  Two Way New Minor Forcing aka xyNT allows you to describe four different responding hand types.
  • Part Score  -- after 1x-1y-1N, 2!H or 2!S ane natural and non-forcing, 2N relays to 3!C P/C.
  • Game Invite -- after 1x-1y-1N, bid 2!C, an artifical relay to 2!D.  Then, all continuations are invitational.
  • Game Force  -- after 1x-1y-1N, bid 2!D, an artifical game force.
  • Slam Invite -- after 1x-1y-1N, a suit bid at the 3-level is a natural slam try.
The deals are leveled so that the hand types occur at about the same frequency.
https://bridgewinners.com/article/view/two-way-new-minor-forcing-aka-xynt
@chat*/

produce 300000
dealer north

# Two Way New Minor Forcing, aka xyNT
# Predict North's opening suit
sN = spades(north)
hN = hearts(north)
dN = diamonds(north)
cN = clubs(north)
nS = sN>4 and sN>=hN and sN>=dN and sN>=cN
nH = not nS and hN>4 and hN>=dN and hN>=cN
nD = not nS and not nH and ((dN>3 and dN>=cN))
nC = not nS and not nH and not nD
northOpens = (nC or nD or nH) and hcp(north)>11 and hcp(north)<15 and shape(north,any 4333+any 4432+any 5332+any 5422 -5xxx)

# Predict South's responding suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
sS = s>=h and s>=d and s>=c
sH = not sS and h>=d and h>=c
sD = not sS and not sH and d>=c and d>4

cO = nC and (sD or sH or sS)
dO = nD and (sH or sS)
hO = nH and sS

# North opens, South responds, and North rebids 1N
southResponds = (cO or dO or hO)
partscore   = hcp(south)<11
invite      = hcp(south)>10 and hcp(south)<13
game        = hcp(south)>12 and hcp(south)<18
slam        = hcp(south)>17

#include "script/Calm-Opponents"
#include "script/Leveling"

levPartscore = partscore and keep19
levInvite    = invite    and keep25
levGame      = game      and keep14
levSlam      = slam      and keep
levelTheDeal = (levPartscore or levInvite or levGame or levSlam)

northOpens and southResponds and calmOpps and (partscore or invite or game or slam)
and levelTheDeal

action
average "partscore " 100 * partscore,
average "invite    " 100 * invite,
average "game      " 100 * game,
average "slam      " 100 * slam,

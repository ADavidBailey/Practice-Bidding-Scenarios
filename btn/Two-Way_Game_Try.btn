# alias: TwoWayGameTry
# button-text: Two-Way Game Try
# dealer-position: S
# gib-works: true
# bba-works: true
# auction-filter: Note...:Two way game tries
# convention-card: Two-Way-Game-Try

# button-width: 50%
/*@chat
--- Two-Way Game Try
After a single raise of a major
. a step bid is a short-suit game try
. any other bid below 3 or the major is a long suit/help suit game try
. bidding 3 of the major is a general strength game try
 https://www.bridgebase.com/doc/gib_system_notes.php#Two-way_Game_Try
@chat*/

# Select Two-Way Game Tries

# Two-Way Game Tries
dealer south

# Calculate Dummy Points for responder North (dp)
v1 = shape(north, any 0xxx) ? 5 : 0
s1 = shape(north, any 1xxx) ? 3 : 0
d1 = shape(north, any 2xxx) ? 1 : 0 // allow for 3 doubletons
d2 = shape(north, any 22xx) ? 1 : 0
d3 = shape(north, any 222x) ? 1 : 0
dp = v1+s1+d1+d2+d3 + hcp(north)

# Calculate Length Points for opener South (lp)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lp = lp1+lp2+lp3+lp4 + hcp(south)

#include "script/Calm-Opponents"
# Defines calmWest, calmEast, and calmOpps

# Define openers
sOpener = spades(south)>4 and spades(south)>=hearts(south) and spades(south)>=diamonds(south) and spades(south)>=clubs(south)
hOpener = hearts(south)>4 and hearts(south)>=diamonds(south) and hearts(south)>=clubs(south) and not sOpener

# Define suit fit
sFit = sOpener and spades(north)>2 and spades(north)<5
hFit = hOpener and hearts(north)>2 and hearts(north)<5 and spades(north)<4 
fit  = (sFit or hFit) and shape(north,any 0xxx+any 1xxx+any 22xx)

(sFit or hFit) and lp>15 and lp<18 and dp>5 and dp<11 and calmWest and shape(south,any 61xx+any 51xx)

action
average "spade fit" 100 * sFit,
average "heart fit" 100 * hFit,
average "hcp(south)" hcp(south),
average "hcp(north)" hcp(north),
frequency "hcp(south)" (hcp(south),14,18),


action printpbn

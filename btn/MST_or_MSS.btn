# alias: MSTorMSS
# button-text: MST or MSS
# dealer-position: S
# gib-works: true
# bba-works: true
# auction-filter: Note....1N-(2[SN]|3C)
# convention-card: 21GF-MSTandMSS

/*@chat
--- After we open 1NT and partner bids Minor Suit Stayman or a Minor Suit Transfer, 
minorwood is on: A jump to 4 of the agreed minor is minorwood.  Responses are in steps.
• 1st step is 1 or 4
• 2nd step is 2
• 3rd step is 3
If the Queen has not been shown or denied, then the first non-trump step asks for the Queen;
otherwise, it asks for Kings.
There are multiple ways to play MSS and MST. GIB plays both together, like this:
• 2!S is MSS; then...
• ...2N - Denies a 4-card minor
• ...3!C - Shows a 4 !C (may have 4!D)
• ...3!D - Shows a 4 !D & denies 4!C
• ...3N - is a super accept with 17 HCP & 4 cards each minor
• 2N is a MST to !C showing 6+!C
• 3!C is a MST to !D showing 6+!D
@chat*/

# Enable MST and MSS and Exclusion Blackwood

# MinorSuitTransfer or MinorSuitStayman
dealer south

#include "script/GIB-1N"
# Defines gibNT

# West passes

# Define good minor suits
gD = top4(north,diamonds)>2
gC = top4(north,clubs)>2

# Define fair minor suits
fD = top4(north,diamonds)>1
fC = top4(north,clubs)>1

# Define 4-card majors
noMajor = spades(north)<4 and hearts(north)<4

# North has 5-4 or better in the minors for MSS
mss = shape(north, xx76+xx67+xx66+xx65+xx56+xx55+xx54+xx45) and fD and fC  // both minors

# North has 6+ D/C for Transfer
mstC = shape(north,xxx9+xxx8+xxx7+xxx6) and gC  // Clubs
mstD = shape(north,xx9x+xx8x+xx7x+xx6x) and gD  // Diamonds
mst  = mstC or mstD

# Now do it
gibNT and (mss or mst) and noMajor and hcp(north)>14 and losers(north)<5

produce 500
action
average "mss   " 100 * mss,
average "mst C " 100 * mstC,
average "mst D " 100 * mstD,
average "hcp S " hcp(south),
average "hcp N " hcp(north),
average "hcp   " hcp(south) + hcp(north),

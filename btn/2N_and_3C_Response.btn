# alias: TwoNTand3C
# button-text: 2NT - 3C
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\n2NT?.*Pass.*3C +
# convention-card-ns: 21GF-DEFAULT
# convention-card-ew: 21GF-GIB

/*@chat
---  20-21 Notrump and a Stayman or Smolen rebid.
The responder's strength -- game or slam -- is leveled.
@chat*/

dealer south

### 2NT and 3C Response

#include "script/Predict-Opening-1-Bid"

### NORTH ###

# Define suit lengths
cN = clubs(north)
dN = diamonds(north)
hN = hearts(north)
sN = spades(north)

# Calculate strength for North -- hcp plus 1 for every card over 4 in any suit 
nlp1 = sN>4 ? sN-4 : 0
nlp2 = hN>4 ? hN-4 : 0
nlp3 = dN>4 ? dN-4 : 0
nlp4 = cN>4 ? cN-4 : 0
strength = nlp1 + nlp2 + nlp3 + nlp4 + hcp(north)

# Define objective
weak = strength<5
game = strength>4 and strength<12
slam = strength>11

# Define One Minor and Both Minors
cSuit  = cN>5 and hcp(north,clubs)>2    and dN<4 and shape(north,xxxx-any 66xx-any 65xx)
dSuit  = dN>5 and hcp(north,diamonds)>2 and cN<4 and shape(north,xxxx-any 66xx-any 65xx)
minor2 = ((cN>4 and dN>3) or (dN>4 and cN>3)) and (hcp(north,clubs) + hcp(north,diamonds))>2

# Define Responses
smolen   = shape(north,54xx+45xx-any 7xxx-any 6xxx-any 55xx)       and (game or slam) and shape(south,33xx+32xx+23xx)  // rebid 2D
stayman  = shape(north,44xx+4xxx+x4xx-54xx-45xx-any 7xxx-any 6xxx) and (game or slam) and not smolen

#include "script/Leveling"

levGame    = game    and keep14
levSlam    = slam    and keep
 
### Do it ###
twoNT and (smolen or stayman) and (levGame or levSlam)

### Generate Statistics ###
produce 5000
action
#printoneline,
average "Smolen       " 100 * smolen,
average "Stayman      " 100 * stayman,
average "Game         " 100 * game,
average "Slam         " 100 * slam,

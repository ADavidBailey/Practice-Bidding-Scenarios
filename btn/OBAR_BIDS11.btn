# alias: OBARBIDS11
# button-text: OBARBIDS 11+ (Lev)
# dealer-position: E
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\n(1C +Pass +2C.* [X2])|(1D +Pass +2D.* [X23])|(1H +Pass +2H.* [X23])|(1S +Pass +2S.* [X3])
# convention-card: No-Inverted-Minors

/*@chat
--- Opponents Bid And Raise -- Bid In Drect Seat.
After opponents bid and raise we bid in direct seat.  Take-out doubles
and overcalls are as weak as 11 HCP.  This is leveled such that North
is equally likely to have a 6-card suit, a 5-card suit, or a take-out double.
This is NOT described by the linked artical.
https://www.bridgebum.com/obar_bids.php
@chat*/

# OBAR -- Opener Bid & Raise, Balance in Direct Seat
dealer east

##### Define calm South -- copied from CalmOpps and modified for South
# Avoid concentration of values
ccs = top4(south,clubs)<2
cds = top4(south,diamonds)<2
chs = top4(south,hearts)<2
css = top4(south,spades)<2
noConSouth = ccs and cds and chs and css
unbalSouth = shape(south,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
balSouth  = shape(south,any 4333 +any 4432)
calmSouth = (unbalSouth and noConSouth and hcp(south)<8) or (balSouth and hcp(south)<12)
#####

# Predict East's opening suit
s = spades(east)
h = hearts(east)
d = diamonds(east)
c = clubs(east)
oS = s>4 and s>=h and s>=d and s>=c
oH = not oS and h>4 and h>=d and h>=c
oD = not (oS or oH) and ((d>3 and d>=c) or c<3)
oC = not (oS or oH or oD)
eOpens = (oS or oH or oD or oC) and hcp(east)>11 and hcp(east)<15

# West has support for East's suit
oneSpade   = oS and spades(west)==3
oneHeart   = oH and hearts(west)==3
oneDiamond = oD and diamonds(west)==4 and hearts(west)<4 and spades(west)<4
oneClub    = oC and clubs(west)==5    and hearts(west)<4 and spades(west)<4
wRaises    = (oneSpade or oneHeart or oneDiamond or oneClub) and hcp(west)>5 and hcp(west)<10

# East opens and West raises
OBAR = eOpens and calmSouth and wRaises

# North has a 5- or 6-card suit headed by the AQ or better
nSpades   = shape(north,6xxx+5xxx) and c13(north,spades)>7   
nHearts   = shape(north,x6xx+x5xx) and c13(north,hearts)>7
nDiamonds = shape(north,xx6x+xx5x) and c13(north,diamonds)>7
nClubs    = shape(north,xxx6+xxx5) and c13(north,clubs)>7

n1Suit    = (nSpades or nHearts or nDiamonds or nClubs) and shape(north,any 6xxx+any 5xxx-any 66xx-any 65xx-any 64xx-any 55xx-any 54xx)
n3Suits   = shape(north,any 5440+any 4441)
BIDS      = (n1Suit or n3Suits) and hcp(north)>10

#include "script/Leveling"

levX = n3Suits                          and keep
lev5 = n1Suit and shape(north,any 5xxx) and keep86
lev6 = n1Suit and shape(north,any 6xxx) and keep89
levelTheDeal = lev5 or lev6 or levX

OBAR and BIDS
and levelTheDeal

action
average "(hcp east)   " hcp(east),
average "(hcp west)   " hcp(west),
average "(hcp north)  " hcp(north),
average "5440 or 4441 " 100 * shape(north,any 5440+any 4441),
average "5-card suit  " 100 * shape(north,any 5xxx-any 6xxx-any 55xx-any 5440),
average "6-card suit  " 100 * shape(north,any 6xxx-any 5xxx),
frequency "HCP North  " (hcp(north), 6, 15),

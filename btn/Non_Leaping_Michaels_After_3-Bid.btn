# alias: NLMAfter3Bid
# button-text: Non-Leaping Michaels
# gib-works: false
# bba-works: true
# auction-filter: Note.*Michaels
# convention-card-ns: 21GF-DEFAULT
# convention-card-ew: 21GF-GIB

/*@chat
--- Non-Leaping Michaels after 3-level preempt
https://www.bridgewebs.com/alton/EBU%20-%20Stage%202%20Conventions%20-%20Leaping%20and%20Non-Leaping%20Michaels.pdf
@chat*/

produce 1000
dealer east

# Define Good suits South -- 5+ cards with at least 2 of the top 3
gSs = spades(south)>4   and top3(south,spades)>1
gHs = hearts(south)>4   and top3(south,hearts)>1
gDs = diamonds(south)>4 and top3(south,diamonds)>1
gCs = clubs(south)>4    and top3(south,clubs)>1

# Define Good 2 Suits South
g2SSnotS = (gHs and gDs) or (gHs and gCs) or (gDs and gCs)
g2SSnotH = (gSs and gDs) or (gSs and gCs) or (gDs and gCs)
g2SSnotD = (gSs and gHs) or (gSs and gCs) or (gHs and gCs)
g2SSnotC = (gSs and gHs) or (gSs and gDs) or (gHs and gDs)
southActs = losers(south)>4

eastHCP = hcp(east)>5 and hcp(east)<10

# East Preempts the the 3-level and South has two good suits and less than 5 losers
p3S = top5(east,spades)>2   and spades(east)==7   and hcp(east,spades)> hcp(east)/2   and g2SSnotS
p3H = top5(east,hearts)>2   and hearts(east)==7   and hcp(east,hearts)> hcp(east)/2   and g2SSnotH
p3D = top5(east,diamonds)>2 and diamonds(east)==7 and hcp(east,diamonds)> hcp(east)/2 and g2SSnotD
p3C = top5(east,clubs)>2    and clubs(east)==7    and hcp(east,clubs)> hcp(east)/2    and g2SSnotC

east3Bid = eastHCP and (p3S or p3H or p3D or p3C) and shape(east,any 7xxx -any 7600 -any 75xx) and southActs

# Now do it
east3Bid

action

average "3C" 100 * p3C,
average "3D" 100 * p3D,
average "3H" 100 * p3H,
average "3S" 100 * p3S,

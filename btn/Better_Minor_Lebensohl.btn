# alias: BetterMinorLebensohl
# button-text: Better Minor Lebe' (Lev)
# gib-works: true
# bba-works: true
# auction-filter: 2[DHS] +X +Pass
# convention-card-ns: 21GF-DEFAULT
# convention-card-ew: 21GF-GIB

/*@chat
--- Better Minor Lebensohl
After 2!D/2!H/2!S, (X), Pass, 
  • A new suit at the 2-level is to play
   • 2N is Better Minor Lebensohl, you will pass (0-7) or
    correct (8+) after partner's response.
  • 3!D shows 5+ cards.  Bidding 2N and correcting partner's 2!C
    to 3!D shows 8+.
  • 3 of the other major, shows 4+ cards and 0-7 HCP.  With 8+, 
    bid 2N and correct to 3OM.
  Bidding 2N and then pulling partner's rebid to a suit you could
  have bid directly is stronger.
https://csbnews.org/conventions-better-minor-lebensohl
@chat*/

# Better Minor Lebensohl
dealer east
produce 1000

# Define East weak 2 bids w/o 3-card support by west
tp = hcp(east)
sp = hcp(east,spades)
hp = hcp(east,hearts)
dp = hcp(east,diamonds)
w2S = shape(east,6xxx-x4xx     -any 7xxx-any 66xx-any 65xx) and 2 * sp > tp and top5(east,spades)>2   and spades(west)<3
w2H = shape(east,x6xx-4xxx     -any 7xxx-any 66xx-any 65xx) and 2 * hp > tp and top5(east,hearts)>2   and hearts(west)<3
w2D = shape(east,xx6x-x4xx-4xxx-any 7xxx-any 66xx-any 65xx) and 2 * dp > tp and top5(east,diamonds)>2 and diamonds(west)<3

### Define West X -- short in opener's suit and 4-cards in each unbid major
sShortS = w2S and spades(south)<3   and shape(south,x4xx)
sShortH = w2H and hearts(south)<3   and shape(south,4xxx)
sShortD = w2D and diamonds(south)<3 and shape(south,44xx)
sShort = (sShortS or sShortH or sShortD)

# Define South stopper
sStopS = w2S and (top2(south,spades)>0 and spades(south)>1)
sStopH = w2H and (top2(south,hearts)>0 and hearts(south)>1)
sStopD = w2D and (top2(south,diamonds)>0 and diamonds(south)>1)
sStop = (sStopS or sStopH or sStopD)

sNT = hcp(south)>15 and hcp(south)<19 and shape(south, any 4432+any 5431+any 5332) and sStop  // South has fewer than 3 in bid suit

eOpensWeak2 = (w2S or w2H or w2D) and hcp(east)>5 and hcp(east)<10

# Define South Double
sDoubles = hcp(south)>12 and shape(south, any 5440 +any 5431 +any 5332 +any 4441 +any 4432) and sShort and not sNT

# West Passes
wPasses = hcp(west)<10

# North has to do something
weak   = hcp(north)<8
invite = hcp(north)>7 and hcp(north)<12
force  = hcp(north)>11

#include "script/Leveling"
# Define leveling
levWeak   = weak   and keep89
levInvite = invite and keep30
levForce  = force  and keep70
levelTheDeal = levWeak or levInvite or levForce

eOpensWeak2 and sDoubles and wPasses and (weak or invite or force)
and levelTheDeal

action
average "Weak  " 100 * weak,
average "Invite" 100 * invite,
average "Force " 100 * force,

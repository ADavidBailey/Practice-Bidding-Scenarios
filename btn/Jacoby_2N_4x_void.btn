# alias: Jacoby2N4xVoid
# button-text: Jacoby 2N (4x-void)
# dealer-position: S
# gib-works: false
# bba-works: true
# auction-filter: Auction.....\\n1[HS] Pass 2N
# button-width: 50%

/*@chat
--- Jacoby 2N w/4x showing a void
Five different Opening hands can be described after a major suit open and a Jacoby 2N response. The percentages
are the approximate frequency of occurrence for the various hand types. Use the (Leveled) script to even that out.
• 4x (08%) - any strength with a void
• 3x (40%) - any strength with a singleton
• 3M (04%) - 18+ Semi-Balanced
• 3N (14%) - 15-17 Semi-Balanced
• 4M (34%) - 12-14 Semi-Balanced
@chat*/

produce 1000
dealer south

#include "script/Predict-Opening-1-Bid"
#include "script/Good-Suits-South"
#include "script/Calm-Opponents"

##### End of Imported Code #####

##### Jacoby 2N #####
# Define Major 4-card fits
sFit4 = oneSpade and spades(north)>3
hFit4 = oneHeart and hearts(north)>3
majorFit4 = sFit4 or hFit4

# Define Majors with 2nd suits
#sFit2nd = sFit4 and (gH or gD or gC)
#hFit2nd = hFit4 and (gS or gD or gC)

# Define the 5 types of opening hands in priority order
# x4 = sFit2nd or hFit2nd
x4 = majorFit4 and shape(south, any 0xxx)
x3 = majorFit4 and shape(south, any 0xxx +any 1xxx) and not x4
M3 = majorFit4 and suitPoints>17 and not (x4 or x3)
N3 = majorFit4 and suitPoints>14 and suitPoints<18 and not (x4 or x3 or M3)
M4 = majorFit4 and suitPoints>11 and suitPoints<15 and not (x4 or x3 or M3 or N3)
##### End of Jacopy 2N #####

#include "script/Leveling"

##### Level the Deal #####

lx4 = x4 and keep56
lx3 = x3 and keep06
lM3 = M3 and keep
lN3 = N3 and keep25
lM4 = M4 and keep06
levelTheDeal = lx4 or lx3 or lM3 or lN3 or lM4

##### End of Level the Deal #####

##### Now do it #####
(x4 or x3 or M3 or N3 or M4) and suitPoints>11 and calmWest and hcp(north)>12
#and levelTheDeal

##### Generate Statistics #####
action
average "1. x4" 100 * x4,
average "2. x3" 100 * x3,
average "3. M3" 100 * M3,
average "4. N3" 100 * N3,
average "5. M4" 100 * M4,


action printpbn

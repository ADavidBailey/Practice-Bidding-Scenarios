# alias: After2Passes
# button-text: After 2 Passes...
# dealer-position: N
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\nPass +Pass +(?!Pass)

/*@chat
--- After 2 Passs...
Leveled so 3rd seat shaded vs full opener occur at about the same frequency.
What should you do in 3rd seat with a marginal hand w/5- or 6-card suit?
@chat*/

# After 2 Passes...

dealer north

ccn = top5(north,clubs   )<3 and hcp(north,clubs   )<5
cdn = top5(north,diamonds)<3 and hcp(north,diamonds)<5
chn = top5(north,hearts  )<3 and hcp(north,hearts  )<5
csn = top5(north,spades  )<3 and hcp(north,spades  )<5
noConNorth = ccn and cdn and chn and csn

cce = top5(east ,clubs   )<3 and hcp(east ,clubs   )<5
cde = top5(east ,diamonds)<3 and hcp(east ,diamonds)<5
che = top5(east ,hearts  )<3 and hcp(east ,hearts  )<5
cse = top5(east ,spades  )<3 and hcp(east ,spades  )<5
noConEast = cce and cde and che and cse

# Hands with 5+ cards and no concentrateion of values are seldom opened with 8 or less HCP
length5or6North = shape(north,xxxx -any 8xxx -any 7xxx -any 65xx -any 55xx)
length5or6East  = shape(east ,xxxx -any 8xxx -any 7xxx -any 65xx -any 55xx)

# Hands with 5+ cards are often opened with 11 HCP
length4North  = shape(north,any 4333 +any 44xx)
length4East   = shape(east ,any 4333 +any 44xx)

calmNorth = ((length5or6North and noConNorth and hcp(north)<9) or (length4North and hcp(north)<12)) and controls(north)<5
calmEast  = ((length5or6East  and noConEast  and hcp(east) <9) or (length4East  and hcp(east) <12)) and controls(east) <5

shaded = hcp(south)>3 and hcp(south)<12 and shape(south,any 64xx+any 63xx+any 54xx+any 5332-any 55xx-xxx6-xxx5)
full   = hcp(south)>11 and hcp(south)<15

#include "script/Leveling"

levShaded = shaded and keep
levFull   = full   and keep53
levelTheDeal = levShaded or levFull

# Now do it...
calmNorth and calmEast and (shaded or full)
and levelTheDeal

action

average "shaded  " 100 * shaded,
average "full    " 100 * full,

average "hcp S   " hcp(south),
average "hcp W   " hcp(west),
average "hcp N   " hcp(north),
average "hcp E   " hcp(east),


action printpbn

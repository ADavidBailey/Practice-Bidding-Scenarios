# alias: LebensohlVsW2Dir
# button-text: leb vs W2-Dir Seat
# dealer-position: E
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\\n2[DHS] X

/*@chat
--- lebensohl Over Weak 2
After 2!D/2!H/2!S, (X), Pass, 
  • A new suit at the 2-level is natural and weak (0-7).
  • A non-jump new suit at the 3-level shows 8-10 and no stop.
  • Cue Bid openers suit to show major suit interest without a stop.
  • Jump bidding a new suit at the 3-level shows 8-10 and no stop.
  • 3N shows game forcing values without major suit interest and no stop -- pass or scramble.
  • Bidding 2N is the Lebensohl relay to 3!C.  After partner's forced 3!C, you may...
    ◦ Pass or Correct to a new lower ranking suit (0-7).
    ◦ Cue Bid openers suit to show major suit interest and with a stop.
    ◦ Correct to a new higher ranking suit is constructive.
    ◦ Bid 3N to show a balanced hand without major suit interest AND with a stop.
https://www.bridgebum.com/lebensohl_over_weak_two.php
@chat*/

# Lebensohl over Weak2 (X)
dealer east
produce 1000

tp = hcp(east)
sp = hcp(east,spades)
hp = hcp(east,hearts)
dp = hcp(east,diamonds)

# Define East weak 2 bids w/o 3-card support by west
w2S = shape(east,6xxx-x4xx     -any 7xxx-any 66xx-any 65xx) and 2 * sp > tp and top5(east,spades)>2   and spades(west)<3
w2H = shape(east,x6xx-4xxx     -any 7xxx-any 66xx-any 65xx) and 2 * hp > tp and top5(east,hearts)>2   and hearts(west)<3
w2D = shape(east,xx6x-x4xx-4xxx-any 7xxx-any 66xx-any 65xx) and 2 * dp > tp and top5(east,diamonds)>2 and diamonds(west)<3

# Define South shortness w/3 suits & shortness in opener's suit
sShortS = w2S and spades(south)<3   and shape(south,x4xx)
sShortH = w2H and hearts(south)<3   and shape(south,4xxx)
sShortD = w2D and diamonds(south)<3 and shape(south,44xx)
sShort = (sShortS or sShortH or sShortD)

# Define South stopper
sStopS = w2S and (top2(south,spades)>0 and spades(south)>1)     or (top3(south,spades)>0 and spades(south)>2)
sStopH = w2H and (top2(south,hearts)>0 and hearts(south)>1)     or (top3(south,hearts)>0 and hearts(south)>2)
sStopD = w2D and (top2(south,diamonds)>0 and diamonds(south)>1) or (top3(south,diamonds)>0 and diamonds(south)>2)
sStop = (sStopS or sStopH or sStopD)

sNT = hcp(south)>15 and hcp(south)<19 and shape(south, any 4333+any 4432+any 5332) and sStop

sDouble1 = hcp(south)>11 and hcp(south)<16 and sShort
sDouble2 = hcp(south)>15 and not sNT
sDoubles = (sDouble1 or sDouble2) and shape(south,any 5440+any 4441+any 4432)

eOpensWeak2 = (w2S or w2H or w2D) and hcp(east)>6 and hcp(east)<10

# West Passes
wPasses = hcp(west)<10

weak   = hcp(north)<8
invite = hcp(north)>7 and hcp(north)<12 and not hascard(west,2C)
force  = hcp(north)>11

eOpensWeak2 and sDoubles and wPasses and (weak or invite or force)

action
average "North Weak   " 100 * weak,
average "North Invite " 100 * invite,
average "North Force  " 100 * force,

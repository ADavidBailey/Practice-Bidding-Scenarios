# alias: MajorSuitFit
# button-text: Major Suit Fit (Lev)
# dealer-position: S
# gib-works: true
# bba-works: true
# auction-filter: Auction.....\\n(1H Pass (1NT|[2-7]H))|1S Pass (1NT|[2-7]S)

/*@chat
--- Major Suit Fit
  • 2M = 8-10 TP and 3+ card support
  • 1N/2M = 4-7 TP and 3+ card support (Forcing/Semi-Forcing NT followed by 2M)
  • 1N/3M = 11-12 TP and 3-card support (Forcing/Semi-Forcing NT followed by 3M)
  • 3M = 11-12 TP and 4+ card support
  • 4M = 0-7 TP and 5+ card support
TP = HCP + short suit points + 1 for 4+ support-not-4333
Check the link below for more information
https://www.larryco.com/bridge-articles/major-openings-part-1
@chat*/

# Major_Suit_Fit
dealer south

#include "script/Predict-Opening-1-Bid"
# Returns oS & oH

#include "script/Total-Points-North"
# Returns nTP

# South opens 1 Major and North has a fit
S_ = oS and spades(north)>2
H_ = oH and hearts(north)>2

# Avoid preempts, weak 2, Michaels, and Unusual NT
wPreempt  = shape(west,any 9xxx +any 8xxx +any 7xxx +any 6xxx) and hcp(west)>3
w2Suits   = shape(west,any 76xx +any 75xx +any 66xx +any 65xx +any 55xx) and hcp(west)>3
wOvercall = shape(west,any 5xxx) and hcp(west)>7
wPasses   = hcp(west)<11 and not(wPreempt or w2Suits or wOvercall)

# Define North's 3- 4+ and 5+ card fits
spadeFit3 = S_ and spades(north)==3
spadeFit4 = S_ and spades(north)>3
spadeFit5 = S_ and spades(north)>4
heartFit3 = H_ and hearts(north)==3
heartFit4 = H_ and hearts(north)>3
heartFit5 = H_ and hearts(north)>4
fit4 = spadeFit4 or heartFit4

# Calculate North's Total Points: nTP + +1 for 4+ support unless 4333
nP = nTP + (fit4 and shape(north,xxxx-any 4333))

# Avoid Splinter
Splinter = fit4 and shape(north,any 0xxx +any 1xxx) and nP>10 and nP<16

preempt = nP<8            and (spadeFit5 or heartFit5)
faulty  = nP>3  and nP<8  and (S_ or H_) and not preempt
simple  = nP>7  and nP<11 and (S_ or H_)
invite3 = nP>10 and nP<13 and (spadeFit3 or heartFit3) 
invite4 = nP>10 and nP<13 and (spadeFit4 or heartFit4)

#include "script/Leveling"

lev1 = faulty  and keep25
lev2 = simple  and keep11
lev3 = invite3 and keep25
lev4 = invite4 and keep89
lev5 = preempt and keep
levelTheDeal = lev1 or lev2 or lev3 or lev4 or lev5

sOpens = (oH or oS) and hcp(south)>11 and hcp(south)<19
nResponds = (preempt or faulty or simple or invite3 or invite4) and not Splinter

sOpens and wPasses and nResponds
and levelTheDeal

action
average "faulty raise  " 100 * faulty,
average "simple raise  " 100 * simple,
average "3-card invite " 100 * invite3,
average "4-card invite " 100 * invite4,
average "preempt       " 100 * preempt,


action printpbn

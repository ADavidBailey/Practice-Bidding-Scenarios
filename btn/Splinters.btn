# alias: Splinters
# button-text: Splinters
# gib-works: true
# bba-works: true
# auction-filter: Note..1.Splinter
# convention-card-ns: 21GF-DEFAULT
# convention-card-ew: 21GF-GIB

/*@chat
--- Splinter Bids are double jumps.  They show a singleton or void in the suit bid and 4+ cards
in support of opener's major.  GIB splinters with a singleton A, K, or Q.  Some players do NOT.
• After 1!H a jump to 3!S, 4!C, or 4!D
• After 1!S a jump to 4!C, 4!D, or 4!H
@chat*/

# Splinters
dealer south

#include "script/Calm-Opponents"
# Defines calmWest
#include "script/GIB-1N"
# Defines gibNT
#include "script/TP"
# Defines tpSouth, tpNorth

sMin = hcp(south)==11 and tpSouth==12
sLen = sMin ? 6 : 5

sFit = spades(south)>=sLen and hearts(south)<5 and spades(north)==4
hFit = hearts(south)>=sLen and spades(south)<5 and hearts(north)==4
fit = sFit or hFit

strong2 = hcp(south)>21 or tpSouth>22 or losers(south)<5
gib2N   = shape(south, any 5332) and (hcp(south)==20 or hcp(south)==21)

sShape = shape(south,any 8xxx +any 7xxx +any 6xxx +any 5xxx -any 85xx -any 76xx -any 75xx -any 65xx)
sOpens = hcp(south)>10 and tpSouth>11 and sShape and not (strong2 or gibNT or gib2N)

# GIB uses a Jacoby 2N response with 16 TP and a singleton
jacoby2N = shape(north,any 1xxx) and tpNorth==16

# North Splinters w/4-card support and any shortness
anyShortness = shape(north,any 1xxx +any 0xxx)

nSplinters = tpNorth>12 and tpNorth<17 and anyShortness and not jacoby2N

game1 = tpSouth==12 or tpSouth==13
game2 = tpSouth==14 or tpSouth==15
slam1 = tpSouth==16 or tpSouth==17
slam2 = tpSouth>17

#include "script/Leveling"
lev1 = game1 and keep25
lev2 = game2 and keep19
lev3 = slam1 and keep33
lev4 = slam2 and keep94
levelTheDeal = lev1 or lev2 or lev3 or lev4

sOpens and fit and nSplinters and calmWest
and levelTheDeal

action
average "12-13 & 13-16" 100 * game1,
average "14-15 & 13-16" 100 * game2,
average "16-17 & 13-16" 100 * slam1,
average "18-21 & 13-16" 100 * slam2,

# alias: MaximalAfterOvercall
# button-text: Maximal  X after Overcall
# dealer-position: W
# gib-works: false
# bba-works: true
# auction-filter: Auction.....\n(1. +1S +2H|1. +1H +2D)
# convention-card: 21GF-MaximalDouble

/*@chat
--- Maximal Double after Overcall
After LHO opens, partner overcalls in a major, and RHO competes in the
maximal suit -- the suit just under partner's -- we need to distinguish
between a competitive raise and an invitational raise. The Maximal Double
is the answer.  After (1?) 1M (2M-1)...
  • 2M is a competitive raise
  • X is an invitational raise
https://www.bridgebum.com/maximal_double.php
@chat*/

/*
Maximal Double after Overcall

convention-card: 21GF-MaximalDouble
auction-filter: Auction.....\n(1[CD] +1H +2D +(2H|X)|1[CHD] +1S +2H +(2S|X))
*/

dealer south

#include "script/TP"
# tpNorth, tpSouth, tpEast, tpWest

# Predict West's opening suit
s = spades(west)
h = hearts(west)
d = diamonds(west)
c = clubs(west)

wS = s>4 and s>=h and s>=d and s>=c
wH = not wS and h>4 and h>=d and h>=c
wD = not (wS or wH) and ((d>3 and d>=c) or c<3)
wC = not (wS or wH or wD)

# North overcals 1H or 1S
nHO = hearts(north)>4 and top4(north,hearts)>1 and hearts(north)>4 and hearts(south)>2
nSO = spades(north)>4 and top4(north,spades)>1 and spades(north)>4 and spades(south)>2
n1Suit = shape(north,any 7xxx +any 6xxx +any 5xxx -any 76xx -any 66xx -any 65xx -any 55xx)

# Define East Maximal Raise and East Maximal Free Bid
eDR = diamonds(east)>2 and tpEast>5
eDF = diamonds(east)>5 and tpEast>9 and top5(east,diamonds)>2
eHR = hearts(east)>2   and tpEast>5
eHF = hearts(east)>5   and tpEast>9 and top5(east,hearts)>2

# Define East's range for Raise and Free Bid
eRangeR = hcp(east)>6
eRangeF = hcp(east)>8

# Define sequences
CHD = wC and nHO and eDF and eRangeF
CSH = wC and nSO and eHF and eRangeF
DHD = wD and nHO and eDR and eRangeR
DSH = wD and nSO and eHF and eRangeF
HSH = wH and nSO and eHR and eRangeR

wRange = hcp(west)>11
nRange = tpNorth>10
sRange = tpSouth>8 and tpSouth<12

# Now do it
(CHD or CSH or DHD or DSH or HSH) and wRange and nRange and sRange and n1Suit

action
average "CDH" 100 * CHD,
average "CSH" 100 * CSH,
average "DHD" 100 * DHD,
average "DSH" 100 * DSH,
average "HSH" 100 * HSH,

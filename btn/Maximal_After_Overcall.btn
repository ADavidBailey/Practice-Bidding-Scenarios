# alias: MaximalAfterOvercall
# button-text: MaximalAfterOvercall
# dealer-position: S
# gib-works: false
# bba-works: true
# auction-filter: Auction.....\n(1H .* 2H +3D|1S .* 2S +3H)
# convention-card: 21GF-MaximalDouble

/*
convention-card: 21GF-MaximalDouble
auction-filter: Auction.....\n(1H .* 2H +3D|1S .* 2S +3H)

Maximal Double
*/

dealer south

#include "script/Predict-Opening-1-Bid"
# oneHeart, oneSpade

#include "script/TP"
# tpNorth, tpSouth, tpEast, tpWest

# Define West's actions & East's Maximal suit bid
wShape = shape(west,xxxx -any 76xx -any 75xx -any 66xx -any 65xx -any 55xx)   // kill the 2-suited hands
wS = spades(west)>4   and top3(west,spades)>1   and wShape and tpWest>7
wC = clubs(west)>4    and top4(west,clubs)>2    and wShape and tpWest>10 and hcp(west)>9
wD = diamonds(west)>4 and top4(west,diamonds)>2 and wShape and tpWest>10 and hcp(west)>9
wH = hearts(west)>4   and top4(west,hearts)>2   and wShape and tpWest>10 and hcp(west)>9
wX = shape(west,any 5440 +any 5431 +any 4432 +any 4441) and tpWest>12 and not (wS or wC or wD or wH)
wP = not (wS or wC or wD or wH or wX)

# Define East's ranges for bidding after  X, raise, new suit, free bid.
respXRange = tpEast<12
maxDresp   = diamonds(east)>=4 and diamonds(east)>spades(east) and diamonds(east)>hearts(east) and diamonds(east)>clubs(east) and hearts(west)<3 and hcp(east,hearts)==0
maxHresp   = hearts(east)>=4   and hearts(east)>spades(east)   and hearts(east)>diamonds(east) and hearts(east)>clubs(east)   and spades(west)<3 and hcp(east,spades)==0
respX = respXRange or maxDresp or maxHresp   // for stats

raiseRange = tpEast>7 and tpEast<11
maxDraise  = diamonds(east)>2 and top5(east,diamonds)>1 and raiseRange
maxHraise  = hearts(east)>2   and top5(east,hearts)>1   and raiseRange
raise = maxDraise or maxHraise               // for stats

newSuitRange = tpEast>12 and tpEast<18
maxDnewsuit = diamonds(east)>5 and top5(east,diamonds)>2 and newSuitRange
maxHnewsuit = hearts(east)>5   and top5(east,hearts)>2   and newSuitRange
newsuit = maxDnewsuit or maxHnewsuit         // for stats

freeRange = tpEast>11 and tpEast<18
maxDfree = diamonds(east)>5 and top3(east,diamonds)>1 and freeRange
maxHfree = hearts(east)>5   and top3(east,hearts)>1   and freeRange
freebid = maxDfree or maxHfree               // for stats

maxD = (maxDresp or maxDraise or maxDnewsuit or maxDfree) and hearts(west)<2
maxH = (maxHresp or maxHraise or maxHnewsuit or maxHfree) and spades(west)<2

# South opens 1H or 1S, North raises, and East overcalls in the Maximal suit
min = hcp(south)<12 and tpSouth<13
len = min ? 6 : 5
sRange = hcp(south)>10 and tpSouth>11 and tpSouth<19
nRange = tpNorth>5 and tpNorth<11
sH = oneHeart and hearts(south)>=len and sRange and hearts(north)==3 and nRange and maxD
sS = oneSpade and spades(south)>=len and sRange and spades(north)==3 and nRange and maxH

#include "script/Leveling"

# Now do it
(sH or sS) and wShape
#and levelTheDeal

produce 5000
action
average "X          " 100 * respX,
average "raise      " 100 * raise,
average "new suit   " 100 * newsuit,
average "free bid   " 100 * freebid,


action printpbn

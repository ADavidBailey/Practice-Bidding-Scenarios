generate 10000
produce 1000
dealer south

### SOUTH
# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and top4(south,spades)==1 and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and top4(south,hearts)==1 and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)==1 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and top4(south,clubs)==1 and not hascard(south,AC) ? 1 : 0
ntDownGrade = S2H or H2H or D2H or C2H

# Define notrump points for south (ntPoints)
ntPoints = ntDownGrade ? hcp(south) : hcp(south) + lengthPoints

# Define suit points for south (suitPoints)
#suitPoints = hcp(south) + lengthPoints

# Define robot notrump shape and exclude any 5card 
ntShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422 -5xxx -x5xx)

# Define ntPoint ranges
#oneNT = ntShape and ntPoints>14 and ntPoints<18
twoNT = ntShape and ntPoints>19 and ntPoints<22
#weakNT = ntShape and ntPoints>10 and ntPoints<15
#overcallNT = ntShape and ntPoints>14 and ntPoints<19  // 15-18

### NORTH
# Calculate strength for North -- hcp plus 1 for every card over 4 in any suit 
nlp1 = spades(north)>4 ? spades(north)-4 : 0
nlp2 = hearts(north)>4 ? hearts(north)-4 : 0
nlp3 = diamonds(north)>4 ? diamonds(north)-4 : 0
nlp4 = clubs(north)>4 ? clubs(north)-4 : 0
strength = nlp1 + nlp2 + nlp3 + nlp4 + hcp(north)

partscore = strength<=3
game = strength>=4 and strength<=11
slam = strength>=12

# How do you prioritize these?
texas = (spades(north)>5 or hearts(north)>5) and not partscore
MSS = ((diamonds(north)>4 and clubs(north)>4) or (diamonds(north)>5 or clubs(north)>4)) and not (partscore or texas)
smolen = shape(north,54xx+45xx) and not (partscore or texas or MSS) 
puppet = shape(north,43xx+34xx+33xx+any 1xxx) and not (partscore or texas or MSS or smolen)
balanced = shape(north,any 4333 +any 4432 +any 5332) and not (partscore or texas or MSS or smolen or puppet)
jacoby = (spades(north)>4 or hearts(north)>4) and not (texas or MSS or smolen or puppet or balanced)
other = not (texas or smolen or MSS or puppet or balanced or jacoby)

### Define Leveling
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)
c5 = hascard(west,4C)

reduce98 = c1 and c2 and c3
reduce94 = c1 and c2
reduce87 = c1 and (c2 or c3)
reduce75 = c1
reduce62 = (c1 or c2) and not (c3 and (c4 or c5)) 
reduce50 = c1 or c2
reduce38 = not reduce62
reduce25 = not reduce75
reduce13 = not reduce87
reduce06 = not reduce94
reduce02 = not reduce98
###

levTexas = not texas or 0
levSmolen = not smolen or 0
levPuppet = not puppet or reduce98
levMSS = not MSS or 0
levBalanced = not balanced or reduce98
levJacoby = not jacoby or 0
levOther = not other or 0

levSlam = not slam or 0
levGame = not game or reduce98
levPartscore = not partscore or reduce98

levelTheCode = levTexas or levSmolen or levMSS or levPuppet or levBalanced or levJacoby or levOther or levSlam or levGame or levPartscore
 
### Ready for Leveling

### Do it
twoNT
#and levelTheCode

### Generate Statistics

action
average "Texas        " 100 * texas,
average "Smolen       " 100 * smolen,
average "MSS          " 100 * MSS,
average "Puppet       " 100 * puppet,
average "Balanced     " 100 * balanced,
average "Jacoby       " 100 * jacoby,
average "Other        " 100 * other,
average "Partscore    " 100 * partscore,
average "Game         " 100 * game,
average "Slam         " 100 * slam,

average "ntPoints     " ntPoints,
average "strength     " strength,
average "lengthPoints " lengthPoints,
average "ntDownGrade  " ntDownGrade,

printoneline

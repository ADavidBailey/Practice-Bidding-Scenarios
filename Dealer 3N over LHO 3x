Script,NToverLHO3x
setDealerCode(
`
produce 1000
dealer west

### 3N over LHO's 3x ###

# define south's controls for each suit
sCs = controls(south,spades)
sCh = controls(south,hearts)
sCd = controls(south,diamonds)
sCc = controls(south,clubs)

# define honor cards
aS = sCs==2
kS = sCs==1
aH = sCh==2
kH = sCh==1
aD = sCd==2
kD = sCd==1
aC = sCc==2
kC = sCc==1

qS = hascard(south,QS)
qH = hascard(south,QH)
qD = hascard(south,QD)
qC = hascard(south,QC)
jS = hascard(south,JS)
jH = hascard(south,JH)
jD = hascard(south,JD)
jC = hascard(south,JC)

# define number of cards in each suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)

# define south's sHCP points
sHCP = hcp(south)

# Define South's Long Rebiddable suits -- 6+ cards with AK or AQJ or better
lrS = spades(south)>5 and hcp(south,spades)>6
lrH = hearts(south)>5 and hcp(south,hearts)>6
lrD = diamonds(south)>5 and hcp(south,diamonds)>6
lrC = clubs(south)>5 and hcp(south,clubs)>6
sRebid = (lrS or lrH or lrD or lrC)

# west preempts and the 3-level
sW = shape(west, 7xxx-x4xx-any 5xxx)      and top2(west,spades)>0  and top5(west,spades)>2
hW = shape(west, x7xx-4xxx-any 5xxx)      and top2(west,hearts)>0   and top5(west,hearts)>2
dW = shape(west, xx7x-4xxx-x4xx-any 5xxx) and top2(west,diamonds)>0 and top5(west,diamonds)>2
cW = shape(west, xxx7-4xxx-x4xx-any 5xxx) and top2(west,clubs)>0    and top5(west,clubs)>2
wPreempts = (sW or hW or dW or cW) and hcp(west)>6 and hcp(west)<11


# North and Easy both pass
nPasses = hcp(north)<11 and shape(north, xxxx - any 7xxx -any 6xxx -any 55xx)
ePasses = hcp(east)<9 and shape(east, xxxx - any 7xxx -any 6xxx -any 55xx)

# define south has only 1 stop -- A, Kx, QJx
s1s = aS or (kS and s>1) or (qS and jS and s>2)
h1s = aH or (kH and h>1) or (qH and jH and s>2)
d1s = aD or (kD and d>1) or (qD and jD and s>2)
c1s = aC or (kC and c>1) or (qC and jC and s>2)
oneStop = (s1s or h1s or d1s or c1s)

# define south has 2 stops -- AK, AQ, KQx
s2s = sW and sCs==3 or (aS and qS) or (kS and qS and s>2)
h2s = hW and sCh==3 or (aH and qH) or (kH and qH and h>2)
d2s = dW and sCd==3 or (aD and qD) or (kD and qD and d>2)
c2s = cW and sCc==3 or (aC and qC) or (kC and qC and c>2)
twoStop = (s2s or h2s or d2s or c2s)

# define the three cases
case1 = oneStop and sHCP>17
case2 = oneStop and sRebid and sHCP>15 and not case1
case3 = twoStop and sHCP>15
case4 = not (case1 or case2 or case3)

### Define Leveling
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)
c5 = hascard(west,4C)

reduce98 = c1 and c2 and c3
reduce94 = c1 and c2
reduce87 = c1 and (c2 or c3)
reduce75 = c1
reduce62 = (c1 or c2) and not (c3 and (c4 or c5)) 
reduce50 = c1 or c2
reduce38 = not reduce62
reduce25 = not reduce75
reduce13 = not reduce87
reduce06 = not reduce94
reduce02 = not reduce98
reduce   = 1

# Level it
level1 = not case1 or reduce94
level2 = not case2 or reduce
level3 = not case3 or reduce94
level = level1 and level2 and level3

# south bids 3N

wPreempts and (case1 or case2 or case3) and nPasses and ePasses
#and level

action
average "1 Stop and >17             " 100 * case1,
average "1 Stop, good suit, and >15 " 100 * case2,
average "2 Stop and >15             " 100 * case3,
average "not one of the above       " 100 * case4

### Leveling Results ###

#  Before:
#  1 Stop and >17             : 61.5
#  1 Stop and >17             : 61.5
#  1 Stop, good suit, and >15 : 9.2
#  2 Stop and >15             : 68
#  not one of the above       : 0
#  Generated 8012214 hands
#  Produced 1000 hands

#  After:
#  1 Stop and >17             : 42.4242
#  1 Stop, good suit, and >15 : 40
#  2 Stop and >15             : 47.2727
#  not one of the above       : 0
#  Generated 10000000 hands
#  Produced 165 hands

`,"W",true)
Script
Button,3NT over LHO's 3x,\n\
---  3NT over LHO's Preempt.\n\
• 18+ and 1 stop\n\
• 16+ and good suit\n\
• 16+ and 2 stops\n\
and a prayer!\n\
%NToverLHO3x%

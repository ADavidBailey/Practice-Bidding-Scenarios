produce 10000000

### Leveling ###

c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep02 = c1 and c2 and c3
keep06 = c1 * c2
keep12 = c1 * (c2 + c3)
keep19 = c1 - (c2 * c3)
keep25 = c1 
keep37 = c1 + (c2 * (c3 + c4))
keep44 = c1 + c2 - (c3 * c4)
keep50 = c1 + c2
keep56 = 1 - keep44
keep63 = 1 - keep37
keep75 = 1 - keep25
keep81 = 1 - keep19
keep88 = 1 - keep12
keep94 = 1 - keep06
keep98 = 1 - keep02
keep   = 1

### Example of how to use this ###
#   # Level it
#   level1 = case1 * keep06     // keep 06% of case 1
#   level2 = case2 * keep98     // keep 98% of case 2
#   level3 = case3 * keep       // keep all of case 3
#   levelTheDeal = level1 or level2 or level3
# 
#   # south bids 3N
#   wPreempts and nPasses and ePasses and (case1 or case2 or case3)
#   and levelTheDeal            // this line can be commented out to skip leveling

### Generate Statistics for Leveling ###
action
average "keep98" 100 * keep98,
average "keep94" 100 * keep94,
average "keep88" 100 * keep88,
average "keep81" 100 * keep81,
average "keep75" 100 * keep75,
average "keep63" 100 * keep63,
average "keep56" 100 * keep56,
average "keep50" 100 * keep50,
average "keep44" 100 * keep44,
average "keep37" 100 * keep37,
average "keep25" 100 * keep25,
average "keep19" 100 * keep19,
average "keep12" 100 * keep12,
average "keep06" 100 * keep06,
average "keep02" 100 * keep02,

average "c1" 100 * c1,
average "c2" 100 * c2,
average "c3" 100 * c3,
average "c4" 100 * c4,

### Results -- the actual probabilities based on 10,000,000 deals. ###
# keep98: 98.4712
# keep94: 93.6322
# keep88: 87.7577
# keep81: 81.388
# keep75: 75.0112
# keep63: 62.7497
# keep56: 56.3801
# keep50: 49.9913
# keep44: 43.6199
# keep37: 37.2503
# keep25: 24.9888
# keep19: 18.612
# keep12: 12.2423
# keep06: 6.36783
# keep02: 1.52877
# c1: 24.9888
# c2: 25.0024
# c3: 24.9853
# Generated 10000000 hands
# Produced 10000000 hands

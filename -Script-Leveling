generate 10000000
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep03 = keep06 and keep44
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep97 = not keep03
keep   = 1

/*
  ### Example of how to use this to level hand types ###
  # Level it
  level1 = case1 and keep06     // keep 06% of case 1
  level2 = case2 and keep98     // keep 98% of case 2
  level3 = case3 and keep       // keep all of case 3
  levelTheDeal = level1 or level2 or level3
  
  # south bids 3N
  wPreempts and nPasses and ePasses and (case1 or case2 or case3)
  and levelTheDeal            // this line can be commented out to skip leveling
*/

/*   Just in case you stubornly insist that c1 + c2 produces a 50% result.
  ### Example of how to get the Produced/Generated ratio
  condition
  keep97
  action
*/

/*
### Example of how to generate Statistics for Leveling ###
action
average "keep97" 100 * keep97,
average "keep94" 100 * keep94,
average "keep89" 100 * keep89,
average "keep86" 100 * keep86,
average "keep81" 100 * keep81,
average "keep75" 100 * keep75,
average "keep70" 100 * keep70,
average "keep67" 100 * keep67,
average "keep56" 100 * keep56,
average "keep53" 100 * keep53,

average "keep47" 100 * keep47,
average "keep44" 100 * keep44,
average "keep33" 100 * keep33,
average "keep30" 100 * keep30,
average "keep25" 100 * keep25,
average "keep19" 100 * keep19,
average "keep14" 100 * keep14,
average "keep11" 100 * keep11,
average "keep06" 100 * keep06,
average "keep03" 100 * keep03,
*/

/*
Results -- the actual probabilities based on 10,000,000 deals.
  keep97: 97.3214
  keep94: 93.6387
  keep89: 89.2858
  keep86: 85.7274
  keep81: 81.3745
  keep75: 75.0132
  keep70: 70.1677
  keep67: 66.9695
  keep56: 56.3635
  keep53: 52.6808
  keep47: 47.3192
  keep44: 43.6365
  keep33: 33.0305
  keep30: 29.8323
  keep25: 24.9868
  keep19: 18.6255
  keep14: 14.2726
  keep11: 10.7142
  keep06: 6.36128
  keep03: 2.67858
*/

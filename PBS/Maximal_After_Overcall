Script,MaximalAfterOvercall
setDealerCode(`

/*
convention-card: 21GF-MaximalDouble
auction-filter: Auction.....\n(1H .* 2H .* 3D|1S .* 2S .* 3H)

Maximal after We Overcall
*/

dealer south

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Predict-Opening-1-Bid
# oneHeart, oneSpade

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/TP
# tpNorth, tpSouth, tpEast, tpWest

# Define West's actions & East's Maximal suit bid
wShape = shape(west,xxxx -any 76xx -any 75xx -any 66xx -any 65xx -any 55xx)                                                       
wS = spades(west)>4   and top3(west,spades)>1   and tpWest>7
wC = clubs(west)>4    and top3(west,clubs)>1    and tpWest>10 and hcp(west)>9
wD = diamonds(west)>4 and top3(west,diamonds)>1 and tpWest>10 and hcp(west)>9
wH = hearts(west)>4   and top3(west,hearts)>1   and tpWest>10 and hcp(west)>9
wX = shape(west,any 5440 +any 5431 +any 5332 +any 4432 +any 4441) and tpWest>11 and tpEast<11
wP = not (wS or wC or wD or wH)

# Define East's ranges for bidding after  X, raise, new suit, free bid.
respXRange = tpEast<13
maxDresp   = diamonds(east)>4 and diamonds(east)>spades(east) and diamonds(east)>hearts(east) and diamonds(east)>clubs(east)
maxHresp   = hearts(east)>4   and hearts(east)>spades(east)   and hearts(east)>diamonds(east) and hearts(east)>clubs(east)
respX = maxDresp or maxHresp           // for stats

raiseRange = tpEast>7 and tpEast<11
maxDraise  = diamonds(east)>2 and top5(east,diamonds)>1 and raiseRange
maxHraise  = hearts(east)>2   and top5(east,hearts)>1   and raiseRange
raise = maxDraise or maxHraise        // for stats

newSuitRange = tpEast>12 and tpEast<18
maxDnewsuit = diamonds(east)>5 and top5(east,diamonds)>2 and newSuitRange
maxHnewsuit = hearts(east)>5   and top5(east,hearts)>2   and newSuitRange
newsuit = maxDnewsuit or maxHnewsuit  // for stats

freeRange = tpEast>11 and tpEast<18
maxDfree = diamonds(east)>5 and top3(east,diamonds)>1 and freeRange
maxHfree = hearts(east)>5   and top3(east,hearts)>1   and freeRange
freebid = maxDfree or maxHfree        // for stats

maxD = (maxDresp or maxDraise or maxDnewsuit or maxDfree) and hearts(west)<2
maxH = (maxHresp or maxHraise or maxHnewsuit or maxHfree) and spades(west)<2

# South opens 1H or 1S, North raises, and East overcalls in the Maximal suit
min = hcp(south)<12 and tpSouth<13
len = min ? 6 : 5
sRange = hcp(south)>10 and tpSouth>11 and tpSouth<19
nRange = tpNorth>5 and tpNorth<11
sH = oneHeart and hearts(south)>=len and sRange and hearts(north)==3 and nRange and maxD
sS = oneSpade and spades(south)>=len and sRange and spades(north)==3 and nRange and maxH

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Leveling

# Now do it
(sH or sS) and wShape
#and levelTheDeal

produce 5000
action
average "X          " 100 * respX,
average "raise      " 100 * raise,
average "new suit   " 100 * newsuit,
average "free bid   " 100 * freebid,

`, "S", true)
Script
Button,Maximal Double,\n\
--- Maximal Overcall Double:  After we've bid and raised a major suit\n\
and they compete in the suit just under ours (the Maximal suit) we need\n\
to distinguish between a competitive raise and an invitational raise.\n\
The Maximal Double is the answer:\n\
  • After 1!H (?) 2!H (3!D)，3!H is competitive and X is invitational\n\
  • After 1!S (?) 2!S (3!H)，3!S is competitive and X is invitational\n\
The BBO Robots do NOT play this.\n\
https://www.nvba.org/novices/Robert_Todd/(195)+Maximal+Doubles.pdf\n\
%MaximalDouble%,backgroundColor=lightpink
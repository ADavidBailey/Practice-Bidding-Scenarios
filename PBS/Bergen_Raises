Script,BergenRaises
setDealerCode(`

# Select Bergen,and 1M 3M blocking
# convention-card: 21GF-Bergen
# auction-filter: Auction.....\\n1[HS].Pass.3[CDHSN]

# Bergen Raises
dealer north

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Calm-Opponents

# Calculate Total Points for south (tp) for south
v1 = shape(south, any 0xxx) ? 5 : 0  // allow for 2 voids
v2 = shape(south, any 00xx) ? 5 : 0
s1 = shape(south, any 1xxx) ? 3 : 0 // allow for 2 singletons
s2 = shape(south, any 11xx) ? 3 : 0
d1 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
d2 = shape(south, any 22xx) ? 1 : 0
d3 = shape(south, any 222x) ? 1 : 0
ssp = v1+v2+s1+s2+d1+d2+d3          // possible short suit points

# Short Suit Points is limited to the number of trump support cards
tp = ssp<4 ? ssp + hcp(south) : 4 + hcp(south)

NT = shape(north, any 4333+4432+5332) and hcp(north)>14 and hcp(north)<18 and hcp(north)>19 and hcp(north)<22
GF = hcp(north)>21

# Define openers
sOpener = not NT and spades(north) >= hearts(north) and spades(north) >=5 and spades(north) >= diamonds(north) and spades(north) >= clubs(north)
hOpener = not NT and not sOpener and hearts(north) >=5 and hearts(north) >= diamonds(north) and hearts(north) >= clubs(north)

# Define suit fit w/4-card support
sFit = sOpener and spades(south)==4
hFit = hOpener and hearts(south)==4
fit = sFit or hFit
nOpens = fit and hcp(north)>11 and hcp(north)<22

# 3M = Preemptive Raise 0-6 TP
case3M = tp<7 and nOpens

# 3C = Constructive 7-10 TP
case3C = tp>6 and tp<11 and nOpens

# 3oM = Ambiguous Splinter 12+ TP
case3oM = tp>11 and shape(south, any 14xx+any 04xx) and nOpens

# 3N = 4333 13-15 TP/HCP
case3N = tp>12 and tp<16 and shape(south, any 4333) and nOpens

# 3D = Limit Raise 11-12 TP
case3D = tp>10 and tp<13 and not (case3oM or case3N) and nOpens

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Leveling

# These need to be in the same order as the statistics
l2 = case3C  and keep14
l5 = case3D  and keep19
l3 = case3oM and keep19
l1 = case3M  and keep
l4 = case3N  and keep
levelTheDeal = l1 or l2 or l3 or l4 or l5

# Now do it
nOpens and (case3M or case3C or case3oM or case3N or case3D)
and levelTheDeal

produce 5000
action
average "3C   7-10 TP      " 100 * case3C,
average "3D  11-12 TP      " 100 * case3D,
average "3oM 11-13 41/40xx " 100 * case3oM,
average "3M   0-6  TP      " 100 * case3M,
average "3N  13-15 4333    " 100 * case3N,


`, "N", true);
Script
Button,Bergen Raises (Lev),\n\
--- Bergen Raises all show exactly 4-card support.\n\
• 3!C = Constructive (7-10) TP\n\
• 3!D = Limit Raise (11-12) TP\n\
• 3M  = Preemptive Raise (0-6) TP\n\
• 3OM = Ambiguous Splinter (12+) TP\n\
• 3N  = 4333 (13-15) HCP\n\
https://www.larryco.com/bridge-articles/bergen-raises\n\
%BergenRaises%,backgroundColor=lightpink
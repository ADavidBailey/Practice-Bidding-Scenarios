Script,GibStayman
setDealerCode(`

/*
auction-filter: Auction.....\n1NT? +Pass +2C +
*/

# Stayman
dealer south

# 1 Notrump Opening
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/GIB-1N
# Defines gibNT
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Calm-Opponents
# Defines calmWest

# Calculate North's Short-Suit Penalty Points
nSP = spades(north)<3 and hcp(north,spades)>0     ? 1 : 0
nHP = hearts(north)<3 and hcp(north,hearts)>0     ? 1 : 0
nDP = diamonds(north)<3 and hcp(north,diamonds)>0 ? 1 : 0
nCP = clubs(north)<3 and hcp(north,clubs)>0       ? 1 : 0
nTP = hcp(north) -nSP -nHP -nDP -nCP

# Boost quality of North's major
nQS = spades(north)==4 and hcp(north,spades)>1
nQH = hearts(north)==4 and hcp(north,hearts)>1
nQ  = nQS or nQH

# Define various 2C bids -- GIB does not play Drop Dead or Crawlling Stayman
smolen      = shape(north,54xx +45xx) and nTP>9
majorInvite = shape(north,54xx +45xx) and nTP>7 and hcp(north)<10
stayman     = shape(north,4xxx +x4xx -any 4441 - any 4450 - any 9xxx -any 8xxx -any 7xxx -any 6xxx) and nTP>7 and nQ and not (smolen or majorInvite)
n2Clubs = stayman or majorInvite or smolen

gibNT and n2Clubs and calmWest

action
average "45xx Invite " 100 * (majorInvite and shape(north,45xx)),
average "54xx Invite " 100 * (majorInvite and shape(north,54xx)),
average "smolen      " 100 * smolen,
average "stayman     " 100 * stayman,

`,"S",true)
Script
Button,GIB Stayman,\n\
--- Stayman asks the opener for a 4-card major.  Responses are as follow...\n\
• 2!D - No 4-card major\n\
• 2!H - 4+!H，may also have 4!S\n\
• 2!S - 4+!S and denies 4!H\n\
Normally，Stayman shows 8+HCP; but，if you're willing to pass anything that your\n\
partner might bid，you may bid 2!C with any strength.\n\
https://www.bridgebum.com/stayman.php\n\
https://www.bridgebum.com/crawling_stayman.php\n\
%GibStayman%

Script,Lebensohl
setDealerCode(`

# Lebensohl
dealer south

# South opens 1N, Robot style
sOpens = hcp(south)>14 and hcp(south)<18 and shape(south,any 4333 +any 4432 +any 5332 -5xxx-x5xx)

# Good Suits West -- QJT or better
GSW = top4(west,spades)>1   and top5(west,spades)>2   and spades(west)>3
GHW = top4(west,hearts)>1   and top5(west,hearts)>2   and hearts(west)>3
GDW = top4(west,diamonds)>1 and top5(west,diamonds)>2 and diamonds(west)>3
GCW = top4(west,clubs)>1    and top5(west,clubs)>2    and clubs(west)>3

wStrong = hcp(west)>15
w2Suit  = (GSW + GHW + GDW + GCW)==2 and shape(west,any 64xx+any 55xx+any 54xx) and hcp(west)>11 and hcp(west)<16
w1Suit  = (GSW or GHW or GDW or GCW) and shape(west,any 7xxx+any 6xxx+any 5xxx) and hcp(west)>10 and hcp(west)<16 and not w2Suit

wCompetes = (wStrong or w2Suit or w1Suit)

# Good Suits North -- 5+ to the QJT or better
GSN = top5(north,spades)>2   and shape(north,6xxx+5xxx+any 1xxx+any 0xxx)
GHN = top5(north,hearts)>2   and shape(north,x6xx+x5xx+any 1xxx+any 0xxx)
GDN = top5(north,diamonds)>2 and shape(north,xx6x+xx5x+any 1xxx+any 0xxx)
GCN = top5(north,clubs)>2    and shape(north,xxx6+xxx5+any 1xxx+any 0xxx)

n0Suit  = shape(north,any 4432+any 4333+any 4441) and hcp(north)>9
n1Suit  = (GSN or GHN or GDN or GCN)              and hcp(north)>8
nForces = (n0Suit or n1Suit)

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Leveling

# Reduce West's 1-suited hands
levStrong = wStrong and keep
lev1Suit  = w1Suit  and keep03
lev2Suit  = w2Suit  and keep
levelTheDeal = (levStrong or lev2Suit or lev1Suit)

# Now do it
sOpens and wCompetes and nForces
and levelTheDeal

action
average "South HCP      " hcp(south),
average "North HCP      " hcp(north),
average "North Balanced " 100 * n0Suit,
average "North 1 Suit   " 100 * n1Suit,
average "West Strong    " 100 * wStrong,
average "West 2-Suited  " 100 * w2Suit,
average "West 1-Suited  " 100 * w1Suit,

`, "S", true);
Script
Button,Lebensohl,\n\
--- Lebensohl\n\
After we open 1N and RHO overcalls 2!D，2!H，or 2!S...\n\
• A suit at the 2-level is natural and not forcing.\n\
• 2N is a relay to 3C and after the opener's 3C bid...\n\
  ◦ A new suit is to play.\n\
  ◦ A cue bid is Stayman WITH A STOP.\n\
  ◦ 3N is to play WITH A STOP.\n\
• A new suit at the 3-level is game-forcing.\n\
• A cue bid is Stayman WITHOUT a stop.\n\
• 3N is natural，WITHOUT a STOP -- opener should pass or scramble.\n\
https://www.larryco.com/bridge-articles/lebensohl\n\
https://www.larryco.com/bridge-articles/transfer-lebensohl\n\
%Lebensohl%
Script,JumpCuebidStrong
setDealerCode(`

# BBA does NOT play Strong Direct Jump Cuebids.  The following filter matches the deals that
# fit the intent of the scenario
#                 (\\n1C.*\\n..* [3-7][CDHS])|(\\n1D.*\\n..* [3-7][CDHS])|(\\n1H.*\\n..* [3-7][CDS])|(\\n1S.*\\n..* [3-7][CDH])

# This filter matches the bidding.
# auction-filter: (\\n1C 3C)|(\\n1D 3D)|(\\n1H 3H)|(\\n1S 3S)

# Jump Cuebid Strong 
dealer east

# Define solid suits interested in 3N if partner has a stopper
solidD = diamonds(south)>6 and top4(south,diamonds)==4
solidC = clubs(south)>6    and top4(south,clubs)==4
solidH = hearts(south)>6   and top4(south,hearts)==4
solidS = spades(south)>6   and top4(south,spades)==4

# Predict East's opening suit
s = spades(east)
h = hearts(east)
d = diamonds(east)
c = clubs(east)
oS = s>4 and s>=h and s>=d and s>=c
oH = not oS and h>4 and h>=d and h>=c
oD = not (oS or oH) and ((d>3 and d>=c) or c<3)
oC = not (oS or oH or oD)

# A or Kx in opponent's suit is a stopper
CS = controls(south,clubs)>1 or (controls(south,clubs)==1 and clubs(south)>1)
DS = controls(south,diamonds)>1 or (controls(south,diamonds)==1 and diamonds(south)>1)
HS = controls(south,hearts)>1 or (controls(south,hearts)==1 and hearts(south)>1)
SS = controls(south,spades)>1 or (controls(south,spades)==1 and spades(south)>1)

openC = oC and (solidD or solidH or solidS) and not CS
openD = oD and (solidC or solidH or solidS) and not DS
openH = oH and (solidC or solidD or solidS) and not HS
openS = oS and (solidC or solidD or solidH) and not SS
openSuit = (openC or openD or openH or openS) and controls(south)>5  // solid suit asking partner to bid 3N with a stopper

# East opens
eOpens = openSuit and hcp(east)>11 and hcp(east)<15

eOpens and shape(south,any 9xxx+any 8xxx+any 7xxx-any 5xxx)

action
average "hcp(east)      " hcp(east),
average "hcp(south)     " hcp(south),
average "losers(south)  " 100 * losers(south),
average "controls(south)" 100 * controls(south),

average "Clubs            " 100 * oC,
average "Diamonds         " 100 * oD,
average "Hearts           " 100 * oH,
average "Spades           " 100 * oS,

`, "E", true);
Script
Button,Jump Cuebid Strong,\n\
--- Direct Jump Cuebid of a major suit.\n\
• 1M (3M) shows any other solid suit (usually a minor) with less than 5\n\
losers and asks partner to bid 3N with a stopper in the opponent's suit.\n\
Without a stopper，bidding a new major is natural，4!C shows\n\
0/1 controls，and 4!D shows 2+ controls. (GIB does **not** play this)\n\
https://tinyurl.com/JumpCueBids\n\
%JumpCuebidStrong%backgroundColor=lightpink

Script,InvitationalJump3Under
setDealerCode(`

# convention-card: 21GF-3-Under-Inv-Jump
# auction-filter: 1D.Pass.3C|1H.Pass.3[CD]|1S.Pass.3[CDH]

# Robot 3 Under Invitational Jump (9-11 HCP)

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Predict-Opening-1-Bid
# Defines oneClub, oneSpade, oneHeart, oneDiamond
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Calm-Opponents
# Define calmWest, calmEast, and calmOpps

# Define NoFit
sFit = oneSpade and spades(north)>2
hFit = oneHeart and hearts(north)>2
dFit = oneDiamond and diamonds(north)>3
noFit = not (sFit or hFit or dFit)

# Define North's good suits -- 6+ cards with at least AK, AQ, or KQJ
goodHearts   = hearts(north)>5   and top5(north,hearts)  >2 and hcp(north,hearts)>5
goodDiamonds = diamonds(north)>5 and top5(north,diamonds)>2 and hcp(north,diamonds)>5
goodClubs    = clubs(north)>5    and top5(north,clubs)   >2 and hcp(north,clubs)>5

# north does NOT have a major
noMajorNorth = spades(north)<4 and hearts(north)<4

n3C = (oneDiamond or oneHeart or oneSpade) and goodClubs and noMajorNorth
n3D = (oneHeart or oneSpade) and goodDiamonds and noMajorNorth
n3H = (oneSpade) and goodHearts

# (Predict South's opening bid sometimes opens w/10hcp
# Now do it 
(n3C or n3D or n3H) and calmWest and noFit and hcp(north)>8 and hcp(north)<12 and hcp(south)>10

produce 1000
action
average "hcp(south)" hcp(south),
average "hcp(north)" hcp(north),
average "1D-3C        " 100 * (oneDiamond and n3C),
average "1H-3C        " 100 * (oneHeart and n3C),
average "1H-3D        " 100 * (oneHeart and n3D),
average "1S-3C        " 100 * (oneSpade and n3C),
average "1S-3D        " 100 * (oneSpade and n3D),
average "1S-3H        " 100 * (oneSpade and n3H),

`, "S", true);
Script
Button,3-under Inv. Jump,\n\
--- 3 Under Invitational Jump\n\
• 1!S - 3!C，3!D，or 3!H\n\
• 1!H - 3!C or 3!D\n\
• 1!D - 3!C\n\
Show a good 6+ card suit with 9-11 HCP and deny 3-card support for opener’s major.\n\
%InvitationalJump3Under%

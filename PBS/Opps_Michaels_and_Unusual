Script,OppsMichaelsAndUnusual
setDealerCode(`

/*
Opps Michaels Cuebid and Unusual 2NT

auction-filter: Auction.....\n(1C +2C|1D +2D|1H +2H|1S +2S|1[CDHS] +2N)
*/
dealer south

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/TP
# Defines tpSouth, tpWest, tpNorth, tpEast

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Predict-Opening-1-Bid
# Defines oneClub, oneDiamond, oneHeart, oneSpade, gibNT

# Define good suits
GoodS = spades(west)>4 and top5(west,spades)>2
GoodH = hearts(west)>4 and top5(west,hearts)>2
GoodD = diamonds(west)>4 and top5(west,diamonds)>2
GoodC = clubs(west)>4 and top5(west,clubs)>2

# Define 2-suits
SH = (GoodS and GoodH)
SD = (GoodS and GoodD)
SC = (GoodS and GoodC)
HD = (GoodH and GoodD)
HC = (GoodH and GoodC)
DC = (GoodD and GoodC)

# Define 2-Suits for Michaels Cuebid
sMCB = oS and (HD or HC)
hMCB = oH and (SD or SC)
dMCB = oD and (SH)
cMCB = oC    and (SH)
MCB  = (sMCB or hMCB or dMCB or cMCB) and tpWest>8

# Define 2-Suits for Unusual NT
sUNT = oS and DC
hUNT = oH and DC
dUNT = oD and HC
cUNT = oC and HD
UNT  = (sUNT or hUNT or dUNT or cUNT) and tpWest>11


oSuit = (MCB or UNT) and shape(south,any xxxx-8xxx-7xxx-6xxx)
oRange = hcp(south)<15

# Intervener is weak or strong
iWeak   = losers(west)<8 and hcp(west)>7  and hcp(west)<12
iStrong = losers(west)<5 and hcp(west)>14 and hcp(west)<20
iRange = iWeak or iStrong

# This is an attempt to get stats for overcaller weak/strong.  I don't think I can get it to work.
wHCP    = iWeak   ? hcp(west)    : 0
wLosers = iWeak   ? losers(west) : 0
sHCP    = iStrong ? hcp(west)    : 0
sLosers = iStrong ? losers(west) : 0

# Responder's minimum HCP -- you might try adjusting this
rRange = hcp(north)>7

### Now do it
oSuit and iRange and rRange

# To get stats for west weak/strong uncomment one of the following
#and iWeak
#and iStrong


action

average "Weak       " 100 * iWeak,
average "  HCP      "     wHCP,
average "  Losers   "     wLosers,
average "Strong     " 100 * iStrong,
average "  HCP      "     sHCP,
average "  Losers   "     sLosers,

average "1S         " 100 * (oS and (HD or HC or DC)),
average "   (2S)  HD" 100 * (oS and HD),
average "   (2S)  HC" 100 * (oS and HC),
average "   (2N)  DC" 100 * (oS and DC),
average "1H         " 100 * (oH and (SD or SC or DC)),
average "   (2H)  SD" 100 * (oH and SD),
average "   (2H)  SC" 100 * (oH and SC),
average "   (2N)  DC" 100 * (oH and DC),
average "1D         " 100 * (oD and (SH or HC or SC)),
average "   (2D)  SH" 100 * (oD and SH),
average "   (2N)  HC" 100 * (oD and HC),
average "         SC" 100 * (oD and SC),
average "1C         " 100 * (oC and (SH or HD or SD)),
average "   (2C)  SH" 100 * (oC and SH),
average "   (2N)  DC" 100 * (oC and HD),
average "         SD" 100 * (oC and SD),

frequency "Opener HCP    " (hcp(south),12,14),
frequency "Intervener HCP" (hcp(west),8,19),
frequency "Responder HCP " (hcp(north),6,17),

`,"S",true)
Script
Button,Opps Michaels and Unusual,\n\
--- Opps Michaels Cuebid and Unusual NT\n\
Michaels Cuebid - 9+ TP:\n\
  • 1!C - 2!C = !H and !S\n\
  • 1!D - 2!D = !H and !S\n\
  • 1!H - 2!H = !S and (!D or !C)\n\
  • 1!S - 2!S = !H and (!D or !C)\n\
Unusual 2NT - 12+ TP:\n\
  • After any 1-level suit opening，2NT shows the two lowest ranking unbid suits.\n\
%OppsMichaelsAndUnusual%

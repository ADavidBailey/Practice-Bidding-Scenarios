Script,GibRespTo1C
setDealerCode(`

/*
South opens 1C and North responds 1D, 1H, 1S, 1N, 2C, 2N, 3C, 3D, 3H, 3S, or 3N.

convention-card: 21GF-GIB
auction-filter: Auction.....\n1C .*Pass .*(1[DHS]|1NT?|2C|2NT?|3[CDHS]|3NT?) +

*/

dealer south

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Calm-Opponents
# Defines calmOpps
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Predict-Opening-1-Bid
# Defines oneClub
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Total-Points-South
# Defines sTP
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Total-Points-North
# Defines nTP

# South bids 1C and North responds
sRange = hcp(south)>10 and hcp(south)<22 and sTP>11 and sTP<23
sClubs = oneClub and clubs(south)<7

nRange = nTP>5 and nTP<16    // avoid strong jump shifts

# Define responses to 1C
nD = diamonds(north)
nH = hearts(north)
nS = spades(north)

## 1. Major suit opening
rS1 = nS>4 and nS>=nH                       // higher ranking of 5+card suits
rS2 = nTP<13 and nS==4 and nS>nH            // skip longer D w/<13
rS3 = shape(north,4xxx) and nS>nH and nD<5  // prefer 4-card H/S to 4-card D
r1S = rS1 or rS2 or rS3

rH1 = nH>4 and nH>nS
rH2 = nTP<13 and nH==4 and nH>=nS
rH3 = shape(north,x4xx) and nD<5  
r1H = (rH1 or rH2 or rH3) and not r1S

rM = r1H or r1S

# GIB defines partial stop: length+HCP = 4 (Qx or Jxx)
dStop = (hcp(north,diamonds) + nD)>3
hStop = (hcp(north,hearts)   + nH)>3
sStop = (hcp(north,spades)   + nS)>3
stops = (dStop + hStop + sStop)

nBal  = shape(north, any 4432 +any 4333)

## 2. Club support

r3x  = clubs(north)>4 and shape(north,any 0xxx +any 1xxx) and nTP>13  and not rM //splinter
r2Ca = nBal and clubs(north)==4 and hcp(north)>10 and stops<3         and not rM
r2Cb = clubs(north)>4 and nD<5  and hcp(north)>9                      and not (rM or r3x)
r2C  = r2Ca or r2Cb
r3C  = clubs(north)>4 and nD<5  and hcp(north)<10 and nTP>4           and not (rM or r3x)
rC   = r2C or r3C or r3x

## 3. Notrump

r1N = nBal and hcp(north)>5  and hcp(north)<11              and not (rM or rC)
r2N = nBal and hcp(north)>10 and hcp(north)<13 and stops==3 and not (rM or rC)
r3N = nBal and hcp(north)>12 and hcp(north)<16 and stops==3 and not (rM or rC)
rN = r1N or r2N or r3N

## 4. Diamonds
r1D = nD>3 and not (rM or rC or rN)

// Change the following line to get the responses you want --------

nResp = r1D or r1H or r1S or r1N or r2C or r2N or r3C or r3x or r3N
 
// ----------------------------------------------------------------

oneClub and sClubs and sRange and nRange and nResp and calmOpps

action
average "CD " 100 * r1D,
average "CH " 100 * r1H,
average "CS " 100 * r1S,
average "CN " 100 * r1N,
average "2C " 100 * r2C,
average "2N " 100 * r2N,
average "3C " 100 * r3C,
average "3x " 100 * r3x,
average "3N " 100 * r3N,

`, "S", false)
Script

Button,GIB after 1!C,\n\
--- GIB responses to 1C\n\
South opens 1!C and North responds w/anything other than Soloway JS.\n\
This does not use 'Randomly rotate' -- go to 'Deal source' to change it.\n\
You may also go to 'Deal source' to get specific responses. Near the bottom\n\
you'll find the line that starts with nResponds = ... pick the response(s) you want.\n\
%GibRespTo1C%
Script,GibRespTo1C
setDealerCode(`

/*
South opens 1C and North responds 1D, 1H, 1S, 1N, 2C, 2N, 3C, 3D, 3H, 3S, or 3N.

convention-card: 21GF-GIB
auction-filter: Auction.....\n1C .*Pass .*(1[DHS]|1NT?|2C|2NT?|3[CDHS]|3NT?) +

*/

dealer south

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Calm-Opponents
# Defines calmOpps
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Predict-Opening-1-Bid
# Defines oneClub
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Total-Points-South
# Defines sTP
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Total-Points-North
# Defines nTP

TakeOutDouble = (balWest and hcp(west)>10 and shape(west,xxx2)) or (balEast and hcp(east)>10 and shape(east,xxx2))  // Must know opening suit
oppsPass = calmOpps and not TakeOutDouble

# South bids 1C and North responds
sRange = hcp(south)>11 and hcp(south)<22 and sTP>11 and sTP<23
sClubs = oneClub and spades(south)<5 and hearts(south)<5

nRange = nTP>5 and nTP<16    // avoid strong jump shifts

# Define responses to 1C
nD = diamonds(north)
nH = hearts(north)
nS = spades(north)

## 1. Major suit opening

rS1 = nS>4 and nS>=nH                       // higher ranking of 5+card suits
rS2 = nTP<13 and nS==4 and nS>nH            // skip longer D w/<13
rS3 = shape(north,4xxx) and nS>nH and nD<5  // prefer 4-card H/S to 4-card D
r1S = rS1 or rS2 or rS3

rH1 = nH>4 and nH>nS
rH2 = nTP<13 and nH==4 and nH>=nS
rH3 = shape(north,x4xx) and nD<5  
r1H = (rH1 or rH2 or rH3) and not r1S

rM = r1H or r1S

## 2. Club support

# GIB defines partial stop: length+HCP = 4 (Qx or Jxx).  But, it bids differently with 3-cards or honor doubleton
dStop = (hcp(north,diamonds) + nD)>3 ? 2 : 0      // Dealer doesn't allow fractions; so 2 pts for stop
hStop = (hcp(north,hearts)   + nH)>3 ? 2 : 0
sStop = (hcp(north,spades)   + nS)>3 ? 2 : 0

dHalf = not dStop and ((hcp(north,diamonds)>0) + nD)==3   // and 1 pt for half-stop: any 3-cards or honor doubleton
hHalf = not hStop and ((hcp(north,hearts)>0)   + nH)==3
sHalf = not sStop and ((hcp(north,spades)>0)   + nS)==3

stops = dStop + hStop + sStop + dHalf + hHalf +sHalf

# I've exceeded the number or 'shape' statements that Dealer allows.
s3334 = spades(north)==3 and hearts(north)==3 and diamonds(north)==3
s3343 = spades(north)==3 and hearts(north)==3 and diamonds(north)==4
sxx44 = (spades(north)==3 or spades(north)==2) and diamonds(north)==4 and clubs(north)==4

r2C34 = s3334       and hcp(north)>10 and hcp(north)<13 and stops<5 and not rM
r2C44 = sxx44       and hcp(north)>10 and hcp(north)<13 and stops<4 and not rM
r2C3N = s3334+sxx44 and hcp(north)>12 and hcp(north)<16             and not rM          // GIB goes through 2C w/a bal 13-15
r3x   = clubs(north)>4 and shape(north,any 0xxx +any 1xxx) and nTP>13               and not rM          // splinter
r2Cx5 = clubs(north)>4 and nD<5  and hcp(north)>9                                   and not (rM or r3x)
r2C   = r2C34 or r2C44 or r2C3N or r2Cx5
r3C   = clubs(north)>4 and nD<5  and hcp(north)<10 and nTP>4                        and not (rM or r3x)
rC    = r2C34 or r2C44 or r2C3N or r2Cx5 or r3C or r3x

## 3. Notrump

// nBal  = shape(north, any 4432 +any 4333) and not (rM or rC)
nBal = (s3334 or s3343 or sxx44) and not (rM or rC) 

r1N  = nBal and hcp(north)>5  and hcp(north)<11
r2Na = nBal and hcp(north)>10 and hcp(north)<13 and s3334 and stops>4  // no alternative
r2Nb = nBal and hcp(north)>10 and hcp(north)<13 and s3343 and stops>5  // prefer 1D
r2Nc = nBal and hcp(north)>10 and hcp(north)<13 and sxx44 and stops>3  // prefer 1D or 2C
r2N = r2Na or r2Nb or r2Nc
r3N  = nBal and hcp(north)>12 and hcp(north)<16 and stops==6
rN  = r1N or r2N or r3N

## 4. Diamonds

r1D = nD>3 and not (rM or rN or rC)

## 5. Check for leaks

rOther  = not (r1D or rM or rN or rC)

// Change the following line to get the responses you want --------
//
nResponds = r1D or r1H or r1S or r1N or r2C or r2N or r3C or r3x or r3N or rOther
//
// ----------------------------------------------------------------

sClubs and sRange and nRange and nResponds and oppsPass

action
average "CD   " 100 * r1D,
average "CH   " 100 * r1H,
average "CS   " 100 * r1S,
average "CN   " 100 * r1N,
average "2C   " 100 * r2C,
average "2N   " 100 * r2N,
average "3C   " 100 * r3C,
average "3x   " 100 * r3x,
average "3N   " 100 * r3N,
average "other" 100 * rOther,

`, "S", false)
Script

Button,GIB after 1C,\n\
--- GIB responses to 1C\n\
South opens 1!C and North responds w/anything other than Soloway JS.\n\
This does not use 'Randomly rotate' -- go to 'Deal source' to change it.\n\
You may also go to 'Deal source' to get specific responses. Near the bottom\n\
you'll find the line that starts with nResponds = ... pick the response(s) you want.\n\
%GibRespTo1C%
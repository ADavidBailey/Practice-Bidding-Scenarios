Script,MaximalDouble
setDealerCode(`

# Add 1M - 3M inviting, add Maximal Doubles, kill Reverse Bergen, Kill Support 1N, kill Two-way game tries
# convention-card: 21GF-MaximalDouble
# auction-filter: 1H .* 2H 3D|1S .* 2S 3H

# Maximal Overcall Double
dealer north

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/GIB-1N
# Defines gibNT
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Total-Points-South
# Defines sTP

# North opens 1M
S_ = spades(north)>4 and hearts(north)<5 and diamonds(north)<=5 and clubs(north)<=5
H_ = hearts(north)>4 and spades(north)<5 and diamonds(north)<=5 and clubs(north)<=5
n1M = (S_ or H_) and hcp(north)>11 and hcp(north)<18 and losers(north)<8 and not gibNT

nLP = S_ ? spades(north) - 4 : hearts(north) - 4
nTP = hcp(north) + nLP

# Define East overcalls in Maximal suit
e2D = H_ and diamonds(east)>5 and top3(east,diamonds)>1 and spades(east)<5
e2H = S_ and hearts(east)>5   and top3(east,hearts)>1
eShape = shape(east,any 7xxx+ any 6xxx - any 76xx - any 75xx - any 65xx)
eRange = hcp(east)>10 and hcp(east)<14 and controls(east)>2
eMaxOC = (e2D or e2H) and eRange and eShape

# avoid other overcalls
eWJS    = shape(east,any 9xxx +any 8xxx +any 7xxx +any 6xxx) and hcp(east)<11
e2suits = shape(east,any 85xx +any 76xx + any 75xx +any 65xx +any 55xx)
HS = spades(east)>4 and hcp(east)>5
HC = clubs(east)>4 and hcp(east)>9
SC = spades(east)>4 and hcp(east)>9
SD = diamonds(east)>4 and hcp(east)>9
ePass = not (eWJS or e2suits or HS or HC or SC or SD)

# Define East Takeout Double
eHCP = hcp(east,spades)<7 and hcp(east,hearts)<7 and hcp(east,diamonds)<7 and hcp(east,clubs)<7  // no concentration
eHX = H_ and hearts(east)<2 and shape(east,any 5440 +any 5431 +any 4441 -5xxx) and hcp(east,hearts)<2 and spades(west)<4
eSX = S_ and spades(east)<2 and shape(east,any 5440 +any 5431 +any 4441 -x5xx) and hcp(east,spades)<2 and hearts(west)<4
eX = (eHX or eSX) and eHCP and hcp(east)>10 and hcp(east)<15 and not eMaxOC

# Define East bids
eBids = ePass or eX or eMaxOC

# Define South's major suit raises with 3/4-cards
spadeFit = S_ and shape(south,3xxx+4xxx) and hcp(south,spades)>1 and (controls(south)-controls(south,spades)==1)
heartFit = H_ and shape(south,x3xx+x4xx) and hcp(south,hearts)>1 and (controls(south)-controls(south,hearts)==1) and spades(south)<4
sRaise = (spadeFit or heartFit) and sTP>5 and sTP<10 and hcp(south)<10 and shape(south,xxxx -any 4333)

# Define West's raise of Maximal overcall
wHR = e2H and shape(west,x3xx+x4xx) and hcp(west,hearts)>1
wDR = e2D and shape(west,xx3x+xx4x) and hcp(west,diamonds)>1
wRaise = (wHR or wDR) and hcp(west)>6 and hcp(west)<11

# Define West's response to X
wD = heartFit and diamonds(west)>4
wH = spadeFit and hearts(west)>4
wRespToX = eX and (wD or wH) and shape(west,xxxx-any 76xx -any 75xx -any 65xx -any 55xx) and hcp(west)>6 and hcp(west)<11

# Define West's free bid of Maximal suit.  //Include 2 of opener's suit to avoid X
wGD = heartFit and top4(west,diamonds)>2  and shape(west,xx6x)
wGH = spadeFit and top4(west,hearts)>2    and shape(west,x6xx)
wFreeBid = ePass and (wGD or wGH) and shape(west,any 0xxx +any 1xxx -any 76xx -any 75xx -any 66xx -any 65xx) and hcp(west)>10 and hcp(west)<14

# Define West's 3 of the Maximal suit
w3Max = (wRaise or wRespToX or wFreeBid)

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Leveling

nMin = nTP<15
nMax = nTP>14

levMax = nMax and keep
levMin = nMin and keep

levelTheDeal = (levMax or levMin)

# Now do it
n1M and eBids and sRaise and w3Max
and levelTheDeal

produce 5000
action
average "south TP       " sTP,
average "north TP       " nTP,
average "raise          " 100 * wRaise,
average "response to X  " 100 * wRespToX,
average "free bid       " 100 * wFreeBid,
average "north 12-14 TP " 100 * nMin,
average "north 15+   TP " 100 * nMax,

`, "N", true)
Script

Button,Maximal Double,\n\
--- Maximal Overcall Double:  After we've bid and raised a major suit\n\
and they compete in the suit just under ours (the Maximal suit) we need\n\
to distinguish between a competitive raise and an invitational raise.\n\
The Maximal Double is the answer:\n\
  • After 1!H (?) 2!H (3!D)，3!H is competitive and X is invitational\n\
  • After 1!S (?) 2!S (3!H)，3!S is competitive and X is invitational\n\
The BBO Robots do NOT play this.\n\
https://www.nvba.org/novices/Robert_Todd/(195)+Maximal+Doubles.pdf\n\
%MaximalDouble%,backgroundColor=lightpink

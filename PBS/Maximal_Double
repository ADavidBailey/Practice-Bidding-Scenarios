Script,MaximalDouble
setDealerCode(`

# Add 1M - 3M inviting, add Maximal Doubles, kill Reverse Bergen, Kill Support 1N, kill Two-way game tries
# convention-card: 21GF-MaximalDouble
# auction-filter: Auction.....\\n((1H .* 2H 3D|1S .* 2S 3H))

# Maximal Overcall Double
dealer north

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/GIB-1N
# Defines gibNT

# Restrict shapes
shapeN = shape(north,any 2xxx+any 1xxx+any 0xxx-any 85xx-any 75xx-any 65xx-any 55xx)
shapeE = shape(east,xxxx-any 9xxx-any 8xxx-any 7xxx-any 6xxx-any 55xx)
shapeS = shape(south,any 2xxx+any 1xxx+any 0xxx-any 6xxx-any 55xx)
shapeW = shape(west,xxxx-any 85xx-any 75xx-any 65xx-any 55xx)
shapes = shapeN and shapeE and shapeS and shapeW

# North opens 1M
S_ = spades(north)>4
H_ = hearts(north)>4
n1M = (S_ or H_) and hcp(north)>11 and hcp(north)<20 and losers(north)<8 and not gibNT

# East overcalls any suit or passes
eGC = clubs(east)>4    and top5(east,clubs)>2
eGD = diamonds(east)>4 and top5(east,diamonds)>2
eGH = hearts(east)>4   and top5(east,hearts)>2
eGS = spades(east)>4   and top5(east,spades)>2 // restrict 5-cards to avoid WJO

# Define East overcalls
e1S = H_ and eGS and hcp(east)>8
e2C =        eGC and hcp(east)>10
e2D =        eGD and hcp(east)>10
e2H = S_ and eGH and hcp(east)>10

# Define East Takeout Double -- we'll prevent this
H_X = H_ and shape(east,any 6430+any 6331+any 5440+any 5431+any 4441+x0xx+x1xx)
S_X = S_ and shape(east,any 6430+any 6331+any 5440+any 5431+any 4441+0xxx+1xxx)
eX = (H_X or S_X) and hcp(east)>9

# Define East Pass
eP = not (e1S or e2C or e2D or e2H or eX)

# Define East Bids
eBids = (eP or e1S or e2C or e2D or e2H) and not eX

# calculate South's dummy points
d0  = shape(south,any 0xxx) ? 3 : 0
d1  = shape(south,any 1xxx) ? 2 : 0
d2a = shape(south,any 2xxx) ? 1 : 0
d2b = shape(south,any 22xx) ? 1 : 0
dp = d1+d2a+d2b+hcp(south)

# A friend plays losing trick count (with Jade Barret) 1M - 2M shows a hand that would accept at least one game try. Thus,
# needs 2-Cover cards.  With less, they respond 1N and rebid 2M.  With more, they bid 3M with 4 or 1N and rebid 3M with 3.

# Define South's major suit raises
spadeFit = S_ and spades(south)>2 and hcp(south,spades)>1 and hcp(south)>4 and dp<10 and (controls(south)-controls(south,spades)==1)
heartFit = H_ and hearts(south)>2 and hcp(south,hearts)>1 and hcp(south)>4 and dp<10 and (controls(south)-controls(south,hearts)==1)
sRaise = (spadeFit or heartFit)

# Define West's fit for Maximal suit and tolerance for non-maximal suit
wS = e1S and spades(west)==2
wH = S_ ? e2H and hearts(west)>2   and hcp(west,hearts)>1   : e2H and hearts(west)==2
wD = H_ ? e2D and diamonds(west)>2 and hcp(west,diamonds)>1 : e2D and diamonds(west)==2
wC = e2C and clubs(west)==2

# West bids 3 of the maximal suit // stats 22 raise, 2 new suit, 114 free bid
maximalRaise = ((spadeFit and wH) or (heartFit and wD)) and hcp(west)>6
maximalNewSuit = ((spadeFit and (wC or wD) and (hearts(west)>4)) or ((heartFit and (wS or wC)) and diamonds(west)>4)) and hcp(west)>9
maximalFreeBid = eP and ((spadeFit and hearts(west)>6) or (heartFit and diamonds(west)>6)) and hcp(west)>8 and shape(west,any 0xxx+any 1xxx)


w3Max = (maximalRaise or maximalNewSuit or maximalFreeBid) and shapes and not eX

# Now do it
n1M and eBids and sRaise and w3Max

produce 5000
action
average "hcp(north)   " hcp(north),
average "hcp(south)   " hcp(south),
average "dummy points " dp,
average "hcp(east)    " hcp(east),
average "hcp(west)    " hcp(west),
average "N 3M         " 100 * (hcp(north)<15),
average "N X          " 100 * (hcp(north)>14 and hcp(north)<18),
average "N 4M         " 100 * (hcp(north)>17),
average "N losers<6   " 100 * (losers(north)<6),
average "N losers>5   " 100 * (losers(north)>5),
average "Max Raise    " 100 * maximalRaise,
average "Max New Suit " 100 * maximalNewSuit,
average "Max Free Bid " 100 * maximalFreeBid,

`, "N", true)
Script

Button,Maximal Double,\n\
--- Maximal Overcall Double:  After we've bid and raised a major suit\n\
and they compete in the suit just under ours (the Maximal suit) we need\n\
to distinguish between a competitive raise and an invitational raise.\n\
The Maximal Double is the answer:\n\
  • After 1!H (?) 2!H (3!D)，3!H is competitive and X is invitational\n\
  • After 1!S (?) 2!S (3!H)，3!S is competitive and X is invitational\n\
The BBO Robots do NOT play this.\n\
https://www.nvba.org/novices/Robert_Todd/(195)+Maximal+Doubles.pdf\n\
%MaximalDouble%,backgroundColor=lightpink

Script,MaximalDouble
setDealerCode(`

# Kill 1M - 3M blocking, kill Reverse Bergen, Add Maximal Doubles
# convention-card: 21GF-MaximalDouble
# auction-filter: Auction.....\\n((1H .* 2H 3D|1S .* 2S 3H))


# Maximal Double
dealer north

# convention-card: 21GF-MaximalDouble

# North opens 1 Major and short in the suit below (the maximal suit)
S_ = spades(north)>4 and hearts(north)<2   and spades(north)>=diamonds(north) and spades(north)>=clubs(north)
H_ = hearts(north)>4 and diamonds(north)<2 and hearts(north)>spades(north)    and hearts(north)>=clubs(north)
n1M = (S_ or H_) and hcp(north)>12 and hcp(north)<16   // was 14-15 and had more 4-bids than X

# East overcalls any suit, makes a takeout double, or passes

eGS = spades(east)>4   and hcp(east,spades)>4
eGC = clubs(east)>4    and hcp(east,clubs)>4    and clubs(east)>spades(east)    and clubs(east)>hearts(east)    and clubs(east)>diamonds(east) 
eGD = diamonds(east)>4 and hcp(east,diamonds)>4 and diamonds(east)>spades(east) and diamonds(east)>hearts(east) and diamonds(east)>clubs(east)
eGH = hearts(east)>4   and hcp(east,hearts)>4   and hearts(east)>spades(east)   and hearts(east)>diamonds(east) and hearts(east)>clubs(east)

e1S = H_ and eGS and hcp(east)>7
e2C =        eGC and hcp(east)>10 and not e1S
e2D =        eGD and hcp(east)>10 and not (e1S or e2C)
e2H = S_ and eGH and hcp(east)>10 and not (e1S or e2C or e2D)

H_X = H_ and shape(east,4054+4045+4144+4135+4153)
S_X = S_ and shape(east,0454+0445+1444+1435+1453)
eX = (H_X or S_X) and hcp(east)>12

eP = not (e1S or e2C or e2D or e2H or eX)

eActs = (eP or eX or e1S or e2C or e2D or e2H) and hcp(east)<16 and shape(east,xxxx-any 8xxx-any 7xxx-any 65xx)

# South raises with a fit
spadeFit = S_ and spades(south)>2 and spades(south)<5 and hcp(south,spades)>1
heartFit = H_ and hearts(south)>2 and hearts(south)<5 and hcp(south,hearts)>1
sRaise = (spadeFit or heartFit) and hcp(south)>4 and hcp(south)<11 and shape(south,any 4432+any 5431+any 5422)

# West bids 3 of the maximal suit
maximalRaise = ((S_ and e2H and hearts(west)>3) or (H_ and e2D and diamonds(west)>3)) and hcp(west)>6
maximalAfterX = ((S_X and hearts(west)>4) or (H_X and diamonds(west)>4)) and hcp(west)>6
maximalFreeBid = ((S_ and hearts(west)>6) or (H_ and diamonds(west)>6)) and hcp(west)>10
w3Max = maximalRaise or maximalAfterX or maximalFreeBid

# Now do it
n1M and eActs and sRaise and w3Max

produce 5000
action
average "hcp(north)   " hcp(north),
average "hcp(south)   " hcp(south),
average "hcp(east)    " hcp(east),
average "N 3M         " 100 * (hcp(north)<15),
average "N X          " 100 * (hcp(north)>14),
average "N losers<6   " 100 * (losers(north)<6),
average "N losers>5   " 100 * (losers(north)>5),

`, "N", true)
Script

Button,Maximal Double,\n\
--- Maximal Overcall Double after we've bid and raised a major suit...\n\
  • After 1!H (?) 2!H (3!D)\n\
  • After 1!S (?) 2!S (3!H)\n\
and they've competed in the suit just under ours，we have an issue.\n\
How do we need destingusih between a competitive raise and an\n\
invitational raise -- the Maximal Overcall Double is invitational\n\
and bidding our suit is competitive.  The BBO Robots do NOT play this.\n\
https://bridgebum.com/maximal_double.php\n\
%MaximalDouble%,backgroundColor=lightpink
Script,MichaelsCuebid
setDealerCode(`

/*
Michaels Cuebid

auction-filter: Auction.....\n(1C +2C|1D +2D|1H +2H|1S +2S)
*/

dealer east

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/TP
# Defines tpWest, tpSouth

##### Imported Script -- Predict Opening 1-Bid #####  Rotated from South to East

# Changed to use gibNT 3/30/2025

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(east) + shape(east,5xxx+x5xx)
nt1 = shape(east, any 5332+any 4432+any 4333) and hcp(east)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(east, any 5422 -5xxx-x5xx-4xxx) and hcp(east)>14 and hcp(east)<17

gibNT = nt1 or nt2

# Calculate length points for South (lengthPoints)
lp1 = spades(east)>4 ? spades(east)-4 : 0
lp2 = hearts(east)>4 ? hearts(east)-4 : 0
lp3 = diamonds(east)>4 ? diamonds(east)-4 : 0
lp4 = clubs(east)>4 ? clubs(east)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for east (suitPoints)
suitPoints = hcp(east) + lengthPoints

TwoNtShape = shape(east, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(east)>19 and hcp(east)<22
threeNT = TwoNtShape and hcp(east)>24 and hcp(east)<28

# Define Game Forcing 2C
case1 = hcp(east)>21
case2 = hcp(east)>18 and losers(east)<5 and (spades(east)>5 or hearts(east)>5)
case3 = hcp(east)>18 and losers(east)<4 and (diamonds(east)>5 or clubs(east)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict East's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT

# Predict East's Opening suit
s = spades(east)
h = hearts(east)
d = diamonds(east)
c = clubs(east)
s1Range = suitPoints>11 and hcp(east)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####

# Define South's suits for Michaels Cuebid
sS = top5(south,spades)>2   and spades(south)>4
sH = top5(south,hearts)>2   and hearts(south)>4
sD = top5(south,diamonds)>2 and diamonds(south)>4
sC = top5(south,clubs)>2    and clubs(south)>4

# Define East's opening bids
eOC = oC and sH and sS
eOD = oD and sH and sS
eOH = oH and (sS and (sC or sD))
eOS = oS and (sH and (sC or sD))
eOpens = (eOC or eOD or eOH or eOS) and hcp(east)>11

# Define ranges for West's continuations
force   = tpWest>=13
invite  = tpWest>=10 and not force
compete = tpWest>=7  and not (force or invite)
preempt = tpWest>=4  and not (force or invite or compete)  
wRange  = force or invite or compete or preempt

sMCB= shape(south,any 76xx +any 67xx +any 66xx +any 65xx +any 56xx +any 55xx) and hcp(south)>9 and tpSouth<19 // avoid strong TO X

levPreempt = preempt and hascard(north,2C) or hascard(north,2D) // keep only 44% of preempts

eOpens and sMCB and wRange
and (force or invite or compete or levPreempt)

action
average "TP >=13 " 100 * force,
average "TP >=10 " 100 * invite,
average "TP >= 7 " 100 * compete,
average "TP >= 4 " 100 * preempt,

`,"S",false)
Script
Button,Opps Michaels Cuebid,\n\
---  Opponents Michaels Cuebid.\n\
Two-suited OR a good hand.\n\
  • 1!C - 2!C = 5+!H & 5+!S\n\
  • 1!D - 2!D = 5+!H & 5+!S\n\
  • 1!H - 2!H = 5+!S & 5+(!C or !D)\n\
  • 1!S - 2!S = 5+!H & 5+(!C or !D)\n\
https://www.bridgebum.com/michaels_cuebid.php\n\
%MichaelsCuebid%
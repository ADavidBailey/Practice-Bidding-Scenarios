Script,GerberAfterNotrump
setDealerCode(`
#
# Gerber after Notrump
#
produce 1000
dealer south
# Define the cases
nt1517 = hcp(south)>14 and hcp(south)<18 and hcp(north)>16
nt1819 = hcp(south)>17 and hcp(south)<20 and hcp(north)>14
nt2021 = hcp(south)>19 and hcp(south)<22 and hcp(north)>11
nt2224 = hcp(south)>21 and hcp(south)<25 and hcp(north)>9
nt2527 = hcp(south)>24 and hcp(south)<28 and hcp(north)>6
nt28 = hcp(south)>27                     and hcp(north)>3
# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
p = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and p<18
# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, 4252+4225+2452+2425+2254+2245) and hcp(south)>14 and hcp(south)<17
GIB1N = nt1 or nt2
anyNT = GIB1N or (shape(south, any 5332+any 4432+any 4333) and hcp(south)>17)
# Responder is unbalanced with a good minor suit
gD = diamonds(north)>4 and top3(north,diamonds)>1
gC = clubs(north)>4    and top3(north,clubs)>1
respUnbal = gD or gC
suitLength = clubs(north) >= diamonds(north) ? clubs(north) : diamonds(north)

#Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Leveling
### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)
keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions
keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06
keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###
# Level the deal
level1 = nt1517 and keep03
level2 = nt1819 and keep11
level3 = nt2021 and keep06
level4 = nt2224 and keep11
level5 = nt2527 and keep89
level6 = nt28   and keep
levelTheDeal = level1 or level2 or level3 or level4 or level5 or level6
# Now do it
anyNT and respUnbal
and levelTheDeal
action
average "nt 15-17   " 100 * nt1517,
average "nt 18-19   " 100 * nt1819,
average "nt 20-21   " 100 * nt2021,
average "nt 22-24   " 100 * nt2224,
average "nt 25-27   " 100 * nt2527,
average "nt 28+     " 100 * nt28,
average "c/d length " suitLength,

`,"S",true)
//Script
Button,Gerber after Notrump (Lev),\n\
--- Gerber after Notrump.\n\
https://www.bridgebum.com/gerber.php\n\
%GerberAfterNotrump%

Script,GibRespTo1C
setDealerCode(`

/*
South opens 1C and North responds 1D, 1H, 1S, 1N, 2C, 2N, 3C, 3D, 3H, 3S, or 3N.

convention-card: 21GF-GIB
auction-filter: Auction.....\n1C +Pass +(1[DHS]|[1-3]NT?|[2-3]C|3[CDHS]) +

*/

dealer south

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Calm-Opponents
# Defines calmOpps
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Predict-Opening-1-Bid
# Defines oneClub
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/TP
# Defines tpSouth, tpWest, tpNorth

# GIB does not open some 12 HCP hands
# Define suits that are OK for opening
top6C = top5(south,clubs) + hascard(south,9C)
cAT  = hascard(south,AC) and top5(south,clubs)>1            // AT or better
cKJ  = hascard(south,KC) and top4(south,clubs)>1            // KJ or better
cQJ9 = hascard(south,QC) and hascard(south,JC) and top6C>2  // QJ9 or better
cOK = cAT or cKJ or cQJ9

top6D = top5(south,diamonds) + hascard(south,9D)
dAT  = hascard(south,AD) and top5(south,diamonds)>1
dKJ  = hascard(south,KD) and top4(south,diamonds)>1
dQJ9 = hascard(south,QD) and hascard(south,JD) and top6D>2
dOK = dAT or dKJ or dQJ9

okToOpen = shape(south,1345 + 3145) and hcp(south)==12 ? cOK and dOK : 1

# Define suits suitable for 4-card overcall
wAS = hascard(west,AS)
wKS = hascard(west,KS)
wQS = hascard(west,QS)
wJS = hascard(west,JS)
wTS = hascard(west,TS)
w9S = hascard(west,9S)

wAH = hascard(west,AH)
wKH = hascard(west,KH)
wQH = hascard(west,QH)
wJH = hascard(west,JH)
wTH = hascard(west,TH)
w9H = hascard(west,9H)

wAD = hascard(west,AD)
wKD = hascard(west,KD)
wQD = hascard(west,QD)
wJD = hascard(west,JD)
wTD = hascard(west,TD)
w9D = hascard(west,9D)

# Define West's possible overcall suits: AKxx，AQJx，AQTx，AQ9x，AJT9，KQJx，or KQTx
wS4 = ((wAS and wKS) or (wAS and wQS and (wJS or wTS or w9S)) or (wAS and wJS and wTS and w9S) or (wKS and wQS and (wJS or wTS))) and spades(west)==4
wH4 = ((wAH and wKH) or (wAH and wQH and (wJH or wTH or w9H)) or (wAH and wJH and wTH and w9H) or (wKH and wQH and (wJH or wTH))) and hearts(west)==4
wD4 = ((wAD and wKD) or (wAD and wQD and (wJD or wTD or w9D)) or (wAD and wJD and wTD and w9D) or (wKD and wQD and (wJD or wTD))) and diamonds(west)==4
wss = (wS4 and wH4) or (wS4 and wD4) or (wH4 or wD4) 

# Avoid Takeout Double -- 13/14 HCP and equal length in the majors
X = (hcp(west)==13 or hcp(west)==14) and spades(west)==hearts(west)==(3 or 4)  // I'm out of 'shape' statements
reverse = shape(south,45xx+x45x+xx45) and hcp(south)>16

wRange = tpWest>=12 and tpWest<=14 and not X

# Define West's overcalls
cOC = oneClub    and (wD4 or wH4 or wS4) and clubs(west)==3    and okToOpen
dOC = oneDiamond and (wH4 or wS4)        and diamonds(west)==3
hOC = oneHeart   and wS4                 and hearts(west)==3
wOvercalls = (cOC or dOC or hOC) and wRange and shape(west,any 4432 +any 4333)


TakeOutDouble = (balWest and hcp(west)>10 and clubs(west)==2) or (balEast and hcp(east)>10 and clubs(east)==2)  // Must know opening suit
oppsPass = calmOpps and not TakeOutDouble

## Define things...

# Define North's suit lengths
nC = clubs(north)
nD = diamonds(north)
nH = hearts(north)
nS = spades(north)

# Define North's rebiddable suit
nRS = nS>5 or (nS==5 and top5(north,spades)>2)
nRH = nH>5 or (nH==5 and top5(north,hearts)>2)
nRD = nD>5 or (nD==5 and top5(north,diamonds)>2)
nRC = nC>5 or (nC==5 and top5(north,clubs)>2)
nRebiddableSuit = nRS or nRH or nRD or nRC

# GIB defines partial stop: length+HCP = 4 (Qx or Jxx).  But, it bids differently with 3-cards or honor doubleton
dStop = (hcp(north,diamonds) + nD)>3 ? 2 : 0      // Dealer doesn't allow fractions; so 2 pts for stop
hStop = (hcp(north,hearts)   + nH)>3 ? 2 : 0
sStop = (hcp(north,spades)   + nS)>3 ? 2 : 0

dHalf = not dStop and ((hcp(north,diamonds)>0) + nD)==3   // and 1 pt for half-stop: any 3-cards or honor doubleton
hHalf = not hStop and ((hcp(north,hearts)>0)   + nH)==3
sHalf = not sStop and ((hcp(north,spades)>0)   + nS)==3

stops = dStop + hStop + sStop + dHalf + hHalf +sHalf

# I've exceeded the number or 'shape' statements that Dealer allows, I'll use the less efficient version.
s3334 = nS==3 and nH==3  and nD==3
s3343 = nS==3 and nH==3  and nD==4
sxx44 = (nS==3 or nS==2) and nD==4 and nC==4  // 3244 or 2344
nBal  = (s3334 or s3343 or sxx44)  // no 4-card major

## Define South's 1C opening and North's responses

sRange = hcp(south)>11 and hcp(south)<22 and tpSouth>11 and tpSouth<23  // avoid strong 2C
sClubs = oneClub and okToOpen
nRange = nRebiddableSuit ? tpNorth>5 and tpNorth<17 : tpNorth>5   // no upper range without rebiddable suit

## 1. Major responses

rS1 = nS>4 and nS>=nH                   // higher ranking of 5+card suits
rS2 = tpNorth<13 and nS==4 and nS>nH        // skip longer D w/<13
rS3 = nS==4 and nS>nH and nD<5          // prefer 4-card H/S to 4-card D
r1S = rS1 or rS2 or rS3

rH1 = nH>4 and nH>nS
rH2 = tpNorth<13 and nH==4 and nH>=nS
rH3 = nH==4 and nD<5  
r1H = (rH1 or rH2 or rH3) and not r1S

rM = r1H or r1S

## 2. Club responses

r3x   = nC>4 and shape(north,any 0xxx +any 1xxx) and tpNorth>13         and not rM          // splinter
r2C34 = s3334       and hcp(north)>9  and hcp(north)<13 and stops<5 and not rM
r2C44 = sxx44       and hcp(north)>9  and hcp(north)<13 and stops<4 and not rM
r2C3N = s3334+sxx44 and hcp(north)>12 and hcp(north)<16             and not rM          // GIB goes through 2C w/a bal 13-15
r2Cx5 = nC>4        and nD<5 and hcp(north)>9                       and not (rM or r3x)
r2C = r2C34 or r2C44 or r2C3N or r2Cx5
r3C = nC>4 and nD<5 and hcp(north)<10 and tpNorth>4                     and not (rM or r3x)

rC  = r2C34 or r2C44 or r2C3N or r2Cx5 or r3C or r3x

## 3. Notrump responses

r1N  = hcp(north)>5  and hcp(north)<11 and nBal and not rC

r2Na = hcp(north)>10 and hcp(north)<13 and sxx44 and stops>3 and hcp(north,diamonds)>0 and not rC // prefer 1D or 2C
r2Nb = hcp(north)>10 and hcp(north)<13 and s3334 and stops>4                           and not rC // no alternative
r2Nc = hcp(north)>10 and hcp(north)<13 and s3343 and stops>5                           and not rC // prefer 1D
r2N = r2Na or r2Nb or r2Nc

r3N = s3343 and hcp(north)>12 and hcp(north)<16  and nBal  and stops>5                 and not rC

rN  = r1N or r2N or r3N

## 4. Diamonds

r1D = nD>3 and not (rM or rN or rC)

// Change the following line to get the responses you want --------
//
nResponds = r1D or r1H or r1S or r1N or r2C or r2N or r3C or r3x or r3N
//
// ----------------------------------------------------------------

sClubs and sRange and not wOvercalls and nRange and nResponds and oppsPass

action
average "1D   " 100 * r1D,
average "1H   " 100 * r1H,
average "1S   " 100 * r1S,
average "1N   " 100 * r1N,
average "2C   " 100 * r2C,
average "2N   " 100 * r2N,
average "3C   " 100 * r3C,
average "3x   " 100 * r3x,
average "3N   " 100 * r3N,

`, "S", false)
Script

Button,GIB after 1C,\n\
--- GIB responses to 1!C\n\
South opens 1!C and North responds w/anything other than Soloway JS.\n\
This does not use 'Randomly rotate' -- go to 'Deal source' to change it.\n\
You may also go to 'Deal source' to get specific responses. Near the bottom\n\
you'll find the line that starts with nResponds = ... pick the response(s) you want.\n\
%GibRespTo1C%
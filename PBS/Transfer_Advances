Script,TransferAdvances
setDealerCode(`


#Nagyiván Gábor
#Hi David, I would like to try a simulation (500 deals) with the following parameters:
#1. West = 1. suit opening (natural, standard, 11-14 HCP, max 6 cards) 

#2. North = Suit overcall without jump, 5-6 cards (NOT weak 2, NOT 7+ cards and NOT like Michaels and Unusual no trump). 
#If 1D, then 10-16 HCP, if 1H or 1S, then 8-16 HCP, over West 1S, then 2H =11-16. If 2C or 2D overcall =11-16 and 6 cards, 
#or 13-16 and 5-6 cards. Suit quality is at least 2 of top 5.   

#3. East = 0-5 HCP without 7+ cards and not a big suit support with West.

#4. South 6+HCP.
#Can you do it? Thank you in advance, G.


# auction-filter: Auction.....\\n((1C.1[DHS]|1D.(1[HS]|2C)|1H.(1S|2[CD])|1S.2[CDH]))

dealer west

# Predict West's opening suit
sW = spades(west)
hW = hearts(west)
dW = diamonds(west)
cW = clubs(west)
wS = sW>4 and sW>=hW and sW>=dW and sW>=cW
wH = not wS and hW>4 and hW>=dW and hW>=cW
wD = not wS and not wH and ((dW>3 and dW>=cW) or cW<3)
wC = not wS and not wH and not wD
westSuit = wS or wH or wD or wC
westRange = hcp(west)>10 and hcp(west)<15
westShape = shape(west,any 6xxx+any 5xxx-any 66xx-any 65xx-any 55xx)
westHand = westSuit and westRange and westShape

# Calculate West's Rank
wRS = wS ? 4 : 0
wRH = wH ? 3 : 0
wRD = wD ? 2 : 0
wRC = wC ? 1 : 0
westRank = wRS+wRH+wRD+wRC   // all except one are zero

# North's longest suit
sN = spades(north)
hN = hearts(north)
dN = diamonds(north)
cN = clubs(north)
nS = sN>=hN and sN>=dN and sN>=cN
nH = not nS and hN>=dN and hN>=cN
nD = not nS and not nH and dN>=cN
nC = not nS and not nH and not nD

# add suit quality
nGS = nS and top5(north,spades)>1
nGH = nH and top5(north,hearts)>1
nGD = nD and top5(north,diamonds)>1
nGC = nC and top5(north,clubs)>1
northSuit = (nGS or nGH or nGD or nGC)

# Calculate North's Rank
nRS = nS ? 4 : 0
nRH = nH ? 3 : 0
nRD = nD ? 2 : 0
nRC = nC ? 1 : 0
northRank = nRS+nRH+nRD+nRC

# Calculate North's suit length
sLength = nGS ? sN : 0
hLength = nGH ? hN : 0
dLength = nGD ? dN : 0
cLength = nGC ? cN : 0
northLength = sLength+hLength+dLength+cLength  // all except one are zero

lowLimit = northRank>westRank and northLength<6 ? 8 : 11  // avoid weak jump shifts
northRange = hcp(north)>lowLimit and hcp(north)<16

northShape = northRank>westRank ? shape(north,any 6xxx+any 5xxx-any 66xx-any 65xx-any 55xx) : shape(north,any 6xxx - any 66xx-any 65xx-any 55xx)
northHand = northSuit and northRange and northShape

eS = wS ? spades(east)<3   : spades(east)<7
eH = wH ? hearts(east)<3   : hearts(east)<7
eD = wD ? diamonds(east)<4 : diamonds(east)<7
eC = wC ? clubs(east)<4    : clubs(east)<7
eastShape = eS and eH and eD and eC and shape(east,any 6xxx+any 5xxx+any 4xxx-any 66xx-any 65xx-any 55xx)
eastHand = eastShape and hcp(east)<6

southHand = hcp(south)>5

#Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Leveling

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###

levC = wC and keep045
levD = wD and keep14
levH = wH and keep33
levS = wS and keep81
levelTheDeal = levC or levD or levH or levS

westHand and northHand and eastHand and southHand
and levelTheDeal

produce 2000
action

average "1C" 100 and wC,
average "1D" 100 and wD,
average "1H" 100 and wH,
average "1S" 100 and wS,

`,"W",false)
Script
Button,Transfer Advances,\n\
--- Transfer Advances\n\
After an opening suit bid，a non-jump overcall， and a pass by responder\n\
the advancer may...\n\
1. bid NT -- natural and non-forcing.\n\
2. make a natural and forcing new suit bid below the 2-level of the opening suit.\n\
3. make a weak raises.\n\
4. transfer into a new suit or the overcaller's suit -- starting with a cue bid of opener's suit.\n\
5. make a natural jump bid above overcaller's suit.\n\
This is leveled such the opening bid is equally divide amoung the four suits.\n\
https://bridgewinners.com/article/view/transfer-advances-of-overcalls/\n\
%TransferAdvances%
Script,SandwichNtBPH
setDealerCode(`

/*
convention-card: 21GF-GIB
auction-filter: Auction.....\nPass +1[CDH] +Pass +1[DHS]\n
*/

dealer south

##### Imported Script - Calculate Total Points for South #####

## Calculate TP for South
# Calculate South's Short-Suit Penalty Points
sSP = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
sHP = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sDP = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
sCP = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv1 = shape(south, any 0xxx) ? 3 : 0
ss1 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss2 = shape(south, any 11xx) ? 2 : 0
sd1 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd2 = shape(south, any 22xx) ? 1 : 0
sd3 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv1+ss1+ss2+sd1+sd2+sd3 -sSP-sHP-sDP-sCP

### End of Calculate TP for South ###

##### Imported Script - Calculate Total Points for West #####

## Calculate TP for West
# Calculate West's Short-Suit Penalty Points
wSP = spades(west)<3   and hcp(west,spades)>0   ? 1 : 0
wHP = hearts(west)<3   and hcp(west,hearts)>0   ? 1 : 0
wDP = diamonds(west)<3 and hcp(west,diamonds)>0 ? 1 : 0
wCP = clubs(west)<3    and hcp(west,clubs)>0    ? 1 : 0

# Calculate West's Short-Suit Points
wv1 = shape(west, any 0xxx) ? 3 : 0 // allow for 1 void
ws1 = shape(west, any 1xxx) ? 2 : 0 // allow for 2 singletons
ws2 = shape(west, any 11xx) ? 2 : 0
wd1 = shape(west, any 2xxx) ? 1 : 0 // allow for 3 doubletons
wd2 = shape(west, any 22xx) ? 1 : 0
wd3 = shape(west, any 222x) ? 1 : 0

# Calculate West's Total Points
wTP = hcp(west) + wv1+ws1+ws2+wd1+wd2+wd3 -wSP-wHP-wDP-wCP

### End of Calculate TP for West ###

##### Imported Script - Calculate Total Points for North #####

## Calculate TP for North
# Calculate Nouth's Short-Suit Penalty Points
nSP = spades(north)<3   and hcp(north,spades)>0   ? 1 : 0
nHP = hearts(north)<3   and hcp(north,hearts)>0   ? 1 : 0
nDP = diamonds(north)<3 and hcp(north,diamonds)>0 ? 1 : 0
nCP = clubs(north)<3    and hcp(north,clubs)>0    ? 1 : 0

# Calculate North's Short-Suit Points
nv1 = shape(north, any 0xxx) ? 3 : 0 // allow for 1 void
ns1 = shape(north, any 1xxx) ? 2 : 0 // allow for 2 singletons
ns2 = shape(north, any 11xx) ? 2 : 0
nd1 = shape(north, any 2xxx) ? 1 : 0 // allow for 3 doubletons
nd2 = shape(north, any 22xx) ? 1 : 0
nd3 = shape(north, any 222x) ? 1 : 0

# Calculate South's Total Points
nTP = hcp(north) + nv1+ns1+ns2+nd1+nd2+nd3 -nSP-nHP-nDP-nCP

### End of Calculate TP for North ###

# Define South's good suits -- KJTxx or QJTxxx or better
sGS = top5(south,spades)  >2 and shape(south,5xxx+6xxx)
sGH = top5(south,hearts)  >2 and shape(south,x5xx+x6xx)
sGD = top5(south,diamonds)>2 and shape(south,xx5x+xx6x)
sGC = top5(south,clubs)   >2 and shape(south,xxx5+xxx6)

# Both suits can't be of minimum quality
sGSpts = hcp(south,spades)
sGHpts = hcp(south,hearts)
sGDpts = hcp(south,diamonds)
sGCpts = hcp(south,clubs)

# Predict West's opening suit
s = spades(west)
h = hearts(west)
d = diamonds(west)
c = clubs(west)
wRange = hcp(west)>11 and wTP>11 and hcp(west)<15
oS = s>4 and s>=h and s>=d and s>=c and wRange 
oH = not oS and h>4 and h>=d and h>=c and wRange
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and wRange
oC = not (oS or oH or oD) and wRange

# Define East's responses
eD  = diamonds(east)>3 and hearts(east)<4 and spades(east)<4
eH  = (hearts(east)==4 and spades(east)<5) or (hearts(east)>4 and hearts(east)>spades(east))
eS  = spades(east)>3 and not eH

# Define East's response and South's good suits -- can't be min suit quality AND min strength
CD = oC and eD and sGH and sGS and ((sGSpts + sGHpts)>6 or sTP>10)
CH = oC and eH and sGD and sGS and ((sGSpts + sGDpts)>6 or sTP>10)
CS = oC and eS and sGD and sGH and ((sGDpts + sGHpts)>6 or sTP>10)
DH = oD and eH and sGC and sGS and ((sGCpts + sGSpts)>6 or sTP>10)
DS = oD and eS and sGC and sGH and ((sGCpts + sGHpts)>6 or sTP>10)
HS = oH and eS and sGC and sGD and hearts(east)<4  //avoid a H raise

sRange = hcp(south)<11 and sTP>9 and sTP<13  // Not a GIB opening hand

# Define possible North overcalls
nDO =  oC              and diamonds(north)==5
nHO = (oC or oD)       and hearts(north)==5
nSO = (oC or oD or oH) and spades(north)==5

# Avoid North's Preempt, Weak 2, Overcall, or Takeout Double
nLongSuit = shape(north, any 9xxx +any 8xxx +any 7xxx +any 6xxx)
nOvercall = (nDO or nHO or nSO) and hcp(north)>7 and nTP>8
nDouble   = nTP>11 and shape(north, any 4333 +any 4432 +any 4441 +any 5332 +any 54xx)
nHand     = hcp(north)<10 and not (nLongSuit or nOvercall or nDouble)

eNT       = shape(east, any 4333) 
eLongSuit = shape(east, any 9xxx +any 8xxx +any 7xxx +any 6xxx)
eHand     = hcp(east)>5 and hcp(east)<11 and not (eNT or eLongSuit)

sRange and nHand and eHand and (CD or CH or CS or DH or DS or HS)

# Generate Statistics
produce 40
action
average "CD " 100 * CD,
average "CH " 100 * CH,
average "CS " 100 * CS,
average "DH " 100 * DH,
average "DS " 100 * DS,
average "HS " 100 * HS,

`,"S",true)
Script
Button,Sandwich 1N BPH,\n\
--- A Sandwich 1N by a passed hand shows the two unbid suits.\n\
%SandwichNtBPH%

Script,MinorOpenerInvRaise
setDealerCode(`

# Minor Opener & Invitational Raise
dealer south

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Calm-Opponents
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Predict-Opening-1-Bid

# Calculate Total Points for north (tp)
v1 = shape(north, any 0xxx) ? 5 : 0  // allow for 2 voids
v2 = shape(north, any 00xx) ? 5 : 0
s1 = shape(north, any 1xxx) ? 3 : 0  // allow for 2 singletons 
s2 = shape(north, any 11xx) ? 3 : 0
d1 = shape(north, any 2xxx) ? 1 : 0  // allow for 3 doubletons
d2 = shape(north, any 22xx) ? 1 : 0
d3 = shape(north, any 222x) ? 1 : 0
tp = v1+v2+s1+s2+d1+d2+d3 + hcp(north)

# Define 1 Minor and Mixed Raise -- no 1-level suit or NT response 
cRaise = oC and clubs(north)>4    and spades(north)<4 and hearts(north)<4 and diamonds(north)<4                    //  5+ card support
dRaise = oD and diamonds(north)>3 and spades(north)<4 and hearts(north)<4 and diamonds(north)>clubs(north)         //  4+ card support
mixedRaise = (cRaise or dRaise) and hcp(north)>10 and hcp(north)<13 and shape(north, any 0xxx+any 1xxx+any 2xxx)   // shortness to eliminate 1N

# Now do it
mixedRaise and calmOpps and hcp(south)<22  // some are included in imported code

produce 1000
action
average "1 Club        " 100 * oC,
average "1 Diamond     " 100 * oD,
average "hcp north     "       hcp(north),
average "TP north      "       tp,
average "any void      " 100 * shape(north,any 0xxx),
average "any singleton " 100 * shape(north,any 1xxx),
average "any doubleton " 100 * shape(north,any 2xxx),
frequency "HCP north" (hcp(north),0,11),
frequency "TP  north" (tp,6,12),

`,"S",true)
Script

Button,1m & Inv Raise,--- Minor Opening w/Invitational Raise\n\
1!C - 2!S shows 5+ card support and invitational values 11-12 HCP\n\
1!D - 2!S shows 4+ card support and invitational values 11-12 HCP\n\
Note:  Responder has no new suit to bid at the 1-level and opener's suit\n\
is responder's longest suit.  Responder has a void，singleton，or doubleton.\n\
%MinorOpenerInvRaise%

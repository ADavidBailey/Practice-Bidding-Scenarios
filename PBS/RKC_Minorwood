Script,rkcMinorwood
setDealerCode(`

# convention-card:
# auction-filter: Note...:Inverted.Minors

dealer south

# RKC Minorwood

#Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Predict-Opening-1-Bid
##### Imported Script -- Predict Opening 1-Bid #####

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and   top4(south,spades)>0   and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and   top4(south,hearts)>0   and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)>0 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and    top4(south,clubs)>0    and not hascard(south,AC) ? 1 : 0
dgPoints = S2H + H2H + D2H + C2H

# Define notrump points for south (ntPoints)
ntPoints = lengthPoints>dgPoints ? hcp(south) + lengthPoints - dgPoints : hcp(south)

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

# Define robot notrump shape and exclude any 5card 
ntShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422 -5xxx -x5xx)

# Define ntPoint ranges
oneNT   = ntShape and ntPoints>14 and ntPoints<18
twoNT   = ntShape and ntPoints>19 and ntPoints<22
threeNT = ntShape and ntPoints>25 and ntPoints<28
weakNT  = ntShape and ntPoints>10 and ntPoints<15

# Define Game Forcing 2C
case1 = hcp(south)>22
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT // or weakNT  weakNT will be duplicated diamond and club openings

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade = oS
oneHeart = oH
oneDiamond = oD
oneClub = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
# Defines oneClub, oneDiamond

# North has no major, no NT,and has support for opener's minor
nUnbal  = shape(north, any 1xxx+any 22xx)
noMajor = spades(north)<4 and hearts(north)<4

cFit = oneClub    and clubs(north)>4
dFit = oneDiamond and diamonds(north)>3
minorRaise = (cFit or dFit) and noMajor and nUnbal

# South opens 1m and North raises to 2m
minorRaise and hcp(south)>18 and hcp(south)<22 and hcp(north)>9

produce 1000
action
average "hcp south" hcp(south),
average "hcp north" hcp(north),
average "hcp total" hcp(south)+hcp(north),

`)
Script
Button,RKC Minorwood,\n\
--- RKC Minorwood\n\
After an Inverted Minor raise，4 of the agreed minor is RKC Minorwood.\n\
The step responses are as follows:\n\
    • 1st: 1/4 Keycards\n\
    • 2nd: 0/3 Keycards\n\
    • 3rd: 2 Keycards\n\
    • 4th: 2Keycards + Queen of trump\n\
As the linked article explains，RKC Minorwood can be extended to other situations\n\
where the 4m bid cannot be construed as competitive.\n\
https://tinyurl.com/RKC-Minorwood\n\
%rkcMinorwood%,backgroundColor=lightpink

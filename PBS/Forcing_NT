Script,ForcingNT
setDealerCode(`

# convention-card: 21GF-ADB
# auction-filter: Auction.....[\s\S][\s\S]?[HS].Pass.1NT
# conv

# Forcing NT Leveled
dealer south

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Calm-Opponents
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Predict-Opening-1-Bid
# defines oneSpade, oneHeart
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/GIB-1N
# defines gibNT

# Calculate Total Points for North (tp)
v1 = shape(north, any 0xxx) ? 5 : 0  // allow for 2 voids
v2 = shape(north, any 00xx) ? 5 : 0
s1 = shape(north, any 1xxx) ? 3 : 0 // allow for 2 singletons
s2 = shape(north, any 11xx) ? 3 : 0
d1 = shape(north, any 2xxx) ? 1 : 0 // allow for 3 doubletons
d2 = shape(north, any 22xx) ? 1 : 0
d3 = shape(north, any 222x) ? 1 : 0
tp = v1+v2+s1+s2+d1+d2+d3 + hcp(north)

# South opens 1H or 1S
oneMajor = (oneSpade or oneHeart) and hcp(south)>11 and hcp(south)<20 and not gibNT

# Defining fit
spadeFit = oneSpade and spades(north)>2
heartFit = oneHeart and hearts(north)>2
fit = spadeFit or heartFit
noFit = not fit

# Avoid 1H - 1S
not1Spade = not(oneHeart and spades(north)>3)

# Long minor
longC = oneClub and clubs(north)>5
longD = oneDiamond and diamonds(north)>5
longMinor = (longC or longD)

# Define ranges - TP for raises; otherwise HCP
wRange = tp>4 and tp<8
iRange = tp>10 and tp<13
ngfRange = hcp(north)>5 and hcp(north)<13

# Defining Forcing NT hand types
wFit = fit and wRange                   and not1Spade
iFit = fit and iRange                   and not1Spade
lMin = noFit and ngfRange and longMinor and not1Spade  // overlaps w/3-under inv jump
nFit = noFit and ngfRange and not lMin  and not1Spade

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Leveling
wFitLev = wFit and keep
iFitLev = iFit and keep
lMinLev = lMin and keep
nFitLev = nFit and keep
levelTheDeal = wFitLev or nFitLev or iFitLev or lMinLev

# South opens 1 H/S and North bids 1N Forcing
oneMajor and (wFit or nFit or iFit) and calmWest
and levelTheDeal

produce 1000
action
average "fit 5-7 TP      " 100 * wFit,
average "fit 11-12 TP    " 100 * iFit,
average "no fit, lMin    " 100 * lMin,
average "no fit 6-12 HCP " 100 * nFit,


`, "S", true);
Script
Button,Forcing NT (Lev),\n\
--- Forcing NT (Leveled)\n\
• 5-7 TP with fit\n\
• 11-12 TP with fit\n\
• 6-12 HCP without fit\n\
These are leveled such that the three types occur at about the same frequency.\n\
%ForcingNT%

Script,ForcingNT
setDealerCode(`

# convention-card: 21GF-ADB
# auction-filter: Note....forcing.1NT

# Forcing NT Leveled
dealer south

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Calm-Opponents
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Predict-Opening-1-Bid
# defines oneSpade, oneHeart
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/GIB-1N
# defines gibNT

# Calculate Total Points for North (tp)
v1 = shape(north, any 0xxx) ? 5 : 0  // allow for 2 voids
v2 = shape(north, any 00xx) ? 5 : 0
s1 = shape(north, any 1xxx) ? 3 : 0 // allow for 2 singletons
s2 = shape(north, any 11xx) ? 3 : 0
d1 = shape(north, any 2xxx) ? 1 : 0 // allow for 3 doubletons
d2 = shape(north, any 22xx) ? 1 : 0
d3 = shape(north, any 222x) ? 1 : 0
tp = v1+v2+s1+s2+d1+d2+d3 + hcp(north)

# South opens 1H or 1S
oneMajor = (oneSpade or oneHeart) and hcp(south)>11 and hcp(south)<20 and not gibNT

# Defining fit
spadeFit = oneSpade and spades(north)>2
heartFit = oneHeart and hearts(north)>2
fit = spadeFit or heartFit
noFit = not fit

# Avoid 1H - 1S
noSpadeBid = not(oneHeart and spades(north)>3)

# Long minor
longC = clubs(north)>5
longD = diamonds(north)>5
longMinor = (longC or longD)

# Define ranges - TP for raises; otherwise HCP
wRange = tp>4 and tp<8
iRange = tp>10 and tp<13
ngfRange = hcp(north)>5 and hcp(north)<13

# Defining hand types for Forcing NT
wFit = fit and wRange
iFit = fit and iRange
lMin = noFit and ngfRange and longMinor  // overlaps w/3-under inv jump
nFit = noFit and ngfRange and not lMin
forcingNT = (wFit or iFit or lMin or nFit) and noSpadeBid

# Defining Opener's rebids
rebidM  = shape(south, any 7xxx+any 6xxx)
rebidN  = shape(south, any 5332+any 5422) and hcp(south)>17 and hcp(south)<20
reverse = shape(south, 45xx+46xx+56xx) and hcp(south)>16 and hcp(south)<22
newSuit = not(rebidM or rebidN or reverse)
openerRebid = rebidM or rebidN or reverse or newSuit

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Leveling

# Level Responses
wFitLev = wFit and keep
iFitLev = iFit and keep81
lMinLev = lMin and keep
nFitLev = nFit and keep015
levelResponse = wFitLev or nFitLev or iFitLev or lMinLev

# Level Rebids
rMajLev = rebidM  and keep015
rNTLev  = rebidN  and keep25
rRevLev = reverse and keep
rNSLev  = newSuit and keep015
levelRebid = rMajLev or rNTLev or rRevLev or rNSLev

levelTheDeal = levelResponse and levelRebid

## Now Do it
# South opens 1 H/S and North bids 1N Forcing and South Rebids
oneMajor and forcingNT and openerRebid and calmWest
and levelTheDeal

produce 5000
action
average "fit 5-7 TP         " 100 * wFit,
average "fit 11-12 TP       " 100 * iFit,
average "no fit, long minor " 100 * lMin,
average "no fit 6-12 HCP    " 100 * nFit,
average "-------------------" 100 * (wFit + iFit + lMin + nFit),
average "rebid Major        " 100 * rebidM,
average "rebid NT           " 100 * rebidN,
average "rebid Reverse      " 100 * reverse,
average "rebid New Suit     " 100 * newSuit,
average "-------------------" 100 * (rebidM + rebidN + newSuit + reverse),

`, "S", true);
Script
Button,Forcing NT (Lev),\n\
--- Forcing NT (Leveled)\n\
Partner opens an Responder has a non-Game Forcing hand with...\n\
• 5-7 TP with fit\n\
• 11-12 TP with fit\n\
• Long minor without fit\n\
• 6-12 HCP without fit\n\
Then Opener Rebids...\n\
• Major\n\
• 2NT\n\
• Reverse\n\
• New Suit\n\
These are leveled such that the responses and rebids at about the same frequency.\n\
%ForcingNT%

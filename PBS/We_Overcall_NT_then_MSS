Script,WeOvercallNtThenMSS
setDealerCode(`

# convention-card: 21GF-MSS
# auction-filter: Note...:Minor Suit Stayman

# We Overcall 1NT then Minor Suit Stayman (MSS)
dealer east

# Predict East's opening suit
s = spades(east)
h = hearts(east)
d = diamonds(east)
c = clubs(east)
eS = s>4 and s>=h and s>=d and s>=c
eH = not eS and h>4 and h>=d and h>=c
eD = not eS and not eH and ((d>3 and d>=c) or c<3)
eC = not eS and not eH and not eD

# Avoid opening 1N
ntShape = shape(east, any 4333 +any 4432 +any 5332 +any 5422 -5xxx-x5xx)
notEastNT = not (hcp(east)>14 and hcp(east)<18 and ntShape)

# South has a stop in East's opening suit -- at least Axx, Kxx or QJx
eSws = eS and hcp(south,spades)>2 and spades(south)>2
eHws = eH and hcp(south,hearts)>2 and hearts(south)>2
eDws = eD and hcp(south,diamonds)>2 and diamonds(south)>2
eCws = eC and hcp(south,clubs)>2 and clubs(south)>2

# East opens 1 of a suit & South has stop
eOpens = notEastNT and (eSws or eHws or eDws or eCws) and hcp(east)>11 and hcp(east)<20

# South bids 1N
sNT = shape(south, any 4333 +any 4432 +any 5332 - 5xxx - x5xx) and hcp(south)>14 && hcp(south)<19

# North bids 2S - MSS
n2S = (shape(north,xx55+xx54+xx45) and hcp(north)>7) or (shape(north, xx75+xx65+xx57+xx56) and hcp(north)>5)

# Now do it
eOpens and sNT and n2S


`, "E", true)
Script
Button,We Overcall 1NT,\n\
--- We Overcall 1NT then partner bids Minor Suit Stayman (2!S)\n\
• 2N - Denies a 4-card minor\n\,
• 3!C - Shows 4-!C and may have 4!D\n\
• 3!D - Shows 4-!D and denies 4!C\n\
%WeOvercallNtThenMSS%

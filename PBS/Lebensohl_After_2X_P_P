Script,LebensohlAfter2X-P-P
setDealerCode(`

# auction-filter: Auction.....\\n2[DHS] Pass Pass X

# Lebensohl after (2x) P (P) X
dealer west
produce 1000

tp = hcp(west)
sp = hcp(west,spades)
hp = hcp(west,hearts)
dp = hcp(west,diamonds)

# Define West's weak 2 bids w/o 3-card support by East nor shortness in North
w2S = shape(west,6xxx-x4xx     -any 7xxx-any 66xx-any 65xx) and 2 * sp > tp and top3(west,spades)>1   and spades(east)<3   and spades(north)>2
w2H = shape(west,x6xx-4xxx     -any 7xxx-any 66xx-any 65xx) and 2 * hp > tp and top3(west,hearts)>1   and hearts(east)<3   and hearts(north)>2
w2D = shape(west,xx6x-x4xx-4xxx-any 7xxx-any 66xx-any 65xx) and 2 * dp > tp and top3(west,diamonds)>1 and diamonds(east)<3 and diamonds(north)>2

# Define South shortness w/3 suits & shortness in opener's suit
sShortS = w2S and spades(south)<3   and shape(south,x4xx) and hcp(south,spades)==0
sShortH = w2H and hearts(south)<3   and shape(south,4xxx) and hcp(south,hearts)==0
sShortD = w2D and diamonds(south)<3 and shape(south,44xx) and hcp(south,diamonds)==0
sShort = (sShortS or sShortH or sShortD)

# Define South stopper
sStopS = w2S and (top2(south,spades)>0 and spades(south)>1)
sStopH = w2H and (top2(south,hearts)>0 and hearts(south)>1)
sStopD = w2D and (top2(south,diamonds)>0 and diamonds(south)>1)
sStop = (sStopS or sStopH or sStopD)

sNT = hcp(south)>15 and hcp(south)<19 and shape(south, any 4333+any 4432+any 5332) and sStop

sDouble1 = hcp(south)>11 and hcp(south)<16 and sShort
sDouble2 = hcp(south)>15 and not sNT
southDoubles = (sDouble1 or sDouble2) and shape(south,any 5440+any 4441+any 4432)

# West opens weak 2
wOpensWeak2 = (w2S or w2H or w2D) and hcp(west)>6 and hcp(west)<10

# North Passes
nPasses = hcp(north)<14 and shape(north, any 5332 + any 4432) and controls(north)<5

# East Passes
ePasses = hcp(east)<10

# North responds
weak   = hcp(north)<8
invite = hcp(north)>7 and hcp(north)<12 and not hascard(west,2C)
force  = hcp(north)>11
nResponds = (weak or invite or force)

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Leveling

# Define leveling
levWeak   = weak   and keep
levInvite = invite and keep
levForce  = force  and keep
levelTheDeal = (levWeak or levInvite or levForce)

wOpensWeak2 and nPasses and ePasses and nResponds
and levelTheDeal

action
average "Weak  " 100 * weak,
average "Invite" 100 * invite,
average "Force " 100 * force,

`,"W",true)
Script
Button,Lebensohl after W2-P-P,\n\
--- Lebensohl After 2x-P-P\n\
After 2!D/2!H/2!S Pass Pass，X is a Lebensohl Double.\n\
  • A new suit at the 2-level is natural and weak (0-7).\n\
  • A non-jump new suit at the 3-level shows 8-10 and no stop.\n\
  • Cue Bid openers suit to show major suit interest without a stop.\n\
  • Jump bidding a new suit at the 3-level shows 8-10 and no stop.\n\
  • 3N shows game forcing values without major suit interest and no stop -- pass or scramble.\n\
  • Bidding 2N is the Lebensohl relay to 3!C.  After partner's forced 3!C，you may...\n\
    ◦ Pass or Correct to a new suit below opener's suit (0-7).\n\
    ◦ Cue Bid openers suit to show major suit interest and with a stop.\n\
    ◦ Bid 3N to show a balanced hand without major suit interest AND with a stop.\n\
https://www.bridgebum.com/lebensohl_over_weak_two.php\n\
%LebensohlAfter2X-P-P%,backgroundColor=lightpink

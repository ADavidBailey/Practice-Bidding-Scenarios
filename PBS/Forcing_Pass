Script,ForcingPass
setDealerCode(`

# Forcing Pass

dealer north

# Define NT
nt = hcp(north)>14 and hcp(north)<18 and shape(north,any 4333+any 4432+any 5332-5xxx-x5xx)

# Predict North's opening suit
sNorth = spades(north)
hNorth = hearts(north)
dNorth = diamonds(north)
cNorth = clubs(north)
nS = sNorth>4 and sNorth>=hNorth and sNorth>=dNorth and sNorth>=cNorth
nH = not nS and hNorth>4 and hNorth>=dNorth and hNorth>=cNorth
nD = not nS and not nH and ((dNorth>3 and dNorth>=cNorth) or cNorth<3)
nC = not nS and not nH and not nD

# Calculate North's Rank
nRS = nS ? 4 : 0
nRH = nH ? 3 : 0
nRD = nD ? 2 : 0
nRC = nC ? 1 : 0
northRank = nRS+nRH+nRD+nRC   // all except one are zero

# South has support for North's opening suit
sSs = nS and top3(north,spades)>1   and spades(south)>2
sHs = nH and top3(north,hearts)>1   and hearts(south)>2
sDs = nD and top3(north,diamonds)>1 and diamonds(south)>3
sCs = nC and top3(north,clubs)>1    and clubs(south)>4
sRaises = (sSs or sHs or sDs or sCs) and shape(south,any 0xxx+any 1xxx)

# East's longest suit for competing
sEast = spades(east)
hEast = hearts(east)
dEast = diamonds(east)
cEast = clubs(east)
eS = sEast>=hEast and sEast>=dEast and sEast>=cEast
eH = not eS and hEast>=dEast and hEast>=cEast
eD = not eS and not eH and dEast>=cEast
eC = not eS and not eH and not eD

# Calculate East's Rank
eRS = eS ? 4 : 0
eRH = eH ? 3 : 0
eRD = eD ? 2 : 0
eRC = eC ? 1 : 0
eastRank = eRS+eRH+eRD+eRC

#West has support for East's longest suit
wSs = eS and top3(east,spades)>1   and spades(west)>3
wHs = eH and top3(east,hearts)>1   and hearts(west)>3
wDs = eD and top3(east,diamonds)>1 and diamonds(west)>3
wCs = eC and top3(east,clubs)>1    and clubs(west)>3

nOpens = hcp(north)>11 and not nt and (nC or nD or nH)
eBids1 = eastRank>northRank and hcp(east)>5 and shape(east,any 51xx+any 50xx)
eJumps = eastRank>northRank and not eBids1 and hcp(east)>5 and hcp(east)<10 and shape(east,any 61xx+any 60xx)
eBids2 = eastRank<northRank and hcp(east)>10 and not eBids1 and not eJumps and shape(east,any 61xx+any 60xx+any 51xx+any 50xx)
eBids  = (eBids1 or eJumps or eBids2)
sInvites = sRaises and hcp(south)>11
wRaises = hcp(west)>1 and (wSs or wHs or wDs or wCs) and shape(west,any 0xxx+any 1xxx)

nOpens and eBids and sInvites and wRaises and (hcp(north) + hcp(south))>25

action

average "HCP N  " hcp(north),
average "HCP E  " hcp(east),
average "HCP S  " hcp(south),
average "HCP W  " hcp(west),
average "HCP N/S" hcp(north) + hcp(south),

`,"N",true);
Script

Button,Forcing Pass,\n\
--- Forcing Pass: When we are in and invitational or better auction and an opponent\n\
intervenesï¼Œa pass is forcing.  It shows extra offensive values and interest in\n\
continuing.  A double denies extra offensive values.  Passing and pulling\n\
partner's double is stronger than bidding directly.\n\
%ForcingPass%
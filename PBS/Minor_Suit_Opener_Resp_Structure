Script,MinorOpenerRespStr
setDealerCode(`

# convention-card:
# auction-filter: (1[CD] Pass 1[DHSN])|(1C Pass 2C)|(1D Pass 2D)|(1[CD] Pass [23]N)

# Minor Opener w/Raise or Balanced Response -- Leveled
dealer south

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Calm-Opponents
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Predict-Opening-1-Bid

# Define 1 Minor and Mixed/Invitational Raise -- no 1-level suit or NT response 
cRaise = oC and clubs(north)>4    and spades(north)<4 and hearts(north)<4 and diamonds(north)<4                    //  5+ card support
dRaise = oD and diamonds(north)>3 and spades(north)<4 and hearts(north)<4 and diamonds(north)>clubs(north)         //  4+ card support
mRange = hcp(north)>5  and hcp(north)<11
iRange = hcp(north)>10

shortness = shape(north, any 0xxx+any 1xxx+any 2xxx)

mixedRaise  = (cRaise or dRaise) and mRange and shortness
inviteRaise = (cRaise or dRaise) and iRange and shortness

# Define one Minor and balanced responses w/o 5-card major
nBalanced = shape(north, any 4333 +any 4432 +any 5422 +any 5332-5xxx-x5xx)

oneMinor = oC or oD

balWkResp  = oneMinor and nBalanced and hcp(north)>5 and hcp(north)<10
balGIResp  = oneMinor and nBalanced and hcp(north)>9 and hcp(north)<13
balGFResp  = oneMinor and nBalanced and hcp(north)>12
anyBalResp = oneMinor and nBalanced and hcp(north)>5

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Leveling

# Do the Leveling
levMix   = mixedRaise  and keep67
levInv   = inviteRaise and keep
levBalWk = balWkResp   and keep015
levBalGI = balGIResp   and keep015
levBalGF = balGFResp   and keep015
levelTheDeal = levMix or levInv or levBalWk or levBalGI or levBalGF

# Now do it
(mixedRaise or inviteRaise or anyBalResp) and calmOpps and hcp(south)<22
and levelTheDeal

produce 1000
action
average "1 Club        " 100 * oC,
average "1 Diamond     " 100 * oD,
average "hcp south     "       hcp(south),
average "hcp north     "       hcp(north),
average "any raise     " 100 * (mixedRaise or inviteRaise),
average "raise w/1 Dbl " 100 * ((mixedRaise or inviteRaise) and shape(north,any 2xxx-any 22xx)),
average "Mixed raise   " 100 * mixedRaise,
average "Invite raise  " 100 * inviteRaise,

average "any void      " 100 * shape(north,any 0xxx),
average "any singleton " 100 * shape(north,any 1xxx),
average "one doubleton " 100 * shape(north,any 2xxx-any 22xx),
average "two doubletons" 100 * shape(north,any 22xx),

average "any balanced  " 100 * nBalanced,
average "Weak balanced " 100 * balWkResp,
average "GI balanced   " 100 * balGIResp,
average "GF balanced   " 100 * balGFResp,


`,"S",true)
Script

Button,1m & Raise/Bal (Lev),\n\
--- 1 Minor w/Raise or Balanced Response -- Leveled\n\
1!C/!D - 2!C/!D shows 5/4+ card support and mixed values 6-10 HCP\n\
1!C/!D - 2!S shows 5/4+ card support and invitational+ values 11+ HCP\n\
1!C/!D - 2!H shows 11-12 HCP balanced\n\
1!C/!D - 2N shows 13-15 or 18-19 HCP balanced\n\
1!C/!D - 3N shows 16-17 HCP balanced\n\
Note:  Responder has no new suit to bid at the 1-level，shortness，and\n\
opener's suit is responder's longest suit.\n\
%MinorOpenerRespStr%,backgroundColor=lightpink

Script,DoubleByAdvancer
setDealerCode(`

# convention-card:
# auction-filter: Auction.....\\n(1C 1[DHS] 2C|1D 1[HS] 2D|1H 1S 2H) X

dealer west

##### Double by Advancer #####
# West Opens, North Bids a new Suit at the 1-level, East Raises, and South has the other 2 suits.

# Predict West's Opening suit
s = spades(west)
h = hearts(west)
d = diamonds(west)
c = clubs(west)
oS = s>4 and s>=h and s>=d and s>=c
oH = not oS and h>4 and h>=d and h>=c
oD = not (oS or oH) and ((d>3 and d>=c) or c<3)
oC = not (oS or oH or oD)
westOpens = (oS or oH or oD or oC) //and shape(west,any 1xxx + any 0xxx)

# Predict North's overcall
nD = diamonds(north)==5 and hcp(north,diamonds)>3
nH = hearts(north)  ==5 and hcp(north,hearts)  >3 and not nD
nS = spades(north)  ==5 and hcp(north,spades)  >3 and not (nD or nH)
northOvercalls = ((oC and (nD or nH or nS)) or (oD and (nH or nS)) or (oH and nS)) and shape(north,xxxx -any 65xx -any 55xx)

# Avoid South's support for North's suit
sDOK = nD ? diamonds(south)<3 : 1
sHOK = nH ? hearts(south)  <3 : 1
sSOK = nS ? spades(south)  <3 : 1
southSupportOK = sDOK and sHOK and sSOK

# Define East's good support -------------------------     avoid negative double -----------
eC = oC and clubs(east)   ==4 and hcp(east,clubs)   >3 and spades(east)<4 and hearts(east)<4
eD = oD and diamonds(east)==4 and hcp(east,diamonds)>2 and spades(east)<4 and hearts(east)<4
eH = oH and hearts(east)  ==3 and hcp(east,hearts)  >1

# East raises ---------------     avoid preemptive raise -----
eastRaises = (eC or eD or eH) and shape(east,xxxx -any 0xxx -any 1xxx)

# Define Opener's & Overcaller's suits
CD = oC and nD
CH = oC and nH
CS = oC and nS
DH = oD and nH
DS = oD and nS
HS = oH and nS

# Define South's doubles showing the 2 other suits
sHS = CD ? shape(south,44xx+54xx+45xx) : 0
sDS = CH ? shape(south,4x4x+4x5x)      : 0
sDH = CS ? shape(south,x44x+x45x)      and spades(south)<3    : 0
sCS = DH ? shape(south,4xx4+4xx5)      and hearts(south)<3    : 0
sCH = DS ? shape(south,x4x4+x4x5)      and spades(south)<3    : 0
sCD = HS ? shape(south,xx44+xx45+xx54) : 0

# Make them good suits
sGHS = sHS and hcp(south,hearts)  >3 and hcp(south,spades)>3
sGDS = sDS and hcp(south,diamonds)>3 and hcp(south,spades)>3
sGDH = sDH and hcp(south,diamonds)>3 and hcp(south,hearts)>3
sGCS = sCS and hcp(south,clubs)   >3 and hcp(south,spades)>3
sGCH = sCH and hcp(south,clubs)   >3 and hcp(south,hearts)>3
sGCD = sCD and hcp(south,diamonds)>3 and hcp(south,clubs) >3

southActs  = (sGHS or sGDS or sGDH or sGCS or sGCH or sGCD) and southSupportOK

minHCP  = hcp(west)>=12 and hcp(north)>=8 and hcp(east)>=6 and hcp(south)>=12

##### Now do it #####
westOpens and northOvercalls and eastRaises and southActs and minHCP

action
average "W 1C  " 100 * oC,
average "W 1D  " 100 * oD,
average "W 1H  " 100 * oH,

average "N 1D  " 100 * nD,
average "N 1H  " 100 * nH,
average "N 1S  " 100 * nS,

average "S H&S " 100 * sGHS,
average "S D&S " 100 * sGDS,
average "S D&H " 100 * sGDH,
average "S C&S " 100 * sGCS,
average "S C&H " 100 * sGCH,
average "S C&D " 100 * sGCD,

average "E 2C  " 100 * eC,
average "E 2D  " 100 * eD,
average "E 2H  " 100 * eH,

`,"W",true)
Script
Button,X by Advancer,\n\
--- Responsive Double by Advancer\n\
After 1x (1y) 2xï¼Œ(X) shows the other two suits.\n\
%DoubleByAdvancer%

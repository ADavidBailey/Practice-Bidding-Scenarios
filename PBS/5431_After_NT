Script,NT5431
setDealerCode(`

# 5431 after 1NT
# convention-card: 21GF-5431-After-NT
# auction-filter: Auction.....\\n1NT Pass 3[HS]

# 5431 After NT
dealer south

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/GIB-1N
# Returns gibNT
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Calm-Opponents
# Returns calmOpps


# TP -1 for stiff A, K, Q, J
s1 = shape(north, 1xxx) and hcp(north,spades)>0 ? 1 : 0
h1 = shape(north, x1xx) and hcp(north,hearts)>0 ? 1 : 0
nTP = hcp(north) - s1 - h1

# Responder holds (3-1 or 3-0 in the majors) and 4+ in each of the minors and 8+ HCP
M31   = shape(north,31xx+13xx) and shape(north,xx54+xx45)
M30   = shape(north,30xx+03xx) and shape(north,xx64+xx46+xx55)
n5431 = (M31 or M30) and nTP>8

# Now do it
gibNT and n5431 and calmOpps

action

average "HCP S " hcp(south),
average "HCP N " hcp(north),
average "1S 3H " 100 * shape(north,1xxx),
average "3S 1H " 100 * shape(north,x1xx),
average "0S 3H " 100 * shape(north,0xxx),
average "3S 0H " 100 * shape(north,x0xx),

average "5D 4C " 100 * shape(north,xx54),
average "4D 5C " 100 * shape(north,xx45),

average "5D 5C " 100 * shape(north,xx55),
average "6D 4C " 100 * shape(north,xx64),
average "4D 6C " 100 * shape(north,xx46),

`,"S",true)
Script
Button,5431 After NT,\n\
---  5431 After Notrump\n\
South opens 1N and responder bids 3M，showing\n\
. a singleton or void in that major，\n\
. exactly 3-cards in the other major，\n\
. at least 5-4 in minors，\n\
. and 8+ HCP (if 1NT 15-17 HCP).\n\
%NT5431%,backgroundColor=lightpink
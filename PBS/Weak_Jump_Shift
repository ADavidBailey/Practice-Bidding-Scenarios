Script,WeakJumpShift
setDealerCode(`

# Weak Jump Shift
# South Open's 1C, 1D, or 1H and North has 5 or less HCP and a 6-card suit
produce 1000

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Calm-Opponents
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/GIB-1N
# Defines gibNT

Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Predict-Opening-1-Bid
# Defines oneSpade, oneHeart, oneDiamond, oneClub

# Define north's WJS suits
nC = clubs(north)>6    and top4(north,clubs)>1
nD = diamonds(north)>5 and top4(north,diamonds)>1
nH = hearts(north)>5   and top4(north,hearts)>1
nS = spades(north)>5   and top4(north,spades)>1

# Define weak jump shifts -- they're usually at the 2-level
s1C = oneClub    and (nD or nH or nS)
s1D = oneDiamond and (nH or nS)
s1H = oneHeart   and nS

# sometimes at the 3-level
s1S3 = oneSpade   and (nC or nD or nH)
s1D3 = oneDiamond and nC
s1H3 = oneHeart   and (nC or nD)

#  A Weak Jump Shift at the 2-level is too weak to make a 1-level response
WJS2 = (s1C or s1D or s1H) and hcp(north)>1 and hcp(north)<6

#  at the 3-level it needs extra length and might be a little stronger
WJS3 = (s1S3 or s1D3 or s1H3) and hcp(north)>1 and hcp(north)<8 and shape(north,any 8xxx+any 7xxx)

# Define opener's rebids
cFit = nC and clubs(south)>2
dFit = nD and diamonds(south)>2
hFit = nH and hearts(south)>2
sFit = nS and spades(south)>2
fit = (cFit or dFit or hFit or sFit)

s1Suit = shape(south,any 8xxx+any 7xxx)
s2Suit = shape(south, any 76xx+any 75xx+any 65xx+any 55xx)

force   = hcp(south)>19    # 2N -- continuations are like those after 2N response to a Weak Two Bid
rebid   = s1Suit and not fit and not force
newSuit = s2Suit and not fit and not force      
pass    =            not fit and not force
raise   =                fit and not force
PorR    = pass or raise

# Level the deal
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Leveling

levForce   = force   and keep
levRebid   = rebid   and keep
levNewSuit = newSuit and keep
levPorR    = PorR    and keep

levelTheDeal = levForce or levRebid or levNewSuit or levPorR

# Now do it
# South opens, West passes, and North makes a Weak Jump Shift
(WJS2 or WJS3) and calmWest and not gibNT
and levelTheDeal

action
average "hcp(south)  " hcp(south),
average "hcp(north)  " hcp(north),
average "pass        " 100 * pass,
average "raise       " 100 * raise,
average "invite      " 100 * invite,

`,"S",true);
Script

Button,Weak Jump Shift (Lev),\n\
--- Weak Jump Shift\n\
South opens，West passes，and North jumps -- a single jump.\n\
• at the 2-level showing 2-5 HCP and 6+ cards -- too weak for the 1-level.\n\
• at the 3-level showing 2-7 HCP and 7+ cards in the suit bid\n\ 
Leveled: Opener's Force，Rebid，New Suit，(Pass or Raise) occur at about the same frequency.\n\
%OneMinorTwoMinor%

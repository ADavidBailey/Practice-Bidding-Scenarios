Script,LebensohlVsOppsW2
setDealerCode(`

# auction-filter: Auction.....\\n2[DHS] (X|Pass Pass X)

# Lebensohl vs Opponents Weak 2 -- Direct or Balanced
dealer west
produce 1000

tp = hcp(west)
sp = hcp(west,spades)
hp = hcp(west,hearts)
dp = hcp(west,diamonds)

# Define West's weak 2 bids w/o 3-card support by East
w2S = shape(west,6xxx-x4xx     -any 7xxx-any 66xx-any 65xx) and 2 * sp > tp and top3(west,spades)>1   and spades(east)<3   //and spades(north)>2
w2H = shape(west,x6xx-4xxx     -any 7xxx-any 66xx-any 65xx) and 2 * hp > tp and top3(west,hearts)>1   and hearts(east)<3   //and hearts(north)>2
w2D = shape(west,xx6x-x4xx-4xxx-any 7xxx-any 66xx-any 65xx) and 2 * dp > tp and top3(west,diamonds)>1 and diamonds(east)<3 //and diamonds(north)>2

### North Direct Lebensohl
# Define North shortness w/3 suits & shortness in opener's suit
nShortS = w2S and spades(north)<3   and shape(north,x4xx) and hcp(north,spades)==0
nShortH = w2H and hearts(north)<3   and shape(north,4xxx) and hcp(north,hearts)==0
nShortD = w2D and diamonds(north)<3 and shape(north,44xx) and hcp(north,diamonds)==0
nShort = (nShortS or nShortH or nShortD)

# Define North stopper
nStopS = w2S and (top2(north,spades)>0 and spades(north)>1)
nStopH = w2H and (top2(north,hearts)>0 and hearts(north)>1)
nStopD = w2D and (top2(north,diamonds)>0 and diamonds(north)>1)
nStop = (nStopS or nStopH or nStopD)

nNT = hcp(north)>15 and hcp(north)<19 and shape(north, any 4333+any 4432+any 5332) and nStop

nDouble1 = hcp(north)>11 and hcp(north)<16 and nShort
nDouble2 = hcp(north)>15 and not nNT
nDoubles = (nDouble1 or nDouble2) and shape(north,any 5440+any 4441+any 4432)

# South Responds
sWeak     = hcp(south)<8
sInvite   = hcp(south)>7 and hcp(south)<12
sForce    = hcp(south)>11
sResponds = (sWeak or sInvite or sForce)

### South Balancing Lebensohl
# Define South shortness w/3 suits & shortness in opener's suit
sShortS = w2S and spades(south)<3   and shape(south,x4xx) and hcp(south,spades)==0
sShortH = w2H and hearts(south)<3   and shape(south,4xxx) and hcp(south,hearts)==0
sShortD = w2D and diamonds(south)<3 and shape(south,44xx) and hcp(south,diamonds)==0
sShort = (sShortS or sShortH or sShortD)

# Define South stopper
sStopS = w2S and (top2(south,spades)>0 and spades(south)>1)
sStopH = w2H and (top2(south,hearts)>0 and hearts(south)>1)
sStopD = w2D and (top2(south,diamonds)>0 and diamonds(south)>1)
sStop = (sStopS or sStopH or sStopD)

sNT = hcp(south)>15 and hcp(south)<19 and shape(south, any 4333+any 4432+any 5332) and sStop

sDouble1 = hcp(south)>11 and hcp(south)<16 and sShort
sDouble2 = hcp(south)>15 and not sNT
sDoubles = (sDouble1 or sDouble2) and shape(south,any 5440+any 4441+any 4432)

# North Responds
nWeak     = hcp(north)<8
nInvite   = hcp(north)>7 and hcp(north)<12 //and not hascard(west,2C)
nResponds = (nWeak or nInvite)
nForce    = 0   //North is a passed hand

# West opens weak 2
wOpensWeak2 = (w2S or w2H or w2D) and hcp(west)>6 and hcp(west)<10

# And East always Passes
ePasses = hcp(east)<14 and shape(east, any 5332 + any 4432) 

### North acts in direct seat
nActs = (nDoubles and ePasses and sResponds)

### South acts in balancing seat
# North Passes
nPasses = hcp(north)<14 and shape(north, any 5332 + any 4432) and controls(north)<5

sActs = ePasses and sDoubles and nResponds


Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/script/Leveling

# Define leveling
//levWeak   = weak   and keep
//levInvite = invite and keep
//levForce  = force  and keep
//levelTheDeal = (levWeak or levInvite or levForce)

wOpensWeak2 and (nActs or sActs)
//and levelTheDeal

action
average "North X" 100 * nActs,
average "South X" 100 * sActs,

`,"W",true)
Script
Button,Leb vs W2-Dir/Bal,\n\
--- Lebensohl After Opps Weak 2 -- Direct or Balanced Seat.\n\
After 2!D/2!H/2!S and a Take-Out Double，lebensohl continuations are...\n\
  • A new suit at the 2-level is natural and weak (0-7).\n\
  • A non-jump new suit at the 3-level shows 8-10 and no stop.\n\
  • Cue Bid openers suit to show major suit interest without a stop.\n\
  • Jump bidding a new suit at the 3-level shows 8-10 and no stop.\n\
  • 3N shows game forcing values without major suit interest and no stop -- pass or scramble.\n\
  • Bidding 2N is the Lebensohl relay to 3!C.  After partner's forced 3!C，you may...\n\
    ◦ Pass or Correct to a new suit below opener's suit (0-7).\n\
    ◦ Cue Bid openers suit to show major suit interest and with a stop.\n\
    ◦ Bid 3N to show a balanced hand without major suit interest AND with a stop.\n\
https://www.bridgebum.com/lebensohl_over_weak_two.php\n\
%LebensohlVsOppsW2%,backgroundColor=lightpink

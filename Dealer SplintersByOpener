Script,SplinterByOpener
setDealerCode(`


dealer south
produce 100

# Splinters by Opener
##### Define Calm Opponents #####
#Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/-Script-Calm-Opponents
# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

unbalEast = shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
unbalWest = shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

balEast   = shape(east,any 4333 +any 4432)
balWest   = shape(west,any 4333 +any 4432)

calmEast = ((unbalEast and hcp(east)<8) or (balEast and hcp(east)<12)) and noConEast
calmWest = ((unbalWest and hcp(west)<8) or (balWest and hcp(west)<12)) and noConWest
calmOpps = calmEast and calmWest
##### End of Define Calm Opponents #####

##### Predict South's opening bid #####
#Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/-Script-Predict-Opening-1-Bid
## Imported Script -- Predict Opening Bid ##

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and top4(south,spades)==1 and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and top4(south,hearts)==1 and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)==1 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and top4(south,clubs)==1 and not hascard(south,AC) ? 1 : 0
ntDownGrade = S2H + H2H + D2H + C2H

# Define notrump points for south (ntPoints)
ntPoints = hcp(south) + lengthPoints - ntDownGrade

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

# Define robot notrump shape and exclude any 5card 
ntShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422 -5xxx -x5xx)

# Define ntPoint ranges
oneNT   = ntShape and ntPoints>14 and ntPoints<18
twoNT   = ntShape and ntPoints>19 and ntPoints<22
threeNT = ntShape and ntPoints>25 and ntPoints<28
weakNT  = ntShape and ntPoints>10 and ntPoints<15

# Define Game Forcing 2C
case1 = hcp(south)>22
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT // or weakNT  weakNT will be duplicated diamond and club openings

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade = oS
oneHeart = oH
oneDiamond = oD                                                              // duplicates weakNT
oneClub = oC                                                                 // duplicates weakNT

##### End of Predict-Opening-Bid #####


###### Define Bidding Sequences #####
# These variables are defined in the imported script
# The number of cards South has in each suit: s, h, d, c
# South's predicted opening suit: oC, oD, oH, oS 

sP = hcp(south)
nP = hcp(north)

# Predict North's 1-level response
sN = spades(north)
hN = hearts(north)
dN = diamonds(north)
cN = clubs(north)

# North responds in D, H, or S (Walsh style), or raises C.  longer than C to avoid 2/1

# Predict North's responding suit
nS = sN>3 and sN>hN and sN>=cN
nH = hN>3           and hN>=cN and not (nS)
nD = dN>3           and dN>=cN and not (nS or nH)
nC = cN>4           and cN>4   and not (nS or nH or nD)
nN = not (nS or nH or nD or nC)

# Define South's possible rebids
sRC = clubs(south)>3
sRD = diamonds(south)>3
sRH = hearts(south)>3
sRS = spades(south)>3
sRN = shape(south,any 4333+any 4432+any 5332)

# Defining South's opening (not NT) & North's non-GF response
CD = oC and nD
CH = oC and nH
CS = oC and nS
CN = oC and nN
CC = oC and nC

DH = oD and nH
DS = oD and nS
DN = oD and nN
DD = oD and not (DH or DS or DN)

HS = sP<11 ? oH and nS and hN<3 : oH and nS and hN<4     // no Jacoby 2N
HN = oH and nN
HH = oH and not (HS or HN)
##### End of Define Bidding Sequences #####

# Don't include a singleton A, K, Q
C1 = shape(south,xxx1) and top3(south,clubs)   ==0
D1 = shape(south,xx1x) and top3(south,diamonds)==0
H1 = shape(south,x1xx) and top3(south,hearts)  ==0
S1 = shape(south,1xxx) and top3(south,spades)  ==0
any1 = (C1 or D1 or H1 or S1)
any0= shape(south,any 0xxx)
anyShort = any0 or any1

# Responder does NOT have direct support for Opener's suit
##   I may need to add this   ##

# South has support for responder's suit
supportD = (CD)             and diamonds(south)>4  and anyShort
supportH = (CH or DH)       and hearts(south)>3    and anyShort
supportS = (CS or DS or HS) and spades(south)>3    and anyShort
supportResponder = (supportD or supportH or supportS)

# North does not have direct support for south
northOK = supportD

# South has 13-14 HCP for invite or 15+ for game force
reverse = (CH or CS or DS or HS) and hcp(south)>12 and hcp(south)<19
jshift  = (CD or DH) and hcp(south)>14 and hcp(south)<19

giSplinter = reverse
gfSplinter = jshift

sOpens = giSplinter or gfSplinter  

northRange = hcp(north)>5  and hcp(north)<17  // no jump shifts

# Now do it
(giSplinter or gfSplinter) and supportResponder and anyShort and northRange and calmOpps

action
average "CD " 100 * CD,
average "CH " 100 * CH,
average "CS " 100 * CS,
average "DH " 100 * DH,
average "DS " 100 * DS,
average "HS " 100 * HS,
average "GI " 100 * giSplinter,
average "GF " 100 * gfSplinter,

`,"S",true)
Script
Button,Splinter's by Opener,\n\
--- Splinter by Opener\n\
An unusual jump is a splinter.\n\
A Jump Reverse is unusual.  Billy Miller refers to these 3-level splinters as 'splimit'.\n\
They're always below the 3-level or responder's suit and they're invitational to game.\n\
https://tinyurl.com/Billy-Miller-SpLimit-Bids\n\
Double jumpshifts are also unusual. They're above the 3-level of responder's suit; thusï¼Œgame forcing.\n\
Many also play that Jump's in 2/1 auctions are splinters. (Not Included Here)\n\
NOTE: The Robots play these 3-level splinters as GAME FORCING.\n\
%SplinterByOpener%

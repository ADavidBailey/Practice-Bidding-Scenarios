Script,Drury
setDealerCode(
`
# Reverse Drury

dealer south
produce 1000

# Caclulate support points (sPoints)
lp1 = spades(south)>4   ? spades(south)-4   : 0
lp2 = hearts(south)>4   ? hearts(south)-4   : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4    ? clubs(south)-4    : 0
sPoints = lp1 + lp2 + lp3 + lp4 + hcp(south)

# Define pesky opps e/w distributions and HCP.  We don’t want them mucking up our auctions
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/-Script-Calm-Opponents

notNT = not (shape(north, any 4333 +any 4432 +any 5332) and hcp(north)>14 and hcp(north)<18)

# South passes
sPasses = shape(south,xxxx-any 9xxx-any 8xxx-any 7xxx-any 6xxx-any 55xx) and hcp(south)<12

# Define North's opening major
nSpades = spades(north)>4 and hearts(south)<5
nHearts = hearts(north)>4 and spades(north)<5
nMajor  = nSpades or nHearts

# Define South's 3- or 4-card support
s3cards = (nSpades and spades(south)==3) or (nHearts and hearts(south)==3)
s4cards = (nSpades and spades(south)>3) or (nHearts and hearts(south)>3)
sSupport = s3cards or s4cards 

# Define responses
pass      = sPoints<4
raise2    = sPoints>3 and sPoints<8 and s3cards
raise3    = sPoints>3 and sPoints<8 and s4cards
splinter0 = shape(south,any 0xxx) and sPoints>11
splinter1 = shape(south,any 1xxx) and sPoints>11
drury     = sPoints>7  and sPoints<12 and not (splinter0 or splinter1)
game      = sPoints>11                and not (splinter0 or splinter1)

nOpens = nMajor and sSupport and hcp(north)>11 and hcp(north)<20 and notNT and shape(north,any 6xxx+any 5xxx-any 65xx-any 55xx)

sPasses and nOpens and calmOpps

action
#printoneline,
average "pass      " 100 * pass,
average "raise2    " 100 * raise2,
average "raise3    " 100 * raise3,
average "splinter0 " 100 * splinter0,
average "splinter1 " 100 * splinter1,
average "drury     " 100 * drury,
average "game      " 100 * game,
frequency "Support Points" (sPoints,0,16),

`,"S",true)
Script
Button,Drury,\n\
--- Drury\n\
After P (P) 1M (P)，2!C is Drury or Reverse Drury.\n\
2!C asks about the opener's hand.\n\
Drury:  2!D denies game interest and 2M shows game interest.\n\
Reverse Drury: 2!D shows some game interest and 2M denies game interest.\n\
%Drury%

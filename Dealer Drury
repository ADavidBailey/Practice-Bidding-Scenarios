Script,Drury
setDealerCode(
`

# Reverse Drury

dealer south
produce 1000

# Define pesky opps e/w distributions and HCP.  We don’t want them mucking up our auctions
#Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/-Script-Calm-Opponents

# Calm pesky opps e/w distributions and HCP.  We don’t want them mucking up our auctions
calmEast = (shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx) and hcp(east)<9) or (shape(east,any 4333 +any 4432) and hcp(east)<12)
calmWest = (shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx) and hcp(west)<9) or (shape(west,any 4333 +any 4432) and hcp(west)<12)
calmOpps = calmEast and calmWest

notNT = not (shape(north, any 4333 +any 4432 +any 5332) and hcp(north)>14 and hcp(north)<18)

# South passes

sPasses = shape(south,xxxx-any 9xxx-any 8xxx-any 7xxx-any 6xxx-any 65xx-any 55xx) and hcp(south)<12

# Define North's opening major
nSpades = spades(north)>4 and hearts(north)<5
nHearts = hearts(north)>4 and spades(north)<5
nMajor  = nSpades or nHearts

# Define South's new suit for 2-level response
sD5  = diamonds(south)==5 and top3(south,diamonds)>1
sH5  = hearts(south)  ==5 and top3(south,hearts)  >1
new5 = sD5 or sH5

# Define South's 3- or 4-card support
s3cards = (nSpades and spades(south)==3) or (nHearts and hearts(south)==3)
s4cards = (nSpades and spades(south)>3)  or (nHearts and hearts(south)>3)
sSupport = s3cards or s4cards

# Define South's Fit Jumps
fjC  = clubs(south)   >4 and top3(south,clubs)>1
fjD  = diamonds(south)>4 and top3(south,diamonds)>1
fjH  = hearts(south)  >4 and top3(south,hearts)>1 and nSpades
fjSuit = fjC or fjD or fjH

# Define South's Support Points (sPoints)
v1 = shape(south, any 0xxx) ? 5 : 0 // allow for 2 voids
v2 = shape(south, any 00xx) ? 5 : 0
s1 = shape(south, any 1xxx) ? 3 : 0 // allow for 2 singletons
s2 = shape(south, any 11xx) ? 3 : 0
d1 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
d2 = shape(south, any 22xx) ? 1 : 0
d3 = shape(south, any 222x) ? 1 : 0
shortSuitPoints = v1+v2+s1+s2+d1+d2+d3
sPoints = sSupport ? shortSuitPoints + hcp(south) : hcp(south)

singleton = shape(south,any 1xxx)
void      = shape(south,any 0xxx)
spadeSuit = spades(south)>3
noSupport = not sSupport

# Define responses
fitJump    = sPoints>7               and s4cards   and fjSuit
raise      = sPoints>3 and sPoints<8 and sSupport
splinter1  = sPoints>11              and sSupport  and singleton and not (fitJump)
splinter0  = sPoints>11              and sSupport  and void      and not (fitJump)
drury      = sPoints>7               and sSupport                and not (fitJump or raise or splinter1 or splinter0)
newSuit    = hcp(south)>9            and noSupport and new5
oneSp      = hcp(south)>5            and noSupport and spadeSuit and not (newSuit)
nt         = hcp(south)>5            and noSupport               and not (newSuit or oneSp)

pass      = not (fitJump or raise or splinter1 or splinter0 or drury or newSuit or oneSp or nt)
 
nOpens = nMajor and hcp(north)>11 and hcp(north)<20 and notNT and shape(north,any 6xxx+any 5xxx-any 65xx-any 55xx)

sPasses and nOpens and calmOpps
and not (oneSp or nt or pass)

action
#printoneline, 
average "fit jump   8+  sPts 4+fit  2nd suit " 100 * fitJump,
average "raise      4-7 sPts 3+fit           " 100 * raise,
average "splinter1  12+ sPts 3+fit  singleton" 100 * splinter1,
average "splinter0  12+ sPts 3+fit  void     " 100 * splinter0,
average "drury      8+  sPts 3+fit           " 100 * drury,
average "2D/2H      10+ hcp  no fit good suit" 100 * newSuit,
average "1S         6+  hcp  no fit 4+S      " 100 * oneSp,
average "1N         6+  hcp  no fit          " 100 * nt,
average "Pass                                " 100 * pass,
frequency "Support Points" (sPoints,0,16),

`,"S",true)
Script
Button,Drury,\n\
--- Drury\n\
After P (P) 1M (P)，2!C is Drury or Reverse Drury.\n\
2!C asks about the opener's hand.\n\
Drury:  2!D denies game interest and 2M shows game interest.\n\
Reverse Drury: 2!D shows some game interest and 2M denies game interest.\n\
%Drury%

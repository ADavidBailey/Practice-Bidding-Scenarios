Script,FourthSuitForcing
setDealerCode(`

dealer south
generate 10000000
produce 5000

# Define Calm Opponents
#Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/-Script-Calm-Opponents
# Define pesky opps e/w distributions and HCP.  We don’t want them mucking up our auctions
calmEast = (shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx) and hcp(east)<9) or (shape(east,any 4333 +any 4432) and hcp(east)<12)
calmWest = (shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx) and hcp(west)<9) or (shape(west,any 4333 +any 4432) and hcp(west)<12)
calmOpps = calmEast and calmWest

# Predict South's opening bid
#Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/-Script-Predict-Opening-1-Bid
## Imported Script -- Predict Opening Bid ##

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and top4(south,spades)==1 and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and top4(south,hearts)==1 and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)==1 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and top4(south,clubs)==1 and not hascard(south,AC) ? 1 : 0
ntDownGrade = S2H + H2H + D2H + C2H

# Define notrump points for south (ntPoints)
ntPoints = hcp(south) + lengthPoints - ntDownGrade

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

# Define robot notrump shape and exclude any 5card 
ntShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422 -5xxx -x5xx)

# Define ntPoint ranges
oneNT   = ntShape and ntPoints>14 and ntPoints<18
twoNT   = ntShape and ntPoints>19 and ntPoints<22
threeNT = ntShape and ntPoints>25 and ntPoints<28
weakNT  = ntShape and ntPoints>10 and ntPoints<15

# Define Game Forcing 2C
case1 = hcp(south)>22
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT // or weakNT  weakNT will be duplicated diamond and club openings

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade = oS
oneHeart = oH
oneDiamond = oD                                                              // duplicates weakNT
oneClub = oC                                                                 // duplicates weakNT

### End of Predict-Opening-Bid ###

sP = hcp(south)
nP = hcp(north)

# South has 2 suits and will not rebid NT
sTwoSuits = shape(south,any 55xx+any 5440+any 5431+any 4441)

# Predict North's 1-level responding suit
sN = spades(north)
hN = hearts(north)
dN = diamonds(north)
cN = clubs(north)

# North responds in D, H, or S (Walsh style)  longer than C to avoid 2/1
nUnbal = shape(north,any 7xxx+any 61xx+any 5431+any 4441)
nS = sN>hN and sN>=dN and sN>=cN  and nUnbal
nH = hN>=dN and hN>=cN            and nUnbal and not (nS)
nD = dN>=cN                       and nUnbal and not (nS or nH)

# North has No support for South's opening suit
nNc = clubs(north)   <4
nNd = diamonds(north)<4
nNh = hearts(north)  <3

# South has No direct support for North's responding suit
resS = nS and spades(south)  <3
resH = nH and hearts(south)  <3
resD = nD and diamonds(south)<3

# Define South's possible rebids
sC4 = clubs(south)>3
sD4 = diamonds(south)>3
sH4 = hearts(south)>3
sS4 = spades(south)>3

# South opens and North is short in South's suit and responds at the 1-level

oneC = oC and sTwoSuits and nNc and (nS or nH or nD)
oneD = oD and sTwoSuits and nNd and (nS or nH)
oneH = oH and sTwoSuits and nNh and (nS)

# Defining South's opening, North's response, and South's rebid
CD = oneC and resD and (sH4 or sS4)
CH = oneC and resH and (sS4 or (sD4 and sP>16))  // D rebid is a reverse
CS = oneC and resS and (sD4 or sH4) and sP>16    // S or H rebids are reverses
DH = oneD and resH and (sS4 or sC4)
DS = oneD and resS and (sC4 or (sH4 and sP>16))  // H rebid is a reverse
HS = oneH and resS and (sC4 or sD4)

(oneC or oneD or oneH) and (CD or CH or CS or DH or DS or HS) and calmOpps and sP>11 and sP<18 and (nP>12 and nP<16)  // no jump responses


action
#printoneline,

average "CD " 100 * CD,
average "CH " 100 * CH,
average "CS " 100 * CS,
average "DH " 100 * DH,
average "DS " 100 * DS,
average "HS " 100 * HS,
average "TP "       sP + nP,
average "S  "       sP,
average "N  "       nP,


`,"S",true)
Script
Button,4th Suit Forcing,\n\
--- Fourth Suit Forcing\n\
After 1!C，1!D，or 1!H，a suit response at the 1-level，and a new suit by\n\
opener，the 4th suit is artificial and game forcing.\n\
EXCEPTION -- 1!C - 1!D - 1!H - 1!S is natural and forcing one round.\n\
%FourthSuitForcing%

Script,FourthBidInviting
window.currentPBSScenario = 'FourthBidInviting';
window.currentPBSScenarioFilename = 'Fourth_Bid_Inviting';
console.log('PBS: Logging scenario selection for FourthBidInviting');
fetch('https://bba.harmonicsystems.com/api/scenario/select', {
    method: 'POST',
    headers: {
        'Content-Type': 'application/json',
        'X-Client-Version': '1.0.0'
    },
    body: JSON.stringify({
        scenario: 'FourthBidInviting',
        user: whoAmI() || 'anonymous'
    })
}).then(function(r){ console.log('PBS: Server response', r.status); }).catch(function(e){ console.log('PBS: Fetch error', e); });
setDealerCode(`

/*
auction-filter: \\n1[CDHS].Pass.1[DHSN].*Pass\\n[12][HSCDN].*Pass.[123][CDHSN].
*/
# Invite after xyz[12]
dealer south

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####
# returns calmOpps
##### Imported Script -- Predict Opening 1-Bid South #####
# 01/19/2026 -- Changed to use TP and separate ranges for C, D, and majors
# 03/30/2025 -- Changed to use gibNT

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422 -5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

## Calculate TP for South -- NOTE:  This uses different variable names than TP to avoid conflicts when both are used.
# Calculate South's Short-Suit Penalty Points
ssp = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
shp = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sdp = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
scp = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv01 = shape(south, any 0xxx) ? 3 : 0
ss01 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss02 = shape(south, any 11xx) ? 2 : 0
sd01 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd02 = shape(south, any 22xx) ? 1 : 0
sd03 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv01+ss01+ss02+sd01+sd02+sd03 -ssp-shp-sdp-scp

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22

# Define Gambling 3N
g3Shape = shape(south,any 9xxx +any 8xxx +any 7xxx -any 4xxx -any 5xxx -any 6xxx)
gamble3 = (top3(south,clubs)==3 and clubs(south)>6) or (top3(south,diamonds)==3 and diamonds(south)>6) and hcp(south)<12 and g3Shape

# Define Game Forcing 2C
case1 = hcp(south)>18 and sTP>21
case2 = hcp(south)>18 and sTP>19 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and sTP>19 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3)

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or gamble3 or twoNT or oneNT

# Define quality and length lower limits for 10 HCP openers

q2of3 = (top3(south,spades)>1 or top3(south,hearts)>1) and controls(south)>3
lgt5  = (spades(south)>4 or hearts(south)>4) and shape(south,any 7xxx +any 64xx +any 55xx +any 1xxx+any 0xxx -any 8xxx -any 54xx) and sTP>(hcp(south) +2)

# Predict South's Opening suit
# What's the longest suit?
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)

## GIB has different requirements for 1H/S, 1D, and 1C openings 
#Define range for 1 H/S
lS = s>4 and s>=h and s>=d and s>=c
lH = h>4 and h>s and h>=d and h>=c
lM = lS or lH

hsOK1 = lM and hcp(south)>11
hsOK2 = lM and hcp(south)>10 and sTP>12 and not hsOK1
hsOK3 = lM and hcp(south)>9  and lgt5 and q2of3 and not (hsOK1 or hsOK2)
hsRange = (hsOK1 or hsOK2 or hsOK3) and not P2

# Define 1D Range
dOK1 = hcp(south)>11 and (diamonds(south)>3 or shape(south, 4432))
dOK2 = hcp(south)>9  and sTP>12 and diamonds(south)>5 and not dOK1
dRange = (dOK1 or dOK2) and not P2

# Define 1C Range
cOK1 = hcp(south)>10 and sTP>12 and spades(south)==4
cOK2 = hcp(south)>9  and sTP>12 and clubs(south)>5 and not cOK1
cOK3 = hcp(south)>11 and clubs(south)>2            and not (cOK1 or cOK2)
cRange = (cOK1 or cOK2 or cOK3) and clubs(south)>2 and clubs(south)>diamonds(south) and not P2

oS = s>4 and s>=h and s>=d and s>=c and hsRange
oH = not oS and h>4 and h>=d and h>=c and hsRange
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and dRange
oC = not (oS or oH or oD) and cRange
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
# returns oS
##### Predict Response to 1-Bid ##### 

# These variables are defined in the imported scripts above
# The number of cards South has in each suit: s, h, d, c
# South's predicted opening suit: oC, oD, oH, oS 

sP = hcp(south)
nP = hcp(north)

# Predict North's 1-level response
sN = spades(north)
hN = hearts(north)
dN = diamonds(north)
cN = clubs(north)

# North responds in D, H, or S (Walsh style), or raises C.
######
# If less than 13, Walsh Style; otherwise, longest suit.
nS  =  nP<13 ? sN>3 and sN>hN : sN>3 and sN>hN and sN>=dN and sN>=cN
nH  = (nP<13 ? hN>3           : hN>3           and hN>=dN and hN>=cN) and not (nS)
nD = dN>3           and dN>=cN and not (nS or nH)
nC = cN>4           and cN>4   and not (nS or nH or nD)
nN = not (nS or nH or nD or nC)

# Define South's possible new-suit/NT rebids
sRC = clubs(south)>3
sRD = diamonds(south)>3
sRH = hearts(south)>3
sRS = spades(south)>3
sRN = shape(south,any 4333+any 4432+any 5332)


# Defining South's opening (not NT) & North's non-GF response
CD = oC and nD
CH = oC and nH
CS = oC and nS
CN = oC and nN
CC = oC and nC

DH = oD and nH
DS = oD and nS
DN = oD and nN
DD = oD and not (DH or DS or DN)

HS = oH and nS
HH = oH and hN>2 and not HS
HN = oH and not (HS or HH)

##### End of Predict Response to 1-Bid #####
# returns nN

SN = oS and nN


##### Predict Opener's Rebid #####
CDH = CD and sRH
CDS = CD and sRS and not (CDH)
CDN = CD and sRN and not (CDH or CDS)
CDC = CD and c>5 and not (CDH or CDS or CDN)
CDD = CD and d>3 and not (CDH or CDS or CDN or CDC)

CHS = CH and sRS
CHN = CH and sRN and not (CHS)
CHC = CH and c>5 and not (CHS or CHN)
CHD = CH and sRD and not (CHS or CHN or CHC)
CHH = CH and sRH and not (CHS or CHN or CHC or CHD)

CSN = CS and sRN
CSC = CS and c>5 and not (CSN)
CSD = CS and sRD and not (CSN or CSC)
CSH = CS and sRH and not (CSN or CSC or CSD)
CSS = CS and sRS and not (CSN or CSC or CSD or CSH)

CNC = CN and c>5
CND = CN and sRD and not (CNC)
CNH = CN and sRH and not (CNC or CND)
CNS = CN and sRS and not (CNC or CND or CNH)
CNN = CN and sRN and not (CNC or CND or CNH or CNS)

CCD = CC and sRD
CCH = CC and sRH and not (CCD)
CCS = CC and sRS and not (CCD or CCH)
CCN = CC and sRN and not (CCD or CCH or CCS)
CCC = CC and c>5 and not (CCD or CCH or CCS or CCN)

DHS = DH and sRS
DHN = DH and sRN and not (DHS)
DHC = DH and sRC and not (DHS or DHN)
DHD = DH and d>5 and not (DHS or DHC or DHN)
DHH = DH and sRH and not (DHS or DHC or DHN or DHD)

DSN = DS and sRN
DSC = DS and sRC and not (DSN)
DSD = DS and d>5 and not (DSN or DSC)
DSH = DS and sRH and not (DSN or DSC or DSD)
DSS = DS and sRS and not (DSN or DSC or DSD or DSH)

DNC = DN and sRC
DND = DN and d>5 and not (DNC)
DNH = DN and sRH and not (DNC or DND)
DNS = DN and sRS and not (DNC or DND or DNH)
DNN = DN and sRN and not (DNC or DND or DNH or DNS)


HSN = HS and sRN
HSC = HS and sRC and not (HSN)
HSD = HS and sRD and not (HSN or HSC)
HSH = HS and h>5 and not (HSN or HSC or HSD)
HSS = HS and sRS and not (HSN or HSC or HSD or HSH)

HNC = HN and sRC
HND = HN and sRD and not (HNC)
HNH = HN and h>5 and not (HNC or HND)
HNS = HN and sRS and not (HNC or HND or HNH)
HNN = HN and sRN and not (HNC or HND or HNH or HNS)
##### End of Predict Opener's Rebid #####
# returns a bunch of variables.  S,H,D,C,and N stand for Spades, Hearts, Diamonds, Clubs, and NT respectively
# for example SNC means 1S (P) 1N (P) 2C
SNC = SN and sRC
SND = SN and sRD and not (SNC)
SNH = SN and sRH and not (SNC or SND)
SNS = SN and sRS and not (SNC or SND or SNH)
SNN = SN and sRN and not (SNC or SND or SNH or SNS)

strength = hcp(south)>11 and hcp(south)<15 and hcp(north)>10 and hcp(north)<13
sShape = shape(south,any 5422 +any 5431 +any 4432 +any 4333)
nShape = shape(north,any 64xx +any 63xx +any 54xx +any 5332 +any 4432 +any 4441)

# Define north's short suits
s012 = shape(north,0xxx+1xxx+2xxx)
h012 = shape(north,x0xx+x1xx+x2xx)
d012 = shape(north,xx0x+xx1x+xx2x)
c012 = shape(north,xxx0+xxx1+xxx2)

# north is short in the 3rd or 4th suit and does not have 4-card support for opener's major
cdh = CDH and s012
cds = CDS and h012
cdn = CDN and (s012 or h012)
chs = CHS and d012           and hearts(south)<4
chn = CHN and (d012 or s012) and hearts(south)<4
csn = CSN and (d012 or h012) and spades(south)<4

dhs = DHS and c012           and hearts(south)<4
dhn = DHN and (s012 or c012) and hearts(south)<4
dhc = DHC and s012           and hearts(south)<4
dsn = DSN and (c012 or h012) and spades(south)<4
dsc = DSC and h012           and spades(south)<4
   
hsn = HSN and (c012 or d012) and spades(south)<4
hsc = HSC and d012           and spades(south)<4
hsd = HSD and c012           and spades(south)<4

snc = SNC and (d012 or h012)
snd = SND and c012
snh = SNH and (d012 or c012)

xyz = (cdh or cds or cdn or chs or chn or csn or dhs or dhn or dhc or dsn or dsc or hsn or hsc or hsd or snc or snd or snh)

xyz and strength and calmOpps and sShape and nShape

produce 100
action

average "64xx" 100 * shape(north,any 64xx),
average "63xx" 100 * shape(north,any 63xx),
average "5422" 100 * shape(north,any 5422),
average "5431" 100 * shape(north,any 5431),
average "5440" 100 * shape(north,any 5440),
average "5332" 100 * shape(north,any 5332),
average "4432" 100 * shape(north,any 4432),
average "4441" 100 * shape(north,any 4441),
average "nD  " 100 * nD,
average "nH  " 100 * nH,
average "nS  " 100 * nS,
average "nN  " 100 * nN,
average "CD  " 100 * CD,
average "CH  " 100 * CH,
average "CS  " 100 * CS,
average "CN  " 100 * CN,
average "DH  " 100 * DH,
average "DS  " 100 * DS,
average "DN  " 100 * DN,
average "HS  " 100 * HS,
average "HN  " 100 * HN,
average "SN  " 100 * SN,
average "CDH " 100 * CDH,
average "CDS " 100 * CDS,
average "CDN " 100 * CDN,
average "CHS " 100 * CHS,
average "CHN " 100 * CHN,
average "CSN " 100 * CSN,
average "DHS " 100 * DHS,
average "DHN " 100 * DHN,
average "DHC " 100 * DHC,
average "DSN " 100 * DSN,
average "DSC " 100 * DSC,
average "HSN " 100 * HSN,
average "HSC " 100 * HSC,
average "HSD " 100 * HSD,
average "SNC " 100 * SNC,
average "SND " 100 * SND,
average "SNH " 100 * SNH,
`, "S", true)
Script
Button,4th Bid Inviting,\n\
--- Fourth Bid Inviting\n\
After 3 bidsï¼Œresponder scrambles w/11-12.\n\
%FourthBidInviting%

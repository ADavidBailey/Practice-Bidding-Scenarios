Script,After1x1N
window.currentPBSScenario = 'After1x1N';
window.currentPBSScenarioFilename = 'After_1x_1N';
console.log('PBS: Logging scenario selection for After1x1N');
fetch('https://bba.harmonicsystems.com/api/scenario/select', {
    method: 'POST',
    headers: {
        'Content-Type': 'application/json',
        'X-Client-Version': '1.0.0'
    },
    body: JSON.stringify({
        scenario: 'After1x1N',
        user: whoAmI() || 'anonymous'
    })
}).then(function(r){ console.log('PBS: Server response', r.status); }).catch(function(e){ console.log('PBS: Fetch error', e); });
setDealerCode(`

/*
auction-filter: 1[CDHS] 1NT
*/
# After 1x (1N)

dealer south

##### Imported Script -- Predict Opening 1-Bid South #####
# 01/19/2026 -- Changed to use TP and separate ranges for C, D, and majors
# 03/30/2025 -- Changed to use gibNT

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422 -5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

## Calculate TP for South -- NOTE:  This uses different variable names than TP to avoid conflicts when both are used.
# Calculate South's Short-Suit Penalty Points
ssp = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
shp = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sdp = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
scp = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv01 = shape(south, any 0xxx) ? 3 : 0
ss01 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss02 = shape(south, any 11xx) ? 2 : 0
sd01 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd02 = shape(south, any 22xx) ? 1 : 0
sd03 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv01+ss01+ss02+sd01+sd02+sd03 -ssp-shp-sdp-scp

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22

# Define Gambling 3N
g3Shape = shape(south,any 9xxx +any 8xxx +any 7xxx -any 4xxx -any 5xxx -any 6xxx)
gamble3 = (top3(south,clubs)==3 and clubs(south)>6) or (top3(south,diamonds)==3 and diamonds(south)>6) and hcp(south)<12 and g3Shape

# Define Game Forcing 2C
case1 = hcp(south)>18 and sTP>21
case2 = hcp(south)>18 and sTP>19 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and sTP>19 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3)

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or gamble3 or twoNT or oneNT

# Define quality and length lower limits for 10 HCP openers

q2of3 = (top3(south,spades)>1 or top3(south,hearts)>1) and controls(south)>3
lgt5  = (spades(south)>4 or hearts(south)>4) and shape(south,any 7xxx +any 64xx +any 55xx +any 1xxx+any 0xxx -any 8xxx -any 54xx) and sTP>(hcp(south) +2)

# Predict South's Opening suit
# What's the longest suit?
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)

## GIB has different requirements for 1H/S, 1D, and 1C openings 
#Define range for 1 H/S
lS = s>4 and s>=h and s>=d and s>=c
lH = h>4 and h>s and h>=d and h>=c
lM = lS or lH

hsOK1 = lM and hcp(south)>11
hsOK2 = lM and hcp(south)>10 and sTP>12 and not hsOK1
hsOK3 = lM and hcp(south)>9  and lgt5 and q2of3 and not (hsOK1 or hsOK2)
hsRange = (hsOK1 or hsOK2 or hsOK3) and not P2

# Define 1D Range
dOK1 = hcp(south)>11 and (diamonds(south)>3 or shape(south, 4432))
dOK2 = hcp(south)>9  and sTP>12 and diamonds(south)>5 and not dOK1
dRange = (dOK1 or dOK2) and not P2

# Define 1C Range
cOK1 = hcp(south)>10 and sTP>12 and spades(south)==4
cOK2 = hcp(south)>9  and sTP>12 and clubs(south)>5 and not cOK1
cOK3 = hcp(south)>11 and clubs(south)>2            and not (cOK1 or cOK2)
cRange = (cOK1 or cOK2 or cOK3) and clubs(south)>2 and clubs(south)>diamonds(south) and not P2

oS = s>4 and s>=h and s>=d and s>=c and hsRange
oH = not oS and h>4 and h>=d and h>=c and hsRange
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and dRange
oC = not (oS or oH or oD) and cRange
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
# returns oC, oD, oH, oS

# West has a stop in South's opening suit -- at least Kxx or QJx -- for NT overcall
sC = oC and (hcp(west,clubs)==3    or hcp(west,clubs)==4)    and clubs(west)==3
sD = oD and (hcp(west,diamonds)==3 or hcp(west,diamonds)==4) and diamonds(west)==3
sH = oH and (hcp(west,hearts)==3   or hcp(west,hearts)==4)   and hearts(west)==3
sS = oS and (hcp(west,spades)==3   or hcp(west,spades)==4)   and spades(west)==3

# South opens & West has stop
sOpens = (sC or sD or sH or sS) and hcp(south)>11

# West overcalls 1N
wOvercalls1N = hcp(west)>14 and hcp(west)<18 and shape(west,any 5332 +any 4432 +any 4333 -5xxx -x5xx)

# Define North's suit quality for 2-suit hands -- all are at least 4-cards
nC2 = clubs(north)>3    and top3(north,clubs)>0
nD2 = diamonds(north)>3 and top3(north,diamonds)>0
nH2 = hearts(north)>3   and top3(north,hearts)>0
nS2 = spades(north)>3   and top3(north,spades)>0

# Define North's suit quality for 1-suit hands
nH1 = hearts(north)>4   and top3(north,hearts)>1
nS1 = spades(north)>4   and top3(north,spades)>1

# Define North's suits
nC5D5     = nC2 and nD2 and clubs(north)>4 and diamonds(north)>4      // 5+C & 5+D

nC4H4t9   = nC2 and nH2 and (clubs(north)    + hearts(north))>8       // 4+C & 4+H min 9
nC4S4t9   = nC2 and nS2 and (clubs(north)    + spades(north))>8       // 4+C & 4+S min 9

nD4H4t9   = nD2 and nH2 and (diamonds(north) + hearts(north))>8       // 4+D & 4+H min 9
nD4S4t9   = nD2 and nS2 and (diamonds(north) + spades(north))>8       // 4+D & 4+S min 9

nH4S4t8   = nH2 and nS2                                               // 4+H & 4+S min 8

nH6orS6   = (nH1 and hearts(north)>5) or (nS1 and spades(north)>5)    // 6+H or 6+S

sm = sC or sD
sM = sH or sS

# Define North's actions
# After 1m-1N
om2C = sm and nH4S4t8
om2D = sm and nH6orS6
om2N = sm and nC5D5

oC2H = sC and nD4H4t9
oC2S = sC and nD4S4t9

oD2H = sD and nC4H4t9
oD2S = sD and nC4S4t9
oMinor = om2C or om2D or om2N or oC2H or oC2S or oD2H or oD2S

# After 1M-1N
oM2C = sM and nD4H4t9
oM2D = sM and nC4H4t9
oM2N = sM and nC5D5

oH2C = sH and nC4S4t9
oH2D = sH and nD4S4t9
oH2S = sH and nS1

oS2C = sS and nC4H4t9
oS2D = sS and nD4H4t9
oS2H = sS and nH1
oMajor = oM2C or oM2D or oM2N or oH2C or oH2D or oH2S or oS2C or oS2D or oS2H

# Define North's TP for raise -- a max of 3 short suit points
v1 = shape(north, any 0xxx) ? 3 : 0
s1 = shape(north, any 1xxx) ? 2 : 0
d1 = shape(north, any 2xxx) ? 1 : 0
d2 = shape(north, any 22xx) ? 1 : 0
nSSP = v1+s1+d1+d2
nTP = nSSP>3 ? hcp(north) + 3 : hcp(north) + nSSP

nDoubles = hcp(north)>9
nBids    = (oMinor or oMajor) and hcp(north)>4 and hcp(north)<10
nRaises  = ((sH and hearts(north)>2) or (sS and spades(north)>2)) and nTP>4 and hcp(north)<10 and not nBids
nPasses  = not (nDoubles or nBids or nRaises)

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###

levDouble = nDoubles and keep03
lev1m2C   = om2C     and keep03
lev1m2D   = om2D     and keep
lev1m2N   = om2N     and keep
lev1C2H   = oC2H     and keep
lev1C2S   = oC2S     and keep
lev1D2H   = oD2H     and keep
lev1D2S   = oD2S     and keep
lev1M2C   = oM2C     and keep
lev1M2D   = oM2D     and keep
lev1M2N   = oM2N     and keep
lev1H2C   = oH2C     and keep
lev1H2D   = oH2D     and keep
lev1H2S   = oH2S     and keep
lev1S2C   = oS2C     and keep
lev1S2D   = oS2D     and keep
levBid    = nBids    and keep
levRaise  = nRaises  and keep19
levPass   = nPasses  and keep015

levelTheDeal = levDouble or lev1m2C or lev1m2D or lev1m2N or lev1C2H or lev1C2S or lev1D2H or lev1D2S or lev1M2C or lev1M2D or lev1M2N or lev1H2C or lev1H2D or lev1H2S or lev1S2C or lev1S2D or levBid or levRaise or levPass

sOpens and wOvercalls1N and (nDoubles or nBids or nRaises or nPasses)
and levelTheDeal

action
average "Double " 100 * nDoubles,
average "om2C   " 100 * om2C,
average "om2D   " 100 * om2D,
average "om2N   " 100 * om2N,
average "oC2H   " 100 * oC2H,
average "oC2S   " 100 * oC2S,
average "oD2H   " 100 * oD2H,
average "oD2S   " 100 * oD2S,

average "oM2C   " 100 * oM2C,
average "oM2D   " 100 * oM2D,
average "oM2N   " 100 * oM2N,
average "oH2C   " 100 * oH2C,
average "oH2D   " 100 * oH2D,
average "oH2S   " 100 * oH2S,
average "oS2C   " 100 * oS2C,
average "oS2D   " 100 * oS2D,
average "oS2H   " 100 * oS2H,

average "Raise  " 100 * nRaises,

average "Pass   " 100 * nPasses,
`, "S", true)
Script
Button,After 1x (1N),\n\
--- After 1x (1N)\n\
• Meckwell vs. 1M (1N)\n\
  • X - Penalty 10+\n\
  • 2!C - 4+!C & 4+oM & 5-9\n\
  • 2!D - 4+!D & 4+oM & 5-9\n\
  • 2oM - 5+oM & 5-9 Non-Force\n\
  • 2M - 3+M & 5-9 Non-Force\n\
• Woolsey vs. 1m (1N)\n\
  • X - Penalty 10+\n\
  • 2!C - both Majors (4+ 4+) & 5-9\n\
  • 2!D - one Major (usually 6+) & 5-9\n\
  • 2!H - 5+!H & 4+om & 5-9 Non-Force\n\
  • 2!S - 5+!S & 4+om & 5-9 Non-Force\n\
  • 2N - 5+ in both minors & 5-9\n\
  https://www.advinbridge.com/this-week-in-bridge/589\n\
%After1x1N%

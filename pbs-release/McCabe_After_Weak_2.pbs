Script,McCabeAfterWeak2
window.currentPBSScenario = 'McCabeAfterWeak2';
window.currentPBSScenarioFilename = 'McCabe_After_Weak_2';
console.log('PBS: Logging scenario selection for McCabeAfterWeak2');
fetch('https://bba.harmonicsystems.com/api/scenario/select', {
    method: 'POST',
    headers: {
        'Content-Type': 'application/json',
        'X-Client-Version': '1.0.0'
    },
    body: JSON.stringify({
        scenario: 'McCabeAfterWeak2',
        user: whoAmI() || 'anonymous'
    })
}).then(function(r){ console.log('PBS: Server response', r.status); }).catch(function(e){ console.log('PBS: Fetch error', e); });
setDealerCode(`

/*
auction-filter: Auction.....\n2[DHS] X
*/
# McCabe after Weak 2
# The McCabe Convention gives useful options for Responder after Partner’s Weak Two Bid is doubled.

dealer south

# Define South's Weak 2 Bids w/o 4-card major side suit -- Strict

     // at least half of south's points are in the suit
     // allow no 4-card side major

tp = hcp(south)
sp = hcp(south,spades)
hp = hcp(south,hearts)
dp = hcp(south,diamonds)
w2s = shape(south,6xxx-x4xx     -any 7xxx-any 66xx-any 65xx) and 2 * sp > tp and top4(south,spades)>1
w2h = shape(south,x6xx-4xxx     -any 7xxx-any 66xx-any 65xx) and 2 * hp > tp and top4(south,hearts)>1
w2d = shape(south,xx6x-x4xx-4xxx-any 7xxx-any 66xx-any 65xx) and 2 * dp > tp and top4(south,diamonds)>1
 
w2Range = tp>6 and tp<10 and losers(south)<8
weakTwo = (w2s or w2h or w2d) and w2Range

// Define responders hand types

//  • 4 of Opener’s suit – Preempt or Rule of 17 raise.
//  • Redouble – I have a long suit of my own. Please keep the bidding
//    open by bidding the cheapest denomination if Advancer passes so
//    that I may bid my suit or pass if you bid it.
//  • 2NT – Asks feature or Ogust just like without their double.
//  • Preempt - 3/4 of Opener’s suit.
//  • New suit – I want you to lead the suit I bid. I have a some spot cards
//    in you suit so you can rebid your own suit if Advancer passes.

# Define fit
sFit = w2s and spades(north)>2
hFit = w2h and hearts(north)>2
dFit = w2d and diamonds(north)>2
fit = sFit or hFit or dFit

### Define West X -- short in opener's suit and 4-cards in each unbid major
sShort = w2s and spades(west)<3 and shape(west,x4xx)
hShort = w2h and hearts(west)<3 and shape(west,4xxx)
dShort = w2d and diamonds(west)<3 and shape(west,44xx)
westX = hcp(west)>12 and (sShort or hShort or dShort) and shape(west,any 5440+any 4441+any 4432)

### Define Rule17 points -- FORCE TO GAME
sLen = sFit ? spades(north)   : 0
hLen = hFit ? hearts(north)   : 0
dLen = dFit ? diamonds(north) : 0
trumpLength = (sLen + hLen + dLen)
rule17 = (hcp(north) + trumpLength)>17

### Define Rule15       -- INVITE GAME
rule15 = (hcp(north) + trumpLength)>15   and not (rule17)

### Define Lead my suit -- MILD PREEMPT 
lS = top3(north,spades)==2   and not w2s
lH = top3(north,hearts)==2   and not w2h
lD = top3(north,diamonds)==2 and not w2d
lC = top3(north,clubs)==2
leadAsk = fit and (lS or lH or lD or lC) and not (rule17 or rule15)

### Define Mild preempt -- MILD PREEMPT
raise = fit and hcp(north)>6    and not(rule17 or rule15 or leadAsk)

### Define North's long & strong suits
sS = (spades(north)==5 and top4(north,spades)==4)     or (spades(north)>5   and top4(north,spades)==3)
sH = (hearts(north)==5 and top4(north,hearts)==4)     or (hearts(north)>5   and top4(north,hearts)==3)
sD = (diamonds(north)==5 and top4(north,diamonds)==4) or (diamonds(north)>5 and top4(north,diamonds)==3)
sC = (clubs(north)==5 and top4(north,clubs)==4)       or (clubs(north)>5    and top4(north,clubs)==3)
mySuit = (sS or sH or sD or sC) and not fit and not (rule17 or rule15 or leadAsk or raise) 

### Define pass
pass = not (rule17 or rule15 or leadAsk or raise or mySuit)

# Define leveling
### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###

# Level the deal
lRule17      = rule17     and keep75
lRule15      = rule15     and keep25
lLeadAsk     = leadAsk    and keep14
lRaise       = raise      and keep06
lmySuit      = mySuit     and keep94
lPass        = pass       and keep03
levelTheDeal = lRule17 or lRule15 or lLeadAsk or lRaise or lmySuit or lPass

# Now do it
weakTwo and westX and (rule17 or rule15 or leadAsk or raise or mySuit or pass)
and levelTheDeal

action
average "Rule of 17    " 100 * rule17,
average "Rule of 15    " 100 * rule15,
average "Lead Ask      " 100 * leadAsk,
average "Raise         " 100 * raise,
average "mySuit        " 100 * mySuit,
average "Pass          " 100 * pass,
`, "S", true)
Script
Button,McCabe over W2 (Lev),\n\
--- McCabe after Weak 2 (X) -- Leveled\n\
Opener has a 6-card suit without a 4-card side major，at least half of the HCP\n\
are in the suit，less than 11 HCP and less than 7 losers.  And West doubles.\n\
After the double，responder has six possible continuations:\n\
• Bid game - Preempt or Rule of 17\n\
• Raise below game - a mild preempt\n\
• Bid a new suit - Please lead this suit.\n\
• Bid 2N - I'm interested in game.  Do you have a feature?\n\
• Redouble - I have a long suit of my own\n\
• Pass - Good Luck!\n\
%McCabeAfterWeak2%,width=50%

Script,BergenRaises
window.currentPBSScenario = 'BergenRaises';
window.currentPBSScenarioFilename = 'Bergen_Raises';
console.log('PBS: Logging scenario selection for BergenRaises');
fetch('https://bba.harmonicsystems.com/api/scenario/select', {
    method: 'POST',
    headers: {
        'Content-Type': 'application/json',
        'X-Client-Version': '1.0.0'
    },
    body: JSON.stringify({
        scenario: 'BergenRaises',
        user: whoAmI() || 'anonymous'
    })
}).then(function(r){ console.log('PBS: Server response', r.status); }).catch(function(e){ console.log('PBS: Fetch error', e); });
setDealerCode(`

/*
convention-card-ns: 21GF-SPECIALS
convention-card-ew: 21GF-GIB
auction-filter: Auction.....\n1[HS] +Pass +3[CDHSN]
*/
/*
Select Bergen, 1M 3M blocking, and turn off 1N opening shape 5422
*/

# Bergen Raises
dealer north

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####
# Defines calmEast

### changed GIB-1N from South to North
# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(north) + shape(north,5xxx+x5xx)
nt1 = shape(north, any 5332+any 4432+any 4333) and hcp(north)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(north, any 5422-5xxx-x5xx) and hcp(north)>14 and hcp(north)<17

gibNT = nt1 or nt2
### changed GIB-1N from South to North

# Calculate Total Points for south (tp) for south
v1 = shape(south, any 0xxx) ? 5 : 0
s1 = shape(south, any 1xxx) ? 3 : 0
d1 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
d2 = shape(south, any 22xx) ? 1 : 0
d3 = shape(south, any 222x) ? 1 : 0
ssp = v1+s1+d1+d2+d3          // possible short suit points

# Short Suit Points is limited to the number of trump support cards
tp = ssp<5 ? ssp + hcp(south) : 4 + hcp(south)

# Define openers
sOpener = spades(north) >= hearts(north) and spades(north) >=5 and spades(north) >= diamonds(north) and spades(north) >= clubs(north)
hOpener = hearts(north) >=5 and not sOpener                    and hearts(north) >= diamonds(north) and hearts(north) >= clubs(north)

# Define suit fit w/4-card support
sFit = sOpener and spades(south)==4
hFit = hOpener and hearts(south)==4
fit = sFit or hFit
nOpens = fit and hcp(north)>11 and hcp(north)<20 and not gibNT

# 3M = Preemptive Raise 3-6 TP
case3M = tp>2 and tp<7

# 3C = Constructive 7-10 TP
case3C = tp>6 and tp<11

# 3oM = Ambiguous Splinter 12+ TP
case3oM = tp>11 and shape(south, any 14xx+any 04xx)

# 3N = 4333 13-15 TP/HCP
case3N = tp>12 and tp<16 and shape(south, any 4333)

# 3D = Limit Raise 11-12 TP
case3D = tp>10 and tp<13 and not (case3oM or case3N)

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###

lC  = case3C  and keep045
lD  = case3D  and keep11
loM = case3oM and keep06
lM  = case3M  and keep19
lN  = case3N  and keep
levelTheDeal = lC or lD or loM or lM or lN

# Now do it
nOpens and (case3M or case3C or case3oM or case3N or case3D) and calmEast
and levelTheDeal

produce 5000
action printoneline,
average "3C   7-10 TP      " 100 * case3C,
average "3D  11-12 TP      " 100 * case3D,
average "3oM 11-13 41/40xx " 100 * case3oM,
average "3M   0-6  TP      " 100 * case3M,
average "3N  13-15 4333    " 100 * case3N,
`, "N", true)
Script
Button,Bergen Raises (Lev),\n\
--- Bergen Raises all show exactly 4-card support.\n\
• 3!C = Constructive (7-10) TP\n\
• 3!D = Limit Raise (11-12) TP\n\
• 3M  = Preemptive Raise (3-6) TP\n\
• 3OM = Ambiguous Splinter (12+) TP\n\
• 3N  = 4333 (13-15) HCP\n\
https://www.bridgebum.com/bergen_raises.php\n\
%BergenRaises%,width=50% backgroundColor=lightpink

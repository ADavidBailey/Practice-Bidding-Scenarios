Script,NewMinorForce
setDealerCode(
`
produce 1000
dealer south

# New Minor Forcing

# Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/-Script-Predict-Opening-1-Bid

## Imported Script -- Predict Opening Bid ##

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and top4(south,spades)==1 and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and top4(south,hearts)==1 and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)==1 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and top4(south,clubs)==1 and not hascard(south,AC) ? 1 : 0
ntDownGrade = S2H + H2H + D2H + C2H

# Define notrump points for south (ntPoints)
ntPoints = hcp(south) + lengthPoints - ntDownGrade

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

# Define robot notrump shape and exclude any 5card 
ntShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422 -5xxx -x5xx)

# Define ntPoint ranges
oneNT   = ntShape and ntPoints>14 and ntPoints<18
twoNT   = ntShape and ntPoints>19 and ntPoints<22
threeNT = ntShape and ntPoints>25 and ntPoints<28
weakNT  = ntShape and ntPoints>10 and ntPoints<15

# Define Game Forcing 2C
case1 = hcp(south)>22
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT // or weakNT  weakNT will be duplicated diamond and club openings

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade = oS
oneHeart = oH
oneDiamond = oD                                                              // duplicates weakNT
oneClub = oC                                                                 // duplicates weakNT

### End of Predict-Opening-Bid ###

# Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/-Script-Calm-Opponents

# Define pesky opps e/w distributions and HCP.  We don’t want them mucking up our auctions
calmEast = (shape(east,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx) and hcp(east)<9) or (shape(east,any 4333 +any 4432) and hcp(east)<12)
calmWest = (shape(west,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx) and hcp(west)<9) or (shape(west,any 4333 +any 4432) and hcp(west)<12)
calmOpps = calmEast and calmWest

### End of Boiler Plate ###

# South opens 1C, 1D, or 1H, a 12-15 hcp balanced hand, and no fit for responder's major
sOpens = (oC or oD or oH) and shape(south,any 5332+any 4432+any 4333-4xxx-x4xx) and hcp(south)>11 and hcp(south)<15

# North responds 1H or 1S with a 5-card major, an invitational hand and no fit for opener's major

nH = shape(north,x5xx)
nS = shape(north,5xxx)
sHasFit = (nH and hearts(south)>2) or (nS and spades(south)>2)

nResponds = (((oC or oD) and (nH or nS)) or (oH and nS)) and shape(north,xxxx-any 55xx-any 65xx-any 75xx) and hcp(north)>10 and hcp(north)<13

# South rebids 1N
sOpens and nResponds and calmOpps

action
printoneline,
average "1C     " 100 * oC,
average "1D     " 100 * oD,
average "1H     " 100 * oH,
average "   - 1H" 100 * nH,
average "   - 1S" 100 * nS,
average "fit    " 100 * sHasFit,

`,"S",true)
Script
Button,New Minor Force,\n\
--- New Minor Forcing\n\
Partner opens 1!C，1!D， or 1!H\n\
You respond 1!H or 1!S with a 5-card suit\n\
Partner rebids 1N\n\
%NewMinorForce%

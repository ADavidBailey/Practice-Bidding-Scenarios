##### Imported Script -- Predict Opening Bid #####

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and top4(south,spades)==1 and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and top4(south,hearts)==1 and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)==1 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and top4(south,clubs)==1 and not hascard(south,AC) ? 1 : 0
ntDownGrade = S2H + H2H + D2H + C2H

# Define notrump points for south (ntPoints)
ntPoints = hcp(south) + lengthPoints - ntDownGrade

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

# Define robot notrump shape and exclude any 5card 
ntShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422 -5xxx -x5xx)

# Define ntPoint ranges
oneNT   = ntShape and ntPoints>14 and ntPoints<18
twoNT   = ntShape and ntPoints>19 and ntPoints<22
threeNT = ntShape and ntPoints>25 and ntPoints<28
weakNT  = ntShape and ntPoints>10 and ntPoints<15


# Define Game Forcing 2C
case1 = hcp(south)>22
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)

gameForce2C = (case1 or case2 or case3) and not threeNT


### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT // or weakNT  weakNT will be duplicated diamond and club openings

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade = oS
oneHeart = oH
oneDiamond = oD                                                              // duplicates weakNT
oneClub = oC                                                                 // duplicates weakNT


### End of Predict-Opening-Bid ###

P3 = P2 or openingSuit
preempt = shape(south, any 9xxx+any 8xxx+any 7xxx) and hcp(south)<11 and not P3

# Define South's Weak 2 Bids w/any 4-card suit -- Loose

     // at least half of south's points are in the suit
     // allow 4-card side major

tp = hcp(south)
sp = hcp(south,spades)
hp = hcp(south,hearts)
dp = hcp(south,diamonds)
w2Range = tp>4 and tp<11 and losers(south)<7
 
w2sLax = shape(south,6xxx-x6xx-x5xx) and 2 * sp > tp    
w2hLax = shape(south,x6xx-6xxx-5xxx) and 2 * hp > tp
w2dLax = shape(south,xx6x-x6xx-x5xx-6xxx-5xxx) and 2* dp > tp
 

w2sStrict = shape(south,6xxx-x6xx-x5xx-x4xx) and 2 * sp > tp    
w2hStrict = shape(south,x6xx-6xxx-5xxx-4xxx) and 2 * hp > tp
w2dStrict = shape(south,xx6x-x6xx-x5xx-x4xx-6xxx-5xxx-4xxx) and 2* dp > tp

weakTwoStrict = (w2sStrict or w2hStrict or w2dStrict) and w2Range and not P3
weakTwoLax = (w2sLax or w2hLax or w2dLax) and w2Range and not P3             // duplicates weakTwoStrict 

P4 = P3 or weakTwoLax or weakTwoStrict or preempt

pass = not P4

condition
threeNT or gameForce2C or twoNT or oneNT or weakNT or openingSuit or weakTwoLax or weakTwoStrict or preempt or pass

action

average "3 Notrump             " 100 * threeNT,
average "2 Clubs               " 100 * gameForce2C,
average "2 Notrump             " 100 * twoNT,
average "1 Notrump             " 100 * oneNT,
average "Weak NT (dup)         " 100 * weakNT,
average "1 Spade               " 100 * oneSpade,
average "1 Heart               " 100 * oneHeart,
average "1 Diamond             " 100 * oneDiamond,
average "1 Club                " 100 * oneClub,
average "Weak Two Strict (dup) " 100 * weakTwoStrict,
average "Weak Two Lax          " 100 * weakTwoLax,
average "Preempt               " 100 * preempt,
average "Pass                  " 100 * pass,

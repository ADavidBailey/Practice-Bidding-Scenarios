Script,McCabeAfterWeak2
setDealerCode(`

# McCabe after Weak 2
# The McCabe Convention gives useful options for Responder after Partner’s Weak Two Bid is doubled.
generate 10000000
produce 1000
dealer south

# Define South's Weak 2 Bids w/o 4-card major side suit -- Strict

     // at least half of south's points are in the suit
     // allow no 4-card side major

tp = hcp(south)
sp = hcp(south,spades)
hp = hcp(south,hearts)
dp = hcp(south,diamonds)
 
w2s = shape(south,6xxx-x6xx-x4xx-any 5xxx)           and 2 * sp > tp
w2h = shape(south,x6xx-6xxx-4xxx-any 5xxx)           and 2 * hp > tp
w2d = shape(south,xx6x-x6xx-x4xx-6xxx-4xxx-any 5xxx) and 2 * dp > tp
 
w2Range = tp>6 and tp<11 and losers(south)<8
 
weakTwo = (w2s or w2h or w2d) and w2Range

// Define responders hand types

//  • 4 of Opener’s suit – Preempt or Rule of 17 raise.
//  • Redouble – I have a long suit of my own. Please keep the bidding
//    open by bidding the cheapest denomination if Advancer passes so
//    that I may bid my suit or pass if you bid it.
//  • 2NT – Asks feature or Ogust just like without their double.
//  • Preempt - 3/4 of Opener’s suit.
//  • New suit – I want you to lead the suit I bid. I have a some spot cards
//    in you suit so you can rebid your own suit if Advancer passes.

# Define fit
sFit = w2s and spades(north)>2
hFit = w2h and hearts(north)>2
dFit = w2d and diamonds(north)>2
fit = sFit or hFit or dFit

### Define West X
sShort = w2s and spades(west)<3
hShort = w2h and hearts(west)<3
dShort = w2d and diamonds(west)<3
westX = hcp(west)>13 and (sShort or hShort or dShort) and shape(west,any 5440+any 4441+any 4432)

### Define Rule17 points -- FORCE TO GAME
sLen = sFit ? spades(north)   : 0
hLen = hFit ? hearts(north)   : 0
dLen = dFit ? diamonds(north) : 0
trumpLength = (sLen + hLen + dLen)
rule17 = (hcp(north) + trumpLength)>17

### Define Rule15       -- INVITE GAME
rule15 = (hcp(north) + trumpLength)>15   and not (rule17)

### Define Lead my suit -- MILD PREEMPT 
lS = top3(north,spades)==2   and not w2s
lH = top3(north,hearts)==2   and not w2h
lD = top3(north,diamonds)==2 and not w2d
lC = top3(north,clubs)==2
leadAsk = fit and (lS or lH or lD or lC) and not (rule17 or rule15)

### Define Mild preempt -- MILD PREEMPT
raise = fit and hcp(north)>6    and not(rule17 or rule15 or leadAsk)

### Define North's long & strong suits
sS = (spades(north)==5 and top4(north,spades)==4)     or (spades(north)>5   and top4(north,spades)==3)
sH = (hearts(north)==5 and top4(north,hearts)==4)     or (hearts(north)>5   and top4(north,hearts)==3)
sD = (diamonds(north)==5 and top4(north,diamonds)==4) or (diamonds(north)>5 and top4(north,diamonds)==3)
sC = (clubs(north)==5 and top4(north,clubs)==4)       or (clubs(north)>5    and top4(north,clubs)==3)
mySuit = (sS or sH or sD or sC) and not fit and not (rule17 or rule15 or leadAsk or raise) 

### Define pass
pass = not (rule17 or rule15 or leadAsk or raise or mySuit)

# Define leveling
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/-Script-Leveling

lRule17      = rule17     and keep86
lRule15      = rule15     and keep25
lLeadAsk     = leadAsk    and keep14
lRaise       = raise      and keep06    //03 //and (c5 or c6)  // 44% of 03
lmySuit      = mySuit     and keep
lPass        = pass       and keep03    //03 //and (c5 or c6)  // 44% of 03
levelTheDeal = lRule17 or lRule15 or lLeadAsk or lRaise or lmySuit or lPass

# Now do it
weakTwo and westX and (rule17 or rule15 or leadAsk or raise or mySuit or pass)
and levelTheDeal

action
average "Rule of 17    " 100 * rule17,
average "Rule of 15    " 100 * rule15,
average "Lead Ask      " 100 * leadAsk,
average "Raise         " 100 * raise,
average "mySuit        " 100 * mySuit,
average "Pass          " 100 * pass,


/*
Rule of 17    : 20.4545
Rule of 15    : 14.2045
Lead Ask      : 19.3182
Raise         : 17.0455
mySuit        : 17.0455
Pass          : 11.9318
Generated 40000000 hands
Produced 176 hands
*/

`,"S",true)
Script
Button,McCabe over W2 (Lev) ,\n\
--- McCabe after Weak 2 (X) -- Leveled\n\
Opener has a 6-card suit without a 4-card side major，at least half of the HCP\n\
are in the suit，less than 11 HCP and less than 7 losers.  And West doubles.\n\
%McCabeAfterWeak2%

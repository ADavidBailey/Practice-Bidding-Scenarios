Script,McCabeAfterWeak2
setDealerCode(`

# The McCabe Convention gives useful options for Responder after Partner’s Weak Two Bid is doubled.

produce 10000
dealer south

# Define South's Weak 2 Bids w/o 4-card major side suit -- Strict

     // at least half of south's points are in the suit
     // allow no 4-card side major

tp = hcp(south)
sp = hcp(south,spades)
hp = hcp(south,hearts)
dp = hcp(south,diamonds)
 
w2s = shape(south,6xxx-x6xx-x5xx-x4xx) and 2 * sp > tp    
w2h = shape(south,x6xx-6xxx-5xxx-4xxx) and 2 * hp > tp
w2d = shape(south,xx6x-x6xx-x5xx-x4xx-6xxx-5xxx-4xxx) and 2 * dp > tp
 
w2Range = tp>4 and tp<11 and losers(south)<7
 
weakTwo = (w2s or w2h or w2d) and w2Range

// Define responders hand types

//  • 4 of Opener’s suit – Preempt or Rule of 17 raise.
//  • Redouble – I have a long suit of my own. Please keep the bidding
//    open by bidding the cheapest denomination if Advancer passes so
//    that I may bid my suit or pass if you bid it.
//  • 2NT – Asks feature or Ogust just like without their double.
//  • Preempt - 3/4 of Opener’s suit.
//  • New suit – I want you to lead the suit I bid. I have a some spot cards
//    in you suit so you can rebid your own suit if Advancer passes.

# Define fit
sFit = w2s and spades(north)>2
hFit = w2h and hearts(north)>2
dFit = w2d and diamonds(north)>2
fit = sFit or hFit or dFit

### Define West X
sShort = w2s and spades(west)<2
hShort = w2h and hearts(west)<2
dShort = w2d and diamonds(west)<2
westX = hcp(west)>13 and (sShort or hShort or dShort) and shape(west,any 5440+any 5431+any 4441 -5xxx -x5xx)

### Define Rule17 points
sLen = sFit ? spades(north)   : 0
hLen = hFit ? hearts(north)   : 0
dLen = dFit ? diamonds(north) : 0
trumpLength = (sLen + hLen + dLen)
rule17 = (hcp(north) + trumpLength)>17

### Define North's solid suits
sS = (spades(north)==5 and top4(north,spades)==4) or (spades(north)>5 and top3(north,spades)==3)
sH = (hearts(north)==5 and top4(north,hearts)==4)or (heart(north)>5 and top3(north,hearts)==3)
sD = (diamonds(north)==5 and top4(north,diamonds)==4) or (diamonds(north)>5 and top3(north,diamonds)==3)
sC = (clubs(north)==5 and top4(north,clubs)==4) or (clubs(north)>5 and top3(north,clubs)==3)
solidSuit = (sS or sH or sD or sC) and not (rule17)

### Define Feature ask
rule15 = (hcp(north) + trumpLength)>15
featureAsk = fit and rule15 and not(rule17 or solidSuit)

### Define Mild preempt
raise = fit and hcp(north)>6 and not(rule17 or solidSuit or featureAsk)

### Define Lead my suit
lS = top3(north,spades)==2   and not w2s
lH = top3(north,hearts)==2   and not w2h
lD = top3(north,diamonds)==2 and not w2d
lC = top3(north,clubs)==2
suitToLead = (lS or lH or lD or lC)
leadMySuit = fit and suitToLead and not (rule17 or solidSuit or featureAsk or raise)

### Define pass
pass = not (rule17 or solidSuit or featureAsk or raise or leadMySuit)

# Define leveling
# Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/-Script-Leveling
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)
c5 = hascard(west,4C)
c6 = hascard(east,4D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions


keep03 = keep06 and keep44
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep97 = not keep03
keep0  = 0
keep   = 1

lRule17      = rule17     and keep
lSolidSuit   = solidSuit  and keep
lFeatureAsk  = featureAsk and keep
lRaise       = raise      and keep03 //and (c5 or c6)  // 44% of 03
lLeadMySuit  = leadMySuit and keep
lPass        = pass       and keep03 and (c5 or c6)  // 44% of 03

levelTheDeal = lRule17 or lSolidSuit or lFeatureAsk or lRaise or lLeadMySuit or lPass

# Now do it
weakTwo and westX and (rule17 or solidSuit or featureAsk or raise or leadMySuit or pass)
and levelTheDeal

action
average "Rule of 17    " 100 * rule17,
average "Solid Suit    " 100 * solidSuit,
average "FeatureAsk    " 100 * featureAsk,
average "Raise         " 100 * raise,
average "Lead My Suit  " 100 * leadMySuit,
average "Pass          " 100 * pass,

`,"S",true)
Script
Button,McCabe--W2 (X) ??,\n\
--- McCabe after Weak 2 (X)\n\
Opener has a 6-card suit without a 4-card side major，at least half of the HCP\n\
are in the suit，less than 11 HCP and less than 7 losers.  And West doubles.\n\
%McCabeAfterWeak2%

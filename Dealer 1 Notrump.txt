Script,Notrump
setDealerCode(
`
produce 1000
dealer south

### Define 1 Notrump Opening

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 or lp2 or lp3 or lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and top4(south,spades)==1 and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and top4(south,hearts)==1 and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)==1 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and top4(south,clubs)==1 and not hascard(south,AC) ? 1 : 0
ntDownGrade = (S2H or H2H or D2H or C2H)

# Define notrump points for south
ntPoints = ntDownGrade ? hcp(south) : hcp(south) + lengthPoints

ntMin = ntPoints<16
ntAvg = ntPoints==16
ntMax = ntPoints>16

# Define 1NT -- these include hands that might have rebid problems if opened in a suit
NT = shape(south, 2452+2425+2245+2254+4522 +any 4333+any 4432+any 5332) and ntPoints>14 and ntPoints<18

# Define Leveling
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)
c5 = hascard(west,4C)

reduce98 = c1 and c2 and c3
reduce94 = c1 and c2
reduce87 = c1 and (c2 or c3)
reduce75 = c1
reduce62 = (c1 or c2) and not (c3 and (c4 or c5)) 
reduce50 = c1 or c2
reduce38 = not reduce62
reduce25 = not reduce75
reduce13 = not reduce87
reduce06 = not reduce94
reduce02 = not reduce98

# Level it
levMin = not ntMin or reduce38
levAvg = not ntAvg or reduce25
levMax = not ntMax or reduce25
level = levMin and levAvg // and levMax

### Ready for Leveling

# Now OPEN 1NT

NT
and level

# Generate Statistics

action

average "Minimum     " 100 * ntMin,
average "Avg         " 100 * ntAvg,
average "Max         " 100 * ntMax,

frequency  "HCP South "  (hcp(south) , 14, 18),
frequency  "ntPoints  "  (ntPoints   , 14, 18),

`
)
Script
Button,15-17 Notrump Opening,\n\
---  15-17 Notrump Opening\n\
2452+2425+2245+2254+4522 +any 4333，4432，or 5332 distribution.\n\
Those 5422 hands are the ones where you are likely to have to reverse to show your suits.\n\
HCP adjusted +1 for any 5-card suit unless you have KQ，KJ，or QJ doubleton.\n\
%Notrump%

Script,TwoNTLeveled
setDealerCode(
`

### 2N and any response

dealer south
#produce 1000

##### Imported Script -- Predict Opening 1-Bid #####

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and   top4(south,spades)>0   and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and   top4(south,hearts)>0   and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)>0 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and    top4(south,clubs)>0    and not hascard(south,AC) ? 1 : 0
dgPoints = S2H + H2H + D2H + C2H

# Define notrump points for south (ntPoints)
ntPoints = lengthPoints>dgPoints ? hcp(south) + lengthPoints - dgPoints : hcp(south)

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

# Define robot notrump shape and exclude any 5card 
ntShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422 -5xxx -x5xx)

# Define ntPoint ranges
oneNT   = ntShape and ntPoints>14 and ntPoints<18
twoNT   = ntShape and ntPoints>19 and ntPoints<22
threeNT = ntShape and ntPoints>25 and ntPoints<28
weakNT  = ntShape and ntPoints>10 and ntPoints<15

# Define Game Forcing 2C
case1 = hcp(south)>22
case2 = hcp(south)>18 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3) and not threeNT

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or threeNT or twoNT or oneNT // or weakNT  weakNT will be duplicated diamond and club openings

# Predict South's Opening suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)
s1Range = suitPoints>11 and hcp(south)>9
oS = s>4 and s>=h and s>=d and s>=c and s1Range and not P2
oH = not oS and h>4 and h>=d and h>=c and s1Range and not P2
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and s1Range and not P2
oC = not (oS or oH or oD) and s1Range and not P2
openingSuit = (oS or oH or oD or oC)
oneSpade = oS
oneHeart = oH
oneDiamond = oD                                                              // duplicates weakNT
oneClub = oC                                                                 // duplicates weakNT
hcp(south)>0
and oneNT and hcp(south)<15

##### End of Imported Script -- Predict Opening 1-Bid #####

### NORTH ###
# Calculate strength for North -- hcp plus 1 for every card over 4 in any suit 
nlp1 = spades(north)>4 ? spades(north)-4 : 0
nlp2 = hearts(north)>4 ? hearts(north)-4 : 0
nlp3 = diamonds(north)>4 ? diamonds(north)-4 : 0
nlp4 = clubs(north)>4 ? clubs(north)-4 : 0
strength = nlp1 + nlp2 + nlp3 + nlp4 + hcp(north)

cN = clubs(north)
dN = diamonds(north)
hN = hearts(north)
sN = spades(north)

smolen   = shape(north,54xx+45xx-any 7xxx-any 6xxx-any 55xx)       and strength>4
stayman  = shape(north,44xx+4xxx+x4xx-54xx-45xx-any 7xxx-any 6xxx) and strength>4 and not smolen
puppet   = shape(north,4xxx+3xxx+x4xx+x3xx-any 6xxx -any 5xxx)     and strength>4 and not (smolen or stayman)
MSS      = ((cN>4 and dN>3) or (dN>4 and cN>3))                    and strength>4 and not (smolen or stayman or puppet)
oneMinor = ((cN>5 and dN<4) or (dN>5 and cN<4))                    and strength>4 and not (stayman or puppet)
balanced = shape(north,any 4333 +any 4432 +any 5332 -5xxx-x5xx)    and strength>4 and not (smolen or stayman or puppet or MSS)
texas    = (sN>5 or hN>5)                                          and strength>4
jacoby   = ((sN>4 and hN<4) or (hN>4 and sN<4))                                   and not texas
pass     = strength<5 and not(jacoby)

part = strength<5
game = strength>4 and strength<12
slam = strength>11

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###


levPass     = pass     and keep25
levBalanced = balanced and keep67
levPuppet   = puppet   and keep56
levSmolen   = smolen   and keep
levStayman  = stayman  and keep14
levTexas    = texas    and keep56
levJacoby   = jacoby   and keep19
levOneMinor = oneMinor and keep67
levMSS      = MSS      and keep
levelTheDeal = (levPass or levBalanced or levSmolen or levPuppet or levStayman or levTexas or levJacoby or levOneMinor or levMSS)

### Do it ###
twoNT
and levelTheDeal

### Generate Statistics ###
produce 10000

action
#printoneline,
average "Pass                  " 100 * pass,
average "Balanced, not Stayman " 100 * balanced, //and not (puppet or stayman or MSS)),
average "Puppet, not Stayman   " 100 * puppet,  // and not (stayman or MSS)),
average "Smolen                " 100 * smolen,
average "Stayman, not Smolen   " 100 * stayman,
average "Texas                 " 100 * texas,
average "Jacoby, not Texas     " 100 * jacoby,   // and not texas),
average "One Minor             " 100 * oneMinor, // and not (puppet or stayman or jacoby)),
average "MSS, not Stayman      " 100 * MSS,      // and not (puppet or stayman)),

average "Partscore    " 100 * part,
average "Game         " 100 * game,
average "Slam         " 100 * slam,

`,"S",true)
Script
Button,(Lev),\n\
---  20-21 Notrump and any Response. The various continuations -- %blue%Pass，Balanced raises%~%，\n\
%blue%Puppet Stayman，Smolen，Stayman，Texas，Jacoby，6-card minor，and Minor-Suit Stayman%~% -- are leveled.\n\
The responder's strength -- %blue%partscore，game，or slam%~% -- is not leveled.\n\
%TwoNTLeveled%,width=12% color=blue

Script,McCabeAfterWJO
setDealerCode(`

# The McCabe Convention gives useful options for Advancer after Partner’s Weak Jump Overcall is doubled.
generate 30000000
produce 10000
dealer east

# Define East's Opening
eastOpens = hcp(east)>11 and hcp(east)<15 and spades(east)<5 and hearts(east)<5

# Define South's Weak Jump Overcall

     // at least half of south's points are in the suit
     // allow no 4-card side major

tp = hcp(south)
sp = hcp(south,spades)
hp = hcp(south,hearts)
dp = hcp(south,diamonds)
 
w2s = shape(south,6xxx-x6xx-any 8xxx-any 7xxx-any 65xx)      and 2 * sp > tp    
w2h = shape(south,x6xx-6xxx-any 8xxx-any 7xxx-any 65xx)      and 2 * hp > tp
w2d = shape(south,xx6x-6xxx-x6xx-any 8xxx-any 7xxx-any 65xx) and 2 * dp > tp
 
w2Range = tp>4 and tp<10 and losers(south)<7
 
weakJumpOvercall = (w2s or w2h or w2d) and w2Range

// Define responders hand types

//  • 4 of Opener’s suit – Preempt or Rule of 17 raise.
//  • Redouble – I have a long suit of my own. Please keep the bidding
//    open by bidding the cheapest denomination if Advancer passes so
//    that I may bid my suit or pass if you bid it.
//  • 2NT – Asks feature or Ogust just like without their double.
//  • Preempt - 3/4 of Opener’s suit.
//  • New suit – I want you to lead the suit I bid. I have a some spot cards
//    in you suit so you can rebid your own suit if Advancer passes.

# Define fit
sFit = w2s and spades(north)>2
hFit = w2h and hearts(north)>2
dFit = w2d and diamonds(north)>2
fit = sFit or hFit or dFit

### Define West X
sShort = w2s and spades(west)<2
hShort = w2h and hearts(west)<2
dShort = w2d and diamonds(west)<2
westX = hcp(west)>5 and (sShort or hShort or dShort) and shape(west,any 5440+any 4441 -5xxx -x5xx)

### Define Rule17 points -- FORCE TO GAME
sLen = sFit ? spades(north)   : 0
hLen = hFit ? hearts(north)   : 0
dLen = dFit ? diamonds(north) : 0
trumpLength = (sLen + hLen + dLen)
rule17 = (hcp(north) + trumpLength)>17

### Define Rule15       -- INVITE GAME
rule15 = (hcp(north) + trumpLength)>15   and not (rule17)

### Define Lead my suit -- MILD PREEMPT 
lS = top3(north,spades)==2   and not w2s
lH = top3(north,hearts)==2   and not w2h
lD = top3(north,diamonds)==2 and not w2d
lC = top3(north,clubs)==2
leadAsk = fit and (lS or lH or lD or lC) and not (rule17 or rule15)

### Define Mild preempt -- MILD PREEMPT
raise = fit and hcp(north)>6    and not(rule17 or rule15 or leadAsk)

### Define North's long & strong suits
sS = (spades(north)==5 and top4(north,spades)==4)     or (spades(north)>5   and top4(north,spades)==3)
sH = (hearts(north)==5 and top4(north,hearts)==4)     or (hearts(north)>5   and top4(north,hearts)==3)
sD = (diamonds(north)==5 and top4(north,diamonds)==4) or (diamonds(north)>5 and top4(north,diamonds)==3)
sC = (clubs(north)==5 and top4(north,clubs)==4)       or (clubs(north)>5    and top4(north,clubs)==3)
mySuit = (sS or sH or sD or sC) and not fit and not (rule17 or rule15 or leadAsk or raise) 

### Define pass
pass = not (rule17 or rule15 or leadAsk or raise or mySuit)

# Define leveling
Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/-Script-Leveling

# Level the deal
lRule17      = rule17     and keep
lRule15      = rule15     and keep19
lLeadAsk     = leadAsk    and keep14
lRaise       = raise      and keep03
lmySuit      = mySuit     and keep
lPass        = pass       and keep015
levelTheDeal = lRule17 or lRule15 or lLeadAsk or lRaise or lmySuit or lPass

# Now do it
eastOpens and weakJumpOvercall and westX and (rule17 or rule15 or leadAsk or raise or mySuit or pass)
and levelTheDeal

action
average "Rule of 17" 100 * rule17,
average "Rule of 15" 100 * rule15,
average "Lead Ask  " 100 * leadAsk,
average "Raise     " 100 * raise,
average "mySuit    " 100 * mySuit,
average "Pass      " 100 * pass,

average "East  HCP "       hcp(east),
average "South HCP "       hcp(south),
average "West  HCP "       hcp(west),
average "North HCP "       hcp(north),

/*
Rule of 17: 15.493
Rule of 15: 12.6761
Lead Ask  : 25.3521
Raise     : 21.1268
mySuit    : 21.1268
Pass      : 4.22535
East  HCP : 12.5775
South HCP : 8.43662
West  HCP : 8.49296
North HCP : 10.493
Generated 20000000 hands
Produced 71 hands
*/

`,"E",true)
Script
Button,McCabe over WJO (Lev),\n\
--- McCabe after Partner's Weak Jump Overcall is Doubled -- Leveled.\n\
After the opponents open and partner's weak jump overcal is doubled...\n\
%McCabeAfterWJO%

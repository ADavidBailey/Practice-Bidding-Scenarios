Script,NegativeFreeBid
setDealerCode(
`

# Negative Free Bid

# North's opening suit
nS = spades(north)>=hearts(north)>4>=diamonds(north)>=clubs(north)
nH = not nS and hearts(north)>4>=diamonds(north)>=clubs(north)
nD = not nS and not nH and diamonds(north)>3>clubs(north)      // 4432 will open 1D
nC = not nS and not nH and not nD

nRS = nS ? 4 : 0
nRH = nH ? 3 : 0
nRD = nD ? 2 : 0
nRC = nC ? 1 : 0
northRank = nRS+nRH+nRD+nRC

# East's longest suit
eS = spades(east)>=hearts(east)>4>=diamonds(east)>=clubs(east)
eH = not eS and hearts(east)>4>=diamonds(east)>=clubs(east)
eD = not eS and not eH and diamonds(east)>=clubs(east)
eC = not eS and not eH and not eD

eRS = eS ? 4 : 0
eRH = eH ? 3 : 0
eRD = eD ? 2 : 0
eRC = eC ? 1 : 0
eastRank = eRS+eRH+eRD+eRC

# South's longest suit
sS = spades(south) >= hearts(south)> 4 >= diamonds(south) >= clubs(south)
sH = not sS and hearts(south) > 4 >= diamonds(south) >= clubs(south)
sD = not sS and not sH and diamonds(south) >= clubs(south)
sC = not sS and not sH and not sD

sRS = sS ? 4 : 0
sRH = sH ? 3 : 0
sRD = sD ? 2 : 0
sRC = sC ? 1 : 0
southRank = sRS + sRH + sRD + sRC

# North opens
northOpens = hcp(north)>11 and hcp(north)<15

# East overcalls
eastOvercalls = hcp(east)>10 and shape(east, any 6xxx+5xxx - any 65xx-55xx)

# South responds -- a non-forcing hand with a good 5-card or any 6-card suit and 5-11 HCP

# Good suit South -- KQJxx or better
gSS = (
   top4(south,clubs)>2
or
   top4(south,diamonds)>2
or
   top4(south,hearts)>2
or
   top4(south,spades)>2
)
a6 = shape(south, any 7xxx+6xxx - any 76xx-75xx-66xx-65xx-55xx)  // any 6+
a5 = shape(south, any 5xxx - any 7xxx-6xxx-55xx)
g5 = a5 and gSS                                                  // good 5       

a6g5 = (a6 or g5) and (hcp(south) > 4 and hcp(south) < 10)  // (any 6+ or good 5) and 5-9
g6 = a6 and gSS and hcp(south) > 9 and hcp(south) < 12      // good 6+ and 10-12

rankOK = (northRank > eastRank) or (eastRank > southRank)   // eliminate 1-level bids for south

sFit = nS and spades(south)>2
hFit = nH and hearts(south)>2
noFit = not(sFit or hFit)                                   //  eliminate hands where South has support for North's major

southResponds = (noFit and rankOK) and (a6g5 or g6) 

# Now do it
northOpens and eastOvercalls and southResponds

`, "N", true);
Script
Button,Negative Free Bid,\n\
--- Negative Free Bid\n\
North Opens & East Overcalls and South has no Major suit fit w/North and\n\
5-9 HCP with a good 5- or any 6-card suit (non-jump) or\n\
10-11 HCP with a good 6+ card suit (jump)\n\
https://kwbridge.com/bb/nfb-1.htm\n\
%NegativeFreeBid%







Script,MeckwellsetDealerCode(`

dealer east
produce 30000

# Define East's 1N Opener
NT = hcp(east)>14 and hcp(east)<18 and shape(east,any 4333+any 4432+any 5422+any 5332-5xxx-x5xx)

# Define South's good suits
sGS = top3(south,spades)>0   and top5(south,spades)>1
sGH = top3(south,hearts)>0   and top5(south,hearts)>1
sGD = top3(south,diamonds)>0 and top5(south,diamonds)>1
sGC = top3(south,clubs)>0    and top5(south,clubs)>1

# Define South's suit lengths
sS = spades(south)
sH = hearts(south)
sD = diamonds(south)
sC = clubs(south)

# Define South's shapes
majorAndLongerC   = ((sGS and sS>3) or (sGH and sH>3)) and sGC and sC>sS and sC>sH 
majorAndLongerD   = ((sGS and sS>3) or (sGH and sH>3)) and sGD and sD>sS and sD>sH 
bothMajors        = sGS and sGH and shape(south,66xx+65xx+56xx+55xx+54xx+45xx)
oneSuit           = shape(south,any 9xxx+any 8xxx+any 7xxx+any 6xxx-any 76xx-any 5xxx-any 4xxx)
goodHand          = oneSuit and (sGS or sGH or sGD or sGC) and hcp(south)>15
oneMinor          = oneSuit and (sGD or sGC)
bothMinors        = sGD and sGC and shape(south,xx76+xx67+xx66+xx65+xx56+xx55+xx54+xx45)

# Define South's Meckwell action
cX  = oneMinor or bothMajors or goodHand
c2C = majorAndLongerC              and not cX
c2D = majorAndLongerD              and not cX
c2H = oneSuit and hearts(south)>4  and not cX
c2S = oneSuit and spades(south)>4  and not cX
c2N = bothMinors

# Import,https://github.com/ADavidBailey/Practice-Bidding-Scenarios/blob/main/-Script-Leveling

c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015= keep06 and c3
keep03 = keep06 and keep44
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep97 = not keep03
keep985= not keep015
keep   = 1
keep0  = 0

# Define Leveling
lX = cX  and keep14
lC = c2C and keep70
lD = c2D and keep81
lH = c2H and keep985
lS = c2S and keep97
lN = c2N and keep89
levelTheDeal = lX or lC or lD or lH or lS or lN

# Now OPEN 1NT
NT and (cX or c2C or c2D or c2H or c2S or c2N) and hcp(south)>9
and levelTheDeal

# Generate Statistics
action
average "X    " 100 * cX,
average "2C   " 100 * c2C,
average "2D   " 100 * c2D,
average "2H   " 100 * c2H,
average "2S   " 100 * c2S,
average "2N   " 100 * c2N,
frequency  "HCP South "  (hcp(south) , 7, 18),

`,"E",true)
Script
Button,Meckwell (Lev),\n\
---  15-17 Notrump Opening and Meckwell action.\n\
  • X   = !C，!D，!H & !S，or a good hand\n\
  • 2!C = !C and a major\n\
  • 2!D = !D and a major\n\
  • 2!H = 5+ !H\n\
  • 2!S = 5+ !S\n\
  • 2N  = 5-5 or better in !C & !D\n\
The Meckwell actions are leveled.  The NT opener is not leveled.\n\
https://www.bridgebum.com/meckwell_defense_to_1nt.php\n\
%Meckwell%

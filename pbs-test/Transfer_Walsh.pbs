Script,TransferWalsh
window.currentPBSScenario = 'TransferWalsh';
window.currentPBSScenarioFilename = 'Transfer_Walsh';
console.log('PBS: Logging scenario selection for TransferWalsh');
fetch('https://bba.harmonicsystems.com/api/scenario/select', {
    method: 'POST',
    headers: {
        'Content-Type': 'application/json',
        'X-Client-Version': '1.0.0'
    },
    body: JSON.stringify({
        scenario: 'TransferWalsh',
        user: whoAmI() || 'anonymous'
    })
}).then(function(r){ console.log('PBS: Server response', r.status); }).catch(function(e){ console.log('PBS: Fetch error', e); });
setDealerCode(`

/*
auction-filter: Auction.....\\n1C.[PX1].* (X|1[DHSN]|[23]N)
*/
# Transfer Walsh


dealer south

##### Imported Script -- Predict Opening 1-Bid South #####
# 01/19/2026 -- Changed to use TP and separate ranges for C, D, and majors
# 03/30/2025 -- Changed to use gibNT

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422 -5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

## Calculate TP for South -- NOTE:  This uses different variable names than TP to avoid conflicts when both are used.
# Calculate South's Short-Suit Penalty Points
ssp = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
shp = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sdp = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
scp = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv01 = shape(south, any 0xxx) ? 3 : 0
ss01 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss02 = shape(south, any 11xx) ? 2 : 0
sd01 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd02 = shape(south, any 22xx) ? 1 : 0
sd03 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv01+ss01+ss02+sd01+sd02+sd03 -ssp-shp-sdp-scp

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22

# Define Gambling 3N
g3Shape = shape(south,any 9xxx +any 8xxx +any 7xxx -any 4xxx -any 5xxx -any 6xxx)
gamble3 = (top3(south,clubs)==3 and clubs(south)>6) or (top3(south,diamonds)==3 and diamonds(south)>6) and hcp(south)<12 and g3Shape

# Define Game Forcing 2C
case1 = hcp(south)>18 and sTP>21
case2 = hcp(south)>18 and sTP>19 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and sTP>19 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3)

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or gamble3 or twoNT or oneNT

# Define quality and length lower limits for 10 HCP openers

q2of3 = (top3(south,spades)>1 or top3(south,hearts)>1) and controls(south)>3
lgt5  = (spades(south)>4 or hearts(south)>4) and shape(south,any 7xxx +any 64xx +any 55xx +any 1xxx+any 0xxx -any 8xxx -any 54xx) and sTP>(hcp(south) +2)

# Predict South's Opening suit
# What's the longest suit?
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)

## GIB has different requirements for 1H/S, 1D, and 1C openings 
#Define range for 1 H/S
lS = s>4 and s>=h and s>=d and s>=c
lH = h>4 and h>s and h>=d and h>=c
lM = lS or lH

hsOK1 = lM and hcp(south)>11
hsOK2 = lM and hcp(south)>10 and sTP>12 and not hsOK1
hsOK3 = lM and hcp(south)>9  and lgt5 and q2of3 and not (hsOK1 or hsOK2)
hsRange = (hsOK1 or hsOK2 or hsOK3) and not P2

# Define 1D Range
dOK1 = hcp(south)>11 and (diamonds(south)>3 or shape(south, 4432))
dOK2 = hcp(south)>9  and sTP>12 and diamonds(south)>5 and not dOK1
dRange = (dOK1 or dOK2) and not P2

# Define 1C Range
cOK1 = hcp(south)>10 and sTP>12 and spades(south)==4
cOK2 = hcp(south)>9  and sTP>12 and clubs(south)>5 and not cOK1
cOK3 = hcp(south)>11 and clubs(south)>2            and not (cOK1 or cOK2)
cRange = (cOK1 or cOK2 or cOK3) and clubs(south)>2 and clubs(south)>diamonds(south) and not P2

oS = s>4 and s>=h and s>=d and s>=c and hsRange
oH = not oS and h>4 and h>=d and h>=c and hsRange
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and dRange
oC = not (oS or oH or oD) and cRange
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
# Defines oneClub and allows a broad range of HCPs

# North responds
bal = shape(north,any 5422+any 5332+any 4432+any 4333)

MH = hearts(north)>3
MS = spades(north)>3 and not MH
M0 = not (MH or MS)

# Define North's responses
n1D = MH
n1H = MS
n1N = M0 and bal and hcp(north)>9  and hcp(north)<13
n2N = M0 and bal and hcp(north)>12 and hcp(north)<16
n3N = M0 and bal and hcp(north)>15
#n1S = M0 and not (n1N or n2N or n3N) and diamonds(north)>clubs(north)
#n2C = M0 and not (n1N or n2N or n3N or n1S)
n2C = M0 and not (n1N or n2N or n3N) and clubs(north)>4
n1S = M0 and not (n1N or n2N or n3N or n2C)

nResponds = hcp(north)>5

# South open's 1C and North Responds -- Transfer Walsh is on in competition
oneClub and hcp(south)>11 and nResponds   // BBO does not open most 11s


#  produce 40
generate 20000000
action
#  printoneline,

average "1D        " 100 * n1D,
average "1H        " 100 * n1H,
average "1S        " 100 * n1S,
average "1N        " 100 * n1N,
average "2N        " 100 * n2N,
average "3N        " 100 * n3N,
average "2C        " 100 * n2C,
average "All       " 100 * (n1D or n1H or n1S or n1N or n2C or n2N or n3N),
average "1S & D>4  " 100 * (n1S and diamonds(north)>4),
`, "S", true)
Script
Button,Transfer Walsh,\n\
--- Transfer Walsh w/X-Y-NT\n\
After 1!C...\n\
• 1!D = 4+!H\n\
• 1!H = 4+!S\n\
• 1!S = No 4+ Major (usually 5+D)\n\
·· If opener rebids 1N...\n\
··• 2!C = relay to 2!D\n\
··• 2!D = artificial Game Force\n\
• 1N = 10-12 balanced\n\
• 2N = 13-15 balanced\n\
https://en.wikipedia.org/wiki/Transfer_Walsh#\n\
%TransferWalsh%

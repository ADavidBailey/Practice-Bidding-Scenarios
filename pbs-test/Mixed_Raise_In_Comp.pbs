Script,MixedRaiseInComp
window.currentPBSScenario = 'MixedRaiseInComp';
window.currentPBSScenarioFilename = 'Mixed_Raise_In_Comp';
console.log('PBS: Logging scenario selection for MixedRaiseInComp');
fetch('https://bba.harmonicsystems.com/api/scenario/select', {
    method: 'POST',
    headers: {
        'Content-Type': 'application/json',
        'X-Client-Version': '1.0.0'
    },
    body: JSON.stringify({
        scenario: 'MixedRaiseInComp',
        user: whoAmI() || 'anonymous'
    })
}).then(function(r){ console.log('PBS: Server response', r.status); }).catch(function(e){ console.log('PBS: Fetch error', e); });
setDealerCode(`

/*
convention-card: 21GF-MixedRaise
auction-filter: Note....Mixed raise
*/
# convention-card: 21GF-MixedRaise


# Changed default CC to include Mixed Raise and 1X-(Y)-2Z forcing. 

dealer west

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####
# Defines calmEast
##### Imported Script - Calculate Total Points for South #####

## Calculate TP for South
# Calculate South's Short-Suit Penalty Points
sSP = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
sHP = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sDP = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
sCP = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv1 = shape(south, any 0xxx) ? 3 : 0
ss1 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss2 = shape(south, any 11xx) ? 2 : 0
sd1 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd2 = shape(south, any 22xx) ? 1 : 0
sd3 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv1+ss1+ss2+sd1+sd2+sd3 -sSP-sHP-sDP-sCP

### End of Calculate TP for South ###
# Defines sTP

# Predict West's opening 1C, 1D, 1H
s = spades(west)
h = hearts(west)
d = diamonds(west)
c = clubs(west)
oS = s>4 and s>=h and s>=d and s>=c
oH = not oS and h>4 and h>=d and h>=c
oD = not (oS or oH) and ((d>3 and d>=c) or c<3)
oC = not (oS or oH or oD)

# East does not have support for West's opening suit AND West does not have a Gambling 3NT
oneHeart   = oH and hearts(east)<3
oneDiamond = oD and diamonds(east)<4 and hcp(west,diamonds)<9
oneClub    = oC and clubs(east)<4    and hcp(west,clubs)<9    

wSuit = oneHeart or oneDiamond or oneClub
wRange = hcp(west)>11 and hcp(west)<15
wOpens = wRange and wSuit

# North overcalls 1H or 1S and South has 6-10 HCP and 4+ Card Support
nSpades  = spades(north)>4 and spades(south)>3 and hcp(north,spades)>4
nHearts  = hearts(north)>4 and hearts(south)>3 and hcp(north,hearts)>4
nShape   = shape(north,xxxx -any 76xx -any 75xx -any 66xx -any 65xx - any 55xx)  // No 2-suited hands
nRange   = hcp(north)>7 and hcp(north)<15

nWeak2   = shape(north,any 6xxx) and hcp(north)<11              // GIB plays WJO and Preempts as 7-10 HCP.
nPreempt = shape(north,any 8xxx +any 7xxx) and hcp(north)<11
nOvercalls = (nSpades or nHearts) and nShape and nRange and not (nWeak2 or nPreempt)

# South has 4+ Card Support and 2+ Controls (A=2, K=1) and at least 1 short-suit point
sShape   = shape(south,xxxx -any 4333 +any 2xxx +any 1xxx +any 0xxx)
sRange   = sTP>6 and sTP<10 and controls(south)>1   // BBO defines Mixed Raise as 7-9 TP

sRaises = sShape and sRange

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###

supCards = nHearts ? hearts(south) : spades(south)

void = shape(south,any 0xxx)
sing = shape(south,any 1xxx)

lev0 = void               and keep
lev1 = sing               and keep
lev2 = not (void or sing) and keep

lev4 = supCards==4        and keep
lev5 = supCards==5        and keep
lev6 = supCards>=6        and keep

levelTheDeal = lev2 or lev0 or lev1 or lev4 or lev5 or lev6

wOpens and nOvercalls and sRaises and calmEast
and levelTheDeal

action
average "N/S Spades     " 100 * nSpades,
average "N/S Hearts     " 100 * nHearts,
average "no shortness   " 100 * lev2,
average "singleton      " 100 * lev1,
average "void           " 100 * lev0,
average "4-cards support" 100 * (supCards==4),
average "5-cards support" 100 * (supCards==5),
average "6+cards support" 100 * (supCards==6),
`, "W", true)
Script
Button,Mixed Raise in Comp,\n\
---  Opponents open 1C/1D/1H.  We overcall one 1H/1S. Partner has\n\
4-card support with 7-9 TP and makes a mixed raise.\n\
%MixedRaiseInComp%

Script,DoubleDouble
window.currentPBSScenario = 'DoubleDouble';
window.currentPBSScenarioFilename = 'Double_double';
console.log('PBS: Logging scenario selection for DoubleDouble');
fetch('https://bba.harmonicsystems.com/api/scenario/select', {
    method: 'POST',
    headers: {
        'Content-Type': 'application/json',
        'X-Client-Version': '1.0.0'
    },
    body: JSON.stringify({
        scenario: 'DoubleDouble',
        user: whoAmI() || 'anonymous'
    })
}).then(function(r){ console.log('PBS: Server response', r.status); }).catch(function(e){ console.log('PBS: Fetch error', e); });
setDealerCode(`

/*
auction-filter: Auction.....\n1[CDHS] +[12][DHSC] +X
*/
# Double, double toil and trouble

dealer south

##### Imported Script -- Predict Opening 1-Bid South #####
# 01/19/2026 -- Changed to use TP and separate ranges for C, D, and majors
# 03/30/2025 -- Changed to use gibNT

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422 -5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

## Calculate TP for South -- NOTE:  This uses different variable names than TP to avoid conflicts when both are used.
# Calculate South's Short-Suit Penalty Points
ssp = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
shp = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sdp = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
scp = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv01 = shape(south, any 0xxx) ? 3 : 0
ss01 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss02 = shape(south, any 11xx) ? 2 : 0
sd01 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd02 = shape(south, any 22xx) ? 1 : 0
sd03 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv01+ss01+ss02+sd01+sd02+sd03 -ssp-shp-sdp-scp

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22

# Define Gambling 3N
g3Shape = shape(south,any 9xxx +any 8xxx +any 7xxx -any 4xxx -any 5xxx -any 6xxx)
gamble3 = (top3(south,clubs)==3 and clubs(south)>6) or (top3(south,diamonds)==3 and diamonds(south)>6) and hcp(south)<12 and g3Shape

# Define Game Forcing 2C
case1 = hcp(south)>18 and sTP>21
case2 = hcp(south)>18 and sTP>19 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and sTP>19 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3)

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or gamble3 or twoNT or oneNT

# Define quality and length lower limits for 10 HCP openers

q2of3 = (top3(south,spades)>1 or top3(south,hearts)>1) and controls(south)>3
lgt5  = (spades(south)>4 or hearts(south)>4) and shape(south,any 7xxx +any 64xx +any 55xx +any 1xxx+any 0xxx -any 8xxx -any 54xx) and sTP>(hcp(south) +2)

# Predict South's Opening suit
# What's the longest suit?
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)

## GIB has different requirements for 1H/S, 1D, and 1C openings 
#Define range for 1 H/S
lS = s>4 and s>=h and s>=d and s>=c
lH = h>4 and h>s and h>=d and h>=c
lM = lS or lH

hsOK1 = lM and hcp(south)>11
hsOK2 = lM and hcp(south)>10 and sTP>12 and not hsOK1
hsOK3 = lM and hcp(south)>9  and lgt5 and q2of3 and not (hsOK1 or hsOK2)
hsRange = (hsOK1 or hsOK2 or hsOK3) and not P2

# Define 1D Range
dOK1 = hcp(south)>11 and (diamonds(south)>3 or shape(south, 4432))
dOK2 = hcp(south)>9  and sTP>12 and diamonds(south)>5 and not dOK1
dRange = (dOK1 or dOK2) and not P2

# Define 1C Range
cOK1 = hcp(south)>10 and sTP>12 and spades(south)==4
cOK2 = hcp(south)>9  and sTP>12 and clubs(south)>5 and not cOK1
cOK3 = hcp(south)>11 and clubs(south)>2            and not (cOK1 or cOK2)
cRange = (cOK1 or cOK2 or cOK3) and clubs(south)>2 and clubs(south)>diamonds(south) and not P2

oS = s>4 and s>=h and s>=d and s>=c and hsRange
oH = not oS and h>4 and h>=d and h>=c and hsRange
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and dRange
oC = not (oS or oH or oD) and cRange
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####

# Predict West's overcall
sW = spades(west)
hW = hearts(west)
dW = diamonds(west)
cW = clubs(west)
wS = sW>=hW and sW>=dW and sW>=cW
wH = not wS and hW>=dW and hW>=cW
wD = not (wS or wH)    and (dW>=cW or cW==3)
wC = not (wS or wH or wD)

# Define West's good 5+ card suit and East's 3-card fit
ewSfit = wS and top3(west,spades)==2   and spades(west)==5   and spades(east)>2
ewHfit = wH and top3(west,hearts)==2   and hearts(west)==5   and hearts(east)>2
ewDfit = wD and top3(west,diamonds)==2 and diamonds(west)==5 and diamonds(east)>2
ewCfit = wC and top3(west,clubs)==2    and clubs(west)==5    and clubs(east)>2

w1suit = shape(west,any 5xxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)
ewFit = (ewSfit or ewHfit or ewDfit or ewCfit) and w1suit

# Define North/South fit -- no direct raise -- secondary support
nsCfit = oC and clubs(north)==3
nsDfit = oD and diamonds(north)==3
nsHfit = oH and hearts(north)==2
nsSfit = oS and spades(north)==2
nsFit = nsCfit or nsDfit or nsHfit or nsSfit

# Define north's two suits with room for secondary support and South has no direct raise
nsh = shape(north,44xx +45xx +54xx) and spades(south)<4   and hearts(south)<4    // S & H
nsd = shape(north,4x4x +4x5x)       and spades(south)<4   and diamonds(south)<4  // S & D
nsc = shape(north,4xx4 +4xx5)       and spades(south)<4   and clubs(south)<4     // S & C
nhd = shape(north,x44x +x45x)       and hearts(south)<4   and diamonds(south)<4  // H & D
nhc = shape(north,x4x4 +x4x5)       and hearts(south)<4   and clubs(south)<4     // H & C
ndc = shape(north,xx44 +xx45 +xx54) and diamonds(south)<4 and clubs(south)<4     // D & C

# North has the other two suits
cd = oC and ewDfit and nsh
ch = oC and ewHfit and nsd
cs = oC and ewSfit and nhd
dh = oD and ewHfit and nsc
ds = oD and ewSfit and nhc
hs = oH and ewSfit and ndc

# South Opens, West Overcalls at the 1-level, and North has the other two suits. 
sOpens = (cd or ch or cs or dh or ds or hs) and shape(south,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx)

lRanges = hcp(south)>=11 and hcp(west)>=8  and hcp(north)>=10 and hcp(east)>=5
uRanges = hcp(south)<=13 and hcp(west)<=10                    and hcp(east)<=8

sOpens and nsFit and ewFit and lRanges and uRanges

action
#printoneline,
average "1C (1D) " 100 * cd,
average "1C (1H) " 100 * ch,
average "1C (1S) " 100 * cs,
average "1D (1H) " 100 * dh,
average "1D (1S) " 100 * ds,
average "1H (1S) " 100 * hs,
`, "S", true)
Script
Button,Double double...,\n\
--- Double，double toil and trouble...\n\
After x (y)，X is a Negative Double.  It's for takeout. At the 1-level\n\
it promises 4-Cards in each unbid major. At higher levels you need a backup\n\
plan if partner doesn't have your major -- maybe a tolerance for opener's suit.\n\
If you double again，it's a scrambling takeout double.\n\
%DoubleDouble%,width=50%

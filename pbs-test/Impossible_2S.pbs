Script,Impossible2S
window.currentPBSScenario = 'Impossible2S';
window.currentPBSScenarioFilename = 'Impossible_2S';
console.log('PBS: Logging scenario selection for Impossible2S');
fetch('https://bba.harmonicsystems.com/api/scenario/select', {
    method: 'POST',
    headers: {
        'Content-Type': 'application/json',
        'X-Client-Version': '1.0.0'
    },
    body: JSON.stringify({
        scenario: 'Impossible2S',
        user: whoAmI() || 'anonymous'
    })
}).then(function(r){ console.log('PBS: Server response', r.status); }).catch(function(e){ console.log('PBS: Fetch error', e); });
setDealerCode(`

/*
convention-card: 21GF-Impossible2S
auction-filter: Auction.....\n(1H +Pass +1NT?.* Pass\n2[CD] +Pass +2S)
*/
# convention-card: 21GF-Impossible2S


# Turn on 'Imposible 2S' (sic); turn off '1N opening shape 5422'

# To find those that don't match add ?! after the opening parenthesis

# The bidding sequence is 1H Pass 1NT (forcing, semi-forcing, or not forcing) Pass 2C/D Pass 2S
dealer south

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####
# Defines calmOpps

##### Imported Script -- GIB 1 Notrump #####

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major or with 4 spades
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422-5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

### End of GIB 1 Notrump ###
# Defines gibNT

### Imported script: TP (Total Points)
//   Total Points = HCP + Short-Suit Points - Penalty Points
//   Short-Suit Points: 3 for void, 2 for singleton, 1 for doubleton
//   Penalty Points: 1 for each honor (K/Q/J) in a short suit (devalues wasted honors like Kx, Qx)

## Calculate TP for North
# Short-Suit Points: void=3, singleton=2, doubleton=1
nSpadeSSP = spades(north)<3   ? 3-spades(north)   : 0
nHeartSSP = hearts(north)<3   ? 3-hearts(north)   : 0
nDiamSSP  = diamonds(north)<3 ? 3-diamonds(north) : 0
nClubSSP  = clubs(north)<3    ? 3-clubs(north)    : 0
nSSP = nSpadeSSP + nHeartSSP + nDiamSSP + nClubSSP

# Penalty Points: deduct 1 for honors in short suits
nSpadePP = spades(north)<3   and hcp(north,spades)>0   ? 1 : 0
nHeartPP = hearts(north)<3   and hcp(north,hearts)>0   ? 1 : 0
nDiamPP  = diamonds(north)<3 and hcp(north,diamonds)>0 ? 1 : 0
nClubPP  = clubs(north)<3    and hcp(north,clubs)>0    ? 1 : 0
nPP = nSpadePP + nHeartPP + nDiamPP + nClubPP

tpNorth = hcp(north) + nSSP - nPP

## Calculate TP for East
eSpadeSSP = spades(east)<3   ? 3-spades(east)   : 0
eHeartSSP = hearts(east)<3   ? 3-hearts(east)   : 0
eDiamSSP  = diamonds(east)<3 ? 3-diamonds(east) : 0
eClubSSP  = clubs(east)<3    ? 3-clubs(east)    : 0
eSSP = eSpadeSSP + eHeartSSP + eDiamSSP + eClubSSP

eSpadePP = spades(east)<3   and hcp(east,spades)>0   ? 1 : 0
eHeartPP = hearts(east)<3   and hcp(east,hearts)>0   ? 1 : 0
eDiamPP  = diamonds(east)<3 and hcp(east,diamonds)>0 ? 1 : 0
eClubPP  = clubs(east)<3    and hcp(east,clubs)>0    ? 1 : 0
ePP = eSpadePP + eHeartPP + eDiamPP + eClubPP

tpEast = hcp(east) + eSSP - ePP

## Calculate TP for South
sSpadeSSP = spades(south)<3   ? 3-spades(south)   : 0
sHeartSSP = hearts(south)<3   ? 3-hearts(south)   : 0
sDiamSSP  = diamonds(south)<3 ? 3-diamonds(south) : 0
sClubSSP  = clubs(south)<3    ? 3-clubs(south)    : 0
sSSP = sSpadeSSP + sHeartSSP + sDiamSSP + sClubSSP

sSpadePP = spades(south)<3   and hcp(south,spades)>0   ? 1 : 0
sHeartPP = hearts(south)<3   and hcp(south,hearts)>0   ? 1 : 0
sDiamPP  = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
sClubPP  = clubs(south)<3    and hcp(south,clubs)>0    ? 1 : 0
sPP = sSpadePP + sHeartPP + sDiamPP + sClubPP

tpSouth = hcp(south) + sSSP - sPP

## Calculate TP for West
wSpadeSSP = spades(west)<3   ? 3-spades(west)   : 0
wHeartSSP = hearts(west)<3   ? 3-hearts(west)   : 0
wDiamSSP  = diamonds(west)<3 ? 3-diamonds(west) : 0
wClubSSP  = clubs(west)<3    ? 3-clubs(west)    : 0
wSSP = wSpadeSSP + wHeartSSP + wDiamSSP + wClubSSP

wSpadePP = spades(west)<3   and hcp(west,spades)>0   ? 1 : 0
wHeartPP = hearts(west)<3   and hcp(west,hearts)>0   ? 1 : 0
wDiamPP  = diamonds(west)<3 and hcp(west,diamonds)>0 ? 1 : 0
wClubPP  = clubs(west)<3    and hcp(west,clubs)>0    ? 1 : 0
wPP = wSpadePP + wHeartPP + wDiamPP + wClubPP

tpWest = hcp(west) + wSSP - wPP

## Partnership Totals
tpNS = tpNorth + tpSouth
tpEW = tpEast + tpWest

### End of Imported Script: TP
# Defines tpNorth

# allow for extreme shapes -- if south rebids 2H, north needs 6/5+ in the minors to bid the impossible 2S
hSouth = hearts(south)==5 or (hearts(south)>5 and shape(north,2065+2056+1165+1156) and (hcp(north,spades)+hcp(north,hearts))==0)

# limit spades to avoid a reverse and limit HCP to avoid a jump shift
oneH = hSouth and spade(south)<4 and diamonds(south)<6 and clubs(south)<6 and hcp(south)>10 and tpSouth>11 and tpSouth<18 and not gibNT

# Things to avoid...
twoOver1 = hcp(north)>12 and tpNorth>13

cNorth = clubs(north)>5 and hcp(north,clubs)>1
dNorth = diamonds(north)>5 and hcp(north,diamonds)>1
invJump = (cNorth or dNorth) and hcp(north)>8 and hcp(north)<12

# North is invitational with <4 spades and <3 hearts support
oneN = spades(north)<4 and hearts(north)<3 and hcp(north)>10 and not (twoOver1 or invJump) 

# South rebids a minor and North has support
rebidC = clubs(south)>=diamonds(south) and clubs(north)>3    and clubs(north)>diamonds(north)
rebidD = diamonds(south)>clubs(south)  and diamonds(north)>3 and diamonds(north)>clubs(north)
sRebid = (rebidC or rebidD)

# North is not suitable for a 2N rebid
nTwoS = shape(north,any 0xxx+any 1xxx)

# Now do it
oneH and oneN and sRebid and nTwoS and calmOpps

action
average "South H = 5  " 100 * (hearts(south)==5),
average "South H > 5  " 100 * (hearts(south)>5),
average "North 0/1 S  " 100 * (spades(north)<2),
average "North 0/1 H  " 100 * (hearts(north)<2),
average "North 6/5+CD " 100 * (clubs(north)>4 and diamonds(north)>4 and clubs(north)+diamonds(north)>10),
`, "S", true)
Script
Button,Impossible 2S,\n\
--- Impossible 2S\n\
South opens 1H with 12-18 HCP and 5+ !H.  North has an invitational\n\
minor oriented hand not suitable for NT or !H and responds 1NT with 11-12\n\
HCP.  With only 5!H South rebids a minor.  North has support for South's\n\
minor and rebids the Impossible 2!S.  If South rebids 2!Hï¼ŒNorth needs\n\
6/5 or better in the minors to bid the Impossible 2!S.\n\
http://kivabridgeclub.com/images/Impossible%202%20Spade.pdf\n\
%Impossible2S%,width=50%

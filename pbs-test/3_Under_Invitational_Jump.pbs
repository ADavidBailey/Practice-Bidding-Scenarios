Script,InvJump3Under
window.currentPBSScenario = 'InvJump3Under';
window.currentPBSScenarioFilename = '3_Under_Invitational_Jump';
console.log('PBS: Logging scenario selection for InvJump3Under');
fetch('https://bba.harmonicsystems.com/api/scenario/select', {
    method: 'POST',
    headers: {
        'Content-Type': 'application/json',
        'X-Client-Version': '1.0.0'
    },
    body: JSON.stringify({
        scenario: 'InvJump3Under',
        user: whoAmI() || 'anonymous'
    })
}).then(function(r){ console.log('PBS: Server response', r.status); }).catch(function(e){ console.log('PBS: Fetch error', e); });
setDealerCode(`

/*
auction-filter: (1D +Pass +3C|1H +Pass +3[CD]|1S +Pass +3[CDH])
*/
/*
convention-card: 21GF-3-Under-Inv-Jump
auction-filter: (1D +Pass +3C|1H +Pass +3[CD]|1S +Pass +3[CDH])

# Robot 3 Under Invitational Jump (9-11 HCP)
*/

##### Imported Script -- Predict Opening 1-Bid South #####
# 01/19/2026 -- Changed to use TP and separate ranges for C, D, and majors
# 03/30/2025 -- Changed to use gibNT

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422 -5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

## Calculate TP for South -- NOTE:  This uses different variable names than TP to avoid conflicts when both are used.
# Calculate South's Short-Suit Penalty Points
ssp = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
shp = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sdp = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
scp = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv01 = shape(south, any 0xxx) ? 3 : 0
ss01 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss02 = shape(south, any 11xx) ? 2 : 0
sd01 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd02 = shape(south, any 22xx) ? 1 : 0
sd03 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv01+ss01+ss02+sd01+sd02+sd03 -ssp-shp-sdp-scp

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22

# Define Gambling 3N
g3Shape = shape(south,any 9xxx +any 8xxx +any 7xxx -any 4xxx -any 5xxx -any 6xxx)
gamble3 = (top3(south,clubs)==3 and clubs(south)>6) or (top3(south,diamonds)==3 and diamonds(south)>6) and hcp(south)<12 and g3Shape

# Define Game Forcing 2C
case1 = hcp(south)>18 and sTP>21
case2 = hcp(south)>18 and sTP>19 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and sTP>19 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3)

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or gamble3 or twoNT or oneNT

# Define quality and length lower limits for 10 HCP openers

q2of3 = (top3(south,spades)>1 or top3(south,hearts)>1) and controls(south)>3
lgt5  = (spades(south)>4 or hearts(south)>4) and shape(south,any 7xxx +any 64xx +any 55xx +any 1xxx+any 0xxx -any 8xxx -any 54xx) and sTP>(hcp(south) +2)

# Predict South's Opening suit
# What's the longest suit?
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)

## GIB has different requirements for 1H/S, 1D, and 1C openings 
#Define range for 1 H/S
lS = s>4 and s>=h and s>=d and s>=c
lH = h>4 and h>s and h>=d and h>=c
lM = lS or lH

hsOK1 = lM and hcp(south)>11
hsOK2 = lM and hcp(south)>10 and sTP>12 and not hsOK1
hsOK3 = lM and hcp(south)>9  and lgt5 and q2of3 and not (hsOK1 or hsOK2)
hsRange = (hsOK1 or hsOK2 or hsOK3) and not P2

# Define 1D Range
dOK1 = hcp(south)>11 and (diamonds(south)>3 or shape(south, 4432))
dOK2 = hcp(south)>9  and sTP>12 and diamonds(south)>5 and not dOK1
dRange = (dOK1 or dOK2) and not P2

# Define 1C Range
cOK1 = hcp(south)>10 and sTP>12 and spades(south)==4
cOK2 = hcp(south)>9  and sTP>12 and clubs(south)>5 and not cOK1
cOK3 = hcp(south)>11 and clubs(south)>2            and not (cOK1 or cOK2)
cRange = (cOK1 or cOK2 or cOK3) and clubs(south)>2 and clubs(south)>diamonds(south) and not P2

oS = s>4 and s>=h and s>=d and s>=c and hsRange
oH = not oS and h>4 and h>=d and h>=c and hsRange
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and dRange
oC = not (oS or oH or oD) and cRange
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
# Defines oneClub, oneSpade, oneHeart, oneDiamond
##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####
# Defines calmWest
### Imported script: TP (Total Points)
//   Total Points = HCP + Short-Suit Points - Penalty Points
//   Short-Suit Points: 3 for void, 2 for singleton, 1 for doubleton
//   Penalty Points: 1 for each honor (K/Q/J) in a short suit (devalues wasted honors like Kx, Qx)

## Calculate TP for North
# Short-Suit Points: void=3, singleton=2, doubleton=1
nSpadeSSP = spades(north)<3   ? 3-spades(north)   : 0
nHeartSSP = hearts(north)<3   ? 3-hearts(north)   : 0
nDiamSSP  = diamonds(north)<3 ? 3-diamonds(north) : 0
nClubSSP  = clubs(north)<3    ? 3-clubs(north)    : 0
nSSP = nSpadeSSP + nHeartSSP + nDiamSSP + nClubSSP

# Penalty Points: deduct 1 for honors in short suits
nSpadePP = spades(north)<3   and hcp(north,spades)>0   ? 1 : 0
nHeartPP = hearts(north)<3   and hcp(north,hearts)>0   ? 1 : 0
nDiamPP  = diamonds(north)<3 and hcp(north,diamonds)>0 ? 1 : 0
nClubPP  = clubs(north)<3    and hcp(north,clubs)>0    ? 1 : 0
nPP = nSpadePP + nHeartPP + nDiamPP + nClubPP

tpNorth = hcp(north) + nSSP - nPP

## Calculate TP for East
eSpadeSSP = spades(east)<3   ? 3-spades(east)   : 0
eHeartSSP = hearts(east)<3   ? 3-hearts(east)   : 0
eDiamSSP  = diamonds(east)<3 ? 3-diamonds(east) : 0
eClubSSP  = clubs(east)<3    ? 3-clubs(east)    : 0
eSSP = eSpadeSSP + eHeartSSP + eDiamSSP + eClubSSP

eSpadePP = spades(east)<3   and hcp(east,spades)>0   ? 1 : 0
eHeartPP = hearts(east)<3   and hcp(east,hearts)>0   ? 1 : 0
eDiamPP  = diamonds(east)<3 and hcp(east,diamonds)>0 ? 1 : 0
eClubPP  = clubs(east)<3    and hcp(east,clubs)>0    ? 1 : 0
ePP = eSpadePP + eHeartPP + eDiamPP + eClubPP

tpEast = hcp(east) + eSSP - ePP

## Calculate TP for South
sSpadeSSP = spades(south)<3   ? 3-spades(south)   : 0
sHeartSSP = hearts(south)<3   ? 3-hearts(south)   : 0
sDiamSSP  = diamonds(south)<3 ? 3-diamonds(south) : 0
sClubSSP  = clubs(south)<3    ? 3-clubs(south)    : 0
sSSP = sSpadeSSP + sHeartSSP + sDiamSSP + sClubSSP

sSpadePP = spades(south)<3   and hcp(south,spades)>0   ? 1 : 0
sHeartPP = hearts(south)<3   and hcp(south,hearts)>0   ? 1 : 0
sDiamPP  = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
sClubPP  = clubs(south)<3    and hcp(south,clubs)>0    ? 1 : 0
sPP = sSpadePP + sHeartPP + sDiamPP + sClubPP

tpSouth = hcp(south) + sSSP - sPP

## Calculate TP for West
wSpadeSSP = spades(west)<3   ? 3-spades(west)   : 0
wHeartSSP = hearts(west)<3   ? 3-hearts(west)   : 0
wDiamSSP  = diamonds(west)<3 ? 3-diamonds(west) : 0
wClubSSP  = clubs(west)<3    ? 3-clubs(west)    : 0
wSSP = wSpadeSSP + wHeartSSP + wDiamSSP + wClubSSP

wSpadePP = spades(west)<3   and hcp(west,spades)>0   ? 1 : 0
wHeartPP = hearts(west)<3   and hcp(west,hearts)>0   ? 1 : 0
wDiamPP  = diamonds(west)<3 and hcp(west,diamonds)>0 ? 1 : 0
wClubPP  = clubs(west)<3    and hcp(west,clubs)>0    ? 1 : 0
wPP = wSpadePP + wHeartPP + wDiamPP + wClubPP

tpWest = hcp(west) + wSSP - wPP

## Partnership Totals
tpNS = tpNorth + tpSouth
tpEW = tpEast + tpWest

### End of Imported Script: TP
# Defines tpSouth

# Define NoFit
sFit = oneSpade   and spades(north)>2
hFit = oneHeart   and hearts(north)>2
dFit = oneDiamond and diamonds(north)>3
noFit = not (sFit or hFit or dFit)

# Define North's good suits -- 6+ cards with at least QJ or better
goodHearts   = hearts(north)>5   and hcp(north,hearts)>2
goodDiamonds = diamonds(north)>5 and hcp(north,diamonds)>2
goodClubs    = clubs(north)>5    and hcp(north,clubs)>2

# Define North's minimum suits -- 6+ with at least the Q
minHearts    = hearts(north)>5   and hcp(north,hearts)>1
minDiamonds  = diamonds(north)>5 and hcp(north,diamonds)>1
minClubs     = clubs(north)>5    and hcp(north,clubs)>1
nMinHCP = (goodHearts or goodDiamonds or goodClubs) ? 8 : 9  // Better HCP w/minimum suit

# north does NOT have a major
noMajorNorth = spades(north)<4 and hearts(north)<4

# For opening with minimum values, require extra suit length
sMin = hcp(south)<12 and tpSouth<13
sShape = sMin ? shape(south,any 7xxx+any 6xxx) : shape(south, xxxx)

n3C = (oneDiamond or oneHeart or oneSpade) and goodClubs and noMajorNorth
n3D = (oneHeart or oneSpade) and goodDiamonds and noMajorNorth
n3H = oneSpade and goodHearts
nHand = (n3C or n3D or n3H) and shape(north,any 9xxx +any 8xxx +any 7xxx +any 6xxx -any 85xx -any 76xx -any 66xx -any 5xxx)

# (Predict South's opening bid -- trying to eliminate 2C openings
sRange = hcp(south)>10 and tpSouth>11 and hcp(south)<22 and tpSouth<23 and (hcp(south) - losers(south))<15
nRange = hcp(north)>nMinHCP and hcp(north)<12


# Now do it 
sShape and sRange and calmWest and noFit and nHand and nRange

produce 1000
action
average "hcp(south)" hcp(south),
average "hcp(north)" hcp(north),
average "1D-3C        " 100 * (oneDiamond and n3C),
average "1H-3C        " 100 * (oneHeart and n3C),
average "1H-3D        " 100 * (oneHeart and n3D),
average "1S-3C        " 100 * (oneSpade and n3C),
average "1S-3D        " 100 * (oneSpade and n3D),
average "1S-3H        " 100 * (oneSpade and n3H),
`, "S", true)
Script
Button,3-Under Inv Jump,\n\
--- 3 Under Invitational Jump\n\
• 1!S - 3!C，3!D，or 3!H\n\
• 1!H - 3!C or 3!D\n\
• 1!D - 3!C\n\
Shows a 6+ card suit w/Q or better and 9-11 HCP and denies\n\
support for opener's suit or 4-cards in either major.\n\
%InvJump3Under%

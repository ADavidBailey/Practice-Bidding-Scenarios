Script,MinorOpenerRespStr
window.currentPBSScenario = 'MinorOpenerRespStr';
console.log('PBS: Logging scenario selection for MinorOpenerRespStr');
fetch('https://bba.harmonicsystems.com/api/scenario/select', {
    method: 'POST',
    headers: {
        'Content-Type': 'application/json',
        'X-Client-Version': '1.0.0'
    },
    body: JSON.stringify({
        scenario: 'MinorOpenerRespStr',
        user: whoAmI() || 'anonymous'
    })
}).then(function(r){ console.log('PBS: Server response', r.status); }).catch(function(e){ console.log('PBS: Fetch error', e); });
setDealerCode(`

/*
auction-filter: (1[CD] Pass 1[DHSN])|(1C Pass 2C)|(1D Pass 2D)|(1[CD] Pass [23]N)
*/
# Minor w/Raise OR Balanced Response -- Leveled
dealer south

##### Imported Script: Define Calm Opponents #####

# Avoid concentration of values
cce = top4(east,clubs)<2
cde = top4(east,diamonds)<2
che = top4(east,hearts)<2
cse = top4(east,spades)<2
noConEast = cce and cde and che and cse

ccw = top4(west,clubs)<2
cdw = top4(west,diamonds)<2
chw = top4(west,hearts)<2
csw = top4(west,spades)<2
noConWest = ccw and cdw and chw and csw

balEast    = shape(east,any 4432 +any 4333)
unbalEast  = shape(east,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

balWest    = shape(west,any 4432 +any 4333)
unbalWest  = shape(west,xxxx -any 4432 -any 4333 -any 9xxx -any 8xxx -any 7xxx-any 6xxx -any 55xx)

calmEast = (unbalEast and noConEast and hcp(east)<8) or (balEast and hcp(east)<11)
calmWest = (unbalWest and noConWest and hcp(west)<8) or (balWest and hcp(west)<11)
calmOpps = calmEast and calmWest

##### End of Imported Script: Define Calm Opponents #####
##### Imported Script -- Predict Opening 1-Bid South #####
# 01/19/2026 -- Changed to use TP and separate ranges for C, D, and majors
# 03/30/2025 -- Changed to use gibNT

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422 -5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

## Calculate TP for South -- NOTE:  This uses different variable names than TP to avoid conflicts when both are used.
# Calculate South's Short-Suit Penalty Points
ssp = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
shp = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sdp = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
scp = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv01 = shape(south, any 0xxx) ? 3 : 0
ss01 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss02 = shape(south, any 11xx) ? 2 : 0
sd01 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd02 = shape(south, any 22xx) ? 1 : 0
sd03 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv01+ss01+ss02+sd01+sd02+sd03 -ssp-shp-sdp-scp

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22

# Define Gambling 3N
g3Shape = shape(south,any 9xxx +any 8xxx +any 7xxx -any 4xxx -any 5xxx -any 6xxx)
gamble3 = (top3(south,clubs)==3 and clubs(south)>6) or (top3(south,diamonds)==3 and diamonds(south)>6) and hcp(south)<12 and g3Shape

# Define Game Forcing 2C
case1 = hcp(south)>18 and sTP>21
case2 = hcp(south)>18 and sTP>19 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and sTP>19 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3)

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or gamble3 or twoNT or oneNT

# Define quality and length lower limits for 10 HCP openers

q2of3 = (top3(south,spades)>1 or top3(south,hearts)>1) and controls(south)>3
lgt5  = (spades(south)>4 or hearts(south)>4) and shape(south,any 7xxx +any 64xx +any 55xx +any 1xxx+any 0xxx -any 8xxx -any 54xx) and sTP>(hcp(south) +2)

# Predict South's Opening suit
# What's the longest suit?
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)

## GIB has different requirements for 1H/S, 1D, and 1C openings 
#Define range for 1 H/S
lS = s>4 and s>=h and s>=d and s>=c
lH = h>4 and h>s and h>=d and h>=c
lM = lS or lH

hsOK1 = lM and hcp(south)>11
hsOK2 = lM and hcp(south)>10 and sTP>12 and not hsOK1
hsOK3 = lM and hcp(south)>9  and lgt5 and q2of3 and not (hsOK1 or hsOK2)
hsRange = (hsOK1 or hsOK2 or hsOK3) and not P2

# Define 1D Range
dOK1 = hcp(south)>11 and (diamonds(south)>3 or shape(south, 4432))
dOK2 = hcp(south)>9  and sTP>12 and diamonds(south)>5 and not dOK1
dRange = (dOK1 or dOK2) and not P2

# Define 1C Range
cOK1 = hcp(south)>10 and sTP>12 and spades(south)==4
cOK2 = hcp(south)>9  and sTP>12 and clubs(south)>5 and not cOK1
cOK3 = hcp(south)>11 and clubs(south)>2            and not (cOK1 or cOK2)
cRange = (cOK1 or cOK2 or cOK3) and clubs(south)>2 and clubs(south)>diamonds(south) and not P2

oS = s>4 and s>=h and s>=d and s>=c and hsRange
oH = not oS and h>4 and h>=d and h>=c and hsRange
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and dRange
oC = not (oS or oH or oD) and cRange
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####

# Define 1 Minor and Mixed/Invitational Raise -- no 1-level suit or NT response 
cRaise = oC and clubs(north)>4    and spades(north)<4 and hearts(north)<4 and diamonds(north)<4                    //  5+ card support
dRaise = oD and diamonds(north)>3 and spades(north)<4 and hearts(north)<4 and diamonds(north)>clubs(north)         //  4+ card support
wkRange = hcp(north)>5  and hcp(north)<11
giRange = hcp(north)>10 and hcp(north)<13
gfRange = hcp(north)>12

shortness = shape(north, any 0xxx+any 1xxx+any 22xx)  // 2+ shortness points

raise  = (cRaise or dRaise) and shortness and hcp(north)>5
wkRaise = raise and wkRange
giRaise = raise and giRange
gfRaise = raise and gfRange

# Define one Minor and balanced responses w/o 5-card major and w/o 2 doubletons
balResp = (oC or oD) and shape(north,any 4333+any 4432+any 5332-5xxx-x5xx) and hcp(north)>5
wkBal   = balResp and wkRange
giBal   = balResp and giRange
gfBal   = balResp and gfRange

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###

# Do the Leveling
levwkb = wkBal and keep03
levgib = giBal and keep045
levgfb = gfBal and keep015

levwkr = wkRaise and keep
levgir = giRaise and keep
levgfr = gfRaise and keep
levelTheDeal = levwkr or levgir or levgfr or levwkb or levgib or levgfb

# Now do it
(wkRaise or giRaise or gfRaise or wkBal or giBal or gfBal) and calmOpps and hcp(south)<22
and levelTheDeal

action
average "1 Club        " 100 * oC,
average "1 Diamond     " 100 * oD,
average "hcp south     "       hcp(south),
average "hcp north     "       hcp(north),

average "--any balanced-" 100 * balResp,
average "Weak balanced  " 100 * wkBal,
average "GI balanced    " 100 * giBal,
average "GF balanced    " 100 * gfBal,

average "---any raise---" 100 * raise,
average "Mixed raise    " 100 * wkRaise,
average "GI raise       " 100 * giRaise,
average "GF raise       " 100 * gfRaise,

average "---bal/raise---" 100 * (raise and shape(north,any 4333+any 2xxx-any 22xx)),

average "any void      " 100 * shape(north,any 0xxx),
average "any singleton " 100 * shape(north,any 1xxx),
average "2/3 doubletons" 100 * shape(north,any 22xx+any 222x),
average "1 doubleton   " 100 * shape(north,any 2xxx-any 22xx-any 222x-any 0xxx-any 1xxx),
`, "S", true)
Script
Button,1m & Raise/Bal (Lev),\n\
--- 1 Minor w/Raise OR Balanced Response -- Leveled\n\
1!C/!D - 1N shows 6-10 HCP balanced\n\
1!C/!D - 2!C/!D shows 5/4+ card support and 6-10 HCP，a Mixed Raise\n\
1!C/!D - 2!H shows 11-12 HCP balanced，forcing\n\
1!C/!D - 2!S shows 5/4+ card support and invitational+ values 11+ HCP，forcing\n\
1!C/!D - 2N shows 13-15 or 18-19 HCP balanced，GF+\n\
1!C/!D - 3N shows 16-17 HCP balanced\n\
This scenario is leveled such that the mixed raise，invitational raise，and\n\
balanced weak，invitational，and forcing responses occur at about the same frequency.\n\
%MinorOpenerRespStr%

Script,DoubleShowing2Suits
window.currentPBSScenario = 'DoubleShowing2Suits';
console.log('PBS: Logging scenario selection for DoubleShowing2Suits');
fetch('https://bba.harmonicsystems.com/api/scenario/select', {
    method: 'POST',
    headers: {
        'Content-Type': 'application/json',
        'X-Client-Version': '1.0.0'
    },
    body: JSON.stringify({
        scenario: 'DoubleShowing2Suits',
        user: whoAmI() || 'anonymous'
    })
}).then(function(r){ console.log('PBS: Server response', r.status); }).catch(function(e){ console.log('PBS: Fetch error', e); });
setDealerCode(`

/*
auction-filter: Auction.....\\n((1C Pass 1[DHS]|1D Pass 1[HS]|1H Pass 1S|1[CDH] 1[DHS] 2[CDH]) X)
*/
dealer west

##### Double Showing 2 Suits #####
# a.  West Opens, North Passes, East Bids a new suit at the 1-level, and South has the other 2 suits.
# b.  West Opens, North Bids a new Suit at the 1-level, East Raises, and South has the other 2 suits.

# Predict West's Opening suit
s = spades(west)
h = hearts(west)
d = diamonds(west)
c = clubs(west)
oS = s>4 and s>=h and s>=d and s>=c
oH = not oS and h>4 and h>=d and h>=c
oD = not (oS or oH) and ((d>3 and d>=c) or c<3)
oC = not (oS or oH or oD)
wSuit = (oS or oH or oD or oC)

##### Predict East's Response ##### 

eP = hcp(east)
wP = hcp(west)

# Predict East's 1-level response
sE = spades(east)
hE = hearts(east)
dE = diamonds(east)
cE = clubs(east)

# West responds in D, H, or S (Walsh style), or raises C.
######
# If less than 13, Walsh Style; otherwise, longest suit.
eS  =  eP<13 ? sE>3 and sE>hE : sE>3 and sE>hE and sE>=dE and sE>=cE
eH  = (eP<13 ? hE>3           : hE>3           and hE>=dE and hE>=cE) and not (eS)
eD = dE>3           and dE>=cE and not (eS or eH)
eC = cE>4           and cE>4   and not (eS or eH or eD)

# East bids new suit
CD = oC and eD
CH = oC and eH
CS = oC and eS
DH = oD and eH
DS = oD and eS
HS = oH and eS

# East raises
CC = oC and  clubs(east)>4
DD = oD and  diamonds(east)>3
HH = oH and  hearts(east)>2

# Define South's doubles showing 2-suits
sHS = CD ? shape(south,44xx+54xx+45xx) : 0
sDS = CH ? shape(south,4x4x+4x5x)      : 0
sDH = CS ? shape(south,x44x+x45x)      : 0
sCS = DH ? shape(south,4xx4+4xx5)      : 0
sCH = DS ? shape(south,x4x4+x4x5)      : 0
sCD = HS ? shape(south,xx44+xx45+xx54) : 0

# Make them good suits
sGHS = sHS and top4(south,hearts)>1   and top4(south,spades)>1
sGDS = sDS and top4(south,diamonds)>1 and top4(south,spades)>1
sGDH = sDH and top4(south,diamonds)>1 and top4(south,hearts)>1
sGCS = sCS and top4(south,clubs)>1    and top4(south,spades)>1
sGCH = sCH and top4(south,clubs)>1    and top4(south,hearts)>1
sGCD = sCD and top4(south,diamonds)>1 and top4(south,clubs) >1

# West opens
westOpens = hcp(west)>11 and hcp(west)<15 and wSuit

# Define North's possible 1-level suits
nS = spades(north)  ==5 and top4(north,spades)  >1
nH = hearts(north)  ==5 and top4(north,hearts)  >1 and not nS         and spades(east)<4
nD = diamonds(north)==5 and top4(north,diamonds)>1 and not (nS or nH) and spades(east)<4 and hearts(east)<4

# Define North's possible 1-bids
nBidsDHS  = oC and (nS or nH or nD)
nBidsHS   = oD and (nS or nH)
nBidsS    = oH and nS

northBids   = (nBidsDHS or nBidsHS or nBidsS) and hcp(north)>6 and shape(north,any 5431 +any 5440)
northPasses = shape(north,any 4333 +any 4432) and hcp(north)<10
northActs   = northBids or northPasses

eastBids   = (eD or eH or eS) and not northBids and hcp(east)>5
eastRaises = northBids and (CC or DD or HH)     and hcp(east)>5
eastActs = eastBids or eastRaises

southActs = (sGHS or sGDS or sGDH or sGCS or sGCH or sGCD) and hcp(south)>11


### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###
levNBids = northBids   and keep0  // It's very hard to get North to bid.  Keeping only .015 of the passes produces less than 10 bids.
levNPass = northPasses and keep
levelTheDeal = levNBids or levNPass

##### Now do it #####
westOpens and northActs and eastActs and southActs
and levelTheDeal

action
average "N bids" 100 * northBids,
average "N pass" 100 * northPasses,
average "H&S   " 100 * sHS,
average "D&S   " 100 * sDS,
average "D&H   " 100 * sDH,
average "C&S   " 100 * sCS,
average "C&H   " 100 * sCH,
average "C&D   " 100 * sCD,
`, "W", true)
Script
Button,Double w/2 Suits,\n\
--- Double Showing 2 Suits\n\
After 1x (P) 1yï¼Œ(X) shows the other two suits.\n\
%DoubleShowing2Suits%

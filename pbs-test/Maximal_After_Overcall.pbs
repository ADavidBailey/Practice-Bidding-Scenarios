Script,MaximalAfterOvercall
window.currentPBSScenario = 'MaximalAfterOvercall';
console.log('PBS: Logging scenario selection for MaximalAfterOvercall');
fetch('https://bba.harmonicsystems.com/api/scenario/select', {
    method: 'POST',
    headers: {
        'Content-Type': 'application/json',
        'X-Client-Version': '1.0.0'
    },
    body: JSON.stringify({
        scenario: 'MaximalAfterOvercall',
        user: whoAmI() || 'anonymous'
    })
}).then(function(r){ console.log('PBS: Server response', r.status); }).catch(function(e){ console.log('PBS: Fetch error', e); });
setDealerCode(`

/*
auction-filter: Auction.....\n(1H .* 2H +3D|1S .* 2S +3H)
*/
/*
convention-card: 21GF-MaximalDouble
auction-filter: Auction.....\n(1H .* 2H +3D|1S .* 2S +3H)

Maximal Double
*/

dealer south

##### Imported Script -- Predict Opening 1-Bid South #####
# 01/19/2026 -- Changed to use TP and separate ranges for C, D, and majors
# 03/30/2025 -- Changed to use gibNT

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422 -5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

## Calculate TP for South -- NOTE:  This uses different variable names than TP to avoid conflicts when both are used.
# Calculate South's Short-Suit Penalty Points
ssp = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
shp = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sdp = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
scp = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv01 = shape(south, any 0xxx) ? 3 : 0
ss01 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss02 = shape(south, any 11xx) ? 2 : 0
sd01 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd02 = shape(south, any 22xx) ? 1 : 0
sd03 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv01+ss01+ss02+sd01+sd02+sd03 -ssp-shp-sdp-scp

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22

# Define Gambling 3N
g3Shape = shape(south,any 9xxx +any 8xxx +any 7xxx -any 4xxx -any 5xxx -any 6xxx)
gamble3 = (top3(south,clubs)==3 and clubs(south)>6) or (top3(south,diamonds)==3 and diamonds(south)>6) and hcp(south)<12 and g3Shape

# Define Game Forcing 2C
case1 = hcp(south)>18 and sTP>21
case2 = hcp(south)>18 and sTP>19 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and sTP>19 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3)

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or gamble3 or twoNT or oneNT

# Define quality and length lower limits for 10 HCP openers

q2of3 = (top3(south,spades)>1 or top3(south,hearts)>1) and controls(south)>3
lgt5  = (spades(south)>4 or hearts(south)>4) and shape(south,any 7xxx +any 64xx +any 55xx +any 1xxx+any 0xxx -any 8xxx -any 54xx) and sTP>(hcp(south) +2)

# Predict South's Opening suit
# What's the longest suit?
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)

## GIB has different requirements for 1H/S, 1D, and 1C openings 
#Define range for 1 H/S
lS = s>4 and s>=h and s>=d and s>=c
lH = h>4 and h>s and h>=d and h>=c
lM = lS or lH

hsOK1 = lM and hcp(south)>11
hsOK2 = lM and hcp(south)>10 and sTP>12 and not hsOK1
hsOK3 = lM and hcp(south)>9  and lgt5 and q2of3 and not (hsOK1 or hsOK2)
hsRange = (hsOK1 or hsOK2 or hsOK3) and not P2

# Define 1D Range
dOK1 = hcp(south)>11 and (diamonds(south)>3 or shape(south, 4432))
dOK2 = hcp(south)>9  and sTP>12 and diamonds(south)>5 and not dOK1
dRange = (dOK1 or dOK2) and not P2

# Define 1C Range
cOK1 = hcp(south)>10 and sTP>12 and spades(south)==4
cOK2 = hcp(south)>9  and sTP>12 and clubs(south)>5 and not cOK1
cOK3 = hcp(south)>11 and clubs(south)>2            and not (cOK1 or cOK2)
cRange = (cOK1 or cOK2 or cOK3) and clubs(south)>2 and clubs(south)>diamonds(south) and not P2

oS = s>4 and s>=h and s>=d and s>=c and hsRange
oH = not oS and h>4 and h>=d and h>=c and hsRange
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and dRange
oC = not (oS or oH or oD) and cRange
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
# oneHeart, oneSpade

### Imported script: TP (Total Points)
//   Total Points = HCP + Short-Suit Points - Penalty Points
//   Short-Suit Points: 3 for void, 2 for singleton, 1 for doubleton
//   Penalty Points: 1 for each honor (K/Q/J) in a short suit (devalues wasted honors like Kx, Qx)

## Calculate TP for North
# Short-Suit Points: void=3, singleton=2, doubleton=1
nSpadeSSP = spades(north)<3   ? 3-spades(north)   : 0
nHeartSSP = hearts(north)<3   ? 3-hearts(north)   : 0
nDiamSSP  = diamonds(north)<3 ? 3-diamonds(north) : 0
nClubSSP  = clubs(north)<3    ? 3-clubs(north)    : 0
nSSP = nSpadeSSP + nHeartSSP + nDiamSSP + nClubSSP

# Penalty Points: deduct 1 for honors in short suits
nSpadePP = spades(north)<3   and hcp(north,spades)>0   ? 1 : 0
nHeartPP = hearts(north)<3   and hcp(north,hearts)>0   ? 1 : 0
nDiamPP  = diamonds(north)<3 and hcp(north,diamonds)>0 ? 1 : 0
nClubPP  = clubs(north)<3    and hcp(north,clubs)>0    ? 1 : 0
nPP = nSpadePP + nHeartPP + nDiamPP + nClubPP

tpNorth = hcp(north) + nSSP - nPP

## Calculate TP for East
eSpadeSSP = spades(east)<3   ? 3-spades(east)   : 0
eHeartSSP = hearts(east)<3   ? 3-hearts(east)   : 0
eDiamSSP  = diamonds(east)<3 ? 3-diamonds(east) : 0
eClubSSP  = clubs(east)<3    ? 3-clubs(east)    : 0
eSSP = eSpadeSSP + eHeartSSP + eDiamSSP + eClubSSP

eSpadePP = spades(east)<3   and hcp(east,spades)>0   ? 1 : 0
eHeartPP = hearts(east)<3   and hcp(east,hearts)>0   ? 1 : 0
eDiamPP  = diamonds(east)<3 and hcp(east,diamonds)>0 ? 1 : 0
eClubPP  = clubs(east)<3    and hcp(east,clubs)>0    ? 1 : 0
ePP = eSpadePP + eHeartPP + eDiamPP + eClubPP

tpEast = hcp(east) + eSSP - ePP

## Calculate TP for South
sSpadeSSP = spades(south)<3   ? 3-spades(south)   : 0
sHeartSSP = hearts(south)<3   ? 3-hearts(south)   : 0
sDiamSSP  = diamonds(south)<3 ? 3-diamonds(south) : 0
sClubSSP  = clubs(south)<3    ? 3-clubs(south)    : 0
sSSP = sSpadeSSP + sHeartSSP + sDiamSSP + sClubSSP

sSpadePP = spades(south)<3   and hcp(south,spades)>0   ? 1 : 0
sHeartPP = hearts(south)<3   and hcp(south,hearts)>0   ? 1 : 0
sDiamPP  = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
sClubPP  = clubs(south)<3    and hcp(south,clubs)>0    ? 1 : 0
sPP = sSpadePP + sHeartPP + sDiamPP + sClubPP

tpSouth = hcp(south) + sSSP - sPP

## Calculate TP for West
wSpadeSSP = spades(west)<3   ? 3-spades(west)   : 0
wHeartSSP = hearts(west)<3   ? 3-hearts(west)   : 0
wDiamSSP  = diamonds(west)<3 ? 3-diamonds(west) : 0
wClubSSP  = clubs(west)<3    ? 3-clubs(west)    : 0
wSSP = wSpadeSSP + wHeartSSP + wDiamSSP + wClubSSP

wSpadePP = spades(west)<3   and hcp(west,spades)>0   ? 1 : 0
wHeartPP = hearts(west)<3   and hcp(west,hearts)>0   ? 1 : 0
wDiamPP  = diamonds(west)<3 and hcp(west,diamonds)>0 ? 1 : 0
wClubPP  = clubs(west)<3    and hcp(west,clubs)>0    ? 1 : 0
wPP = wSpadePP + wHeartPP + wDiamPP + wClubPP

tpWest = hcp(west) + wSSP - wPP

## Partnership Totals
tpNS = tpNorth + tpSouth
tpEW = tpEast + tpWest

### End of Imported Script: TP
# tpNorth, tpSouth, tpEast, tpWest

# Define West's actions & East's Maximal suit bid
wShape = shape(west,xxxx -any 76xx -any 75xx -any 66xx -any 65xx -any 55xx)   // kill the 2-suited hands
wS = spades(west)>4   and top3(west,spades)>1   and wShape and tpWest>7
wC = clubs(west)>4    and top4(west,clubs)>2    and wShape and tpWest>10 and hcp(west)>9
wD = diamonds(west)>4 and top4(west,diamonds)>2 and wShape and tpWest>10 and hcp(west)>9
wH = hearts(west)>4   and top4(west,hearts)>2   and wShape and tpWest>10 and hcp(west)>9
wX = shape(west,any 5440 +any 5431 +any 4432 +any 4441) and tpWest>12 and not (wS or wC or wD or wH)
wP = not (wS or wC or wD or wH or wX)

# Define East's ranges for bidding after  X, raise, new suit, free bid.
respXRange = tpEast<12
maxDresp   = diamonds(east)>=4 and diamonds(east)>spades(east) and diamonds(east)>hearts(east) and diamonds(east)>clubs(east) and hearts(west)<3 and hcp(east,hearts)==0
maxHresp   = hearts(east)>=4   and hearts(east)>spades(east)   and hearts(east)>diamonds(east) and hearts(east)>clubs(east)   and spades(west)<3 and hcp(east,spades)==0
respX = respXRange or maxDresp or maxHresp   // for stats

raiseRange = tpEast>7 and tpEast<11
maxDraise  = diamonds(east)>2 and top5(east,diamonds)>1 and raiseRange
maxHraise  = hearts(east)>2   and top5(east,hearts)>1   and raiseRange
raise = maxDraise or maxHraise               // for stats

newSuitRange = tpEast>12 and tpEast<18
maxDnewsuit = diamonds(east)>5 and top5(east,diamonds)>2 and newSuitRange
maxHnewsuit = hearts(east)>5   and top5(east,hearts)>2   and newSuitRange
newsuit = maxDnewsuit or maxHnewsuit         // for stats

freeRange = tpEast>11 and tpEast<18
maxDfree = diamonds(east)>5 and top3(east,diamonds)>1 and freeRange
maxHfree = hearts(east)>5   and top3(east,hearts)>1   and freeRange
freebid = maxDfree or maxHfree               // for stats

maxD = (maxDresp or maxDraise or maxDnewsuit or maxDfree) and hearts(west)<2
maxH = (maxHresp or maxHraise or maxHnewsuit or maxHfree) and spades(west)<2

# South opens 1H or 1S, North raises, and East overcalls in the Maximal suit
min = hcp(south)<12 and tpSouth<13
len = min ? 6 : 5
sRange = hcp(south)>10 and tpSouth>11 and tpSouth<19
nRange = tpNorth>5 and tpNorth<11
sH = oneHeart and hearts(south)>=len and sRange and hearts(north)==3 and nRange and maxD
sS = oneSpade and spades(south)>=len and sRange and spades(north)==3 and nRange and maxH

### Imported Leveling Code ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)

keep06 = c1 and c2          // this is used later w/c3 & c4 expressions
keep44 = c3 or c4           // this is used later w/c1 & c2 expressions

keep015 = keep06 and c3
keep03 = keep06 and keep44
keep045 = keep06 and not c3
####06 = c1 and c2
keep11 = c1 and keep44
keep14 = c1 and not keep44
keep19 = c1 and not c2
keep25 = c1
keep30 = keep06 or c3
keep33 = c1 or (c2 and keep44)
####44 = c3 or c4
keep47 = keep44 or keep06

keep53 = not keep47
keep56 = not keep44
keep67 = not keep33
keep70 = not keep30
keep75 = not keep25
keep81 = not keep19
keep86 = not keep14
keep89 = not keep11
keep94 = not keep06
keep955 = not keep045
keep97 = not keep03
keep985 = not keep015
keep   = 1
keep0  = 0
### End of Imported Leveling Code ###

# Now do it
(sH or sS) and wShape
#and levelTheDeal

produce 5000
action
average "X          " 100 * respX,
average "raise      " 100 * raise,
average "new suit   " 100 * newsuit,
average "free bid   " 100 * freebid,
`, "S", true)
Script
Button,MaximalAfterOvercall,%MaximalAfterOvercall%

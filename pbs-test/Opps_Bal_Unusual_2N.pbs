Script,OppsBalUnusualNT
window.currentPBSScenario = 'OppsBalUnusualNT';
console.log('PBS: Logging scenario selection for OppsBalUnusualNT');
fetch('https://bba.harmonicsystems.com/api/scenario/select', {
    method: 'POST',
    headers: {
        'Content-Type': 'application/json',
        'X-Client-Version': '1.0.0'
    },
    body: JSON.stringify({
        scenario: 'OppsBalUnusualNT',
        user: whoAmI() || 'anonymous'
    })
}).then(function(r){ console.log('PBS: Server response', r.status); }).catch(function(e){ console.log('PBS: Fetch error', e); });
setDealerCode(`

/*
auction-filter: Auction.....\n1[CDHS] +Pass +2[CDHSN] +Pass\nPass
*/
# Opponent's Balancing Unusual NT
dealer south
produce 1000



predeal west DT9,CT9

##### Imported Script -- Predict Opening 1-Bid South #####
# 01/19/2026 -- Changed to use TP and separate ranges for C, D, and majors
# 03/30/2025 -- Changed to use gibNT

# GIB opens 1N w/15-17 HCP or 15-16 and a 5-card major
ntP = hcp(south) + shape(south,5xxx+x5xx)
nt1 = shape(south, any 5332+any 4432+any 4333) and hcp(south)>14 and ntP<18

# GIB does not open with 5422 and a 5-card major
# GIB does not open with 5422 and the strength to reverse
nt2 = shape(south, any 5422 -5xxx-x5xx-4xxx) and hcp(south)>14 and hcp(south)<17

gibNT = nt1 or nt2

## Calculate TP for South -- NOTE:  This uses different variable names than TP to avoid conflicts when both are used.
# Calculate South's Short-Suit Penalty Points
ssp = spades(south)<3 and hcp(south,spades)>0     ? 1 : 0
shp = hearts(south)<3 and hcp(south,hearts)>0     ? 1 : 0
sdp = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
scp = clubs(south)<3 and hcp(south,clubs)>0       ? 1 : 0

# Calculate South's Short-Suit Points
sv01 = shape(south, any 0xxx) ? 3 : 0
ss01 = shape(south, any 1xxx) ? 2 : 0 // allow for 2 singletons
ss02 = shape(south, any 11xx) ? 2 : 0
sd01 = shape(south, any 2xxx) ? 1 : 0 // allow for 3 doubletons
sd02 = shape(south, any 22xx) ? 1 : 0
sd03 = shape(south, any 222x) ? 1 : 0

# Calculate South's Total Points
sTP = hcp(south) + sv01+ss01+ss02+sd01+sd02+sd03 -ssp-shp-sdp-scp

# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Define suit points for south (suitPoints)
suitPoints = hcp(south) + lengthPoints

TwoNtShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422)

# Define ntPoint ranges
oneNT   = gibNT
twoNT   = TwoNtShape and hcp(south)>19 and hcp(south)<22

# Define Gambling 3N
g3Shape = shape(south,any 9xxx +any 8xxx +any 7xxx -any 4xxx -any 5xxx -any 6xxx)
gamble3 = (top3(south,clubs)==3 and clubs(south)>6) or (top3(south,diamonds)==3 and diamonds(south)>6) and hcp(south)<12 and g3Shape

# Define Game Forcing 2C
case1 = hcp(south)>18 and sTP>21
case2 = hcp(south)>18 and sTP>19 and losers(south)<5 and (spades(south)>5 or hearts(south)>5)
case3 = hcp(south)>18 and sTP>19 and losers(south)<4 and (diamonds(south)>5 or clubs(south)>5)
gameForce2C = (case1 or case2 or case3)

### Predict South's opening BID
P1 = gameForce2C
P2 = P1 or gamble3 or twoNT or oneNT

# Define quality and length lower limits for 10 HCP openers

q2of3 = (top3(south,spades)>1 or top3(south,hearts)>1) and controls(south)>3
lgt5  = (spades(south)>4 or hearts(south)>4) and shape(south,any 7xxx +any 64xx +any 55xx +any 1xxx+any 0xxx -any 8xxx -any 54xx) and sTP>(hcp(south) +2)

# Predict South's Opening suit
# What's the longest suit?
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)

## GIB has different requirements for 1H/S, 1D, and 1C openings 
#Define range for 1 H/S
lS = s>4 and s>=h and s>=d and s>=c
lH = h>4 and h>s and h>=d and h>=c
lM = lS or lH

hsOK1 = lM and hcp(south)>11
hsOK2 = lM and hcp(south)>10 and sTP>12 and not hsOK1
hsOK3 = lM and hcp(south)>9  and lgt5 and q2of3 and not (hsOK1 or hsOK2)
hsRange = (hsOK1 or hsOK2 or hsOK3) and not P2

# Define 1D Range
dOK1 = hcp(south)>11 and (diamonds(south)>3 or shape(south, 4432))
dOK2 = hcp(south)>9  and sTP>12 and diamonds(south)>5 and not dOK1
dRange = (dOK1 or dOK2) and not P2

# Define 1C Range
cOK1 = hcp(south)>10 and sTP>12 and spades(south)==4
cOK2 = hcp(south)>9  and sTP>12 and clubs(south)>5 and not cOK1
cOK3 = hcp(south)>11 and clubs(south)>2            and not (cOK1 or cOK2)
cRange = (cOK1 or cOK2 or cOK3) and clubs(south)>2 and clubs(south)>diamonds(south) and not P2

oS = s>4 and s>=h and s>=d and s>=c and hsRange
oH = not oS and h>4 and h>=d and h>=c and hsRange
oD = not (oS or oH) and ((d>3 and d>=c) or c<3) and dRange
oC = not (oS or oH or oD) and cRange
openingSuit = (oS or oH or oD or oC)
oneSpade   = oS
oneHeart   = oH
oneDiamond = oD
oneClub    = oC

##### End of Imported Script -- Predict Opening 1-Bid #####
### Imported script: TP (Total Points)
//   Total Points = HCP + Short-Suit Points - Penalty Points
//   Short-Suit Points: 3 for void, 2 for singleton, 1 for doubleton
//   Penalty Points: 1 for each honor (K/Q/J) in a short suit (devalues wasted honors like Kx, Qx)

## Calculate TP for North
# Short-Suit Points: void=3, singleton=2, doubleton=1
nSpadeSSP = spades(north)<3   ? 3-spades(north)   : 0
nHeartSSP = hearts(north)<3   ? 3-hearts(north)   : 0
nDiamSSP  = diamonds(north)<3 ? 3-diamonds(north) : 0
nClubSSP  = clubs(north)<3    ? 3-clubs(north)    : 0
nSSP = nSpadeSSP + nHeartSSP + nDiamSSP + nClubSSP

# Penalty Points: deduct 1 for honors in short suits
nSpadePP = spades(north)<3   and hcp(north,spades)>0   ? 1 : 0
nHeartPP = hearts(north)<3   and hcp(north,hearts)>0   ? 1 : 0
nDiamPP  = diamonds(north)<3 and hcp(north,diamonds)>0 ? 1 : 0
nClubPP  = clubs(north)<3    and hcp(north,clubs)>0    ? 1 : 0
nPP = nSpadePP + nHeartPP + nDiamPP + nClubPP

tpNorth = hcp(north) + nSSP - nPP

## Calculate TP for East
eSpadeSSP = spades(east)<3   ? 3-spades(east)   : 0
eHeartSSP = hearts(east)<3   ? 3-hearts(east)   : 0
eDiamSSP  = diamonds(east)<3 ? 3-diamonds(east) : 0
eClubSSP  = clubs(east)<3    ? 3-clubs(east)    : 0
eSSP = eSpadeSSP + eHeartSSP + eDiamSSP + eClubSSP

eSpadePP = spades(east)<3   and hcp(east,spades)>0   ? 1 : 0
eHeartPP = hearts(east)<3   and hcp(east,hearts)>0   ? 1 : 0
eDiamPP  = diamonds(east)<3 and hcp(east,diamonds)>0 ? 1 : 0
eClubPP  = clubs(east)<3    and hcp(east,clubs)>0    ? 1 : 0
ePP = eSpadePP + eHeartPP + eDiamPP + eClubPP

tpEast = hcp(east) + eSSP - ePP

## Calculate TP for South
sSpadeSSP = spades(south)<3   ? 3-spades(south)   : 0
sHeartSSP = hearts(south)<3   ? 3-hearts(south)   : 0
sDiamSSP  = diamonds(south)<3 ? 3-diamonds(south) : 0
sClubSSP  = clubs(south)<3    ? 3-clubs(south)    : 0
sSSP = sSpadeSSP + sHeartSSP + sDiamSSP + sClubSSP

sSpadePP = spades(south)<3   and hcp(south,spades)>0   ? 1 : 0
sHeartPP = hearts(south)<3   and hcp(south,hearts)>0   ? 1 : 0
sDiamPP  = diamonds(south)<3 and hcp(south,diamonds)>0 ? 1 : 0
sClubPP  = clubs(south)<3    and hcp(south,clubs)>0    ? 1 : 0
sPP = sSpadePP + sHeartPP + sDiamPP + sClubPP

tpSouth = hcp(south) + sSSP - sPP

## Calculate TP for West
wSpadeSSP = spades(west)<3   ? 3-spades(west)   : 0
wHeartSSP = hearts(west)<3   ? 3-hearts(west)   : 0
wDiamSSP  = diamonds(west)<3 ? 3-diamonds(west) : 0
wClubSSP  = clubs(west)<3    ? 3-clubs(west)    : 0
wSSP = wSpadeSSP + wHeartSSP + wDiamSSP + wClubSSP

wSpadePP = spades(west)<3   and hcp(west,spades)>0   ? 1 : 0
wHeartPP = hearts(west)<3   and hcp(west,hearts)>0   ? 1 : 0
wDiamPP  = diamonds(west)<3 and hcp(west,diamonds)>0 ? 1 : 0
wClubPP  = clubs(west)<3    and hcp(west,clubs)>0    ? 1 : 0
wPP = wSpadePP + wHeartPP + wDiamPP + wClubPP

tpWest = hcp(west) + wSSP - wPP

## Partnership Totals
tpNS = tpNorth + tpSouth
tpEW = tpEast + tpWest

### End of Imported Script: TP
# Defines tpNorth, tpSouth, tpWest, tpEast

# The bidding starts: 1M - P - 2M - P - P
ForcingNT = hcp(north)<7 or hcp(north)>9   // faulty raise of invitational raise

s3 = spades(north)==3 and tpNorth>6 and tpNorth<11 and not ForcingNT
s4 = spades(north)==4 and tpNorth>6 and tpNorth<9                   // avoid jump raise
h3 = hearts(north)==3 and tpNorth>6 and tpNorth<11 and not ForcingNT
h4 = hearts(north)==4 and tpNorth>6 and tpNorth<9

oneS = oS and (s3 or s4) and spades(west)>1
oneH = oH and (h3 or h4) and hearts(west)>1

### Then, in balancing seat, we get a balancing 2NT

# Define good suits
GoodD = diamonds(west)>3 and top5(west,diamonds)>2
GoodC = clubs(west)>3    and top5(west,clubs)>2

# Intervener is 54 or 45 in the minors
wDC = GoodD and GoodC and shape(west,xx54 +xx45)      // West can have 2 to 4 in openers Major
wRange = losers(west)<8 and hcp(west)<12 and tpWest>hcp(west) and tpWest>11 and tpWest<13  // avoid honors in short suits

eHand = spades(east)<5 and hearts(east)<5 and tpEast<12       // East must start with a Pass

oSuit = (oneS or oneH) and shape(south,any xxxx-8xxx-7xxx-6xxx)
oRange = hcp(south)>11 and hcp(south)<15 and tpSouth>11 and tpSouth<16

### Now do it
oSuit and oRange and wDC and wRange and eHand
`, "S", true)
Script
Button,Bal w/Unusual NT,\n\
--- Opponents Bal with Unusual NT\n\
This shows 5/4 or 4/5 in the minors.\n\
%OppsBalUnusualNT%

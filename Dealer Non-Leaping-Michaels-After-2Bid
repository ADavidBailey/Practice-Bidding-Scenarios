Script,NLMAfter2Bid
`

produce 1000
dealer east

# Define Good suits North -- 5+ cards with at least 2 of the top 3
gSn = spades(north)>4   and top3(north,spades)>1
gHn = hearts(north)>4   and top3(north,hearts)>1
gDn = diamonds(north)>4 and top3(north,diamonds)>1
gCn = clubs(north)>4    and top3(north,clubs)>1

# Define Good 2 Suits North
g2SNnotS = (gHn and gDn) or (gHn and gCn) or (gDn and gCn)
g2SNnotH = (gSn and gDn) or (gSn and gCn) or (gDn and gCn)
g2SNnotD = (gSn and gHn) or (gSn and gCn) or (gHn and gCn)
northActs = hcp(north)>11 and losers(north)<7

eastHCP = hcp(east)>5 and hcp(east)<11

# Define East's 2-level Preempt with West's support and North's 2 good suits and less than 7 losers
w2S = top5(east,spades)>2   and spades(east)==6   and spades(west)==3   and g2SNnotS
w2H = top5(east,hearts)>2   and hearts(east)==6   and hearts(west)==3   and g2SNnotH
w2D = top5(east,diamonds)>2 and diamonds(east)==6 and diamonds(west)==3 and g2SNnotD

southPass = shape(south,xxxx -any 8xxx -any 7xxx -any 6xxx -any 55xx) and hcp(south)<9
westRaise = hcp(west)>3 and hcp(west)<7 and shape(west, any 0xxx +any 1xxx +any 22xx)
eastBids = eastHCP and (w2S or w2H or w2D) and shape(east,any 6430 +any 6421 +any 6331 +any 6322 -4xxx-x4xx)

east2Bid = eastBids and southPass and westRaise and northActs

# Now do it
east2Bid

action

average "2D" 100 * w2D,
average "2H" 100 * w2H,
average "2S" 100 * w2S,

`, "E", true);
Script
Button,NLM after 2-Bid & Raise,\n\
--- Non-Leaping Michaels after a Weak 2 & Raise\n\
https://www.bridgewebs.com/alton/EBU%20-%20Stage%202%20Conventions%20-%20Leaping%20and%20Non-Leaping%20Michaels.pdf\n\
%NLMAfter2Bid%

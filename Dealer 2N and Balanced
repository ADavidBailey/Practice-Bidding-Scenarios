Script,TwoNTandBalanced
setDealerCode(
`

produce 1000
dealer south

### SOUTH ###
# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and top4(south,spades)==1 and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and top4(south,hearts)==1 and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)==1 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and top4(south,clubs)==1 and not hascard(south,AC) ? 1 : 0
ntDownGrade = S2H or H2H or D2H or C2H

# Define notrump points for south (ntPoints)
ntPoints = ntDownGrade ? hcp(south) : hcp(south) + lengthPoints

# Define robot notrump shape and exclude any 5-card major
ntShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422 -5xxx -x5xx)

# Define ntPoint ranges
twoNT = ntShape and ntPoints>19 and ntPoints<22

### NORTH ###
# Calculate strength for North -- hcp plus 1 for every card over 4 in any suit 
nlp1 = spades(north)>4 ? spades(north)-4 : 0
nlp2 = hearts(north)>4 ? hearts(north)-4 : 0
nlp3 = diamonds(north)>4 ? diamonds(north)-4 : 0
nlp4 = clubs(north)>4 ? clubs(north)-4 : 0
strength = nlp1 + nlp2 + nlp3 + nlp4 + hcp(north)

game = strength>=4 and strength<=11
slam = strength>=12

balanced = shape(north,any 4333 +any 4432 +any 5332 -5xxx-x5xx)

### Do it ###
twoNT and balanced and (ntPoints + strength)>30   // Use strength to balance game/slam

### Generate Statistics

action

average "Game         " 100 * game,
average "Slam         " 100 * slam,

`,"S",true)
Script
Button,2 NT and Balanced,\n\
---  20-21 Notrump and a Balanced response.  The responder's hand is leveled to\n\
make game or slam at about the same frequency.\n\
%TwoNTandBalanced%

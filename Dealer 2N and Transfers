Script,TwoNTandXfer
setDealerCode(
`

produce 1000
dealer south

### SOUTH ###
# Calculate length points for South (lengthPoints)
lp1 = spades(south)>4 ? spades(south)-4 : 0
lp2 = hearts(south)>4 ? hearts(south)-4 : 0
lp3 = diamonds(south)>4 ? diamonds(south)-4 : 0
lp4 = clubs(south)>4 ? clubs(south)-4 : 0
lengthPoints = lp1 + lp2 + lp3 + lp4

# Calculate doubleton honor NT downgrade(s) for South -- 2 cards, 1 honor, not the Ace
S2H = spades(south)==2 and top4(south,spades)==1 and not hascard(south,AS) ? 1 : 0
H2H = hearts(south)==2 and top4(south,hearts)==1 and not hascard(south,AH) ? 1 : 0
D2H = diamonds(south)==2 and top4(south,diamonds)==1 and not hascard(south,AD) ? 1 : 0
C2H = clubs(south)==2 and top4(south,clubs)==1 and not hascard(south,AC) ? 1 : 0
ntDownGrade = S2H or H2H or D2H or C2H

# Define notrump points for south (ntPoints)
ntPoints = ntDownGrade ? hcp(south) : hcp(south) + lengthPoints

# Define suit points for south (suitPoints)
#suitPoints = hcp(south) + lengthPoints

# Define robot notrump shape and exclude any 5card 
ntShape = shape(south, any 4333 +any 4432 +any 5332 +any 5422 -5xxx -x5xx)

# Define Two Notrump
twoNT = ntShape and ntPoints>19 and ntPoints<22

### NORTH ###
# Calculate strength for North -- hcp plus 1 for every card over 4 in any suit 
nlp1 = spades(north)>4 ? spades(north)-4 : 0
nlp2 = hearts(north)>4 ? hearts(north)-4 : 0
nlp3 = diamonds(north)>4 ? diamonds(north)-4 : 0
nlp4 = clubs(north)>4 ? clubs(north)-4 : 0
strength = nlp1 + nlp2 + nlp3 + nlp4 + hcp(north)

texas = (spades(north)>5 or hearts(north)>5)
MSS   = ((diamonds(north)>4 and clubs(north)>4) or (diamonds(north)>5 or clubs(north)>5))

game = strength>=4 and strength<=11
slam = strength>=12

### Define Leveling ###
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)
c5 = hascard(west,4C)

reduce98 = c1 and c2 and c3
reduce94 = c1 and c2
reduce87 = c1 and (c2 or c3)
reduce75 = c1
reduce62 = (c1 or c2) and not (c3 and (c4 or c5)) 
reduce50 = c1 or c2
reduce38 = not reduce62
reduce25 = not reduce75
reduce13 = not reduce87
reduce06 = not reduce94
reduce02 = not reduce98
###

levTexas = not texas or reduce06
levMSS = not MSS or 1
levSlam = not slam or 1
levGame = not game or 1

levelTheCode = levTexas and levMSS and levSlam and levGame

### Do it ###
twoNT and (texas or MSS) and (ntPoints + strength)>29
and levelTheCode

### Generate Statistics

action
average "Texas        " 100 * texas,
average "MSS          " 100 * MSS,
average "Game         " 100 * game,
average "Slam         " 100 * slam,

`,"S",true)
Script
Button,2 NT and Xfer,\n\
---  20-21 Notrump and a Texas or Minor-suit transfer response.  The responder's hand is\n\
leveled to make Texas or Minor-suit transfer AND game or slam at about the same frequency.\n\
%TwoNTandXfer%

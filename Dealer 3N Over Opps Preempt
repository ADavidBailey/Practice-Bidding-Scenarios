Script,NTover3
setDealerCode(
`
### Opponents Preempt and we overcall 3N ###

produce 1000

# define honor cards
aS = hascard(south,AS)
aH = hascard(south,AH)
aD = hascard(south,AD)
aC = hascard(south,AC)
kS = hascard(south,KS)
kH = hascard(south,KH)
kD = hascard(south,KD)
kC = hascard(south,KC)
qS = hascard(south,QS)
qH = hascard(south,QH)
qD = hascard(south,QD)
qC = hascard(south,QC)
jS = hascard(south,JS)
jH = hascard(south,JH)
jD = hascard(south,JD)
jC = hascard(south,JC)

# define number of cards in each suit
s = spades(south)
h = hearts(south)
d = diamonds(south)
c = clubs(south)

# define south's hc points
hc = hcp(south)

# define south's long rebiddable suits
# Define Rebiddable suits -- 6+ cards with 3 of the top 4
lrS = spades(south)>5 and top4(south,spades)>2
lrH = hearts(south)>5 and top4(south,hearts)>2
lrD = diamonds(south)>5 and top4(south,diamonds)>2
lrC = clubs(south)>5 and top4(south,clubs)>2
longRebid = (lrS or lrH or lrD or lrC)


# east preempts and the 3-level
sE = shape(east, 7xxx-x4xx) and top2(east,spades)==1 and top5(east,spades)>2
hE = shape(east, x7xx-4xxx) and top2(east,hearts)==1 and top5(east,hearts)>2
dE = shape(east, xx7x-4xxx-x4xx) and top2(east,diamonds)==1 and top5(east,diamonds)>2
cE = shape(east, xxx7-4xxx-x4xx) and top2(east,clubs)==1 and top5(east,clubs)>2
ePreempts = (sE or hE or dE or cE) and hcp(east)>6 and hcp(east)<11

# define south's long rebiddable suits
# Define Rebiddable suits -- 6+ cards with 3 of the top 4
rS = spades(south)>5 and top4(south,spades)>2
rH = hearts(south)>5 and top4(south,hearts)>2
rD = diamonds(south)>5 and top4(south,diamonds)>2
rC = clubs(south)>5 and top4(south,clubs)>2


# define Case1:  south has only 1 stop -- A, Kx, QJx
s1s = sE and (aS or (kS and s>1) or (qS and jS and s>2)) and hcp(south,spades)<5
h1s = hE and (aH or (kH and h>1) or (qH and jH and h>2)) and hcp(south,hearts)<5
d1s = dE and (aD or (kD and d>1) or (qD and jD and d>2)) and hcp(south,diamonds)<5
c1s = cE and (aC or (kC and c>1) or (qC and jC and c>2)) and hcp(south,clubs)<5
oneStop = (s1s or h1s or d1s or c1s)

case1 = oneStop and hc>17
case2 = oneStop and longRebid and hc>15 and not case1

# define Case2:  south has 2 stops -- AK, AQ, KQx
s2s = sE and ((aS and kS) or (aS and qS) or (kS and qS and s>2))
h2s = hE and ((aH and kH) or (aH and qH) or (kH and qH and h>2))
d2s = dE and ((aD and kD) or (aD and qD) or (kD and qD and d>2))
c2s = cE and ((aC and kC) or (aC and qC) or (kC and qC and c>2))
twoStop = (s2s or h2s or d2s or c2s)
case3 = twoStop and hc>15

# define 4th seat

### Define Leveling
c1 = hascard(west,2C)
c2 = hascard(east,2D)
c3 = hascard(west,3C)
c4 = hascard(east,3D)
c5 = hascard(west,4C)

reduce98 = c1 and c2 and c3
reduce94 = c1 and c2
reduce87 = c1 and (c2 or c3)
reduce75 = c1
reduce62 = (c1 or c2) and not (c3 and (c4 or c5)) 
reduce50 = c1 or c2
reduce38 = not reduce62
reduce25 = not reduce75
reduce13 = not reduce87
reduce06 = not reduce94
reduce02 = not reduce98
reduce   = 1

# Level it
level1 = not case1 or reduce87
level2 = not case2 or reduce06 or reduce02
level3 = not case3 or reduce62
level = level1 and level2 and level3

# south bids 3N
ePreempts and (case1 or case2 or case3)
and level

action
average "1 Stop and >17             " 100 * case1,
average "1 Stop, good suit, and >15 " 100 * case2,
average "2 Stop and >15             " 100 * case3,


### Leveling Results ###

#  Before:
#  1 Stop and >17             : 70.4
#  1 Stop, good suit, and >15 : 8.3
#  2 Stop and >15             : 21.3
#  Generated 7539098 hands
#  Produced 1000 hands

#  After:
#  1 Stop and >17             : 34.7458
#  1 Stop, good suit, and >15 : 32.4859
#  2 Stop and >15             : 32.7684
#  Generated 10000000 hands
#  Produced 354 hands

`,"E",true)
Script
Button,3NT over RHO's 3x,\n\
---  3NT over Opps Preempt\n\
• 18+ and 1 stop\n\
• 16+ and good suit\n\
• 16+ and 2 stops\n\
and a prayer!\n\
%NTover3%
